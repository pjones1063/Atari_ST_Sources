
10 mode 1 : key off
20 if length(6)=0 then reserve as data 6,5008 : fill start(6) to start(6)+length(6),0 : loke start(6),38964820 : doke start(6)+4,98 : doke start(6)+6,100

30 centre "<<< CSV2STOS v0.85 >>>" : print
34 centre "A *.CSV file import/export map utility" : print
35 centre "For users of Tiled, STOS Basic & The Missing Link" : print
40 centre "<<< by M. Keenleyside (Dec 2019) >>>" : print : print

60 print "1) Import CSV:"
70 print "(Convert CSV file to World or Landscape map)"
80 print
90 print "2) Export CSV:"
100 print "(Export World or Landscape map to CSV file)" : print : print

110 print "Please enter 1 or 2:"
120 input "> ";O
130 if O=1 then goto 135 else if O=2 then erase 6 : goto 1000
135 print
136 print "All tiles/blocks must be 16x16 pixels."
137 print
138 print "Make sure you have the height & width of your"
139 print "CSV map file data ready to enter in below."
140 rem (for future version) print "Check what the lowest tile ID no. is within your CSV file." 
141 print
142 centre "ENTER MAP SIZE" : print
150 print : input "WIDTH OF MAP: ";W : if W<20 then W=20
160 locate 23,22 : input "HEIGHT OF MAP: ";H : if H<12 then H=12
165 rem (for future version maybe) locate 47,22 : input "LOWEST TILE ID#: ";ID
166 locate 0,23 : input "1 for World, 2 for Landscape --> MAP TYPE: ";MAPTYPE

170 erase 1 : bcopy 6 to 1 : erase 6 : reserve as data 6,(W*H*2)+8 : fill start(6) to start(6)+length(6),0
180 kopy start(1),start(6),8
190 doke start(6)+4,(W*2)-2 : doke start(6)+6,H*2
200 OB=start(1)+8 : B=start(6)+8
210 for YY=1 to MH : kopy OB,B,MW*2
220 B=B+(W*2) : OB=OB+(OW*2)
230 next YY

240 F$=file select$("*.mbk","Load World or Landscape Blocks")
250 if F$="" then goto 10000
260 if length(5)=0 then load F$,5

270 F$=file select$("*.csv","Load CSV File")
280 if F$="" then goto 10000

290 mode 0 : key off : curs off : flash off : hide : mouse off
300 D=palt(start(5)) : if MAPTYPE=2 then N=MAP TOGGLE(start(6)) : goto 504 else if MAPTYPE=1 then goto 305

304 rem World map conversion process
305 S1=start(1) : S5=start(5) : S6=start(6) 
310 world 0,0,320,200,0,1
320 world back,S5,S6,0,0,0
330 logic=back
340 dim BLOCKW(W) : B=8 : C=8 : open in #1,F$
350 A=1 : repeat
360 if eof(#1) then close #1 : wipe logic : wipe back : logic=logic : screen swap : wait vbl : mouse on : show : goto 445
370 input #1,BLOCKW(A)
380 set block S6,B,C,BLOCKW(A)
390 world back,S5,S6,0,0,0
400 screen swap : wait vbl
410 B=B+16 : inc A
420 until A=W+1
430 B=8 : C=C+16
440 goto 350

445 default : key off
450 F$=file select$("*.MBK","Save a World map")
460 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
470 AA=instr(F$,".") : if AA=0 then F$=F$+".MBK"
475 if upper$(right$(F$,4))=".MBK" then save F$,6 : goto 490
480 bsave F$,start(6) to start(6)+length(6)
490 default : print "NEW MAP SAVED." : wait 5 : goto 10000

504 rem Landscape map conversion process
505 S1=start(1) : S5=start(5) : S6=start(6) 
510 landscape 0,0,320,200,0,1
520 landscape back,S5,S6,0,0,0
530 logic=back
540 dim BLOCKL(W) : B=8 : C=8 : open in #1,F$
550 A=1 : repeat
560 if eof(#1) then close #1 : wipe logic : wipe back : logic=logic : screen swap : wait vbl : mouse on : show : goto 645
570 input #1,BLOCKL(A)
580 set block S6,B,C,BLOCKL(A)
590 landscape back,S5,S6,0,0,0
600 screen swap : wait vbl
610 B=B+16 : inc A
620 until A=W+1
630 B=8 : C=C+16
640 goto 550


645 default : key off
650 F$=file select$("*.MBK","Save a Landscape map")
660 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
670 AA=instr(F$,".") : if AA=0 then F$=F$+".MBK"
675 if upper$(right$(F$,4))=".MBK" then save F$,6 : goto 690
680 bsave F$,start(6) to start(6)+length(6)
690 default : print "NEW MAP SAVED." : wait 5 : goto 10000





1000 print : input "1 for World, 2 for Landscape --> MAP TYPE: ";MAPTYPE
1005 if MAPTYPE=1 then goto 1010 else if MAPTYPE=2 then goto 3010

1010 F$=file select$("*.MBK","Load World Map")
1024 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
1025 if length(5)=0 then load F$,5 : D=depack(start(5))
1026 MAPWID=(deek(start(5)+4)+2)/2 : MAPHGT=deek(start(5)+6)/2

1030 F$=file select$("*.MBK","Load World Blocks")
1031 if F$="" then goto 10000
1032 if length(6)=0 then load F$,6 : D=depack(start(6))

1035 F$=file select$("*.CSV","Save CSV File")
1036 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
1037 AA=instr(F$,".") : if AA=0 then F$=F$+".CSV"

1040 mode 0 : key off : curs off : flash off : hide : mouse off

1050 S1=start(1) : S5=start(5) : S6=start(6)
1060 world 0,0,320,200,0,1
1070 world back,S6,S5,0,0,0 : D=palt(start(6)) : print : print
1073 print : logic=back : centre "Please wait, working..."
1074 screen swap : wait vbl

1081 X=8 : Y=8 : HGT=1 : open out #1,F$

1090 W=1 : repeat
1100 if HGT>MAPHGT then close #1 : goto 2000
1106 print #1,(which block(S5,X,Y)+1);",";
1120 X=X+16 : inc W
1130 until W>MAPWID : print #1," "
1140 X=8 : Y=Y+16 : inc HGT
1150 goto 1090

2000 default 
2010 print "Map width: ";MAPWID
2015 print "Map height: ";MAPHGT
2020 print "CSV FILE SAVED." : goto 10000



3010 F$=file select$("*.MBK","Load Landscape Map")
3024 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
3025 if length(5)=0 then load F$,5 : D=depack(start(5))
3026 MAPWID=(deek(start(5)+4)+2)/2 : MAPHGT=deek(start(5)+6)/2

3030 F$=file select$("*.MBK","Load Landscape Blocks")
3031 if F$="" then goto 10000
3032 if length(6)=0 then load F$,6 : D=depack(start(6))

3035 F$=file select$("*.CSV","Save CSV File")
3036 if F$="" then default : print "NOT DONE. CANCELLED." : goto 10000
3037 AA=instr(F$,".") : if AA=0 then F$=F$+".CSV"

3040 mode 0 : key off : curs off : flash off : hide : mouse off

3050 S1=start(1) : S5=start(5) : S6=start(6)
3060 landscape 0,0,320,200,0,1
3070 landscape back,S6,S5,0,0,0 : D=palt(start(6)) : print : print
3073 print : logic=back : centre "Please wait, working..."
3074 screen swap : wait vbl

3081 X=8 : Y=8 : HGT=1 : open out #1,F$

3090 W=1 : repeat
3100 if HGT>MAPHGT then close #1 : goto 4000
3106 print #1,(which block(S5,X,Y)+1);",";
3120 X=X+16 : inc W
3130 until W>MAPWID : print #1," "
3140 X=8 : Y=Y+16 : inc HGT
3150 goto 3090

4000 default 
4010 print "Map width: ";MAPWID
4015 print "Map height: ";MAPHGT
4020 print "CSV FILE SAVED." : goto 10000

10000 default : mouse on : erase 1 : erase 5 : erase 6 : end
