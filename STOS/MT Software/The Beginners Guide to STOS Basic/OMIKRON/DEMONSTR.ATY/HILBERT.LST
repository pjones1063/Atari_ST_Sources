0 REM Interpreter: 18.81     Compiler: 0.67     ==> Faktor 28
1 '
2 XBIOS (R,4)
3 IF R<>2 THEN
4    PRINT "Leider nur auf monochrom lauff„hig"
5    END
6   ENDIF
7 Hilbert(6)
8 CLIP X0,0,640-X0,400: FILL X0,199,-1
9 END
10
11 DEF PROC Hilbert(Rekur)
12    CLS : PRINT @(0,70); CHR$(27);"f";: USING "######.##"
13    Y0=200-2^(Rekur+1)
14    X0=64
15    M= LPEEK($44E)+Y0*80+X0 SHR 3:B=7: BIT (B,(M))=1
16    T= TIMER
17    Hilbertd(Rekur)
18    PRINT( TIMER -T)/200: USING
19 RETURN
20
21 DEF PROC Hilbertd(Rekur)
22    IF Rekur>0 THEN Hilbertr(Rekur-1)
23    Drawr
24    IF Rekur>0 THEN Hilbertd(Rekur-1)
25    Drawd
26    IF Rekur>0 THEN Hilbertd(Rekur-1)
27    Drawl
28    IF Rekur>0 THEN Hilbertl(Rekur-1)
29 RETURN
30 DEF PROC Hilbertu(Rekur)
31    IF Rekur>0 THEN Hilbertl(Rekur-1)
32    Drawl
33    IF Rekur>0 THEN Hilbertu(Rekur-1)
34    Drawu
35    IF Rekur>0 THEN Hilbertu(Rekur-1)
36    Drawr
37    IF Rekur>0 THEN Hilbertr(Rekur-1)
38 RETURN
39 DEF PROC Hilbertr(Rekur)
40    IF Rekur>0 THEN Hilbertd(Rekur-1)
41    Drawd
42    IF Rekur>0 THEN Hilbertr(Rekur-1)
43    Drawr
44    IF Rekur>0 THEN Hilbertr(Rekur-1)
45    Drawu
46    IF Rekur>0 THEN Hilbertu(Rekur-1)
47 RETURN
48 DEF PROC Hilbertl(Rekur)
49    IF Rekur>0 THEN Hilbertu(Rekur-1)
50    Drawu
51    IF Rekur>0 THEN Hilbertl(Rekur-1)
52    Drawl
53    IF Rekur>0 THEN Hilbertl(Rekur-1)
54    Drawd
55    IF Rekur>0 THEN Hilbertd(Rekur-1)
56 RETURN
57
58 DEF PROC Drawd
59    M=M+80: BIT (B,(M))=1
60    M=M+80: BIT (B,(M))=1
61 RETURN
62 DEF PROC Drawu
63    M=M-80: BIT (B,(M))=1
64    M=M-80: BIT (B,(M))=1
65 RETURN
66 DEF PROC Drawr
67    B=B-1: BIT (B,(M))=1
68    IF B=0 THEN B=7:M=M+1 ELSE B=B-1
69    BIT (B,(M))=1
70 RETURN
71 DEF PROC Drawl
72    IF B=7 THEN B=0:M=M-1 ELSE B=B+1
73    BIT (B,(M))=1
74    B=B+1: BIT (B,(M))=1
75 RETURN
