10    REM ***********************
20    REM *
30    REM *      Molecule       *
40    REM * by  John M. Sinyard *
50    REM *  (c) Atari ST User  *
60    REM *                     *
70    REM * Written in ST Basic *
80    REM *                     *
90    REM ***********************
100   a#=GB
110   CONTROL=PEEK(A#):GLOBAL=PEEK(A#+4)
120   GINTIN=PEEK(A#+8):GINTOUT=PEEK(A#+12)
130   ADDRIN=PEEK(A#+16):ADDROUT=PEEK(A#+20)
140   OPTION BASE 0:DIM GRID(9,9):FULLW 2
150   GAMESTART:
160   RESTORE: SCORE=0:CLEARW 2:COLOR 1,1,1:GOTOXY 6,8:N$=" "
170   INPUT "Please input number of balls to be hidden (1-5) ? ",N$
180   IF VAL(N$)<1 OR VAL(N$)>5 THEN GOTOXY 10,10:SOUND 1,15,4,4,0:                    PRINT "Ivalid input, please try again":WAVE 7,1,0,18000,30:GOTO 160
190   NOBALLS%=VAL(N$):CLEARW 2
200   GOTOXY 1,1:PRINT "TOTAL NUMBER OF"
210   GOTOXY 1,2:PRINT "BALLS HIDDEN =";NOBALLS%
220   DRAWOUTSIDEBIT:LINEF 194,21,434,21:LINEF 194,321,434,321
230   LINEF 193,20,434,20:LINEF 193,322,434,322
240   LINEF 163,50,465,50:LINEF 163,290,465,290
250   LINEF 164,51,164,291:LINEF 464,51,464,291
260   LINEF 163,51,163,291:LINEF 465,51,465,291
270   LINEF 193,21,193,321:LINEF 433,21,433,321
280   DRAWGRID:FOR A%=51 TO 291 STEP 30
290   SOUND 1,15,(325-A%)/30,6,0:WAVE 7,1,4,0,0:LINEF 164,A%,464,A%
300   NEXT A%:FOR A%=194 TO 434 STEP 30
310   SOUND 1,15,A%/40,6,0:WAVE 7,1,0,0,0:LINEF A%,21,A%,321:NEXT A%
320   BOXCLEAR:FOR A=0 TO 9:FOR B=0 TO 9:GRID(A,B)=0:NEXT B,A
330   RANDOMIZE (0)
340   FOR A%=1 TO NOBALLS%
350   RNDPOS:BALLX%=INT(1+8*RND):BALLY%=INT(1+8*RND)
360   IF GRID(BALLX%,BALLY%)=2 THEN GOTO RNDPOS ' ball already here
370   GRID(BALLX%,BALLY%)=2
380   NEXT A%
390   GUESSES%=0
400   GOTOXY 1,9:PRINT "CURRENT SCORE=";SCORE
410   PRESSBUTON:REM:POKE 422048,0
420   GEMSYS(79):MX%=PEEK(GINTOUT+2):MY%=PEEK(GINTOUT+4)
430   SCANX%=(MX%-165)/30:SCANY%=(MY%-59)/30
440   ORIGINALSCANX%=SCANX%:ORIGINALSCANY%=SCANY%
450   IF PEEK(GINTOUT+6)=2 AND GUESSES%=NOBALLS% THEN GOTO CHECK
460   IF PEEK(GINTOUT+6)=1 THEN GOTO 520
470   IF PEEK(GINTOUT+6)=0 THEN GOTO PRESSBUTON ' wait until button pressed
480   IF (NOBALLS%-GUESSES%)>1 THEN A$="[1][ STILL REQUIRE " :                       A$=A$+STR$(NOBALLS%-GUESSES%)+" |  MORE GUESSES ][ OK ]"                       ELSE A$="[1][  STILL REQUIRE | ANOTHER GUESS ][ OK ]"
490   A=VARPTR(a$):A1=INT(A/65536):A2=A-(A1*65536):POKE ADDRIN,A1
500   POKE ADDRIN+2,A2:POKE GINTIN,1
510   GEMSYS (52):A=PEEK(GINTOUT):GOTO PRESSBUTON
520   IF MX%>194 AND MX%<434 AND MY%>329 AND MY%<359 THEN GOTO FACE
530   IF MX%>164 AND MX%<194 AND MY%>89 AND MY%<329 THEN GOTO FACE
540   IF MX%>434 AND MX%<464 AND MY%>89 AND MY%<329 THEN GOTO FACE
550   IF MX%>194 AND MX%<434 AND MY%>59 AND MY%<89 THEN GOTO FACE
560   IF MX%<194 OR MX%>434 OR MY%<89 OR MY%>329 THEN                                   GOSUB OOPS:GOTO PRESSBUTON
570   IF GRID(SCANX%,SCANY%)-INT(GRID(SCANX%,SCANY%))=0 THEN GOTO 620
580   GUESSES%=GUESSES%-1
590   GRID(SCANX%,SCANY%)=INT(GRID(SCANX%,SCANY%)):COLOR 1,1,0
600   CIRCLE 179+30*SCANX%,36+30*SCANY%,7:GEMSYS(79)
610   WHILE PEEK(GINTOUT+6)<>0:GEMSYS(79):WEND:GOTO PRESSBUTON
620   IF GUESSES%<NOBALLS% THEN GOTO 680
630   A$="[1][ YOU HAVE ALREADY| ENTERED THE CORRECT|"
640   A$=A$+" NUMBER OF GUESSES][ OK ]"
650   A=VARPTR(a$):A1=INT(A/65536):A2=A-(A1*65536):POKE ADDRIN,A1
660   POKE ADDRIN+2,A2:POKE GINTIN,1
670   GEMSYS (52):A=PEEK(GINTOUT):GOTO PRESSBUTON
680   GUESSES%=GUESSES%+1
690   GRID(SCANX%,SCANY%)=0.5+GRID(SCANX%,SCANY%) : COLOR 1,1,1
700   CIRCLE 179+30*SCANX%,36+30*SCANY%,7:GEMSYS(79)
710   WHILE PEEK(GINTOUT+6)<>0:GEMSYS(79):WEND:GOTO PRESSBUTON
720   FACE:IF INT(GRID(SCANX%,SCANY%))<>1 THEN GOTO 790
730   A$="[1][YOU HAVE ALREADY|ENTERED A PROBE|"
740   A$=A$+"     HERE ][ OK ]"
750   A=VARPTR(a$):A1=INT(A/65536):A2=A-(A1*65536):POKE ADDRIN,A1
760   POKE ADDRIN+2,A2:POKE GINTIN,1
770   GEMSYS (52):A=PEEK(GINTOUT):GEMSYS(79)
780   WHILE PEEK(GINTOUT+6)<>0:GEMSYS(79):WEND:GOTO PRESSBUTON
790   GRID(SCANX%,SCANY%)=1
800   IF SCANX%=0 THEN INCSCANX%=1:INCSCANY%=0
810   IF SCANX%=9 THEN INCSCANX%=-1:INCSCANY%=0
820   IF SCANY%=0 THEN INCSCANX%=0:INCSCANY%=1
830   IF SCANY%=9 THEN INCSCANX%=0:INCSCANY%=-1
840   IF INT(GRID(SCANX%+INCSCANX%,SCANY%+INCSCANY%))=2 THEN                         COLOR 1,1,1,1,1:GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON: ' black
850   IF SCANX%=0 THEN IF INT(GRID(1,SCANY%+1))=2 OR INT(GRID(1,SCANY%-1))=2         THEN COLOR 1,0,1:GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON:' white
860   IF SCANX%=9 THEN IF INT(GRID(8,SCANY%+1))=2 OR INT(GRID(8,SCANY%-1))=2         THEN COLOR 1,0,1:GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON:' white
870   IF SCANY%=0 THEN IF INT(GRID(SCANX%+1,1))=2 OR INT(GRID(SCANX%-1,1))=2         THEN COLOR 1,0,1:GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON:' white
880   IF SCANY%=9 THEN IF INT(GRID(SCANX%+1,8))=2 OR INT(GRID(SCANX%-1,8))=2         THEN COLOR 1,0,1:GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON:' white
890   IF SCANY%=0 OR SCANY%=9 THEN GOTO YMOVEMENT
900   XMOVEMENT:IF INT(GRID(SCANX%+INCSCANX%,SCANY%))=2 THEN                         COLOR 1,1,1,1,1:SCANX%=ORIGINALSCANX%:SCANY%=ORIGINALSCANY%:
910   IF INT(GRID(SCANX%+INCSCANX%,SCANY%+1))=2 AND                                     INT(GRID(SCANX%+INCSCANX%,SCANY%-1))=2 THEN GOTO WHITEBOX : ' white
920   IF INT(GRID(SCANX%+INCSCANX%,SCANY%+1))=2 THEN                                    INCSCANX%=0 : INCSCANY%=-1 : GOTO YMOVEMENT
930   IF INT(GRID(SCANX%+INCSCANX%,SCANY%-1))=2 THEN                                    INCSCANX%=0:INCSCANY%=1:GOTO YMOVEMENT
940   SCANX%=SCANX%+INCSCANX%
950   IF SCANX%>0 AND SCANX%<9 THEN GOTO XMOVEMENT
960   GRID(SCANX%,SCANY%)=1
970   READ PATT1,PATT2:COLOR 1,1,1,PATT2,PATT1
980   GOSUB DRAWLITTLEBOX
990   SCANX%=ORIGINALSCANX%:SCANY%=ORIGINALSCANY%:GOSUB DRAWLITTLEBOX
1000  GOTO PRESSBUTON
1010  YMOVEMENT:IF INT(GRID(SCANX%,SCANY%+INCSCANY%))=2 THEN                         COLOR 1,1,1,1,1:SCANX%=ORIGINALSCANX%:SCANY%=ORIGINALSCANY%:                   GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON:' have hit BLACK
1020  IF INT(GRID(SCANX%+1,SCANY%+INCSCANY%))=2 AND                                     INT(GRID(SCANX%-1,SCANY%+INCSCANY%))=2 THEN GOTO WHITEBOX:' white
1030  IF INT(GRID(SCANX%+1,SCANY%+INCSCANY%))=2 THEN                                    INCSCANX%=-1:INCSCANY%=0:GOTO XMOVEMENT
1040  IF INT(GRID(SCANX%-1,SCANY%+INCSCANY%))=2 THEN                                    INCSCANX%=1:INCSCANY%=0:GOTO XMOVEMENT
1050  SCANY%=SCANY%+INCSCANY%
1060  IF SCANY%>0 AND SCANY%<9 THEN GOTO YMOVEMENT
1070  GRID(SCANX%,SCANY%)=1:' have reached face
1080  READ PATT1,PATT2:COLOR 1,1,1,PATT2,PATT1:' read new fill pattern
1090  GOSUB DRAWLITTLEBOX
1100  SCANX%=ORIGINALSCANX%:SCANY%=ORIGINALSCANY%:GOSUB DRAWLITTLEBOX
1110  GOTO PRESSBUTON
1120  DRAWLITTLEBOX:BOXX%=168+SCANX%*30:BOXY%=25+SCANY%*30
1130  LINEF BOXX%,BOXY%,BOXX%+22,BOXY%
1140  LINEF BOXX%+22,BOXY%,BOXX%+22,BOXY%+22
1150  LINEF BOXX%+22,BOXY%+22,BOXX%,BOXY%+22
1160  LINEF BOXX%,BOXY%+22,BOXX%,BOXY%
1170  FILL BOXX%+4,BOXY%+4
1180  SCORE=SCORE+1:GOTOXY 1,9:PRINT "CURRENT SCORE=";SCORE:RETURN
1190  WHITEBOX:COLOR 1,0,1:                                                                     SCANX%=ORIGINALSCANX%:SCANY%=ORIGINALSCANY%:                                   GOSUB DRAWLITTLEBOX:GOTO PRESSBUTON
1200  OOPS:A$="[1][YOU SELECT SENCING POINTS BY|POINTING THE MOUSE AT ONE|"
1210  A$=A$+"OF THE BOXES AT THE EDGE OF|THE GRID AND THEN PRESSING|"
1220  A$=A$+"THE LEFT MOUSE BUTTON][ OK ]"
1230  A=VARPTR(a$):A1=INT(A/65536):A2=A-(A1*65536):POKE ADDRIN,A1
1240  POKE ADDRIN+2,A2:POKE GINTIN,1
1250  GEMSYS (52):A=PEEK(GINTOUT):RETURN
1260  CHECK:GOTOXY 1,9:PRINT "                 "
1270  FOR SCANX%=1 TO 8:FOR SCANY%=1 TO 8
1280  IF GRID(SCANX%,SCANY%)<>2.5 THEN goto 1390
1290  GOTOXY 30,7:PRINT "YOU DETECTED"
1300  GOTOXY 30,8:PRINT "THIS BALL"
1310  FOR I%=1 TO 6
1320  COLOR 1,0,0 : PCIRCLE 179+30*SCANX%,36+30*SCANY%,5
1330  SOUND 1,15,6,6,0 : WAVE 7,1,0,1000,3
1340  COLOR 1,1,1,1,1 : PCIRCLE 179+30*SCANX%,36+30*SCANY%,5
1350  SOUND 1,15,6,6,0 : WAVE 7,1,0,1000,3
1360  NEXT I%
1370  GOTOXY 30,7:PRINT "            "
1380  GOTOXY 30,8:PRINT "         ":GOTO 1530
1390  IF GRID(SCANX%,SCANY%)=0.5 THEN SCORE=SCORE+5
1400  IF GRID(SCANX%,SCANY%)<>2 THEN GOTO 1530
1410  GOTOXY 30,7:PRINT "YOU FAILED"
1420  GOTOXY 30,8:PRINT "TO DETECT"
1430  GOTOXY 30,9:PRINT "THIS BALL"
1440  FOR I%=1 TO 6
1450  COLOR 1,0,0:PCIRCLE 179+30*SCANX%,36+30*SCANY%,8
1460  SOUND 1,15,4,4,0:WAVE 7,1,0,1000,3
1470  COLOR 1,1,1,1,1:PCIRCLE 179+30*SCANX%,36+30*SCANY%,8
1480  SOUND 1,15,4,4,0:WAVE 7,1,0,1000,3
1490  NEXT I%
1500  GOTOXY 30,7:PRINT "          "
1510  GOTOXY 30,8:PRINT "         "
1520  GOTOXY 30,9:PRINT "         "
1530  NEXT SCANY%,SCANX%
1540  FOR T=1 TO 4000:NEXT T
1550  A$="[3][ YOUR SCORE IS "+STR$(SCORE)
1560  A$=A$+"| PLAY AGAIN ? ][ YES | NO ]"
1570  A=VARPTR(a$):A1=INT(A/65536):A2=A-(A1*65536):POKE ADDRIN,A1
1580  POKE ADDRIN+2,A2:POKE GINTIN,1
1590  GEMSYS (52):IF PEEK(GINTOUT)=1 THEN GOTO GAMESTART
1600  DATA 2,4,2,22,3,3,3,21,2,9,2,14,2,19,3,12
1610  DATA 2,7,3,2,2,10,2,16,3,9,2,11,2,5,2,24,3,8
