DEFINITION RealSupport;

(*****************************************************************************)
(* 15-Okt-95, Holger Kleinschmidt                                            *)
(*****************************************************************************)


PROCEDURE min ((* in / -- *) x : REAL;
               (* in / -- *) y : REAL ): REAL;

(* Returns the minimum value of <x> and <y>. *)


PROCEDURE max ((* in / -- *) x : REAL;
               (* in / -- *) y : REAL ): REAL;

(* Returns the maximum value of <x> and <y>. *)


PROCEDURE clip ((* in / -- *) x     : REAL;
                (* in / -- *) lower : REAL;
                (* in / -- *) upper : REAL ): REAL;

(* If <x> is smaller than <lower>, <lower> will be returned; if <x> is
   greater than <upper>, then <upper> will be returned, else <x>.
   If <lower> is greater than <upper>, <lower> will be ignored and the
   minimum of <x> and <upper> will be returned.
 *)


PROCEDURE frexp ((* in / -- *)     real : REAL;
                 (* -- /out *) VAR exp  : INTEGER ): REAL;

(* Returns in <exp> the exponent value of <real> and returns the
   significant part 'mant' of <real> as the result of the function call.
   The following relationship holds:

           real = mant * 2^exp; 0.5 <= |mant| < 1.0.

   If <real> is zero, zero will be returned and <exp> will become zero.
 *)


PROCEDURE ldexp ((* in / -- *) mant : REAL;
                 (* in / -- *) exp  : INTEGER ): REAL;

(* Returns the value of mant * 2^exp.

   The following relationship holds:

          x := frexp(real, exp);
          real = ldexp(x, exp).
 *)


PROCEDURE modf ((* in / -- *)     real : REAL;
                (* -- /out *) VAR int  : REAL ): REAL;

(* Returns in <int> the integral part of <real> and returns the
   fractional part 'fract' of <real> as the result of the function call.
   Both 'fract' and <int> have the sign of <real>.
   The following relationship holds:

                   real = int + fract.
 *)


PROCEDURE fmod ((* in / -- *) real : REAL;
                (* in / -- *) div  : REAL ): REAL;

(* Returns the value of <real> mod <div> with the same sign as <real>.

   If <div> is zero, zero will be returned.
 *)


PROCEDURE floor ((* in / -- *) real : REAL ): REAL;

(* Returns the greatest integral value not greater than <real>.
   This corresponds to rounding towards negative infinity.

   Examples:

     floor(-1.5) = -2.0   ,   floor(1.5) = 1.0
     floor(-1.0) = -1.0   ,   floor(1.0) = 1.0
 *)


PROCEDURE ceil ((* in / -- *) real : REAL ): REAL;

(* Returns the smallest integral value not less than <real>.
   This corresponds to rounding towards positive infinity.

   Examples:

     ceil(-1.5) = -1.0    ,   ceil(1.5) = 2.0
     ceil(-1.0) = -1.0    ,   ceil(1.0) = 1.0
 *)

END RealSupport.
