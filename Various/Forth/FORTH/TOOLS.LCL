( Local tools)
( some little words for use with local variables. Some are already in
 the kernel. It's easy to get carried away)

( ^	is used to leave the address of a local on the stack at run-time. This
	is so you can w@, etc. to/from them. i.e.   ^ addr c@
  add->	is used to add a value to a local. i.e.  -5 add-> sum
  inc->	is used to increment a local by one i.e., inc-> counter
  dec->	is used to decrement a local by one i.e., dec-> numToGo
  ->	is used to save a value into a local, i.e., 43 -> temp  )

hex
: (localTool)	( returns the offset of the local variable that follows)
	[compile] ' 
	4 + W@  		( valid for local variables only)
	STATE @ 0=
	ABORT" Compile only!"
	;


: ^ 
	(localTool)	( get the proper offset)
	41EA		( LEA d(A2),A0)
	w, w,
	2D08		( MOVE.L A0,-[A6])
	W,
	; IMMEDIATE

: add-> 		( sim to +!  used   5 add-> local)
	(localTool)	( get the proper offset)
	201ED1AA	( mov.l a6+,d0   add.l D0,d(A2) )
	, w,
	; IMMEDIATE

: inc-> 		( add 1 to the local:   inc-> local)
	(localTool)	( get the proper offset)
	52AA		( ADDQ.L  #1,d(A2) )
	W, W,
	; IMMEDIATE

: dec-> 		( sub 1 from the local:   dec-> local)
	(localTool)	( get the proper offset)
	53AA		( SUBQ.L  #1,d(A2) )
	W, W,
	; IMMEDIATE

: -> 			( save to the local:    value -> local)
	(localTool)	( get the proper offset)
	255E		( MOVE.L (A6)+,d(A2) )
	W, W,
	; IMMEDIATE

decimal
