100   REM A SIMPLE EXAMPLE OF USING SAMPLES FROM ATARI BASIC
110   REM    BY A.RACINE             (C) 2-BIT SYSTEMS LTD
120   REM           VERSION FOR 'METACOMCO' BASIC
130   REM
140   DIM CODE%(2000)    : REM SPACE FOR CODE
150   DIM SAMPLE%(13000) : REM SPACE FOR SAMPLE
160   PRINT "Load Machine Code ...."
170   BLOAD "BASCODE.EXE",VARPTR(CODE%(0))  : REM LOAD CODE
180   PLAY = VARPTR(CODE%(0))+28 : REM PLAY SAMPLE
190   BACK     = PLAY + 4   : REM PLAY SAMPLE BACKWARDS
200   WAIT     = PLAY + 8   : REM WAIT FOR END OF SAMPLE
210   HALT     = PLAY + 12  : REM STOP
220   HEAR     = PLAY + 16  : REM LISTEN TO INPUT
230   RECORD   = PLAY + 20  : REM RECORD SAMPLE
240   REM --------------------------------------------------
250   SPEED    = PLAY + 24  : REM PLAY BACK SPEED
260   LOOP     = PLAY + 26  : REM 0=LOOP OFF ELSE LOOP ON
270   SAMSTART = PLAY + 28  : REM START ADDRESS OF SAMPLE
280   LENGTH   = PLAY + 32  : REM LENGTH OF SAMPLE (BYTES)
290   PRINT "Load sample ...."
300   BLOAD "SAMPLE.SAM",VARPTR(SAMPLE%(0)) : REM LOAD SAMPLE
310   START = VARPTR(SAMPLE%(0)) + 12
320   POKE_W LOOP,0            : REM LOOPING OFF
330   POKE_L SAMSTART,START    : REM SAMPLE START
340   POKE_L LENGTH,25000      : REM LENGTH OF SAMPLE
350   PRINT "Play sample at different speeds ...."
360   FOR X = 0 TO 4
370   POKE_W SPEED,X
380   CALL PLAY
390   CALL WAIT
400   NEXT X
410   PRINT "Play sample backwards 5 times."
420   POKE_W LOOP,1
430   CALL BACK
440   for X= 1 to 5
450   PRINT X;" ";
460   CALL WAIT
470   NEXT X
480   CALL HALT
490   PRINT
500   PRINT "FINISHED."
