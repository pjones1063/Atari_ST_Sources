' ********************DATOBERT V.1.1 program j.fiedler 3/87 (c)*****************
Alert 3,"  DATOBERT V.1.1 j.fiedler  |BITTE 1x 20 DEMARK auf Konto|102033 005 VOBA Heidenheim|   BLZ 632 901 10  GELL ?!",1,"o.k.|habschon",Var
If Var=1
  Stop
  Alert 1,"TU ES BALD  !!!!!!!!!!!!",1,"CLARO",Var
Else
  Alert 1," DANKE !!!!!!!!!!!!",1,"bitte",Var
Endif
Dim A$(5)
Dim N$(5)
Dim W(5,50)
Dim Nsa$(50)
Dim Nsb$(50)
Dim X(50)
Dim Y(50)
' ############GEMleiste
Dim Leiste$(50)
For I=0 To 50
  Read Leiste$(I)
  Exit If Leiste$(I)="***"
Next I
Leiste$(I)=""
Leiste$(I+1)=""
Data desk,graphdat(c)
Data -----------------------------
Data 1,2,3,4,5,6,""
Data  DATEN , eingabe, Ñndern,speichern, laden,""
Data  GRAFIK , kuchen , linien , balken , sÑulen , blîcke ,""
Data  DRUCKER ,datenausgabe,hardcopy,hcopy A4,""
Data  HILFE ,helpdat,helpgraf,helpprint,""
Data  ENDE ,quit,""
Data ***
Menu Leiste$()
Openw 0
Mnx=2
Gosub Mnakt
On Menu  Gosub Menue
Do
  On Menu
Loop
' ################# HAUPTMENUE
Procedure Menue
  Menu Off
  On Menu(0)-0 Gosub Descrit
  On Menu(0)-10 Gosub Eingabe,Eingabe,Speichern,Laden
  On Menu(0)-16 Gosub Kuchen,2d,2d,3d,3d
  On Menu(0)-23 Gosub Datendruck,Hardcopy,Hardcpa4
  On Menu(0)-28 Gosub Helpdat,Helpgraf,Helpprint
  On Menu(0)-33 Gosub Exit
  Menu Off
Return
' ########################PROCEDUREN
Procedure Eingabe
  Aen=0
  If Leiste$(Menu(0))=" Ñndern"
    Aen=1
  Endif
  Cls
  Deftext 1,0,0,13
  Deffill 1,2,2
  Pbox 20,20,618,300
  Print "   *graphdat*     EINGABE :"
  If Aen=1
    Print At(1,1);"   *graphdat*     énderung :"
    Print At(5,20);"Falls ein Wert geÑndert werden soll, geben Sie bitte jeweils den"
    Print At(5,21);"neuen Wert ein. Sonst weiter mit 'RETURN'"
    Print At(5,22);"               ** press any key **"
    X=Inp(2)
  Endif
  Gosub Loeschen
  Print At(5,21);"Bitte geben Sie die HauptÅberschrift ein (<=35 Zeichen) !"
  Form Input 35,A$
  If A$<>""
    Ueb1$=A$
  Endif
  Deftext 1,0,0,32
  Text 30,65,Ueb1$
  Gosub Loeschen
  Print At(5,21);"Bitte geben Sie den Untertitel ein (<=70 Zeichen) !"
  Form Input 70,A$
  If A$<>""
    Ueb2$=A$
  Endif
  Deftext 1,0,0,6
  Text 30,83,Ueb2$
  Gosub Loeschen
  Ra=0
  If Aen=1
    Print At(5,21);"Soll eine ganze Reihe entfernt werden (j/n) ?";
    Input An$
    If An$="j"
      Ra=1
      Gosub Loeschen
      Print At(5,21);"welche Reihe soll gelîscht werden (Nr.: )?";
      Input Nr
      If Nr<Rei
        For I=Nr To Rei-1
          N$(I)=N$(I+1)
          For J=1 To Spa
            Swap W(I,J),W(I+1,J)
          Next J
        Next I
      Endif
      Rei=Rei-1
    Endif
  Endif
  Gosub Loeschen
  If Ra=0
    Print At(5,21);"Wieviele Reihen (hintereinander  (<=4))";
    Input A$
    If A$<>""
      Rei=Val(A$)
      If Rei>4
        Rei=4
      Endif
    Endif
  Endif
  Print At(5,21);"Wieviele Spalten (nebeneinander) (<=14 )  ";
  Input A$
  If A$<>""
    Spa=Val(A$)
  Endif
  Text 30,130,Rei
  Text 50,130,"Reihe(n)"
  Text 30,150,Spa
  Text 50,150,"Spalten"
  Gosub Loeschen
  For I=1 To Rei
    Deftext 1,0,0,13
    Print At(5,21);"Bezeichnung der Reihe ";I,
    Form Input 14,A$
    If A$<>""
      N$(I)=A$
    Endif
    Deftext 1,0,0,6
    Text 35,155+20*I,N$(I)
    Gosub Loeschen
  Next I
  W$=""
  If Aen=1
    Print At(5,21);"Sollen Zahlenwerte geÑndert werden (Daten) (n/j)?";
    Input W$
    Gosub Loeschen
  Endif
  If W$<>"n"
    For I=1 To Rei
      Deftext 1,0,0,13
      Print At(5,21);I;". Reihe :"
      Deftext 1,0,0,6
      Text 35,145+20*I,I
      Text 45,145+20*I,". Reihe:"
      For J=1 To Spa
        Deftext 1,0,0,13
        Print At(5,22);"                            "
        Print At(5,22);"Wert#";J;" (<10000)";
        Input A$
        If A$<>""
          W(I,J)=Val(A$)
        Endif
        Deftext 1,0,0,6
        Text 100+36*J,155+20*I,W(I,J)
      Next J
      Gosub Loeschen
    Next I
  Endif
  Deftext 1,0,0,13
  Print At(5,21);"Eingabe des maximalen y-Anzeigewerts (textmaximum):"
  Print At(5,22);"(falls mit dem hîchsten Zahlenwert identisch --> RETURN)";
  Input A$
  A=Val(A$)
  If A<>0
    Maks=A
  Endif
  Gosub Loeschen
  W$=""
  If Aen=1
    Print At(5,21);"Sollen Spaltenbezeichnungen geÑndert werden (Daten) (n/j)?";
    Input W$
    Gosub Loeschen
  Endif
  If W$<>"n"
    For I=1 To Spa
      Print At(5,21);"1.Bezeichnung der Spalte ";I
      Form Input Int(50/Spa+1),A$
      If A$<>""
        Nsa$(I)=A$
      Endif
      Deftext 1,0,0,6
      Text 30+(I-1)*500/Spa,260,Nsa$(I)
      Gosub Loeschen
      Print At(5,21);"2.Bezeichnung der Spalte ";I
      Form Input Int(70/Spa+1),A$
      If A$<>""
        Nsb$(I)=A$
      Endif
      Deftext 1,0,0,4
      Text 30+(I-1)*500/Spa,280,Nsb$(I)
      Gosub Loeschen
    Next I
  Endif
  Gosub Loeschen
  Print At(5,21);"ENDE DER EINGABE"
  Mnx=3
  Gosub Mnakt
Return
Cls
Do
  On Menu
Loop
Procedure Loeschen
  Deftext 1,0,0,13
  Print At(5,20);Spc(230)
Return
' ############### ###############
Procedure Laden
  Fileselect "DATOBERT\*.DTO*",Bn$,An$
  If An$=""
    Do
      On Menu
    Loop
  Endif
  Defmouse 2
  Open "I",#1,An$
  Input #1,Ueb1$,Ueb2$,Rei,Spa
  For I=1 To Rei
    Input #1,N$(I)
    For J=1 To Spa
      Input #1,W(I,J)
    Next J
  Next I
  For I=1 To Spa
    Input #1,Nsa$(I),Nsb$(I)
  Next I
  Input #1,Maks
  Close #1
  Defmouse 3
  Mnx=3
  Gosub Mnakt
  Do
    On Menu
  Loop
  Stop
Return
' ######################         #########
Procedure Speichern
  Fileselect "DATOBERT\*.DTO",Bn$,An$
  If An$=""
    Do
      On Menu
    Loop
  Endif
  Defmouse 2
  Open "O",#1,An$+".GRF"
  Write #1,Ueb1$,Ueb2$,Rei,Spa
  For I=1 To Rei
    Write #1,N$(I)
    For J=1 To Spa
      Write #1,W(I,J)
    Next J
  Next I
  For I=1 To Spa
    Write #1,Nsa$(I),Nsb$(I)
  Next I
  Write #1,Maks
  Close #1
  Defmouse 3
  Do
    On Menu
  Loop
Return
' ******         *********         **********
Procedure 3d
  Cls
  Deftext 1,0,0,32
  Text 20,32,Ueb1$
  Deftext 1,0,0,6
  Text 20,44,Ueb2$
  X1=30
  Y1=355
  Vx=25
  Vy=50
  Laenge=470
  Hoehe=250
  Ho=Hoehe
  La=Laenge
  X2=X1+Vx
  Y2=Y1-Vy
  Gosub Um
  X3=X2+La
  Y3=Y2-Ho
  X4=X1+La
  Y4=Y1-Ho
  B=0
  C=B
  D=(X2-X1)/Rei
  E=(Y2-Y1)/Rei
  For A=0 To Rei
    Draw X1+B,Y1+C To X4+B,Y1+C
    B=B+D
    C=C+E
  Next A
  B=0
  D=(X4-X1)/Spa
  For A=0 To Spa
    Draw X1+B,Y1 To X2+B,Y2
    Draw X2+B,Y2 To X2+B,Y3
    B=B+D
  Next A
  B=0
  C=B
  D=(Y4-Y1)/Az
  For A=0 To Az
    Draw X1,Y1+B To X2,Y2+B
    Draw X2,Y2+B To X3,Y2+B
    B=B+D
    St1=X3+2
    St2=Y2+A*D
    O=Int(A*J/Az)
    Te$=Str$(O)
    Z=4
    Deftext 1,0,0,Z
    Text St1,St2,Te$
  Next A
  Draw X1,Y1 To X1,Y4
  D=(X4-X1)/Spa
  E=(Y1-Y2)/Rei
  G=(X2-X1)/Rei
  O=D/4
  C=0
  H=C
  If Leiste$(Menu(0))=" sÑulen "
    Gosub Saeulen
  Else
    Gosub Bloecke
  Endif
  Do
    On Menu
  Loop
Return
' ########        ##########
Procedure Saeulen
  For Fm=Rei Downto 1
    B=0
    Ft=1
    Deffill 1,2,Fm*2-2
    Te$=N$(Fm)
    For A=1 To Spa
      A1=X2+B+1+O*0.5-H-3*G/4
      A2=A1+D-O
      B2=Y2+C+3*E/4
      B1=B2-W(Fm,A)*U
      Box A1,B1,A2,B2
      Pbox A1+1,B1+1,A2-1,B2-1
      Gosub Seiten
      Gosub Oben
      B=B+D
    Next A
    Z=6
    St1=A1+D+4
    St2=B2+4
    Deftext 1,0,0,Z
    Text St1,St2,N$(Fm)
    H=H+G
    C=C+E
  Next Fm
  For A=1 To Spa
    St1=X1+D*A-(D/4)*3.5
    St2=Y1+8+K
    Te$=Str$(A)
    Deftext 1,0,0,6
    Text St1,St2,Nsa$(A)
    Deftext 1,0,0,4
    Text St1,St2+7,Nsb$(A)
  Next A
Return
Procedure Seiten
  X(1)=A2
  X(2)=A2+2*G/4
  X(3)=A2+2*G/4
  X(0)=A2
  Y(1)=B2
  Y(2)=B2-2*E/4
  Y(3)=B1-2*E/4
  Y(0)=B1
  Polyfill 4,X(),Y()
Return
Procedure Oben
  X(1)=A1
  X(2)=A2
  X(3)=A2+2*G/4
  X(0)=A1+2*G/4
  Y(1)=B1
  Y(2)=B1
  Y(3)=B1-2*E/4
  Y(0)=B1-2*E/4
  Polyfill 4,X(),Y()
Return
' *******
Procedure Bloecke
  For Fm=Rei Downto 1
    Deffill 1,2,Fm*2-2
    Gosub Poly
    Te$=N$(Fm)
    St1=X2+Laenge+5-Fm1*G
    St2=Y2+E+Fm1*E
    Deftext 1,0,0,6
    Text St1,St2,N$(Fm)
  Next Fm
  For I=0 To Spa-1
    St1=X1+I*D-20
    St2=Y1+10
    Deftext 1,0,0,6
    Text St1,St2,Nsa$(I+1)
    Deftext 1,0,0,4
    Text St1,St2+7,Nsb$(I+1)
  Next I
Return
' #########
Procedure Poly
  Spa1=Spa-1
  Fm1=Rei-Fm
  For S=0 To Spa1
    P=Spa1-S
    X(S)=X2+S*D-Fm1*G-G
    X(Spa+P)=X(S)+G
    Y(S)=Y2-U*W(Fm,S+1)+4.2*E/4+Fm1*E
    Y(Spa+P)=Y(S)-E
  Next S
  Polyfill Spa*2,X(),Y()
  For S=0 To Spa1
    Draw X(S),Y(S) To X(S)+G,Y(S)-E
  Next S
  X(Spa)=X(Spa1)
  Y(Spa)=Y2+E+Fm1*E
  X(Spa+1)=X2-Fm1*G-G
  Y(Spa+1)=Y(Spa)
  Polyfill Spa+2,X(),Y()
  X(1)=X(Spa1)
  Y(1)=Y(Spa1)
  X(2)=X(1)+G
  Y(2)=Y(1)-E
  X(3)=X(2)
  Y(3)=Y2+Fm1*E
  X(0)=X(1)
  Y(0)=Y2+E+Fm1*E
  Polyfill 4,X(),Y()
Return
' ******************************
Procedure 2d
  Cls
  Gosub Um
  Aby=260/Az
  Abx=450/(Spa-1)
  Wx=450/(Spa-1)
  Spb=Spa-1
  Ba=0
  If Leiste$(Menu(0))=" balken "
    Ba=1
    Spb=Spa
    Abx=450/Spa
    Wx=Abx
  Endif
  Awy=J/Az
  Wy=260/J
  X=40
  Y=330
  Draw X,Y To X+450,Y
  For I=1 To Az
    Y=330-I*Aby
    Draw X,Y To 490,Y
  Next I
  Y=330
  For I=0 To Spb
    X=40+I*Abx
    Draw X,Y To X,Y-260
  Next I
  For R=1 To Rei
    If R<3
      Lin=3
    Else
      Lin=5
    Endif
    X=40
    Y=330
    If Ba=1
      Gosub Balken
    Endif
    Plot X,Y-(W(R,1)*Wy)
    Defline 1,Lin,2,2
    For I=2 To Spa
      X=X+Wx
      Y=330-(W(R,I)*Wy)
      Gosub Draw
    Next I
    If R/2<>Int(R/2)
      Gosub Drawin
    Endif
    Deftext 1,0,0,6
    Text X+25,Y,N$(R)
  Next R
  Defline 1,1,0,0
  Gosub Text
  Do
    On Menu
  Loop
Return
' **************
Procedure Draw
  Draw  To X,Y
Return
Procedure Drawin
  X=40
  Y=330
  Plot X,Y-(W(R,1)*Wy)
  Defline 1,1,2,2
  Graphmode 3
  For I=2 To Spa
    X=X+Wx
    Y=330-(W(R,I)*Wy)
    Draw  To X,Y
  Next I
  Graphmode 1
Return
' ***********************
Procedure Balken
  Wx=450/Spa
  Wwx=Wx/Rei
  For I=1 To Rei
    Deffill 1,2,I*2-1
    For P=1 To Spa
      Xx=Wx*P-35+(I-1)*Wwx
      Xx=40+(P-1)*Wx+(I-1)*Wwx
      Xx2=Xx+Wwx
      Yy2=330-(W(I,P)*Wy)
      Pbox Xx,330,Xx2,Yy2
    Next P
  Next I
  Spp=(12-Spa)*10
  For A=1 To Rei
    Deffill 1,2,A*2-1
    Pcircle 535,Spp+A*(340/14)+47,12
  Next A
  For A=1 To Rei
    Deftext 1,0,0,6
    Text 551,Spp+49+A*24.2,N$(A)
  Next A
  Gosub Text
  Do
    On Menu
  Loop
Return
' #######
Procedure Text
  Deftext 1,0,0,32
  Text 20,30,Ueb1$
  Deftext 1,0,0,6
  Text 20,45,Ueb2$
  For I=1 To Az
    Deftext 1,0,0,4
    Text 500,330-(I*Aby),Int(I*Awy)
  Next I
  Xxx=500-(Ba=1)
  J=0
  For I=25 To Xxx Step Wx
    Ii=I
    If Ba=1
      Ii=Ii+20
    Endif
    J=J+1
    Deftext 1,0,0,6
    Text Ii,340,Nsa$(J)
    Deftext 1,0,0,4
    Text Ii,348,Nsb$(J)
  Next I
Return
' **********
Procedure Um
  J=0
  For X=1 To Rei
    For B=1 To Spa
      If W(X,B)>J
        J=W(X,B)
      Endif
    Next B
  Next X
  If Maks>J
    J=Maks
  Endif
  If J=0
    J=1
  Endif
  U=Ho/J
  If J<31
    Az=J
  Else
    Az=Int(Sqr(J))
  Endif
Return
' *****************************
Procedure Kuchen
  Cls
  Alert 2,"Beschriftung der Kuchenteile :",1,"keine|Zahlwert|% -wert",Var
  For P=1 To Rei
    Su=0
    For I=1 To Spa
      Su=Su+W(P,I)
    Next I
    W1=3600/Su
    We=0
    If Rei=1
      E1=240
      E2=190
      E3=135
      E4=105
    Endif
    If Rei=2
      E3=105
      E4=90
      If P=1
        E1=130
        E2=200
      Else
        E1=380
        E2=200
      Endif
    Endif
    If Rei=3
      E3=87
      E4=78
      E1=105
      E2=145
      If P=2
        E1=400
        E2=145
      Endif
      If P=3
        E1=250
        E2=270
      Endif
    Endif
    If Rei=4
      E3=75
      E4=67
      E1=90
      E2=125
      If P=2
        E1=320
        E2=125
      Endif
      If P=3
        E1=200
        E2=285
      Endif
      If P=4
        E1=430
        E2=285
      Endif
    Endif
    Ellipse E1,E2,E3,E4
    For I=1 To Spa
      Deftext 1,0,0,4
      Wa=We
      We=We+W(P,I)*W1
      Deffill I>1,2,I
      Pellipse E1,E2,E3,E4,Wa,We
      Ellipse E1,E2,E3,E4,Wa,We
      If Var>1
        Wx=((Wa+We)/2)*Pi/1800
        W2=0
        If Wx>Pi/2
          If Wx<5.3
            W2=8
          Endif
        Endif
        Wk=W(P,I)
        If Var=3
          Wk=Int(100*Wk/Su)
        Endif
        Text E1-W2+E3*1.1*Sin(Wx+0.5*Pi),E2+E4*1.1*Cos(Wx+0.5*Pi),Wk
      Endif
    Next I
    Deftext 1,0,0,6
    Su$="Summe="+Str$(Su)
    Text E1-E3/3,E2+5*E4/4+9,Su$
    Deftext 1,1,0,6
    Text E1-E3/3,E2+5*E4/4,N$(P)
  Next P
  Spp=(12-Spa)*10
  Circle 530,Spp+70,12
  For A=2 To Spa
    Deffill A>1,2,A
    Pcircle 530,Spp+A*(340/14)+47,12
  Next A
  For A=1 To Spa
    Deftext 1,0,0,6
    Text 550,Spp+49+A*24.2,Nsa$(A)
    Deftext 1,0,0,4
    Text 550,Spp+57+A*24.2,Nsb$(A)
  Next A
  Deftext 1,12,0,32
  Text 10,32,Ueb1$
  Deftext 1,0,0,6
  Text 20,47,Ueb2$
  Deftext 1,0,0,13
  Do
    On Menu
  Loop
Return
' @@@@@@@@@@@@@@@@@@
Procedure Mnakt
  Menu 12,Mnx
  Menu 13,Mnx
  Menu 18,Mnx
  Menu 17,Mnx
  Menu 19,Mnx
  Menu 20,Mnx
  Menu 21,Mnx
  Menu 24,Mnx
  Menu 25,Mnx
  Menu 26,Mnx
Return
' ##################
Procedure Helpdat
  A$(0)="EINGABE DER DATEN :"
  A$(1)="nach dem Programmstart zuerst eine Datei LADEN oder EINGEBEN !"
  A$(2)="am besten ist, sie laden zunÑchst eine Beispieldatei (>'LADEN')."
  A$(3)="danach kînnen Sie jederzeit Text und Werte Ñndern (> 'éNDERN')"
  A$(4)="auch ganze Reihen kînnen öber 'éNDERN' entfernt werden."
  A$(5)="probieren sie's einfach mal aus !"
  Gosub Helpp
Return
Procedure Helpgraf
  A$(0)="DARSTELLUNG DER DATEN :"
  A$(1)="nach dem LADEN oder EINGEBEN der Daten kînnen die Reihen"
  A$(2)="in beliebiger Form Åber das MenÅ GRAFIK sofort dargestellt werden."
  A$(3)="entscheiden Sie selbst, in welcher Form ihre Daten sinnvoll"
  A$(4)="dargestellt oder ausgedruckt werden sollen.  "
  A$(5)="probieren sie's !!"
  Gosub Helpp
Return
Procedure Helpprint
  A$(0)="AUSDRUCK DER DATEN :"
  A$(1)="sie haben die Mîglichkeit, ihre Datenreihen auf dem Drucker"
  A$(2)="in Schriftreihen auszugeben (DATENAUSGABE), die Grafiken "
  A$(3)="als HARDCOPY normal oder im DIN A4-Format auszudrucken."
  A$(4)="es kann auch sinnvoll sein, beides hintereinander zu tun."
  A$(5)="probieren geht Åber studieren......"
  Gosub Helpp
Return
Procedure Exit
  Alert 2,"Sind die Daten gesichert ???",2,"MenÅ|sichern|exit",Var
  If Var=1
    Do
      On Menu
    Loop
  Endif
  If Var=2
    Gosub Speichern
  Else
    Alert 3,"DATOBERT V.1.1 j.fiedler 3/87 |BITTE 1x 20 DEMARK auf Konto|102033 005 VOBA Heidenheim|BLZ 632 901 10  GELL ??!!!!",1,"o.k.|habschon",Var
    If Var=1
      Alert 1,"TU ES BALD  !!!!!!!!!!!!",1,"CLARO",Var
    Else
      Alert 1," DANKE !!!!!!!!!!!!",1,"bitte",Var
    Endif
  Endif
  Stop
Return
' ~~~~~~~~~~~~~~~~~~~~~~~~~
Procedure Hardcopy
  Hidem
  Hardcopy
  Showm
Return
' ************************************************************
Procedure Hardcpa4
  Hidem
  Local N%,M%,L%,K%,J%,I%,B$
  Lprint Chr$(27);"A";Chr$(8);Chr$(27);"2";
  Quench%=Max(Quench%,300)
  L%=800-400 Div Quench%
  M%=L% Div 256
  N%=L%-M%*256
  L%=Xbios(2)
  For K%=L% To L%+79
    Lprint " ";
    Lprint Chr$(27);"L";Chr$(N%);Chr$(M%);
    J%=0
    For I%=K%+31920 To K% Step -80
      B$=Chr$(Peek(I%))
      Inc J%
      If J%<Quench% Then
        Lprint B$+B$;
      Else
        Lprint B$;
        J%=0
      Endif
    Next I%
    Lprint
  Next K%
  Lprint Chr$(27);"A";Chr$(12);Chr$(27);"2";
  Showm
Return
' #########            ############
Procedure Datendruck
  Lprint Chr$(27);Chr$(112);Chr$(1)
  Lprint Chr$(27);Chr$(45);Chr$(1)
  Lprint Ueb1$
  Lprint Chr$(27);Chr$(45);Chr$(0);
  Lprint Ueb2$
  Lprint Chr$(27);Chr$(112);Chr$(0)
  Lprint Chr$(15)
  If Spa<8
    Ix=1
    Spax=Spa
    Gosub Druckdat
  Else
    Ix=1
    Spax=7
    Gosub Druckdat
    Lprint
    Ix=8
    Spax=Spa
    Gosub Druckdat
  Endif
Return
' ********           ********
Procedure Druckdat
  For I=Ix To Spax
    Lprint Nsa$(I),
  Next I
  Lprint
  For I=Ix To Spax
    Lprint Nsb$(I),
  Next I
  Lprint
  For I=1 To Rei
    For J=Ix To Spax
      Lprint W(I,J),
    Next J
    Lprint N$(I)
  Next I
Return
Procedure Descrit
  Alert 1,"ostalb statistics  presents :  |DATOBERT V.1.1#3/87(GFA-Basic)|johannes fiedler /hohe str.55|7920 heidenheim/brenz",1,"aha",Var
  Cls
Return
' ++++++++++            ++++++++++++
Procedure Helpp
  Cls
  Box 20,100,618,300
  Deftext 1,16,0,32
  Text 30,150,A$(0)
  Deftext 1,4,0,13
  For F=1 To 5
    Text 30,180+20*F,A$(F)
  Next F
Return
