
#    Copyright 2007-2022 Pawel Goralski
#    This file is part of AMIDILIB.
#    See license.txt for licensing information.

import sys
import os

# adds set FastRam flag on executable postbuild action
def setFastRamFlags(env, toolchainType, target):
    if(toolchainType != 'GCCBROWNELF'): # TODO fix / patch for brownelf
        env.AddPostAction(target, Action('m68k-atari-mint-flags --mfastram --mfastload --mfastalloc $TARGET'))

# returns git revision
def getVersion(env):
    git = env.WhereIs('git')
    if git:
        import subprocess
        # get current branch name
        p = subprocess.Popen('git rev-parse --abbrev-ref HEAD', shell=True, stdout=subprocess.PIPE, stderr=subprocess.STDOUT)
        branchName = p.stdout.readline().rstrip().decode("utf-8")
        # get revision list
        gitRevisionListCmd = 'git rev-list --count ' + branchName
        p = subprocess.Popen(gitRevisionListCmd, shell=True, stdout=subprocess.PIPE, stderr=subprocess.STDOUT)
        return p.stdout.readline().rstrip().decode("utf-8")
    else:
        print ("git not found")

# calls external tool (brownout) to convert elf files to tos executables
def convertElfToTos(env, toolchainType, buildTypeName, targetName, outputDirPath, outputExtension, generateDriGstSymbols):
    if(toolchainType == 'GCCBROWNELF'):
        outputPath = outputDirPath + targetName  + '.' + outputExtension
        print('Converting {}.elf to TOS binary --> {}...'.format(targetName,outputPath))

        symbolGeneration=''
        if( generateDriGstSymbols == 'yes'):
            symbolGeneration=' -x'

        env.AddPostAction(targetName +'.elf', Action('brown.out'+ symbolGeneration +' -i $TARGET -o ' + outputPath))

def stripSymbols(env, toolchainType, buildTypeName, targetName, outputDirPath, outputExtension, strip_symbols):
    if(strip_symbols == "yes"):
        if(toolchainType == 'GCC'):
            print("Stripping symbols from " + targetName + '.' + outputExtension)
            env.AddPostAction(targetName + '.' + outputExtension, Action( env['STRIP'] + ' -s -g -S -d --strip-unneeded --strip-dwo -X -x $TARGET -o ' + outputDirPath + targetName + '.' + outputExtension ))

# move scons database to build folder
SConsignFile(os.path.join("../build/", '.sconsign.dblite')) 

# Set number of jobs, depending on number of cores
num_cpu = int(os.environ.get('NUMBER_OF_PROCESSORS', 2))
SetOption('num_jobs', num_cpu)

print("Running with %d job(s)." % GetOption('num_jobs')) 

env = Environment(ENV = os.environ,tools=['default'])

Help("AMIDILIB Copyright 2007-2022 Pawel Goralski\n\
Type: 'Set build variables in SConstruct'\n\
where name of 'targetPlatform' can be: \n\
    'F030' - for Atari Falcon030 / TT build\n\
    'ST' - for Atari ST build \n\
    'CT60' - for Atari Falcon CT60/63\n\
 'host' - computer host name,\n\
 'toolchain' - toolchain type 'GCC' or 'GCCBROWNELF',\n\
 'toolchain_c_prefix' - toolchain compiler name prefix,\n\
 'toolchain_version' - compiler version string (x.x.x, eg. '4.3.4' or '7.3.1'),\n\
 'toolchain_prefix' - toolchain prefix path (root of the toolchain folder),\n\
 'toolchain_use_cross' - use cross compiler, set to 'y' by default\n\
 'build_config' - build type ('debug', 'release'),\n\
 'use_fpu' - enable / disable hardware fpu code generation (valid only on F030 builds),\n\
 'enable_lzo_compression' - enable/disable lzo compression support.\n\
 'enable_lto' - enable link time optimisation. Only valid on gcc 6.x.x> brownelf compilers.\n\
 'enable_stack_protector' - enable stack protector. \n\
 'debug_strip_symbols' - strip symbols. \n\
 'debug_generate_dri' - generate DRI/GST symbol table (useful for Hatari and native debuggers). \n\
 'debug_generate_symbols' - generate debug symbols. \n\
 'debug_generate_symbol_map_file' - generate symbol map file. \n\
 'ikbd_direct_write' - bypass O/S when writing to MIDI ports\n\
 'tx_enable' - skips writing to ACIA ikbd port, sending bytes is via ikbd tx interrupt,\n\
 'opl3_enable' - enables opl3 support (opl2 is default),\n\
 'conout_enable' - disable / enable console output,\n\
 'gemdos_io_enable' - uses gemdos i/o instead of standard libc file i/o,\n\
 'use_libc' - if set to 'no', builds library without libc dependencies, reducing binary size,\n\
 'debug_level' - sets debug level, only valid in debug builds. Level can be set to (0..2)\n"\
)

Import(
        'host',\
        'toolchain',\
        'toolchain_c_prefix',\
        'toolchain_version',\
        'toolchain_prefix',\
        'toolchain_use_cross',\
        'targetPlatform',\
        'build_config',\
        'use_fpu',\
        'enable_lzo_compression',\
        'enable_lto',\
        'enable_stack_protector',\
        'debug_strip_symbols',\
        'debug_generate_dri',\
        'debug_generate_symbols',\
        'debug_generate_symbol_map_file',\
        'ikbd_direct_write',\
        'tx_enable',\
        'opl3_enable',\
        'conout_enable',\
        'gemdos_io_enable',\
        'use_libc',\
        'debug_level'\
        )

# helper paths
rootdir = Dir('../').abspath
projdir = Dir('#').abspath
outputdir = Dir('.').abspath

# environment 
LIBRARYNAME = 'amidi'

PREFIX = toolchain_prefix
GCC_PREFIX = toolchain_c_prefix
GCC_VERSION_STRING = toolchain_version

if(toolchain=='GCC'):
    if(use_libc == 'yes'):
        if(PREFIX):
            env['CPPPATH'] = [PREFIX + '/' + 'm68k-atari-mint/include', projdir + '/deps/nanoprintf']
            env['LIBPATH'] = [PREFIX + '/' + 'm68k-atari-mint/lib']
        else:
            env['CPPPATH'] = ['m68k-atari-mint/include']
            env['LIBPATH'] = ['m68k-atari-mint/lib']
    else:
        env['CPPPATH'] = [projdir + '/deps/atari-gcc-startup/include', projdir + '/deps/nanoprintf']
        env['LIBPATH'] = []

elif(toolchain=='GCCBROWNELF'):
    if(use_libc == 'yes'):
        if(PREFIX):
            env['CPPPATH'] = [PREFIX + '/' + GCC_PREFIX + '/include', projdir + '/deps/nanoprintf'] 
            env['LIBPATH'] = [PREFIX + '/' + GCC_PREFIX + '/lib']
        else:
            env['CPPPATH'] = [GCC_PREFIX + '/include', projdir + '/deps/nanoprintf']
            env['LIBPATH'] = [GCC_PREFIX + '/lib']
    else:
        env['CPPPATH'] = [projdir + '/deps/atari-gcc-startup/include', projdir + '/deps/nanoprintf']
        env['LIBPATH'] = []
else:
    print('ERROR: Undefined target toolset. Exiting...')
    exit(-1)

if(PREFIX):
    env['CC'] = PREFIX + '/bin/' + GCC_PREFIX + '-gcc' + '-' + GCC_VERSION_STRING
    env['OBJCOPY'] = PREFIX + '/bin/' + GCC_PREFIX + '-objcopy'
    env['STRIP'] = PREFIX + '/bin/' + GCC_PREFIX + '-strip'
    env['STACK'] = PREFIX + '/bin/' + GCC_PREFIX + '-stack'
    env['AR'] = PREFIX + '/bin/' + GCC_PREFIX + '-ar'
    env['RANLIB'] = PREFIX + '/bin/' + GCC_PREFIX + '-ranlib'
else:   
    env['CC'] = GCC_PREFIX + '-gcc'
    env['OBJCOPY'] = GCC_PREFIX + '-objcopy'
    env['STRIP'] = GCC_PREFIX + '-strip'
    env['STACK'] = GCC_PREFIX + '-stack'
    env['AR'] = GCC_PREFIX + '-ar'
    env['RANLIB'] = GCC_PREFIX + '-ranlib'

if(toolchain == 'GCCBROWNELF'):
    env['PROGSUFFIX'] = '.elf'
else:
    env['PROGSUFFIX'] = '.tos'

env['LIBSUFFIX'] ='.a'

# VASM
env['AS']='vasmm68k_mot'
env['VASM'] = env['AS']

# ST/e, F030, CT60 program flags
CFLAGS=''
LDFLAGS=''
VASM_FLAGS=''
CPPFLAGS=''
CODEGEN = '-std=c99 -fno-plt -fno-pic -pipe'

LIB_TARGET='not_set'

STACK_SIZE=0
    
TARGET='-DTARGET_'
OBJ_FORMAT=''

VASM_INCLUDES = '-I' + 'include/m68k/ '

if(debug_generate_symbols=='yes'):
    CODEGEN+=' -g -grecord-gcc-switches'

if(enable_stack_protector=='yes'):
    CODEGEN+=' -fstack-protector-strong'

if(use_fpu == 'yes'):
    fpu_flags = '-m68881 '
    VASM_FLAGS += '-m68881 '
else:
    fpu_flags = '-msoft-float '
    VASM_FLAGS += '-no-fpu '

if(toolchain=='GCC'):
    OBJ_FORMAT='aout'
    fpu_flags = ''

    if(targetPlatform == 'ST'):
        CFLAGS += '-m68000 ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68000 '
        VASM_FLAGS += '-m68000 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        LIB_TARGET = '000'
    elif(targetPlatform == 'F030'):
        CFLAGS += '-m68020-40  ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68020-40 '
        VASM_FLAGS += '-m68030 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        LIB_TARGET = '020-40'
    elif(targetPlatform == 'CT60'):
        CFLAGS += '-m68060 ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68060 '
        VASM_FLAGS += '-m68060 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        LIB_TARGET = '060'
    else:
        print('Unsupported target. Exiting...')
        exit(-1)
elif(toolchain == 'GCCBROWNELF'):
    OBJ_FORMAT='brownelf'

    if(enable_lto == 'yes'):
        CFLAGS += '-flto '
        LDFLAGS += '-flto '

    if(targetPlatform == 'ST'):
        CFLAGS += '-m68000 ' + CODEGEN + ' -fomit-frame-pointer -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68000 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68000 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES

        LIB_TARGET = '000'

        # floating point support
        env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/' + GCC_PREFIX + '/' + GCC_VERSION_STRING + '/m68000','/usr/' + GCC_PREFIX + '/lib/m68000']
    elif(targetPlatform == 'F030'):
        CFLAGS += '-m68020-40 ' + CODEGEN + ' -fomit-frame-pointer -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68020-40 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68030 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES

        LIB_TARGET = '020-40'

        # floating point support
        if(use_fpu == 'yes'):
            # hardware fpu support
            env['LIBPATH']=env['LIBPATH'] + ['/lib/gcc/' + GCC_PREFIX + '/' + GCC_VERSION_STRING + '/m68020', '/usr/' + GCC_PREFIX + '/lib/m68020']
        else:
            # soft float
            env['LIBPATH']=env['LIBPATH'] + ['/lib/gcc/' + GCC_PREFIX + '/' + GCC_VERSION_STRING + '/m68020/softfp', '/usr/' + GCC_PREFIX + '/lib/m68020/softfp']

    elif(targetPlatform == 'CT60'):
        CFLAGS += '-m68060 ' + CODEGEN + ' -fomit-frame-pointer -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68060 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68060 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES

        LIB_TARGET = '060'

        # floating point support
        # hardware fpu support
        env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/' + GCC_PREFIX + '/' + GCC_VERSION_STRING + '/m68060', '/usr/' + GCC_PREFIX + '/lib/m68060']

    else:
        print('Unsupported target. Exiting...')
        exit(-1)
else:
    print('ERROR: Undefined target toolset. Exiting...')
    exit(-1)

TARGET = TARGET + targetPlatform

# indicate that we are not linking to standard libc libraries
if(use_libc == 'yes'):
    CFLAGS+='-DAMIDILIB_USE_LIBC=1 '
    LDFLAGS+=' '
else:
    CFLAGS+='-DAMIDILIB_USE_LIBC=0 -I' + projdir + '/deps/atari-gcc-startup/include '
    LDFLAGS+='-nostdlib -nodefaultlibs -nolibc -nostartfiles -Wl,-e_start '

# skip writing directly to ikbd port completely
if (tx_enable == "yes"):
    VASM_FLAGS += '-DTX_ENABLE=1 '
else:
    VASM_FLAGS += '-DTX_ENABLE=0 '

if(opl3_enable == "yes"):
    VASM_FLAGS += '-DENABLE_OPL3=1 '
else:
    VASM_FLAGS += '-DENABLE_OPL3=0 '

if (conout_enable == "no"):
    print ("Silent mode. Console output disabled.")
    CFLAGS += '-DSUPRESS_CON_OUTPUT '

if (gemdos_io_enable == "yes"):
    print ("Use GEMDOS for file I/O not libc functions.")
    CFLAGS += '-DENABLE_GEMDOS_IO '

env['CPPPATH'] += [projdir + '/include'] + [projdir + '/include/ym2149'] + [projdir + '/include/input']

if(enable_lzo_compression=='yes'):
    env['CPPPATH'] += [projdir + '/deps/lzo']   

env['LIBPATH'] += [outputdir + '/']

if(use_libc!='yes'):
    env['LIBPATH'] += [projdir + '/deps/atari-gcc-startup/lib/' + OBJ_FORMAT + '/' + build_config + '/mc68' + LIB_TARGET + '/']

#############################################################################################################################
# additional defines  
#############################################################################################################################
# DEBUG_BUILD - enables debug build
# DEBUG_MEM logs memory function calling (works only if (DEBUG_BUILD && (DEBUG_OUTPUT_TO_*) == true )
# FORCE_MALLOC forces use of libc malloc() for memory allocation, not native functions
# MIDI_PARSER_DEBUG debug output during midi parsing (works only if (DEBUG_BUILD && (DEBUG_OUTPUT_TO_*) == true )
# MIDI_PARSER_TEST outputs loaded and parsed midi file in human readable form
# IKBD_MIDI_SEND_DIRECT all Midiws() calls will be replaced with low level function that writes directly to midi ports without os calls.
# USE_INLINE if defined some functions are inlined
# EVENT_LINEAR_BUFFER library uses custom memory pool for midi events instead of using standard memory allocation calls (per sequence file),
# ENABLE_STACK_PROTECTOR enables gcc stack protector
# NKT_MANUAL_STEP useful for debugging nkt player update step. Update isn't hooked to TiC interrupt, each step is executed after keypress (used in mkt replay demo).
# AM_MANUAL_STEP useful for debugging main midi player update step. Update isn't hooked to TiC interrupt, each step is executed after keypress (used in midi player demo).

if(enable_lzo_compression=='yes'):
    CFLAGS += '-DENABLE_LZO_COMPRESSION=1 '
else:
    CFLAGS += '-DENABLE_LZO_COMPRESSION=0 '

if( ( (debug_generate_dri=='yes') and (toolchain!='GCCBROWNELF')) ):
    print ("Generating DRI/GST symbol table.")
    LDFLAGS += "-Wl,--traditional-format "

if(enable_stack_protector=='yes'):
    print ("Stack protector enabled.")
    CFLAGS += '-DENABLE_STACK_PROTECTOR=1 '
else:
    print ("Stack protector disabled.")
    CFLAGS += '-DENABLE_STACK_PROTECTOR=0 '

if ( build_config == 'debug'):

  if(debug_level=='1'):
    print ("Debug build level 1")
    CFLAGS += '-DDEBUG_BUILD -O0 -DEVENT_LINEAR_BUFFER -DUSE_INLINE ' 
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  elif(debug_level=='2'):
    print ("Debug build level 2")
    CFLAGS += '-DDEBUG_BUILD -O0 -DEVENT_LINEAR_BUFFER -DMIDI_PARSER_DEBUG -DUSE_INLINE '
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  elif(debug_level=='3'):
    print ("Debug build level 3")
    CFLAGS += '-DDEBUG_BUILD -O0 -DEVENT_LINEAR_BUFFER -DMIDI_PARSER_DEBUG -DMIDI_PARSER_TEST -DNKT_LOAD_TEST '
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  else:
    print('ERROR: Undefined debug_level. Should be in 1..3 range. Exiting...')
    exit(-1)
  
else:
    print ("Release build... Code optimisation is turned on.")
    LDFLAGS += ''
    CFLAGS += '-DEVENT_LINEAR_BUFFER ' +' -O2 ' + ' -DUSE_INLINE ' + '-DNDEBUG '
    VASM_FLAGS += ' -DNDEBUG -DDEBUG_BUILD=0 '
    
if(ikbd_direct_write == "yes"):
    print ("Sending data directly via IKBD ENABLED")
    CFLAGS += '-DIKBD_MIDI_SEND_DIRECT '
    VASM_FLAGS += '-DIKBD_MIDI_SEND_DIRECT=1 '
elif(ikbd_direct_write=='no'):
    print ("Sending data directly via IKBD DISABLED")
    CFLAGS += ' '
    VASM_FLAGS += '-DIKBD_MIDI_SEND_DIRECT=0 '

env['CFLAGS'] = CFLAGS  
env['LINKFLAGS'] = LDFLAGS 
env['ASFLAGS'] = VASM_FLAGS
env['VASM_FLAGS'] = VASM_FLAGS

# core functionality
core_src = ["src/common/core/vartypes.c","src/common/core/amprintf.c","src/common/core/logger.c","src/tos/logger_m68k.s"]
containers_src = ["src/common/containers/stack.c","src/common/containers/ringbuffer.c"]
roland_src = ["src/common/roland/rol_ptch.c","src/common/roland/rolinstr.c"]
nkt_format_src = ["src/common/formats/seq2nkt.c","src/common/formats/midi2nkt.c","src/tos/nkt_rep_m68k.s","src/common/formats/nkt_rep.c"]
midiformats_src = ["src/common/formats/xmidi.c","src/common/formats/mus2midi.c"] + nkt_format_src
midiparser_src = ["src/common/midi/mparser.c"]
amidilib_src = ["src/common/core/amidilib.c","src/common/core/config.c"]

# other: native keyboard input (Atari only), ym2149 output, random functions, lzo compression
m68kasm_common_src = ["src/tos/common.s"]
input_src =  ["src/tos/ikbd.s"]
fileio_src = ["src/common/fileio.c","src/tos/gemdosio.c"]
timing_src = ["src/common/timing/miditim.c","src/tos/common.s"]
midi_src = ["src/tos/midi_rep.c","src/common/midi/midiseq.c","src/common/midi/eventlist.c","src/common/midi/midi.c","src/tos/common.s","src/tos/midiReplay.s"]
midi_in_src = ["src/common/midi/midi_in.c"]
memory_src = ["src/tos/memory.c","src/common/memory/linalloc.c"]
string_src = ["src/tos/string/ctype.c","src/tos/string/ctype2.c","src/tos/string/string.c","src/tos/string/memcpy.s","src/tos/string/memcmp.s","src/tos/string/memset.s","src/tos/string/strcmp.s","src/tos/string/strlen.s","src/tos/string/strstr.s"]
machine_src =["src/tos/machine.c"]
ym2149_src = ["src/common/ym2149.c"]
opl3lpt_src = ["src/tos/opl3lpt/opl3lpt_m68k.s","src/tos/opl3lpt/opl3lpt.c"]

lzo_src = []

if(enable_lzo_compression=='yes'):
    lzo_src = [projdir + '/deps/lzo/minilzo.c']

ct60_src = ["src/tos/ct60_m68k.s","src/tos/sv.c"];

# main library build
amidilib_src += core_src + machine_src + containers_src + fileio_src + memory_src + machine_src + ct60_src + timing_src + midi_src + midi_in_src + opl3lpt_src + midiparser_src + roland_src + midiformats_src + lzo_src

if(use_libc == 'no'):
    amidilib_src += string_src

env.StaticLibrary(LIBRARYNAME, amidilib_src)
stripSymbols(env, toolchain, build_config, 'lib' + LIBRARYNAME, outputdir, 'a', debug_strip_symbols)

#################### test programs entry points
amidi_test_src = [projdir + "/demo/midiplay/midiplay.c"]
midiOut_test_src = [projdir + "/demo/midiout/midiout.c"]
oplOut_test_src = [projdir + "/demo/oplout/oplout.c"]
timings_test_src = [projdir + "/demo/timings/timings.c", projdir + "/demo/timings/sampleSequence.c", projdir + "/demo/timings/customSeqReplay.s"]
ym2149_test_src = [projdir + "/demo/ymout/ymout.c"]
nktReplay_src = [projdir + "/demo/nktReplay/nktReplay.c"]
mid2nkt_src = [projdir + "/demo/mid2nkt/mid2nkt.c"]

##################### nkt
nkt_src = ["src/common/core/vartypes.c","src/common/core/logger.c","src/tos/logger_m68k.s","src/common/roland/rol_ptch.c","src/common/formats/midi2nkt.c","src/common/formats/mus2midi.c","src/tos/nkt_rep_m68k.s","src/common/formats/nkt_rep.c","src/common/midi/mparser.c", "src/common/core/amprintf.c" ]

nkt_src += fileio_src

ADDLIBS=['']

# output executable names
MIDIPLAY_NAME = 'midiplay' 
YMOUT_NAME ='ymout' 
MIDIOUT_NAME = 'midiout'
MIDITIMINGS_NAME = 'timings' 
NKT_REPLAY_NAME = 'nktrep' 
MID2NKT_NAME = 'mid2nkt' 
OPLOUT_NAME = 'oplout'

outputdir = rootdir + '/' + build_config + '/'

if(use_libc == 'yes'):
# build test programs and link them with amidilib
    ADDLIBS = ['c']
    
    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags=' -Wl,-Map=' + outputdir + MIDIPLAY_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (MIDIPLAY_NAME + '.ttp'), source = [env.Object(input_src) + amidi_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS + [LIBRARYNAME] , PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, MIDIPLAY_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDIPLAY_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MIDIOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (MIDIOUT_NAME + env['PROGSUFFIX']), source = [env.Object(input_src) + midiOut_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS + [LIBRARYNAME], PROGSUFFIX = env['PROGSUFFIX'])
    convertElfToTos(env, toolchain, build_config, MIDIOUT_NAME, outputdir,'tos', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDIOUT_NAME, outputdir, 'tos', debug_strip_symbols)
    
    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + OPLOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (OPLOUT_NAME + '.ttp'), source = [env.Object(input_src) + oplOut_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS + [LIBRARYNAME], PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, OPLOUT_NAME, outputdir,'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, OPLOUT_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + YMOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (YMOUT_NAME + env['PROGSUFFIX']), source = [env.Object(m68kasm_common_src) + env.Object(input_src) + env.Object(ym2149_src) + ym2149_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS=ADDLIBS + [LIBRARYNAME], PROGSUFFIX = env['PROGSUFFIX'])
    convertElfToTos(env, toolchain, build_config, YMOUT_NAME, outputdir,'tos', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, YMOUT_NAME, outputdir, 'tos', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MIDITIMINGS_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (MIDITIMINGS_NAME + '.ttp'), source = [env.Object(core_src) + env.Object(input_src) + env.Object(ym2149_src) + timings_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS, PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, MIDITIMINGS_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDITIMINGS_NAME, outputdir, 'ttp', debug_strip_symbols)
    
    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + NKT_REPLAY_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (NKT_REPLAY_NAME + '.ttp'), source = [env.Object(nkt_src) + env.Object(input_src) + env.Object(timing_src) + env.Object(memory_src) + env.Object(machine_src) + env.Object(ct60_src) + env.Object(lzo_src) + nktReplay_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS=ADDLIBS + [''], PROGSUFFIX = '.ttp');
    convertElfToTos(env, toolchain, build_config, NKT_REPLAY_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, NKT_REPLAY_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MID2NKT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (MID2NKT_NAME + '.ttp'), source = [env.Object(nkt_src) + env.Object(timing_src) + env.Object(memory_src) + env.Object(machine_src) + env.Object(ct60_src) + env.Object(lzo_src) + mid2nkt_src ], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS=ADDLIBS + [''], PROGSUFFIX = '.ttp');
    convertElfToTos(env, toolchain, build_config, MID2NKT_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MID2NKT_NAME, outputdir, 'ttp', debug_strip_symbols)

else:
# build test programs and link them with amidilib and custom startup
    ADDLIBS += ['startup' + LIB_TARGET]
    linkedLibs = [LIBRARYNAME] + ADDLIBS
    
    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags=' -Wl,-Map=' + outputdir + MIDIPLAY_NAME + '.map '
    else:
        additionalLinkerFlags=''

    env.Program(target = (MIDIPLAY_NAME + '.ttp'), source = [env.Object(input_src) + env.Object(string_src) + amidi_test_src ], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = linkedLibs, PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, MIDIPLAY_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDIPLAY_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MIDIOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (MIDIOUT_NAME + env['PROGSUFFIX']),source = [env.Object(input_src) + env.Object(string_src) + midiOut_test_src ], LINKFLAGS = env["LINKFLAGS"] +  additionalLinkerFlags, LIBS = linkedLibs, PROGSUFFIX = env['PROGSUFFIX'])
    convertElfToTos(env, toolchain, build_config, MIDIOUT_NAME, outputdir, 'tos', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDIOUT_NAME, outputdir, 'tos', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + OPLOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (OPLOUT_NAME  + '.ttp'),source = [env.Object(input_src) + env.Object(string_src) + oplOut_test_src ], LINKFLAGS = env["LINKFLAGS"] +  additionalLinkerFlags, LIBS = linkedLibs, PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, OPLOUT_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, OPLOUT_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + YMOUT_NAME + '.map '
    else:
        additionalLinkerFlags=''
    
    env.Program(target = (YMOUT_NAME + env['PROGSUFFIX']), source = [env.Object(m68kasm_common_src) + env.Object(input_src) + env.Object(ym2149_src) + env.Object(string_src) + ym2149_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags,  LIBS = linkedLibs , PROGSUFFIX = env['PROGSUFFIX'])
    convertElfToTos(env, toolchain, build_config, YMOUT_NAME, outputdir, 'tos', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, YMOUT_NAME, outputdir, 'tos', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MIDITIMINGS_NAME + '.map '
    else:
        additionalLinkerFlags=''

    env.Program(target = (MIDITIMINGS_NAME + '.ttp'),source = [env.Object(core_src) + env.Object(input_src) + env.Object(ym2149_src) + env.Object(string_src) + timings_test_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS , PROGSUFFIX = '.ttp')
    convertElfToTos(env, toolchain, build_config, MIDITIMINGS_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MIDITIMINGS_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + NKT_REPLAY_NAME + '.map '
    else:
        additionalLinkerFlags=''

    env.Program(target = (NKT_REPLAY_NAME + '.ttp'), source = [env.Object(nkt_src) + env.Object(input_src) + env.Object(timing_src) + env.Object(memory_src) + env.Object(machine_src) + env.Object(ct60_src) + env.Object(lzo_src) + env.Object(string_src) + nktReplay_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS = ADDLIBS + [], PROGSUFFIX='.ttp');
    convertElfToTos(env, toolchain, build_config, NKT_REPLAY_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, NKT_REPLAY_NAME, outputdir, 'ttp', debug_strip_symbols)

    if(debug_generate_symbol_map_file=='yes'):
        additionalLinkerFlags='-Wl,-Map=' + outputdir + MID2NKT_NAME + '.map '
    else:
        additionalLinkerFlags=''

    env.Program(target = (MID2NKT_NAME + '.ttp'), source = [env.Object(nkt_src)  + env.Object(timing_src)+ env.Object(memory_src) + env.Object(machine_src) + env.Object(ct60_src) + env.Object(lzo_src) + env.Object(string_src) + mid2nkt_src], LINKFLAGS = env["LINKFLAGS"] + additionalLinkerFlags, LIBS= ADDLIBS + [], PROGSUFFIX = '.ttp'); 
    convertElfToTos(env, toolchain, build_config, MID2NKT_NAME, outputdir, 'ttp', debug_generate_dri)
    stripSymbols(env, toolchain, build_config, MID2NKT_NAME, outputdir, 'ttp', debug_strip_symbols)
