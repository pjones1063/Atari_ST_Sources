#    Copyright 2007-2022 Pawel Goralski
#    
#    This file is part of AMIDILIB / BadMood.
#    See license.txt for licensing information.

import sys
import os

# adds set FastRam flag on executable postbuild action
def setFastRamFlags(env, toolchainType, target):
    if(toolchainType != 'GCCBROWNELF'): # TODO fix / patch for brownelf
        env.AddPostAction(target, Action('m68k-atari-mint-flags --mfastram --mfastload --mfastalloc $TARGET'))

# returns git revision
def getVersion(env):
    git = env.WhereIs('git')
    if git:
        import subprocess
        # get current branch name
        p = subprocess.Popen('git rev-parse --abbrev-ref HEAD', shell=True, stdout=subprocess.PIPE, stderr=subprocess.STDOUT)
        branchName = p.stdout.readline().rstrip().decode("utf-8")
        # get revision list
        gitRevisionListCmd = 'git rev-list --count ' + branchName
        p = subprocess.Popen(gitRevisionListCmd, shell=True, stdout=subprocess.PIPE, stderr=subprocess.STDOUT)
        return p.stdout.readline().rstrip().decode("utf-8")
    else:
        print ("git not found")

# calls external tool (brownout) to convert elf files to tos executables
def convertElfToTos(env, toolchainType, buildTypeName, targetName, outputDirPath, outputExtension, generateDriGstSymbols):
    if(toolchainType == 'GCCBROWNELF'):
        outputPath = outputDirPath + targetName  + '.' + outputExtension
        print('Converting {}.elf to TOS binary --> {}...'.format(targetName,outputPath))

        symbolGeneration=''
        if( generateDriGstSymbols == 'yes'):
            symbolGeneration=' -x'

        env.AddPostAction(targetName +'.elf', Action('brown.out'+ symbolGeneration +' -i $TARGET -o ' + outputPath))

def stripSymbols(env, toolchainType, buildTypeName, targetName, outputDirPath, outputExtension):
    if(debug_strip_symbols == "yes"):
        if(toolchainType == 'GCC'):
            print("Stripping symbols from " + targetName + '.' + outputExtension)
            env.AddPostAction(targetName + '.' + outputExtension, Action( env['STRIP'] + ' -s -g -S -d --strip-unneeded --strip-dwo -X -x $TARGET -o ' + outputDirPath + targetName + '.' + outputExtension ))

env = Environment(ENV = os.environ,tools=['default'])

Help("AMIDILIB Copyright 2007-2022 Pawel Goralski\n \
Type: 'Set build variables in SConstruct'\n \
where name of 'targetPlatform' can be: \n \
        'F030' - for Atari Falcon030 / TT build\n \
        'ST' - for Atari ST build \n \
        'CT60' - for Atari Falcon CT60/63\n \
 'host' - computer host name,\n\
 'toolchain' - toolchain type 'GCC' or 'GCCBROWNELF',\n\
 'toolchain_c_prefix' - toolchain compiler name prefix,\n\
 'toolchain_version' - compiler version string (x.x.x, eg. '4.3.4' or '7.3.1'),\n\
 'toolchain_prefix' - toolchain prefix path (root of the toolchain folder),\n\
 'toolchain_use_cross' - use cross compiler, set to 'y' by default\n\
 'host' - host machine,\n \
 'build_config' - build type ('debug', 'release'),\n \
 'use_fpu' - enable/disable hardware fpu code generation (valid only on F030 builds),\n \
 'enable_lzo_compression' - enable/disable lzo compression support.\n \
 'enable_lto' - eneable link time optimisation. Only valid on gcc 7.xx brownelf.\n \
 'enable_stack_protector' - enable stack protector. \n\
 'debug_strip_symbols' - strip symbols. \n\
 'debug_generate_dri' - generate DRI/GST symbol table (useful for Hatari and native debuggers). \n\
 'debug_generate_symbols' - generate debug symbols. \n\
 'debug_generate_symbol_map_file' - generate symbol map file. \n\
 'ikbd_direct_write' - bypass O/S when writing to MIDI ports\n \
 'tx_enable' - skips writing to ACIA ikbd port, sending bytes is via ikbd tx interrupt,\n \
 'opl3_enable' - enables opl3 support (opl2 is default),\n\
 'conout_enable' - disable / enable console output,\n \
 'gemdos_io_enable' - uses gemdos i/o instead of standard libc file i/o,\n \
 'use_libc' - - if set to 'no', builds library without libc dependencies, reducing binary size,\n \
 'debug_level' - sets debug level, only valid in debug builds. Level can be set to (0..2)\n")

Import(
        'host',\
        'toolchain',\
        'toolchain_c_prefix',\
        'toolchain_version',\
        'toolchain_prefix',\
        'toolchain_use_cross',\
        'targetPlatform',\
        'build_config',\
        'use_fpu',\
        'enable_lzo_compression',\
        'enable_lto',\
        'enable_stack_protector',\
        'debug_strip_symbols',\
        'debug_generate_dri',\
        'debug_generate_symbols',\
        'debug_generate_symbol_map_file',\
        'ikbd_direct_write',\
        'tx_enable',\
        'opl3_enable',\
        'conout_enable',\
        'gemdos_io_enable',\
        'use_libc',\
        'debug_level'
        )

# helper paths
rootdir = Dir('../').abspath
projdir = Dir('#').abspath
outputdir = Dir('.').abspath

LIBRARYNAME = 'nkt'

PREFIX = toolchain_prefix
GCC_PREFIX = toolchain_c_prefix
GCC_VERSION_STRING = toolchain_version

# move scons database to build folder
SConsignFile(os.path.join("../nktlib/", '.sconsign.dblite')) 

num_cpu = int(os.environ.get('NUMBER_OF_PROCESSORS', 2))
SetOption('num_jobs', num_cpu)
print ("running with ", GetOption('num_jobs'), "jobs.") 

if(toolchain=='GCC'):
    if(use_libc == 'yes'):
        if(PREFIX):
            env['CPPPATH'] = [PREFIX + '/' + 'm68k-atari-mint/include', projdir + '/../deps/nanoprintf']
            env['LIBPATH'] = [PREFIX + '/' + 'm68k-atari-mint/lib']
        else:
            env['CPPPATH'] = ['m68k-atari-mint/include']
            env['LIBPATH'] = ['m68k-atari-mint/lib']
    else:
        env['CPPPATH'] = [projdir + '/../deps/atari-gcc-startup/include', projdir + '/../deps/nanoprintf']
        env['LIBPATH'] = []

elif(toolchain=='GCCBROWNELF'):
    if(use_libc == 'yes'):
        if(PREFIX):
            env['CPPPATH'] = [PREFIX + '/' + GCC_PREFIX + '/include', projdir + '/../deps/nanoprintf'] 
            env['LIBPATH'] = [PREFIX + '/' + GCC_PREFIX + '/lib']
        else:
            env['CPPPATH'] = [GCC_PREFIX + '/include', projdir + '/../deps/nanoprintf']
            env['LIBPATH'] = [GCC_PREFIX + '/lib']
    else:
        env['CPPPATH'] = [projdir + '/../deps/atari-gcc-startup/include', projdir + '/../deps/nanoprintf']
        env['LIBPATH'] = []
else:
    print('ERROR: Undefined target toolset. Exiting...')
    exit(-1)

if(PREFIX):
    env['CC'] = PREFIX + '/bin/' + GCC_PREFIX + '-gcc' + '-' + GCC_VERSION_STRING
    env['OBJCOPY'] = PREFIX + '/bin/' + GCC_PREFIX + '-objcopy'
    env['STRIP'] = PREFIX + '/bin/' + GCC_PREFIX + '-strip'
    env['STACK'] = PREFIX + '/bin/' + GCC_PREFIX + '-stack'
    env['AR'] = PREFIX + '/bin/' + GCC_PREFIX + '-ar'
    env['RANLIB'] = PREFIX + '/bin/' + GCC_PREFIX + '-ranlib'
else:   
    env['CC'] = GCC_PREFIX + '-gcc'
    env['OBJCOPY'] = GCC_PREFIX + '-objcopy'
    env['STRIP'] = GCC_PREFIX + '-strip'
    env['STACK'] = GCC_PREFIX + '-stack'
    env['AR'] = GCC_PREFIX + '-ar'
    env['RANLIB'] = GCC_PREFIX + '-ranlib'

if(toolchain == 'GCCBROWNELF'):
    env['PROGSUFFIX'] = '.elf'
else:
    env['PROGSUFFIX'] = '.tos'

env['LIBSUFFIX'] ='.a'

# VASM
env['AS']='vasmm68k_mot'
env['VASM'] = env['AS']

# ST/e, F030, CT60 program flags
CFLAGS=''
LDFLAGS=''
VASM_FLAGS=''
CODEGEN = '-std=c99 -pipe'

LIB_POSTFIX='not_set'
LIB_TARGET='not_set'

STACK_SIZE=0

TARGET='-DTARGET_'
CSTARTUP_FOLDER=''

if(debug_generate_symbols=='yes'):
    CODEGEN+=' -g -grecord-gcc-switches'

if(enable_stack_protector=='yes'):
    CODEGEN+=' -fstack-protector-strong'

VASM_INCLUDES = '-I' + './nktlib/include '

if(toolchain=='GCC'):
    fpu_flags = ''

    if(use_fpu == 'yes'):
        fpu_flags='-m68881 '
    else:
        fpu_flags='-msoft-float '

    if(targetPlatform == 'ST'):
        CFLAGS += '-m68000 ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68000 '
        VASM_FLAGS += '-m68000 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/aout/mc68000/'

        LIB_TARGET = '000'
    elif(targetPlatform == 'F030'):
        CFLAGS += '-m68020-40 ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68020-40 '
        VASM_FLAGS += '-m68030 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/aout/mc68020-40/'

        LIB_TARGET = '020-40'
    elif(targetPlatform == 'CT60'):
        CFLAGS += '-m68060 ' + CODEGEN + ' -ffast-math -fomit-frame-pointer ' + fpu_flags
        LDFLAGS += '-m68060 '
        VASM_FLAGS += '-m68060 -quiet -Faout -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/aout/mc68060/'

        LIB_TARGET = '060'
    else:
        print('Unsupported target. Exiting...')
        exit(-1)
elif(toolchain == 'GCCBROWNELF'):
    fpu_flags = ''

    if(use_fpu == 'yes'):
        fpu_flags='-m68881 '
    else:
        fpu_flags='-msoft-float '

    if(targetPlatform == 'ST'):
        CFLAGS += '-m68000 ' + CODEGEN + ' -fomit-frame-pointer  -fstrict-aliasing -fcaller-saves -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68000 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68000 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/brownelf/mc68000/'
        LIB_TARGET = '000'
        # floating point support
        env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/m68k-ataribrown-elf/'+ toolchain_version + '/m68000','/usr/m68k-ataribrown-elf/lib/m68000']
    elif(targetPlatform == 'F030'):
        CFLAGS += '-m68020-40 ' + CODEGEN + ' -fomit-frame-pointer -fstrict-aliasing -fcaller-saves -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68020-40 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68030 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/brownelf/mc68020-40/'
        LIB_TARGET = '020-40'
        # floating point support

        if(use_fpu == 'yes'):
        # hardware fpu support
            env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/m68k-ataribrown-elf/'+ toolchain_version + '/m68020', '/usr/m68k-ataribrown-elf/lib/m68020']
        else:
        # soft float
            env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/m68k-ataribrown-elf/'+ toolchain_version + '/m68020/softfp', '/usr/m68k-ataribrown-elf/lib/m68020/softfp']
    elif(targetPlatform == 'CT60'):
        CFLAGS += '-m68060 ' + CODEGEN + ' -fomit-frame-pointer  -fstrict-aliasing -fcaller-saves -fleading-underscore -ffunction-sections -fdata-sections ' + fpu_flags
        LDFLAGS += '-m68060 -Wl,--gc-sections -Wl,--emit-relocs -Ttext=0 '
        VASM_FLAGS += '-m68060 -quiet -Felf -quiet -spaces ' + VASM_INCLUDES
        CSTARTUP_FOLDER='../deps/startup/brownelf/mc68060/'

        LIB_TARGET = '060'
        # floating point support
        # hardware fpu support
        env['LIBPATH'] = env['LIBPATH'] + ['/lib/gcc/m68k-ataribrown-elf/'+ toolchain_version + '/m68060', '/usr/m68k-ataribrown-elf/lib/m68060']
    else:
        print('Unsupported target. Exiting...')
        exit(-1)
else:
    print('ERROR: Undefined target toolset. Exiting...')
    exit(-1)

TARGET = TARGET + targetPlatform

# indicate that we are not linking to standard libc libraries

if(use_libc == 'yes'):
    CFLAGS+='-DAMIDILIB_USE_LIBC=1 '
    LDFLAGS+=' '
else:
    CFLAGS+='-DAMIDILIB_USE_LIBC=0 -I' + projdir + '/../deps/atari-gcc-startup/include '
    LDFLAGS+='-nostdlib -nodefaultlibs -nolibc -nostartfiles -Wl,-e_start '

# skip writing directly to ikbd port completely
if (tx_enable == "yes"):
    VASM_FLAGS += '-DTX_ENABLE=1 '
else:
    VASM_FLAGS += '-DTX_ENABLE=0 '

if(opl3_enable == "yes"):
    VASM_FLAGS += '-DENABLE_OPL3=1 '
else:
    VASM_FLAGS += '-DENABLE_OPL3=0 '

if (conout_enable == "no"):
    print ("Silent mode. Console output disabled.")
    CFLAGS += '-DSUPRESS_CON_OUTPUT '

if (gemdos_io_enable == "yes"):
    print ("Use GEMDOS for file I/O not libc functions.")
    CFLAGS += '-DENABLE_GEMDOS_IO '

if(enable_lto == 'yes'):
    CFLAGS += '-flto '
    LDFLAGS += '-flto '
    
env["CPPPATH"] =env["CPPPATH"] + ["../","./include","./include/lzo"]
env["LIBPATH"] = env["LIBPATH"] + ['../bin/']

if( ( (debug_generate_dri=='yes') and (toolchain!='GCCBROWNELF')) ):
    print ("Generating DRI/GST symbol table.")
    LDFLAGS += "-Wl,--traditional-format "

if(enable_stack_protector=='yes'):
    print ("Stack protector enabled.")
    CFLAGS += '-DENABLE_STACK_PROTECTOR=1 '
else:
    print ("Stack protector disabled.")
    CFLAGS += '-DENABLE_STACK_PROTECTOR=0 '

if ( build_config == 'debug'):
  print ("Debug build...")

  if(debug_level=='1'):
    CFLAGS += '-DDEBUG_BUILD -DEVENT_LINEAR_BUFFER ' + '-DUSE_INLINE ' + '-DDEBUG_SERIAL_OUTPUT '
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  elif(debug_level=='2'):
    CFLAGS += '-DDEBUG_BUILD -DMIDI_PARSER_DEBUG -DEVENT_LINEAR_BUFFER '
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  elif(debug_level=='3'):
    CFLAGS += '-DDEBUG_BUILD -DEVENT_LINEAR_BUFFER -DDEBUG_SERIAL_OUTPUT -DMIDI_PARSER_TEST -DNKT_LOAD_TEST '
    VASM_FLAGS += ' -DDEBUG_BUILD=1 '
  else:
    print('ERROR: Undefined debug_level. Should be in 1..3 range. Exiting...')
    exit(-1)

  LIB_POSTFIX = LIB_TARGET+'d'
else:
        print ("Release build... Code optimisation is turned on.")
        CFLAGS += '-DEVENT_LINEAR_BUFFER ' +'-O2 ' + '-DUSE_INLINE '
        VASM_FLAGS += ' -DNDEBUG -DDEBUG_BUILD=0 '
        LIB_POSTFIX = LIB_TARGET

if(ikbd_direct_write == "yes"):
    print ("Sending data directly via IKBD ENABLED")
    CFLAGS += CFLAGS +'-DIKBD_MIDI_SEND_DIRECT '
    VASM_FLAGS += '-DIKBD_MIDI_SEND_DIRECT=1 '
elif(ikbd_direct_write=='no'):
    print ("Sending data directly via IKBD DISABLED")
    CFLAGS += ' '
    VASM_FLAGS += '-DIKBD_MIDI_SEND_DIRECT=0 '

env["CFLAGS"] = CFLAGS
env["LINKFLAGS"] = LDFLAGS
env["ASFLAGS"] = VASM_FLAGS
env['VASM_FLAGS'] = VASM_FLAGS

if(toolchain == 'GCCBROWNELF'):
    lib_dir='./lib/elf/'
else:
    lib_dir='./lib/aout/'
    
# file i/o
fileio_src = ["./src/fileio.c","./src/gemdosio.c"]

ct60_src = ["./src/ct60_m68k.s", "./src/sv.c"];
nkt_replay_src = ["./src/nkt_rep.c","./src/nkt_rep_m68k.s","./src/rol_ptch.c","./src/common.s","./src/machine.c","./src/memory.c","./src/linalloc.c"]
nkt_conv_src = ["./src/midi2nkt.c","./src/mus2midi.c","./src/vartypes.c","./src/logger.c","./src/logger_m68k.s","./src/miditim.c","./src/mparser.c"]
lzo_src = ["./src/minilzo.c"]
opl3lpt_src = ["./src/opl3lpt_m68k.s","./src/opl3lpt.c"]

# build library
libname = lib_dir + LIBRARYNAME + LIB_POSTFIX

additionalLinkerFlags=''
if(debug_generate_symbol_map_file=='yes'):
    additionalLinkerFlags=' -Wl,-Map=' + outputdir + libname + '.map '
else:
    additionalLinkerFlags=''

env.StaticLibrary(libname, nkt_replay_src + nkt_conv_src + opl3lpt_src + lzo_src + fileio_src , LINKFLAGS = env['LINKFLAGS'] + additionalLinkerFlags)
