***********************************
*=================================*
* RESERVOIR GODS LIBRARY ROUTINES *
*=================================*
*        |              |         *
*        |VECTOR LIBRARY|         *
*        |              |         *
***********************************
* CODE: MR. PINK | (C): 22/02/96  *
***********************************
* UPDATE: 22/2/96  *
********************

**************************************************************************
	SECTION TEXT
**************************************************************************


rlvc_vector_constructor
	movem.l	a0-1/d0,-(a7)
	move.w	sr,-(a7)
	move	#$2700,sr

	lea	rlvc_vector_space,a0
	movec	CACR,d0
	move.l	d0,(a0)+
	movec	VBR,d0
	move.l	d0,(a0)+
	movec	MSP,d0
	move.l	d0,(a0)+
	movec	USP,d0
	move.l	d0,(a0)+

	movea.w	#8,a1			;start of vectors
	move.w	#253,d0
.vex	move.l	(a1)+,(a0)+
	dbra	d0,.vex

	movea.w	#$fa00,a1		;mfp address
	moveq	#11,d0
.mfp	move.l	(a1)+,(a0)+
	dbra	d0,.mfp

	move.w	(a7)+,sr
	movem.l	(a7)+,a0-1/d0
	rts

rlvc_vector_deconstructor
	movem.l	a0-1/d0,-(a7)
	move.w	sr,-(a7)
	move	#$2700,sr

	lea	rlvc_vector_space,a0
	move.l	(a0)+,d0
	movec	d0,CACR
	move.l	(a0)+,d0
	movec	d0,VBR
	move.l	(a0)+,d0
	movec	d0,MSP
	move.l	(a0)+,d0
	movec	d0,USP

	movea.w	#8,a1
	move.w	#253,d0
.vex	move.l	(a0)+,(a1)+
	dbra	d0,.vex

	movea.w	#$fa00,a1
	moveq	#11,d0
.mfp	move.l	(a0)+,(a1)+
	dbra	d0,.mfp
	move.b	#$c0,$fffffa23.w

	move.w	(a7)+,sr
	movem.l	(a7)+,a0-1/d0
	rts

rlvc_install_vbl
	move.w	sr,-(a7)
	move.w	#$2700,sr
	move.l	#rlvc_my_vbl,$70.w
	move.w	(a7)+,sr
	rts	

rlvc_wait_vbl
	move.l	d0,-(a7)
	move.l	$466.w,d0
.wait	cmp.l	$466.w,d0
	beq.s	.wait
	move.l	(a7)+,d0
	rts

rlvc_install_vbl_routine:
	movem.l	d1/a1,-(a7)
	move.w	sr,-(a7)
	move.w	#$2700,sr
	lea	rlvc_vector_space,a1
	lea	rlvc_vbl_routines(a1),a1
	moveq	#63,d1				;max 64 routines
.find	tst.l	(a1)+
	beq.s	.hole_found
	dbra	d1,.find
	bra.s	.no_hole
.hole_found
	move.l	a0,-(a1)
.no_hole
	move.w	(a7)+,sr
	movem.l	(a7)+,d1/a1
	rts

rlvc_remove_vbl_routine
	movem.l	a1/d0-1,-(a7)
	move.w	sr,-(a7)
	move.w	#$2700,sr

	lea	rlvc_vector_space,a1
	lea	rlvc_vbl_routines(a1),a1

	moveq	#63,d0
.find	cmp.l	(a1)+,a0
	bne.s	.neq
	move.w	d0,d1
.shift	move.l	(a1)+,-8(a1)		;remove routine
	dbra	d1,.shift
.neq	dbra	d0,.find

	move.w	(a7)+,sr
	movem.l	(a7)+,a1/d0-1
	rts

rlvc_my_vbl
	movem.l	d0-a6,-(a7)
	
	lea	rlvc_vector_space,a0
	lea	rlvc_vbl_routines(a0),a0
.next	move.l	(a0)+,a1
	cmpa.l	#0,a1
	beq.s	.last
	move.l	a0,-(a7)
	jsr	(a1)
	move.l	(a7)+,a0
	bra.s	.next
.last
	addq.l	#1,$466.w
	movem.l	(a7)+,d0-a6
	rte

rlvc_install_my_reset:
	move.l	#rlvc_my_reset,$42a.w
	move.l	#$31415926,$426.w
	rts

rlvc_my_reset:
	move.w	#$2300,(a7)
	move.l	#rlib_deconstruct_all,2(a7)
	rte


	even
	RSRESET
rlvc_old_cacr		rs.l	1
rlvc_old_vbr		rs.l	1
rlvc_old_msp		rs.l	1
rlvc_old_usp		rs.l	1
rlvc_old_vectors	rs.l	254
rlvc_old_mfp		rs.l	12
rlvc_vbl_routines	rs.l	65
rlvc_vector_buffer	rs.l	1
	even

rlvc_vector_space:	ds.b	rlvc_vector_buffer
	even