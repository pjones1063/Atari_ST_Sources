***********************************
*=================================*
* RESERVOIR GODS LIBRARY ROUTINES *
*=================================*
*        |              |         *
*        |GEMDOS LIBRARY|         *
*        |              |         *
***********************************
* CODE: MR. PINK | (C): 19/02/96  *
***********************************
* UPDATE: 19/2/96  *
********************

***************************************************************************
	SECTION TEXT
***************************************************************************	

rlgd_super	MACRO

	clr.w	-(a7)
	pea	$200000
	trap	#1	

		ENDM

rlgd_mshrink:	MACRO

        move.l  4(a7),a5		
	lea	$80(a5),a0
	lea	rlgd_command_line,a1
	
	move.b	(a0)+,d0
	subq.b	#1,d0
	bmi.s	.no_cmd
.copy	move.b	(a0)+,(a1)+
	dbra	d0,.copy
.no_cmd

 	move.l	12(a5),d0
        add.l	20(a5),d0
        add.l	28(a5),d0
	add.l	#$100,d0
        move.l	d0,-(a7)
	pea 	(a5)
        move.w	d0,-(a7)
        move.w	#$4a,-(a7)		* mshrink
        trap	#1
	lea	12(a7),a7

		ENDM

rlgd_mouse_off:	MACRO

	dc.w	$a00a

		ENDM

rlgd_mouse_on:	MACRO

	dc.w	$a009

		ENDM

rlgd_pterm:	MACRO

	clr.w	-(a7)
	trap	#1

		ENDM

rlgd_mxalloc:
	movem.l	d1-2/a0-2,-(a7)
	move.w	d0,-(a7)
	move.l	d1,-(a7)
	move.w	#$44,-(a7)
	trap	#1
	addq.l	#8,a7
	movem.l	(a7)+,d1-2/a0-2
	rts

rlgd_mfree:
	movem.l	d1-2/a0-2,-(a7)
	pea	(a0)			
	move.w	#$49,-(a7)
	trap	#1
	addq.l	#6,a7
	movem.l	(a7)+,d1-2/a0-2
	rts

rlgd_print:
	movem.l	d0-2/a0-2,-(a7)
	pea	(a0)
	move.w	#9,-(a7)
	trap	#1
	addq.l	#6,a7
	movem.l	(a7)+,d0-2/a0-2
	rts


rlgd_conis
	movem.l	d1-2/a0-2,-(a7)
	move.w	#$b,-(a7)
	trap	#1
	addq.l	#2,a7
	tst.w	d0
	movem.l	(a7)+,d1-2/a0-2
	rts

rlgd_conin
	movem.l	d1-2/a0-2,-(a7)
	move.w	#8,-(a7)
	trap	#1
	addq.l	#2,a7
	movem.l	(a7)+,d1-2/a0-2
	rts

	SECTION DATA

rlgd_command_line	ds.b	128