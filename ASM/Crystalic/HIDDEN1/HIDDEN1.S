*******************************
*   MAYBE A HIDDEN SCREEN!!   *
*     	 BY: FEAR...          *
*******************************
	JSR	SETSUPER
	JSR	SETSCREEN
	JSR	ROTATESPR
	JSR	CLEARSCRS
	JSR	SETPAL
	JSR	FIXLOGOPAL
	JSR	SETPIC
	JSR	SETVAR
	JSR	STARTMUSIC
	JSR	INIT
	JSR	CALCULATE
	JSR	SAVESEC
DEMOLOOP:
	CMP.W	#1,WFNC
	BNE	DEMOLOOP
	MOVE.W	#0,WFNC

	MOVE.B	#0,$FF8260

*	MOVE.W	#$033,$FF8240
	JSR	CLRPIXELS
*	MOVE.W	#$003,$FF8240
	JSR	SETPIXELS
*	MOVE.W	#$202,$FF8240
	JSR	SAVESEC
*	MOVE.W	#$020,$FF8240
	JSR	GETXPOS
	JSR	GETYPOS
	JSR	PUTSPRITE
*	MOVE.W	#$333,$FF8240
	JSR	TEXTSCROLL
	JSR	TEXTSCROLL
	JSR	SHOWSCROLL
*	MOVE.W	#$300,$FF8240
	JSR	CALCULATE
*	MOVE.W	#$220,$FF8240
	JSR	WAVELOGO
*	MOVE.W	#$000,$FF8240

	CMP.W	#5,COUNT
	BNE	NONEWFONT
	JSR	SETFONT
	MOVE.W	#0,COUNT
NONEWFONT:
	ADD.W	#1,COUNT

	JMP	DEMOLOOP

	JSR		RETINIT
	CLR.W	-(SP)
	TRAP	#1
*******************************
SETSUPER:
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAVESUPER
	RTS
ENDSUPER:
	MOVE.L	SAVESUPER,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	RTS
*******************************
SETSCREEN:
	MOVE.W	#0,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.W	#$5,-(SP)
	TRAP	#14
	ADD.L	#12,SP
	RTS
*******************************
SETVAR:
	MOVE.L	#SPRTAB1,SPRPTR
	MOVE.L	#SPRTAB2,SPRPTR2
	MOVE.L	#SCROLLTEXT,TEXTPTR
	MOVE.W	#0,POS
	RTS 
*******************************
SETPAL:
	MOVE.L	#LOGO+2,A0
	MOVE.L	#$FF8240,A1
	REPT	8
	MOVE.L	(A0)+,(A1)+
	ENDR
	RTS
*******************************
FIXLOGOPAL:
	MOVE.L	#RASTER2,A0
	REPT	23
	MOVE.W	$FF8248,(A0)+
	ENDR
	RTS
*******************************
SETPIC:
	MOVE.L	#LOGO+34+160*100,A0
	MOVE.L	#$78000,A1
	MOVE.W	#40*52,D0
SETPICLOOP:
	MOVE.L	(A0)+,(A1)+
	DBF.W	D0,SETPICLOOP	
	RTS
*******************************
STARTMUSIC:
	MOVEQ	#1,D0
	JSR	MUSIC
	RTS
ENDMUSIC:
	MOVE.L	#$08080000,$FF8800
	MOVE.L	#$09090000,$FF8800
	MOVE.L	#$0A0A0000,$FF8800
	RTS
*******************************
SAVESEC:
	LEA	POINTSAVE,A0
	LEA	POINTSAVE2,A1
SECLOOP:
	MOVE.W	(A0)+,D0
	CMP.W	#1000,D0
	BEQ	SECWRAP
	MOVE.W	D0,(A1)+
	JMP	SECLOOP
SECWRAP:
	MOVE.W	#1000,(A1)+
	RTS
*******************************
CLEARSCRS:
	LEA	$78000,A0
	MOVE.W	#8000,D0
CLRLOOP:
	CLR.L	(A0)+
	DBF.W	D0,CLRLOOP
	RTS
*******************************
INIT:
	MOVE.L	$70.W,OLDVBL
	MOVE.L	$120.W,OLDHBL
	MOVE.B	$FFFFFA07.W,OLD07
	MOVE.B	$FFFFFA09.W,OLD09
	MOVE.B	$FFFFFA13.W,OLD13
	MOVE.B	$FFFFFA1B.W,OLD1B
	
	MOVE.W	 #$2700,SR
	MOVE.L	 #NEWVBL,$70.W
	MOVE.L	 #NEWHBL,$120.W
	CLR.B	 $FFFFFA07.W	
	CLR.B 	 $FFFFFA09.W
	ORI.B 	 #1,$FFFFFA07.W	
	ORI.B 	 #1,$FFFFFA13.W
	CLR.B 	 $FFFFFA1B.W	
	MOVE.W 	 #$2300,SR	
	RTS
RETINIT:
	MOVE.L	OLDVBL,$70.W
	MOVE.L	OLDHBL,$120.W
	MOVE.B	OLD07,$FFFFFA07.W
	MOVE.B	OLD09,$FFFFFA09.W
	MOVE.B	OLD13,$FFFFFA13.W
	MOVE.B	OLD1B,$FFFFFA1B.W
	RTS
***********************************
CALCULATE:
	LEA	POINTS,A6
	LEA	POINTSAVE,A5
CALCLOOP:
	MOVE.W	(A6)+,D0  * GRADEN
	CMP.W	#1000,D0
	BEQ	CALCWRAP
	MOVE.W	(A6)+,D2  * RADIEN
	MOVE.W	D2,D3
	MOVE.W	D2,D4
	MOVE.W	D0,D1

	MOVE.L	#COSTAB,A0	* RAD*COS(GRAD)=X
*	MULS	#2,D0
	ADD.W	D0,A0
	MOVE.W	(A0)+,D0
	MULS	D0,D2
	DIVS	#16384,D2
	ADD.W	#160,D2

	MOVE.L	#SINTAB,A0	* RAD*SIN(GRAD)=Y
*	MULS	#2,D1
	ADD.W	D1,A0
	MOVE.W	(A0)+,D1
	MULS	D1,D3
	DIVS	#16384,D3
	ADD.W	#100,D3

	MOVE.W	D2,(A5)+
	MOVE.W	D3,(A5)+
	
	SUBQ.W	#2,A6
	CMP.W	#20,D4		* D4=RADIEN
	BGE	RBIG1	
	ADDQ.W	#1,D4
BACK:
	CMP.W	#320,D2
	BGE	RES
	CMP.W	#0,D2
	BLE	RES
	CMP.W	#200,D3
	BGE	RES
	CMP.W	#54,D3
	BLE	RES
NORES:
	MOVE.W	D4,(A6)+

	JMP	CALCLOOP
RES:
	MOVEQ.W #6,D4
	JMP	NORES
CALCWRAP:
	MOVE.W	#1000,(A5)+
	RTS
RBIG1:
	CMP.W	#40,D4
	BGE	RBIG2
	ADDQ.W	#2,D4
	JMP	BACK	
RBIG2:
	CMP.W	#80,D4
	BGE	RBIG3
	ADDQ.W	#4,D4
	JMP	BACK	
RBIG3:
	CMP.W	#120,D4
	BGE	RBIG4
	ADD.W	#8,D4
	JMP	BACK	
RBIG4:
	CMP.W	#150,D4
	BGE	RBIG5
	ADD.W	#11,D4
	JMP	BACK	
RBIG5:
	ADD.W	#15,D4
	JMP	BACK	
*******************************
SETPIXELS:
	LEA	POINTSAVE,A5
PIXELSLOOP:
	MOVE.W	(A5)+,D4
	CMP.W	#1000,D4
	BEQ	PIXELSWRAP
	MOVE.W	(A5)+,D5
	JSR	PUTPIXEL
	JMP	PIXELSLOOP	
PIXELSWRAP:
	RTS
*******************************
CLRPIXELS:
	LEA	POINTSAVE2,A5
CLRPIXELSLOOP:
	MOVE.W	(A5)+,D4
	CMP.W	#1000,D4
	BEQ	CLRPIXELSWRAP
	MOVE.W	(A5)+,D5
	JSR	CLRPIXEL
	JMP	CLRPIXELSLOOP	
CLRPIXELSWRAP:
	RTS
*******************************
PUTPIXEL:
	MOVE.L	$44E,A1	* X-COR I D4, Y-COR I D5
	MULU	#160,D5
	ADD.W	D5,A1
	MOVE.W	D4,D5
	AND.W	#$FFF0,D5
	ASR.W	D5
	ADD.W	D5,A1
	LEA	NIBBLES,A0
	AND.W	#$000F,D4
	ASL.W	D4
	MOVE.W	(A0,D4.W),D0
	OR.W	D0,(A1)
	RTS
*******************************
CLRPIXEL:
	MOVE.L	$44E,A1	* X-COR I D4, Y-COR I D5
	MULU	#160,D5
	ADD.W	D5,A1
	MOVE.W	D4,D5
	AND.W	#$FFF0,D5
	ASR.W	D5
	ADD.W	D5,A1
	LEA	NIBBLES,A0
	AND.W	#$000F,D4
	ASL.W	D4
	MOVE.W	(A0,D4.W),D0
	CLR.W	(A1)
	RTS
*******************************
NIBBLES:
	DC.W	$8000,$4000,$2000,$1000
	DC.W	$800,$400,$200,$100
	DC.W	$80,$40,$20,$10
	DC.W	$8,$4,$2,$1

***************************************
PUTSPRITE:
	MOVE.L	#$78000,A1
	MOVE.W	YCOR,D0
	MULS	#160,D0
	ADD.L	D0,A1
	MOVE.W	XCOR,D0
	MOVE.W	D0,D1
	AND.W	#$FFF0,D0
	DIVU.W	#2,D0
	ADD.W	D0,A1
	LEA	SPRBUFF,A0
	AND.W	#$000F,D1
	MULS	#1008,D1
	ADD.L	D1,A0

	MOVE.W	#35,D0
PUTSPRITELOOP:
	REPT	14
	MOVE.W	(A0),4(A1)
	ADDQ.W	#2,A0
	ADDQ.W	#8,A1
	ENDR
	ADD.W	#48,A1
	DBF.W	D0,PUTSPRITELOOP	
	RTS
*******************************
GETXPOS:
	MOVE.L	SPRPTR,A0
	CLR.W	D0
	MOVE.W	(A0)+,D0
	CMP.W	#-1,D0
	BNE	NORESTORE
	MOVE.L	#SPRTAB1,SPRPTR
	JMP	GETXPOS
NORESTORE:
	MOVE.L	A0,SPRPTR
	SUB.W	#100,D0
	MOVE.W	D0,XCOR	
	RTS
*******************************
GETYPOS:
	MOVE.L	SPRPTR2,A0
	CLR.W	D0
	MOVE.W	(A0)+,D0
	CMP.W	#-1,D0
	BNE	NORESTORE2
	MOVE.L	#SPRTAB2,SPRPTR2
	JMP	GETYPOS
NORESTORE2:
	MOVE.L	A0,SPRPTR2
*	ADD.W	#20,D0
	MOVE.W	D0,YCOR	
	RTS
*******************************
* ROTATE THE BIG SPRITES..... *
*******************************
ROTATESPR:
	MOVE.W	#0,D1
	JSR	COPYSPR
ROT:
	MOVE.W	#14,D2
ROTLOOP:
	MOVE.L	#SPRITE+34,A0
	MOVE.W	#35,D0
	JSR	ROTATELOOP
	ADD.W	#1008,D1
	JSR	COPYSPR	
	DBF.W	D2,ROTLOOP
	RTS		
COPYSPR:
	MOVE.L	#SPRITE+34,A0
	MOVE.L	#SPRBUFF,A1
	ADD.W	D1,A1
	MOVE.W	#35,D0
COPYSPRLOOP:
	REPT	14
	MOVE.W	(A0),(A1)
	ADDQ.W	#8,A0
	ADDQ.W	#2,A1
	ENDR
	ADD.W	#48,A0
	DBF.W	D0,COPYSPRLOOP	
	RTS
ROTATELOOP:
	REPT	14
	ROXR.W	(A0)
	ADDQ.W	#8,A0
	ENDR
	ADD.W	#48,A0
	DBF.W	D0,ROTATELOOP
	RTS
*******************************
* WAVE THE UPPER-LOGO........ *
*******************************
WAVELOGO:
	MOVE.L	#WAVETAB2,A2
	MOVE.L	#$78000,A1
	MOVE.W	#46,D1
WAVELOOP2:
	MOVE.B	(A2)+,D2
	CMP.W	#1,D2
	BNE	NOW1
	JSR	ROTATELEFT2
	JMP	NOTHING2
NOW1:
	CMP.W	#2,D2
	BNE	NOTHING2
	JSR	ROTATERIGHT2
NOTHING2:
	ADD.W	#160,A1
	DBF.W	D1,WAVELOOP2

	MOVE.L	#WAVETAB2,A0
	MOVE.L	#BUFF,A1
	MOVE.L	#WAVETAB2,A2
	MOVE.B	(A0)+,(A1)+
	REPT	199
	MOVE.B	(A0)+,(A2)+
	ENDR	
	MOVE.B	BUFF,(A2)+
	RTS
*******************************
	INCLUDE	A:\HIDDEN1\ROXR.DAT
*******************************
TEXTSCROLL:
	MOVE.L	#SCRAREA,A1
	MOVE.W	#7,D1
SCRLOOP:
	ROXL.W	160(A1)
	ROXL.W	152(A1)
	ROXL.W	144(A1)
	ROXL.W	136(A1)
	ROXL.W	128(A1)
	ROXL.W	120(A1)
	ROXL.W	112(A1)
	ROXL.W	104(A1)
	ROXL.W	96(A1)
	ROXL.W	88(A1)
	ROXL.W	80(A1)
	ROXL.W	72(A1)
	ROXL.W	64(A1)
	ROXL.W	56(A1)
	ROXL.W	48(A1)
	ROXL.W	40(A1)
	ROXL.W	32(A1)
	ROXL.W	24(A1)
	ROXL.W	16(A1)
	ROXL.W	8(A1)
	ROXL.W	(A1)
	ADD.W	#168,A1
	DBF.W	D1,SCRLOOP
	RTS
*******************************
SHOWSCROLL:
	MOVE.L	#SCRAREA,A1
	MOVE.L	#$78000+160*192,A2
	MOVE.W	#7,D1
SHOWLOOP:
	MOVE.W	(A1),(A2)
	MOVE.W	8(A1),8(A2)
	MOVE.W	16(A1),16(A2)
	MOVE.W	24(A1),24(A2)
	MOVE.W	32(A1),32(A2)
	MOVE.W	40(A1),40(A2)
	MOVE.W	48(A1),48(A2)
	MOVE.W	56(A1),56(A2)
	MOVE.W	64(A1),64(A2)
	MOVE.W	72(A1),72(A2)
	MOVE.W	80(A1),80(A2)
	MOVE.W	88(A1),88(A2)
	MOVE.W	96(A1),96(A2)
	MOVE.W	104(A1),104(A2)
	MOVE.W	112(A1),112(A2)
	MOVE.W	120(A1),120(A2)
	MOVE.W	128(A1),128(A2)
	MOVE.W	136(A1),136(A2)
	MOVE.W	144(A1),144(A2)
	MOVE.W	152(A1),152(A2)
	ADD.W	#168,A1
	ADD.W	#160,A2
	DBF.W	D1,SHOWLOOP
	RTS
*******************************
SETFONT:
	MOVE.L	TEXTPTR,A3
	CLR.W	D1
	MOVE.B	(A3)+,D1
	CMP.W	#$FF,D1
	BEQ	TEXTWRAP

	SUB.W	#32,D1
	MULS	#2,D1

	MOVE.L	#FONTTAB,A1
	ADD.W	D1,A1
	MOVE.W	(A1)+,D1
	MOVE.L	#LOGO+34,A1
	ADD.W	D1,A1
	MOVE.L	#SCRAREA+160,A2
	REPT	8
	MOVE.W	(A1),(A2)
	ADD.W	#160,A1
	ADD.W	#168,A2
	ENDR

	MOVE.L	A3,TEXTPTR
	RTS	
TEXTWRAP:
	MOVE.L	#SCROLLTEXT,TEXTPTR
	JMP	SETFONT
*******************************
NEWVBL:
	JSR	MUSIC+8
	MOVE.W	#1,WFNC
	
	MOVE.W	RASTER,$FF8242
	MOVE.W	RASTER2,$FF8248

	CLR.B 	$FFFFFA1B.W	
	MOVE.B 	#2,$FFFFFA21.W	
	MOVE.L	#RASTER,RASTERPTR
	MOVE.L	#RASTER2,RASTERPTR2
	MOVE.B 	#8,$FFFFFA1B.W	
	RTE			
NEWHBL:
	MOVE.L	A6,-(SP)
	MOVE.L	A5,-(SP)
	MOVEA.L RASTERPTR,A6		
	MOVEA.L RASTERPTR2,A5		
	MOVE.W  (A6)+,$FF8242
	MOVE.W  (A5)+,$FF8248
	MOVE.L  A6,RASTERPTR		
	MOVE.L  A5,RASTERPTR2		
	MOVEA.L	(SP)+,A5
	MOVEA.L	(SP)+,A6

	BCLR 	#0,$FFFFFA0F.W	
	RTE			
*******************************
MUSIC:		INCBIN	A:\HIDDEN1\MAX_3.MUS
*******************************
SAVESUPER:	DC.L	0
OLDVBL:		DC.L	0
OLDHBL:		DC.L	0
OLD07:		DC.B	0
OLD09:		DC.B	0
OLD13:		DC.B	0
OLD1B:		DC.B	0
WFNC:		DC.W	0
COUNT:		DC.W	0
TEXTPTR:	DC.L	0
RASTERPTR:	DC.L	0
RASTERPTR2:	DC.L	0
*******************************
		EVEN
POS:		DC.W	0
BUFF:		DS.B	1
		EVEN
WAVETAB2:
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,2
	DC.B	0,0,2,0,0,2,0,0,0,0
	DC.B	1,0,0,1,0,0,1,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,1,1,0,0
	DC.B	2,2,0,0,1,1,0,0,2,2
	DC.B	0,0,1,1,0,0,2,2,0,0
	DC.B	1,1,0,0,2,2,0,0,1,1
	DC.B	0,0,1,0,0,1,0,0,1,0
	DC.B	0,0,0,2,0,0,2,0,0,2
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
	DC.B	0,0,0,0,0,0,0,0,0,0
		EVEN
POINTSAVE:	DS.W	150
POINTSAVE2:	DS.W	150
POINTS:		INCLUDE	A:\HIDDEN1\3DPLANE3.DAT
		DC.W	1000
		EVEN
SINTAB:		INCLUDE	A:\HIDDEN1\SINTAB.DAT
COSTAB:		INCLUDE	A:\HIDDEN1\COSTAB.DAT
*******************************
SPRPTR:		DC.L	0
SPRPTR2:	DC.L	0
XCOR:		DC.W	0
YCOR:		DC.W	0
SPRTAB1:	INCLUDE	A:\HIDDEN1\BIGTAB.DAT
		DC.W	-1
		DC.W	-1
SPRTAB2:	INCLUDE	A:\HIDDEN1\BIGTAB2.DAT
		DC.W	-1
		DC.W	-1
SPRBUFF:	DS.W	16*1108+16
		EVEN
SPRITE:		INCBIN	A:\HIDDEN1\BIGSPRIT.PI1
		EVEN
LOGO:		INCBIN	A:\HIDDEN1\RES_LOGO.PI1
		EVEN
FONTTAB:
	DC.W	5152,2632,2648,5152,5152,5152,5152,5152,2608,2616,5152,5152,2656,5152,2624,5152
	DC.W    5136,2664,2672,2680,2688,2696,2704,2712
	DC.W	5120,5128
 	DC.W	5152,5152,5152,5152,5152,2640,5152	
	DC.W	0,8,16,24,32,40,48,56,64,72,80,88,96,104,112,120,128,136,144,152
	DC.W	2560,2568,2576,2584,2592,2600
		EVEN
SCROLLTEXT:	
	DC.B	'    TJENA HORNY CORNY.... FAN VA BRA VA!!!   ',$FF
		EVEN
RASTER:
	DC.W	$406,$406,$406,$406,$406,$406,$406,$406,$406,$406
	DC.W	$406,$406,$406,$406,$406,$406,$406,$406,$406,$406
	DC.W	$406,$406,$406,$000,$720,$720,$710,$710,$700,$700
	DC.W	$701,$701,$702,$702,$703,$703,$704,$704,$705,$705
	DC.W	$706,$706,$707,$707,$607,$607,$507,$507,$407,$407
	DC.W	$307,$307,$207,$207,$107,$107,$007,$007,$017,$017
	DC.W	$027,$027,$037,$037,$047,$047,$057,$057,$067,$067
	DC.W	$077,$077,$076,$076,$075,$075,$074,$074,$073,$073
	DC.W	$072,$072,$071,$071,$070,$070,$171,$171,$272,$272
	DC.W	$373,$373,$474,$474,$575,$640,$730,$720,$610,$000
RASTER2:
	DC.W	$777,$777,$776,$776,$775,$775,$774,$774,$773,$773
	DC.W	$772,$772,$771,$771,$770,$770,$760,$760,$750,$750
	DC.W	$740,$740,$730,$730,$720,$720,$710,$710,$700,$700
	DC.W	$701,$701,$702,$702,$703,$703,$704,$704,$705,$705
	DC.W	$706,$706,$707,$707,$607,$607,$507,$507,$407,$407
	DC.W	$307,$307,$207,$207,$107,$107,$007,$007,$017,$017
	DC.W	$027,$027,$037,$037,$047,$047,$057,$057,$067,$067
	DC.W	$077,$077,$076,$076,$075,$075,$074,$074,$073,$073
	DC.W	$072,$072,$071,$071,$070,$070,$171,$171,$272,$272
	DC.W	$373,$373,$474,$474,$575,$575,$676,$676,$777,$777
SCRAREA:
	DS.B	160*20                         