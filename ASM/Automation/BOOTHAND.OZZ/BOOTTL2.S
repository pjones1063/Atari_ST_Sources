	OPT	O+
	
CURSOFF	MOVE.W	#$2,-(A7)
	MOVE.W	#$0,-(A7)
	MOVE.W	#$15,-(A7)
	TRAP	#14
	ADDQ.L	#6,A7
	
START	MOVE.W	#12,D0	; CLEAR FILE NAME BUFFER
	LEA	FILENAM(PC),A0
CLEANAM	CLR.B	(A0)+
	DBF	D0,CLEANAM

GETKEY	LEA	TITLE(PC),A0
	BSR	DISKCHK	
	CMP.B	#$31,D0
	BNE.S	NEXT1
	BSR	CONVERT
NEXT1	CMP.B	#$32,D0
	BNE.S	NEXT2
	BSR	BOOTCHK
NEXT2	CMP.B	#$33,D0
	BNE.S	NEXT2A
	BSR	INSTALL
NEXT2A	CMP.B	#$34,D0
	BNE.S	NEXT3
	BSR	INSTALL
NEXT3	CMP.B	#$35,D0
	BNE.S	NEXT4
	BSR	VIRUS
NEXT4	CMP.B	#$36,D0
	BNE.S	GETKEY
	MOVE.W	#$0,-(A7)
	TRAP	#$1
	
; READ BOOT SECTOR IN

	
CONVERT	PEA	NAMEIN(PC)
	BSR	PRINT
	PEA	BUFFER(PC)
	MOVE.W	#$A,-(A7)
	TRAP	#$01
	ADDQ.L	#6,A7
	MOVE.W	#$2,-(A7)
	PEA	FILENAM(PC)
	MOVE.W	#$3D,-(A7)
	TRAP	#$1
	ADDQ.L	#$8,A7
	BMI.S	START
	MOVE.W	D0,D6
	MOVE.L	#$40000,-(A7)
	MOVE.L	#$FFFF,-(A7)
	MOVE.W	D6,-(A7)
	MOVE.W	#$3F,-(A7)
	TRAP	#$01
	ADD.L	#$C,A7
	MOVE.W	D6,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#$01
	ADDQ.L	#4,A7
	
	LEA	CONV(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BSR	READBOOT
	
	MOVE.L	#$60380000,$30000
	MOVE.W	#$0000,$30004
	MOVE.L	#$3003A,A1
	MOVE.L	#$4001c,A0
	MOVE.W	$40004,D0
MOVEIT	MOVE.B	(A0)+,(A1)+
	DBF	D0,MOVEIT
	BSR	FIXCHK
	BSR	PUTBOOT
	RTS
	
BOOTCHK	LEA	BOOT(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BSR	READBOOT
	BSR	FIXCHK
	BSR	PUTBOOT
	RTS
	
INSTALL	MOVE.B	D0,D5
	LEA	INST(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BSR	READBOOT
	MOVE.L	#$60380000,$30000
	MOVE.L	#$3003A,A0
SETUP	CLR.B	(A0)+
	CMP.L	#$301FC,A0
	BNE.S	SETUP
	CMP.B	#$34,D5
	BEQ.S	ONPLUS
	LEA	VIRBOOT(PC),A0
	BRA.S	COPIER
ONPLUS	LEA	BOOTPLUS(PC),A0
COPIER	MOVE.L	#$3003A,A1
	MOVE.W	#480,D0
INLOOP	MOVE.B	(A0)+,(A1)+
	DBF	D0,INLOOP
	BSR	FIXCHK
	BSR	PUTBOOT
	RTS

VIRUS	LEA	VIR1(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BSR	READBOOT
	CMP.W	#$6038,$30000
	BNE.S	ALLCLEAR
	CMP.L	#$41FAFFC4,$3003A
	BEQ.S	ALIVE
	LEA	PROTECT(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BRA	AGAIN
ALLCLEAR	CMP.B	#$60,$30000
	BNE.S	NOVIR
	LEA	SOMETHIN(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BRA.S	AGAIN
NOVIR	LEA	NOVIRUS(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BRA.S	AGAIN
ALIVE	LEA	FOUNDVIR(PC),A0
	BSR	DISKCHK
	ORI.W	#32,D0
	CMP.B	#$79,D0
	BNE.S	AGAIN
	MOVE.L	#$3003A,A1
	MOVE.W	#370,D0
ZEROLOOP	MOVE.B	#$0,(A1)+
	DBF	D0,ZEROLOOP
	BSR	FIXCHK
	CMP.W	#$0,$301FE
	BEQ.S	CHECK2
	MOVE.W	#$0,$301FE
	BRA.S	GOON
CHECK2	MOVE.W	#$FFFF,$301FE
GOON	BSR	PUTBOOT
	LEA	KILLED(PC),A0
	MOVE.W	#$5A5A,D6
	BSR	DISKCHK
	BRA.S	AGAIN
AGAIN	LEA	DOMORE(PC),A0
	BSR	DISKCHK
	ORI.W	#32,D0
	CMP.B	#$79,D0
	BEQ	VIRUS
	RTS

DISKCHK	MOVE.L	A0,-(A7)
	BSR	PRINT
	CMP.W	#$5A5A,D6
	BNE.S	KEYIN
	PEA	KEYPLEA(PC)
	BSR	PRINT
	MOVE.L	#$0,D6
KEYIN	MOVE.W	#$8,-(A7)
	TRAP	#$01
	ADDQ.L	#2,A7
	RTS

READBOOT	MOVE.W 	#$0001,-(A7)
	MOVE.W 	#$0000,-(A7)
	MOVE.W 	#$0000,-(A7)
	MOVE.W 	#$0001,-(A7)
	MOVE.W 	#$0000,-(A7)
	CLR.L 	-(A7)
	MOVE.L 	#$30000,-(A7)
	MOVE.W 	#$8,-(A7)
	TRAP 	#$0E
	ADD.L 	#$14,A7
	RTS

PUTBOOT	MOVE.W	#$0001,-(A7)
	MOVE.W	#$0000,-(A7)
	MOVE.W 	#$0000,-(A7)
	MOVE.W	#$0001,-(A7)
	MOVE.W 	#$0000,-(A7)
	CLR.L 	-(A7)
	MOVE.L 	#$30000,-(A7)
	MOVE.W 	#$9,-(A7)
	TRAP 	#$0E
	ADD.L 	#$14,A7
	RTS

FIXCHK	MOVE.L	#$30000,A0
	CLR.L	D0
	CLR.W	D1
LOOP	ADD.W	(A0)+,D0
	CMP.L	#$301FC,A0
	BNE.S	LOOP
SUMUP	CMP.W	#$1234,D0
	BEQ.S	DONE
	ADDQ.W	#$1,D0
	ADDQ.W	#$1,D1
	BRA.S	SUMUP
DONE	MOVE.W	D1,$301FE
	RTS
	
PRINT	MOVE.L	(A7)+,A6
	MOVE.W	#$9,-(A7)
	TRAP	#$01
	ADDQ.L	#6,A7
	JMP	(A6)


TITLE	DC.B	$1b,$45
	DC.B	"    L.S.D. BOOT SECTOR HANDLER V1.1",$D,$A
	DC.B	"    ------ ---- ------ ------- ----",$D,$A
	DC.B	$A
	DC.B	"    1. CONVERT PROG TO BOOT SECTOR",$0D,$0A,$0A
	DC.B	"    2. CHECKSUM A BOOT SECTOR",$0D,$0A,$0A
	DC.B	"    3. INSTALL LSD VIRUS PROOF BOOT",$0D,$0A,$0A
	DC.B	"    4. INSTALL LSD BOOTPLUS",$0D,$0A,$0A
	DC.B	"    5. CHECK A DISK FOR VIRUS",$0D,$0A,$0A
	DC.B	"    6. EXIT TO GEM",$0 
KEYPLEA	DC.B	$D,$A,$A,"     ** HIT ANY KEY TO CONTINUE! **"
	dc.b	$D,$A,$0
CONV	DC.B	$1B,$45
	DC.B	"  INSERT DISK TO WRITE BOOT SECTOR TO.",$D,$A,$0
BOOT	DC.B	$1B,$45
	DC.B	" INSERT DISK THAT NEEDS CHECKSUM FIXING",$D,$A,$0
INST	DC.B	$1B,$45
	DC.B	"   INSERT THE DISK YOU WISH TO WRITE",$D,$A
	DC.B	"       THE L.S.D. BOOT SECTOR TO.",$D,$A,$0
VIR1	DC.B	$1B,$45
	DC.B	"          LSD VIRUS CHECK V1.0",$D,$A,$A
	DC.B	"   INSERT THE DISK YOU WISH TO CHECK.",$0D,$0A,$0
PROTECT	DC.B	$1B,$45
	DC.B	"          THIS DISK IS IMMUNE!",$0
SOMETHIN	DC.B	$1B,$45
	DC.B	"SOMETHING ON THIS DISK. IT MAY BE A BOOT",$D,$A
	DC.B	"         DISK OR OTHER BOOT CODE!",$0
NOVIRUS	DC.B	$1B,$45
	DC.B	"  THIS DISK IS CLEAR, BUT IS NOT SAFE",$D,$A
	DC.B	"  FROM INFECTION. IT MAY BE WISE TO",$D,$A
	DC.B	"  PUT THE L.S.D. BOOT SECTOR ONTO IT",$D,$A
	DC.B	"  TO PREVENT IT GETTING INFECTED.",$0
FOUNDVIR	DC.B	$1B,$45
	DC.B	"      ! ACHTUNG ! - VIRUS ALARM -",$D,$A,$A
	DC.B	"           CLEAR IT (Y/N) ? ",$D,$0
KILLED	DC.B	"         * VIRUS EXTERMINATED *",$D,$A,$0
DOMORE	DC.B	$A,$A,$D
	DC.B	"CHECK ANY MORE (Y/N) ? ",$0
NAMEIN	DC.B	$1B,$45
	DC.B	" ENTER NAME OF FILE TO CONVERT",$0D,$0A
	DC.B	"-> ",$0
BUFFER	DC.B	12
	DC.B	0
FILENAM	DS.B	12

	EVEN 
	DC.L	0
	
VIRBOOT	INCBIN	"A:\BOOTY.RAW"
BOOTPLUS	INCBIN	"A:\BOOTPLUS.RAW"