*** ROUTINE FLI - MAIN CODE BY RAZORBACK ***
***               ADD CODE BY ZANAK      ***

	SECTION TEXT
	
SUPER	MOVE.L	#0,-(SP)
	MOVE.W	#$20,-(SP) 	
	TRAP	#1
	ADDQ.L	#6,SP   

	MOVE.W	#-1,-(A7) 
	MOVE.W	#$58,-(A7)
	TRAP	#$E 
	ADDQ.L	#4,A7 
	MOVE.W	D0,SAUVE_RES

	MOVE.W	#2,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	MOVE.L	D0,SAVE_SCREEN
	
	CLR.L	$FFFF9800.W
	
	MOVE.L	#ANIMATION,BUFFER

INTRO_FLI_PLAY
	MOVE.W	#$24,-(A7)
	MOVE.W	#$58,-(A7)
	TRAP	#14
	ADDQ.L	#4,A7
END_MONITOR	MOVE.L	#ECRAN,D0 
	LSR.L	#8,D0 
	MOVE.B	D0,-$7DFD.W 
	LSR.L	#8,D0 
	MOVE.B	D0,-$7DFF.W 
	MOVE.L	#ECRAN,D0 
	MOVE.B	D0,-$7DF3.W 
	MOVE.L	$70.L,SAUVE_VBL 
	MOVE.L	#BYE,8.L 
	MOVE.L	#BYE,$C.L
	MOVE.L	#BYE,$10.L 
	MOVE.L	#BYE,$14.L 
	MOVE.L	#BYE,$18.L 
	MOVE.L	#BYE,$1C.L 
	MOVE.L	#VBL,$70.W
	BSR	READ_FLI_HEADER 
INIT_FILM	MOVE.W	FRAMES,D0 
	MOVE.W	D0,PAGE 
	MOVEA.L	BUFFER,A6 
	ADDA.L	#$80,A6 
READ_PAGE	MOVE.W	#0,TIME 
	BSR	READ_EXTRA_FLI_H
	MOVE.L	#5,D0		; SPEED
SUITE_VBL	CMP.W	TIME,D0 
	BGT.S	SUITE_VBL 
	CMPI.B	#$39,-$3FE.W
	BEQ	BYE
	SUBQ.W	#1,PAGE 
	TST.W	PAGE
	BNE.S	READ_PAGE 
;	BRA	INIT_FILM	; BOUCLE 
	
BYE	MOVE.L	SAUVE_VBL,$70.W 

USER	MOVE.L	#0,-(SP)
	MOVE.W	#$20,-(SP) 	
	TRAP	#1
	ADDQ.L	#6,SP   

	MOVE.W	SAUVE_RES,-(SP)
	MOVE.W	#$58,-(SP)
	TRAP	#14
	ADDQ.L	#4,SP

	MOVE.L	SAVE_SCREEN,D0
	MOVE.W	#-1,-(SP)
	MOVE.W	#-1,-(SP)
	MOVE.L	D0,-(SP)
	MOVE.L	D0,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	ADDA.L	#14,SP
		
	CLR.L	-(SP)
	TRAP	#1

VBL	ADDQ.W	#1,TIME 
	RTE 

INIT_SIZE	MOVEA.L	#BUFFER,A0
	MOVEQ	#0,D0 
	MOVE.B	3(A0),D0
	LSL.W	#8,D0 
	MOVE.B	2(A0),D0
	LSL.L	#8,D0 
	MOVE.B	1(A0),D0
	LSL.L	#8,D0 
	MOVE.B	(A0),D0 
	MOVE.L	D0,SIZE 
	RTS 

READ_FLI_HEADER
	MOVEA.L	BUFFER,A0 
	MOVEQ	#0,D0 
	MOVE.B	7(A0),D0
	LSL.W	#8,D0 
	MOVE.B	6(A0),D0
	MOVE.W	D0,FRAMES 
	MOVEQ	#0,D0 
	MOVE.B	9(A0),D0
	LSL.W	#8,D0 
	MOVE.B	8(A0),D0
	MOVE.W	D0,WIDTH
	MOVEQ	#0,D0 
	MOVE.B	11(A0),D0 
	LSL.W	#8,D0 
	MOVE.B	10(A0),D0 
	MOVE.W	D0,HEIGHT 	
	MOVEQ	#0,D0 
	MOVE.B	19(A0),D0 
	LSL.L	#8,D0 
	MOVE.B	18(A0),D0 
	LSL.L	#8,D0 
	MOVE.B	17(A0),D0 
	LSL.W	#8,D0 
	MOVE.B	16(A0),D0 
	MOVE.L	D0,SPEED
	RTS 

READ_EXTRA_FLI_H
	MOVEA.L	A6,A0 
	MOVEQ	#0,D0 
	MOVE.B	3(A0),D0
	LSL.L	#8,D0 
	MOVE.B	2(A0),D0
	LSL.L	#8,D0 
	MOVE.B	1(A0),D0
	LSL.L	#8,D0 
	MOVE.B	(A0),D0 
	MOVE.L	D0,SIZE_FRAME 
	MOVEQ	#0,D0 
	MOVE.B	5(A0),D0
	LSL.W	#8,D0 
	MOVE.B	4(A0),D0
	MOVEQ	#0,D0 
	MOVE.B	7(A0),D0
	LSL.W	#8,D0 
	MOVE.B	6(A0),D0
	MOVE.W	D0,NBRE_SS_CHUNK
	MOVEA.L	A6,A5 
	ADDA.L	#$10,A5 
NEXT_CHUNK	TST.W	NBRE_SS_CHUNK 
	BEQ.S	END_CHUNK 
	BSR	READ_CHUNK
	SUBQ.W	#1,NBRE_SS_CHUNK
	BRA.S	NEXT_CHUNK
END_CHUNK	ADDA.L	SIZE_FRAME,A6 
	RTS 

READ_CHUNK	MOVEA.L	A5,A0 
	MOVEQ	#0,D0 
	MOVE.B	3(A0),D0
	LSL.L	#8,D0 
	MOVE.B	2(A0),D0
	LSL.L	#8,D0 
	MOVE.B	1(A0),D0
	LSL.L	#8,D0 
	MOVE.B	(A0),D0 
	MOVE.L	D0,SIZE_DATA_CHUNK
	MOVEQ	#0,D0 
	MOVE.B	5(A0),D0
	LSL.W	#8,D0 
	MOVE.B	4(A0),D0
	MOVE.W	D0,TYPE_CHUNK 
	CMPI.W	#$B,TYPE_CHUNK
	BEQ	READ_PALETTE
	CMPI.W	#$C,TYPE_CHUNK
	BEQ	READ_LC 
	CMPI.W	#$F,TYPE_CHUNK
	BEQ	READ_BRUN 
	CMPI.W	#$10,TYPE_CHUNK 
	BEQ	READ_COPY 
	RTS 

READ_COPY	LEA	(6,A5),A0
	MOVEA.L	#BUFFER_COLOR,A3
	MOVE.L	#0,LINE_MIN
	MOVE.W	#$C8,LINE_NBRE
	MOVEA.L	#ECRAN,A1
	MOVE.W	#$C7,D7
COPY_LIGNES	MOVE.W	#$13F,D6
COPY_POINTS3
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	MOVE.W	(0.B,A3,D0.W*2),(A1)+
	DBF	D6,COPY_POINTS3
	DBF	D7,COPY_LIGNES
	ADDA.L	SIZE_DATA_CHUNK,A5
	RTS
	
READ_BRUN	LEA	(6,A5),A0
	MOVEA.L	#BUFFER_COLOR,A3
	MOVE.L	#0,LINE_MIN
	MOVE.W	#$C8,LINE_NBRE
	MOVEA.L	#ECRAN,A1
	MOVE.W	#$C7,D7
COPY_LINES	MOVEQ	#0,D6
	MOVE.B	(A0)+,D6
	SUBQ.W	#1,D6
COPY_PACK	MOVEQ	#0,D5
	MOVE.B	(A0)+,D5
	TST.B	D5
	BLT.B	COPY_POINTS
	SUBQ.W	#1,D5
	MOVEQ	#0,D4
	MOVE.B	(A0)+,D4
COPY_POINT	MOVE.W	(0.B,A3,D4.W*2),(A1)+
	DBF	D5,COPY_POINT
RETOUR_POINTS
	DBF	D6,COPY_PACK
	DBF	D7,COPY_LINES
	ADDA.L	SIZE_DATA_CHUNK,A5
	RTS
		
COPY_POINTS	EXT.W	D5
	NEG.W	D5
	SUBQ.W	#1,D5
	MOVEQ	#0,D4
COPY_POINTS2
	MOVE.B	(A0)+,D4
	MOVE.W	(0.B,A3,D4.W*2),(A1)+
	DBF	D5,COPY_POINTS2	
	BRA.B	RETOUR_POINTS

READ_LC	LEA	(6,A5),A0
	MOVEA.L	#BUFFER_COLOR,A3
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.B	(1,A0),D3
	LSL.L	#8,D3
	MOVE.B	(A0),D3
	MULU.W	#$280,D3
	MOVE.L	D3,LINE_MIN
	MOVEQ	#0,D7
	MOVE.B	(3,A0),D7
	LSL.L	#8,D7
	MOVE.B	(2,A0),D7
	SUBQ.W	#1,D7
	MOVE.W	D7,LINE_NBRE
	ADDQ.L	#4,A0
	MOVEA.L	#ECRAN,A1
	ADDA.L	D3,A1
LC_LINES	MOVEQ	#0,D6
	MOVE.B	(A0)+,D6
	TST.B	D6
	BEQ.B	NEXT_LINE_LC
	SUBQ.W	#1,D6
LC_PACK	MOVEQ	#0,D5
	MOVE.B	(A0)+,D5
	ADDA.W	D5,A1
	ADDA.W	D5,A1
	MOVE.B	(A0)+,D5
	TST.B	D5
	BLT.B	LC_POINTS
	SUBQ.W	#1,D5
LC_POINT	MOVEQ	#0,D4
	MOVE.B	(A0)+,D4
	MOVE.W	(0.B,A3,D4.W*2),(A1)+
	DBF	D5,LC_POINT	
RETOUR_LC	DBF	D6,LC_PACK
NEXT_LINE_LC
	ADDI.L	#$280,D3
	MOVEA.L	#ECRAN,A1 
	ADDA.L	D3,A1 
	DBF	D7,LC_LINES 
	ADDA.L	SIZE_DATA_CHUNK,A5
	RTS 

LC_POINTS	EXT.W	D5
	NEG.W	D5
	SUBQ.W	#1,D5 
	MOVEQ	#0,D4 
	MOVE.B	(A0)+,D4
LC_POINTS2	MOVE.W	(0.B,A3,D4.W*2),(A1)+
	DBF	D5,LC_POINTS2 
	BRA.S	RETOUR_LC 
READ_PALETTE
	MOVEA.L	#BUFFER_COLOR,A1
	LEA	10(A0),A2 
	MOVE.W	#$FF,D0 
INSTALLE_COLORS
	MOVEQ	#0,D1 
	MOVEQ	#0,D2 
	MOVE.B	(A2)+,D2
	LSR.W	#1,D2 
	LSL.W	#8,D2 
	LSL.W	#3,D2 
	MOVE.W	D2,D1 
	MOVEQ	#0,D2 
	MOVE.B	(A2)+,D2
	LSR.W	#1,D2 
	LSL.W	#6,D2 
	OR.W	D2,D1 
	MOVEQ	#0,D2 
	MOVE.B	(A2)+,D2
	LSR.W	#1,D2 
	OR.W	D2,D1 
	MOVE.W	D1,(A1)+
	DBF	D0,INSTALLE_COLORS
	ADDA.L	SIZE_DATA_CHUNK,A5
	RTS 


	SECTION DATA
	
ANIMATION	INCBIN	D:\FLI_ANI\ATROCITY.FLI
	EVEN

	SECTION BSS
SAUVE_RES	DS.W	1	
BUFFER_COLOR	
	DS.B      1024
BUFFER	DS.B	8
FRAMES	DS.W	1 
HEIGHT	DS.W	1 
LINE_MIN	DS.L	1 
LINE_NBRE	DS.B	102 
NBRE_SS_CHUNK
	DS.W	1 
PAGE	DS.W	1 
SAUVE_VBL	DS.L	1 
SIZE	DS.L	1 
SIZE_FRAME	DS.B	8 
SIZE_DATA_CHUNK
	DS.L	1 
SPEED	DS.L	1 
TIME	DS.W	1 
TYPE_CHUNK	DS.W	1 
WIDTH	DS.W	1 
SAVE_SCREEN	DS.L	1 
ECRAN	DS.B	121632
