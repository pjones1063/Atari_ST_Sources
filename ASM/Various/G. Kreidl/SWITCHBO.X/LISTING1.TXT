' Beispielroutinen zur Anwendung des
' 3K - AUTOMON 1/2 in GFA-Basic 2.02
'
' Vor dem ersten Aufruf von
' Set_resolution muû zunÑchst mit
' Automon_init initialisiert werden.
' Vor dem Programmende sollte mit 
' Automon_exit die alte Konfiguration 
' (Auflîsung, Farbpalette) wiederhergestellt
' werden; sonst kann man mit GEM bîse 
' öberrraschungen erleben.
' Old_resolution stellt jederzeit im
' Programm die Ausgangssituation
' wieder her (wichtig fÅr GEM-Funktionen).
' Benîtigt werden die globalen Variablen
' Old_reset% und Syscol$, die im Verlauf des
' Programmes keinesfalls geÑndert werden dÅrfen.
' Alle anderen Variablen sind lokal definiert.
'
Procedure Set_resolution(Resolution%,Logbase%,Physbase%,Palette%)
  ' resolution% = Low (0), Medium (1), High (2)
  ' Physbase% = neue physikalische Bildschirmadresse
  '             oder -1
  ' Logbase% = neue logische Bildschirmadresse oder -1
  ' Palette% = Zeiger auf 16 Worte (16 Bit) 
  '            mit neuer Farbpalette oder -1
  Local Xbios_nr%,Gpo%
  If Resolution%=2
    Xbios_nr%=29
    Gpo%=&HBF
  Else
    Xbios_nr%=30
    Gpo%=&H40
  Endif
  If Palette%<>-1
    Void Xbios(6,L:Palette%)
  Endif
  Void Xbios(5,L:Logbase%,L:Physbase%,Resolution%)
  Void Xbios(Xbios_nr%,Gpo%)
Return
'
Procedure Automon_init
  Local Ret_adr%,Locptr%,Loci%
  Ret_adr%=&HFC0000
  Repeat
    Add Ret_adr%,2
  Until Dpeek(Ret_adr%)=&H4E75
  Oldreset%=Lpeek(&H46E)
  Slpoke &H46E,Ret_adr%
  Syscol$=String$(34,0)
  Locptr%=Varptr(Syscol$)
  Dpoke Locptr%,Xbios(4)
  Add Locptr%,2
  For Loci%=0 To 30 Step 2
    Dpoke Locptr%+Loci%,Dpeek(&HFF8240+Loci%)
  Next Loci%
Return
'
Procedure Old_resolution
  @Set_resolution(Dpeek(Varptr(Syscol$)),-1,-1,Varptr(Syscol$)+2)
Return
'
Procedure Automon_exit
  @Old_resolution
  Slpoke &H46E,Oldreset%
Return
