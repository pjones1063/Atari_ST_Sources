*     AUTOCOLOR - Software-Umschaltung auf Farbmonitor
*                 fÅr 3K  AUTOMON 1 / 2
*        wird fÅr Programme benîtigt, die im Farbmodus
*        gestartet werden, aber spÑter auf
*        Monochrom-Ausgabe umschaltbar sein sollen
*        Start aus Desktop oder Åber Auto-Ordner
*  (C) 3K-EDV Entwicklungen -- Kreidl-Knops-Kreidl GBR
*  4150 Krefeld - Nordstr. 23 
*  4154 Tînisvorst 1 - HÅlser. Str. 76
*       Programmierung: G. Kreidl  11/87   V 1.1 9/88

magic_nr: .equ $31415926
resvalid: .equ $426
resvect:  .equ $42A

START:    move.l #SUPER,-(SP)   * Supervisormodus
          move.w #38,-(SP)
          trap #14
          add.l #6,SP
          clr.w -(SP)
          trap  #1              * Ende

SUPER:    move.l #$FF8800,A0    * GPO schon initialisiert?
          move.b #$E,(A0)
          move.b (A0),D0
          and.l #$40,D0
          cmp   #$40,D0
          bne SETMAGIC       * wenn nicht, dann vorbereiten
          rts                * sonst ist Schluû

SETMAGIC: move.l resvalid,MAGICRES   * Magisches Langwort &
          move.l resvect,RESRES      * Resetvektor retten
          move.l #magic_nr,resvalid  * und neu setzen
          move.l #NEWINIT,resvect
          move.l #$FF8800,A0         * Automon Åber GPO auf
          move.b #$E,(A0)            * Farbe programmieren
          move.b (A0),D0
          or.b #$40,D0
          move.b D0,2(A0)
WAIT:     bra WAIT              * Auf Reset vom TOS warten

NEWINIT:  move.l #$FF8800,A0    * Diese Routine ersetzt die
          move.b #7,(A0)        * Soundchip-Initialisierung
          move.b #$C0,2(A0)     * des TOS
          move.b #$E,(A0)
          move.b #$47,2(A0)     * Farbe gesetzt!
          add.l  #64,A6         * alte Init. Åberspringen
          move.l RESRES,A0      * Resetvektor & magisches     
          move.l A0,resvect     * Langword restaurieren
          move.l MAGICRES,D0
          move.l D0,resvalid    * war schon 
          cmp.l #magic_nr,D0    * ein Vektor gÅltig?
          beq   ANOTHER         * dann ausfÅhren
          jmp (A6)              * sonst zurÅck ins TOS
ANOTHER:  jmp (A0)

MAGICRES: dc.l 0
RESRES:   dc.l 0
