' Exemple de r‚cup‚ration de fichier ressource cr‚‚ avec EIKONA.
' Ce listing est librement copiable, vous pouvez en r‚cup‚rer les routines
' et les utiliser dans vos programmes personnels.
'
'       B.MARICHAL      02/93
'
RESERVE 32000
OPTION BASE 0
'
DIM wind&(4)
@init_trees
'
~GRAF_MOUSE(0,0)                ! curseur = flŠche
~RSRC_LOAD("icones.rsc")        ! charger le ressource
~RSRC_GADDR(0,0,newdesk%)       ! chercher adresse de l'arbre des icones
~WIND_GET(0,4,wind&(0),wind&(1),wind&(2),wind&(3))      ! dimensions du bureau
SUB wind&(0),1                  ! modifier les coordonn‚es du fond...
SUB wind&(1),1
ADD wind&(2),2
ADD wind&(3),2
OB_X(newdesk%,0)=wind&(0)
OB_Y(newdesk%,0)=wind&(1)
OB_W(newdesk%,0)=wind&(2)
OB_H(newdesk%,0)=wind&(3)
OB_SPEC(newdesk%,0)=&HFE11C3    ! ...et son aspect
'
~WIND_SET(0,14,(SHR(newdesk%,16) AND &HFFFF),(newdesk% AND &HFFFF),0,0)
~FORM_DIAL(3,0,0,0,0,wind&(0),wind&(1),wind&(2),wind&(3))
'                               ! installer et redessiner le nouveau bureau
MENU menu_str$()                ! installer le menu
'
REPEAT
  IF (event=0) THEN
    event=EVNT_MULTI(18,2,1,1,0,0,0,0,0,0,0,0,0,0,0,0,mx&,my&,mk&,bid&,bid&,nk&)
  ENDIF
  IF ((event AND 16)<>0)                ! ‚vŠnement 'message' ??
    IF (MENU(1)=10)                     ! oui, 'clic dans un menu' ??
      SELECT (MENU(5))                  ! oui, effectuer les actions requises
      CASE 23                           ! pourquoi ??? et ben parce que, na !
        ~FORM_ALERT(2,alerte$)
      CASE 32                           ! idem...
        end=1
      ENDSELECT
      MENU OFF                          ! d‚s‚lectionner le titre
    ENDIF
    event=event AND (NOT 16)
  ELSE
    IF ((event AND 2)<>0)               ! ‚vŠnement 'clic' ??
      i&=OBJC_FIND(newdesk%,0,1,mx&,my&)! chercher l'objet
      IF (i&>0)                         ! trouv‚ ??
        ~GRAF_MKSTATE(mx&,my&,mk&,bid&) ! oui, v‚rifier le bouton gauche
        IF (mk&<>0)                     ! enfonc‚ ??
          oldx&=@getx(newdesk%,i&)      ! oui, d‚placer l'icone
          oldy&=@gety(newdesk%,i&)
          ~GRAF_DRAGBOX(OB_W(newdesk%,i&),OB_H(newdesk%,i&),oldx&,oldy&,wind&(0),wind&(1),wind&(2),wind&(3),newx&,newy&)
          setx(newdesk%,i&,newx&)
          sety(newdesk%,i&,newy&)
          ~FORM_DIAL(3,0,0,0,0,oldx&,oldy&,OB_W(newdesk%,i&),OB_H(newdesk%,i&))
          '                             ! redessiner
          ~FORM_DIAL(3,0,0,0,0,newx&,newy&,OB_W(newdesk%,i&),OB_H(newdesk%,i&))
        ELSE                            ! redessiner ici aussi
          OB_STATE(newdesk%,i&)=OB_STATE(newdesk%,i&) XOR 1
          ~FORM_DIAL(3,0,0,0,0,@getx(newdesk%,i&),@gety(newdesk%,i&),OB_W(newdesk%,i&),OB_H(newdesk%,i&))
        ENDIF                           ! simple clic -> s‚lectionner icone
      ELSE
        ~GEMDOS(2,7)                    ! objet non trouv‚ -> driIIing !
      ENDIF
      event=event AND (NOT 2)
    ENDIF
  ENDIF
UNTIL end=1
'
MENU KILL                               ! enlever le menu
~WIND_SET(0,14,0,0,0,0)                 ! remettre l'ancien bureau
~RSRC_FREE()                            ! lib‚rer le ressource
END
'
FUNCTION getx(tree%,obj&)
  LOCAL x&,y&
  ~OBJC_OFFSET(tree%,obj&,x&,y&)
  RETURN x&
ENDFUNC
'
FUNCTION gety(tree%,obj&)
  LOCAL x&,y&
  ~OBJC_OFFSET(tree%,obj&,x&,y&)
  RETURN y&
ENDFUNC
'
PROCEDURE setx(tree%,obj&,x&)
  LOCAL oldx&
  oldx&=@getx(tree%,obj&)
  OB_X(tree%,obj&)=OB_X(tree%,obj&)+(x&-oldx&)
RETURN
'
PROCEDURE sety(tree%,obj&,y&)
  LOCAL oldy&
  oldy&=@gety(tree%,obj&)
  OB_Y(tree%,obj&)=OB_Y(tree%,obj&)+(y&-oldy&)
RETURN
'
PROCEDURE init_trees
  '
  ' >>>>>>>>>>>>>>D‚finition de l'alerte <<<<<<<<<<<<<<
  '
  alerte$="[0][ Programme de d‚monstration  |d'utilisation  des  fichiers|ressources cr‚‚s avec EIKONA| Vous  pouvez  d‚placer  et|s‚lectionner les ic“nes...][DEMO1.GFA|   OK   ]"
  '
  '
  ' >>>>>>>>>>>>>>> D‚finition du menu <<<<<<<<<<<<<<<<
  '
  menu_data:
  '
  DATA "","  A propos de cet exemple...  ","------------------------------",1,2,3,4,5,6,
  DATA " Fichiers","  Quitter  ",
  DATA "*"
  '
  DIM menu_str$(13)
  '
  RESTORE menu_data
  '
  READ a$
  WHILE a$<>"*"
    menu_str$(i%)=a$
    INC i%
    READ a$
  WEND
  '
RETURN
