100   REM *********************************
110   REM *    STEREO PLAYBACK DEMO       *
120   REM *    ATARI BASIC ROUTINES       *
130   REM *********************************
135   REM (C) AUDIO VISUAL RESEARCH 1991
140   REM THIS PROGRAM PLAYS A MONO SAMPLE ON 
150   REM THE LEFT,RIGHT AND BOTH CHANNELS.
160   REM THE PROGRAM THEN PLAYS A STEREO SAMPLE.
170   REM 
180   REM MAKE SPACE FOR SAMPLE AND CODE
190   DIM c(1000),s(3000),s1(8000)
200   REM 
210   REM ' *****************************
220   REM ' * LOAD MACHINE CODE ROUTINE *
230   REM ' *****************************
240   code=VARPTR(c(0))
250   BLOAD "PLAYBACK.PRG",code
260   code=code+28
270   REM 
280   play=code
290   back=code+4
300   halt=code+8
310   wait=code+12
320   REM 
330   REM  ***************************
340   REM  * LOAD SIGNED MONO SAMPLE *
350   REM  ***************************
360   PRINT "LOADING MONO SAMPLE...."
370   sample=VARPTR(s(0))
380   BLOAD "WELCOME.AVR",sample
390   REM 
400   REM  *********************************
410   REM  * LOAD SIGNED STEREO AVR SAMPLE *
420   REM  *********************************
430   PRINT "LOADING STEREO SAMPLE...."
440   sample1=VARPTR(s1(0))
450   BLOAD "one2.avr",sample1
460   REM 
470   REM  PLAY UNSIGNED MONO SAMPLE
480   REM 
490   REM  POKE START ADDRESS OF SAMPLE + AVR HEADER
500   POKE_L code+20,sample+128
510   REM  POKE LENGTH OF SAMPLE
520   POKE_L code+24,6700
530   REM  POKE SAMPLE FREQUENCY
540   POKE_W code+28,6
550   REM POKE LOOP OFF
560   REM 
570   POKE_W code+30,0
580   REM  POKE PLAY LEFT MODE
590   POKE_W code+32,4
600   PRINT "PLAYING MONO SAMPLE ON LEFT CHANNEL...."
610   CALL play
620   CALL wait
630   REM 
640   REM  POKE PLAY RIGHT MODE
650   POKE_W code+32,5
660   PRINT "PLAYING MONO SAMPLE ON RIGHT CHANNEL...."
670   CALL play
680   CALL wait
690   REM
700   REM  POKE BOTH CHANNEL MODE
710   POKE_W code+32,6
720   PRINT "PLAYING MONO SAMPLE ON BOTH CHANNELS...."
730   REM  POKE MODE
740   CALL play
750   CALL wait
760   PRINT
770   PRINT
780   REM
790   REM  PLAY STEREO AVR SAMPLE
800   REM 
810   REM  POKE START ADDRESSES OF STEREO SAMPLE
820   POKE_L code+20,sample1+128
830   REM  POKE LENGTH OF SAMPLE
840   POKE_L code+24,30000-128
850   REM  POKE LOOP OFF
860   POKE_W code+30,0
870   REM  POKE STEREO PLAY MODE
880   POKE_W code+32,7
890   REM 
900   FOR x=0 TO 4
910   REM POKE SAMPLE FREQUENCY
920   POKE_W code+28,x+4
930   CALL play
940   PRINT "PLAYING STEREO AVR SAMPLE AT FREQUENCY ";x+4
950   CALL wait
960   NEXT x
