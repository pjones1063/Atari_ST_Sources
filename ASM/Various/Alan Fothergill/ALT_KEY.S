***************************
*  Alternate Key Definer  *
*   By Alan Fothergill    *
*   (c)  ATARI ST USER    *
*        Feb.1988         *
***************************

START:
 MOVEA.L 4(A7),A5
 MOVEA.L 24(A5),A0
 SUBA.L A5,A0
 MOVEA.L #STACK,A7
 MOVE.L A0,-(A7)
 MOVE.L A5,-(A7)
 CLR.W -(A7)
 MOVE #$4A,-(A7)
 TRAP #1
 ADDA.L #12,A7
 MOVE.L #STARTMS,-(A7)
 MOVE.W #$09,-(A7)
 TRAP #1
 ADDQ.L #6,A7
 JSR READSTRINGS
 MOVE.L #-1,D0
 MOVE.L D0,-(A7)
 MOVE.L D0,-(A7)
 MOVE.L D0,-(A7)
 MOVE.W #16,-(A7)
 TRAP #14
 ADD.L #14,A7
 MOVEA.L D0,A0
 MOVE.L (A0),KEYTBL
 MOVE.L #READVEC,-(A7)
 MOVE.W #38,-(A7)
 TRAP #14
 ADDQ.L #6,A7
 MOVE.L #PUTVEC,-(A7)
 MOVE.W #38,-(A7)
 TRAP #14
 ADDQ.L #6,A7
 CLR.W -(A7)
 MOVE.L #FINISH,D0
 SUB.L #START,D0
 ADD.L #$100,D0
 MOVE.L D0,-(A7)
 MOVE.W #$31,-(A7)
 TRAP #1
STARTMS:
 DC.B 'Alternate key definer'
 DC.B $0D,$0A
 DC.B 'By Allan Fothergill'
 DC.B $0D,$0A
 DC.B '(c) Atari ST User'
 DC.B $0D,$0A
 DC.B 'Loading A:\ALTNATE.DEF'
 DC.B $0D,$0A,$00,$00
WEDGE:
 MOVE.W (A7)+,D0
 MOVE.L (A7)+,A1
 MOVE.L A7,A0
 BTST #$D,D0
 BNE AA
 MOVE.L USP,A0
AA:
 MOVE.W (A0),D1
 MOVE.W 2(A0),D2
 MOVE.L A1,-(A7)
 MOVE.W D0,-(A7)
 CMP.W #2,D2
 BNE CONTBIOS
 CMP.W #2,D1
 BEQ DOCONIN
 CMP.W #1,D1
 BNE CONTBIOS
 TST.W SENDING
 BEQ CONTBIOS
 MOVE.L #$FFFFFFFF,D0
 RTE
DOCONIN:
 TST.W SENDING
 BEQ REALCONIN
 MOVE.W SENDING,D1
 ADDQ.W #1,SENDING
 MOVE.L ALTNATE,A0
 MOVE.W -1(A0,D1),D0
 TST.B 0(A0,D1)
 BNE AB
 CLR.W SENDING
AB:
 EXT D0
 EXT.L D0
 RTE
REALCONIN:
 MOVE.W #2,-(A7)
 MOVE.W #2,-(A7)
 MOVE.L #RETFROMBIOS,-(A7)
 MOVE SR,-(A7)
 MOVE.L CONT,A0
 JMP (A0)
RETFROMBIOS:
 ADDQ.L #4,A7
 TST.W D0
 BNE AC
 MOVE.L D0,D1
 SWAP D1
 MOVE.L KEYTBL,A0
 EXT.L D1
 ADD.L D1,A0
 MOVE.B (A0),D1
 EXT D1
 SUB.W #'A',D1
 BMI AC
 CMP #26,D1
 BGE AC
 JSR FINDADDR
 MOVE.W #1,SENDING
 BRA DOCONIN
AC:
 RTE
CONTBIOS:
 MOVE.L CONT,A0
 JMP (A0)
FINDADDR:
 MOVE.L #MESSAGE,A0
AD:
 TST.W D1
 BEQ AG
AE:
 TST.B (A0)+
 BNE AE
 SUBQ.W #1,D1
AF:
 TST.B (A0)+
 BEQ AF
 SUBQ #1,A0
 BRA AD
AG:
 MOVE.L A0,ALTNATE
 RTS
READSTRINGS:
 MOVE.L #MESSAGE,A0
 MOVE.W #26*60,D0
AH:
 CLR.B (A0)+
 SUBQ.W #1,D0
 BNE AH
 CLR.W -(A7)
 MOVE.L #FILENAME,-(A7)
 MOVE.W #$3D,-(A7)
 TRAP #1
 ADD.L #8,A7
 MOVE.W D0,HANDLE
 BMI ABORT
 MOVE.L #MESSAGE,-(A7)
 MOVE.L #26*60,-(A7)
 MOVE.W D0,-(A7)
 MOVE.W #$3F,-(A7)
 TRAP #1
 ADD.L #12,A7
 MOVE.W HANDLE,-(A7)
 MOVE.W #$3E,-(A7)
 TRAP #1
 ADD.L #4,A7
 JSR REPMESSG
AI:
 RTS
ABORT:
 MOVE.L #ABORTMS,-(A7)
 MOVE.W #$09,-(A7)
 TRAP #1
 ADDQ.L #6,A7
 CLR.W -(A7)
 TRAP #1
ABORTMS:
 DC.B 'Could not load A:\ALTN'
 DC.B 'ATE.DEF. Aborting....'
 DC.B $0D,$0A,$00
FILENAME:
 DC.B 'A:\ALTNATE.DEF'
 DC.B 0
 EVEN
HANDLE:
 DC.B 1
 EVEN
REPMESSG:
 MOVE.W #26*60,D0
 MOVE.L #MESSAGE,D0
AM:
 TST.B (A0)
 BNE AJ
 MOVE.L A0,D1
 BTST #0,D1
 BEQ AJ
 MOVE.B #'A',(A0)
AJ:
 CMP.B #$0D,(A0)
 BNE AK
 CLR.B (A0)+
 CLR.B (A0)
 SUBQ.W #1,D0
AK:
 CMP.B #'~',(A0)
 BNE AL
 MOVE.B #$0D,(A0)
AL:
 ADDQ.L #1,A0
 SUBQ.W #1,D0
 BPL AM
 RTS
READVEC:
 MOVE.L 180,D0
 MOVE.L D0,CONT
 RTS
PUTVEC:
 MOVE.L #WEDGE,180
 RTS
CONT:
 DC.L 0
ALTNATE:
 DS.L 1
SENDING:
 DC.W 0
KEYTBL:
 DC.L 0
MESSAGE:
 DS.B 26*60
 DS.L 20
STACK:
 DS.L 10
FINISH:
 END


