MODULE Microwire;

(*----------------------------------------------------------------------------
 * System-Version: MOS 3.5
 *----------------------------------------------------------------------------
 * Version       : 1.0
 *----------------------------------------------------------------------------
 * Text-Version  : V#00000
 *----------------------------------------------------------------------------
 * Modul-Holder  : Meinolf Schneider
 *----------------------------------------------------------------------------
 * Copyright May 1990 by Digital Art Meinolf Schneider
 *----------------------------------------------------------------------------
 * MS  : Meinolf Schneider
 *----------------------------------------------------------------------------
 * Datum    Autor Version Bemerkung (Arbeitsbericht)
 *----------------------------------------------------------------------------
 * 27.05.90 MS    1.0     Grundversion
 *----------------------------------------------------------------------------
 * Modul-Beschreibung:
 *
 * Residentes Programm zur Programmierung des Microwire-Interfaces des
 * Atari STE. Hiermit wird der Fehler im Rainbow-TOS 1.6 korrigiert. Das
 * Programm kann in den Autoordner kopiert werden.
 *
 * ACHTUNG beim Linken: Keine Prelink Modul notwendig (z.B. M2Init) !
 *----------------------------------------------------------------------------
 *) (*$S-,M-,N+,L-,C-*)

CONST (* GEMDOS functions *)
      SuperOn   =         $20;
      SuperOff  =         $20;
      PTerm0    =         $00;

BEGIN
  ASSEMBLER
  
  ; Titletext ausgeben:
  lea           Title(PC),A0
  
  clr.w         D0
 !NextTitleChar
  move.b        0(A0,D0.W),D2
  beq           TitleED
  
  movem.l       A0/D0,-(A7)
  move.w        D2,-(A7)
  move.w        #2,-(A7)
  move.w        #3,-(A7)
  trap          #13
  addq.w        #6,A7
  movem.l       (A7)+,A0/D0
  addq.w        #1,D0
  bra           NextTitleChar
 !TitleED
  
  
  ; Mittels Bus-Error Microwire-Interface erkennen:
  clr.l         -(A7)
  move.w        #SuperOn,-(A7)
  trap          #1
  addq.l        #6,A7
  
  move.w        SR,D2
  ori.w         #$0700,SR                 ; Interrupts abschalten
  lea           $FF8900,A2
  move.l        $8,A1                     ; Bus-Error-Vektor retten
  pea           BusErr(PC)
  move.l        (A7)+,$8                  ; und eigenen setzen
  tst.b         (A2)
  bra           NoBusErrThere
 !BusErr
  lea           14(A7),A7                 ; war wohl nix...
  bra.w         Goon
 !NoBusErrThere
  move.w        #$07FF,$24(A2)
  move.w        #10011101000%,D1          ; Master Volume 0 dB
  bsr.s         MicrowireWrite
  move.w        #10101010011%,D1          ; Left Volume 0 dB
  bsr.s         MicrowireWrite
  move.w        #10100010100%,D1          ; Right Volume 0 dB
  bsr.s         MicrowireWrite
  move.w        #10010001100%,D1          ; Treble -6 dB
  bsr.s         MicrowireWrite
  move.w        #10001000110%,D1          ; Bass  0 dB
  bsr.s         MicrowireWrite
  move.w        #10000000001%,D1          ; MIX GI
  bsr.s         MicrowireWrite
  bra           Goon
  
 !MicrowireWrite
  cmpi.w        #$07FF,$24(A2)
  bne.s         MicrowireWrite
  move.w        D1,$22(A2)
  rts
  
 !Goon
  move.l        A1,$8
  move.w        D2,SR                   ; Interrupts wieder freigeben
  
  move.l        D0,-(A7)
  move.w        #SuperOff,-(A7)
  trap          #1
  addq.l        #6,A7
  
  ; Programm beenden
  move.w        #PTerm0,-(A7)
  trap          #1
  
  
 !Title
  dc.b          15c, 14c
  asc           'Ð MICROWIRE-CORRECTION ½ May 1990 by Meinolf Schneider'
  dc.b          15c, 14c, 0c
  END
END Microwire.

