;*** AES ***

AES:MACRO f
 MOVE.L #\1,D0
 LEA CONTRL,A0
 MOVEP.L D0,1(A0)
 MOVE.L #AESAA,D1
 MOVE #200,D0
 TRAP #2
 ENDM

; SHEL_READ

; SHEL_WRITE

SHEL_ENVRN:MACRO b,v
 MOVE.L \1,ADDRIN
 MOVE.L \2,ADDRIN+4
 AES $7D000102
 MOVE INTOUT,D0
 ENDM

SHEL_FIND:MACRO f
 MOVE.L \1,ADDRIN
 AES $7C000101
 MOVE INTOUT,D0
 ENDM

; SHEL_GET

; SHEL_PUT

; SCRP_READ

; SCRP_WRITE

FILE_SELECT:MACRO p,f,x,i
 LEA ADDRIN,A0
 MOVE.L \1,(A0)+
 MOVE.L \2,(A0)+
 MOVE.L \4,(A0)
 MOVE.L #$5A000202,D0
 CMPI #$0140,GLOBAL
 BLO.S .L1
 MOVE.L #$5B000203,D0
.L1 LEA CONTRL,A0
 MOVEP.L D0,1(A0)
 MOVE.L #AESAA,D1
 MOVE #200,D0
 TRAP #2
 MOVE INTOUT,D0
 MOVE INTOUT+2,\3
 ENDM

APPL_INIT:MACRO
 AES $0A000100
 MOVE INTOUT,D0
 ENDM

APPL_EXIT:MACRO
 AES $13000100
 MOVE INTOUT,D0
 ENDM

; APPL_FIND

; APPL_READ

; APPL_WRITE

; APPL_TRECORD

; APPL_TPLAY

 ;pour les EVNT  D0=states|scan|0|ascii

EVNT_KBRD:MACRO
 AES $14000100
 CLR.L D0
 MOVE.B INTOUT,D0
 SWAP D0
 MOVE.B INTOUT+1,D0
 ENDM

EVNT_BUTT:MACRO ci,m,b,co,xy
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVE \3,(A0)
 AES $15030500
 LEA INTOUT,A0
 MOVE (A0)+,\4
 MOVEA.L \5,A1
 MOVE.L (A0)+,(A1)
 CLR.L D0
 MOVE (A0)+,D0
 ROXR.B #1,D0
 ROR.B #1,D0
 ROXR.B #2,D0
 ANDI.B #%1100000,D0
 OR (A0),D0
 ROR.L #8,D0
 ENDM

EVNT_MOUS:MACRO i,a,xy
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $16050500
 LEA INTOUT+2,A0
 MOVEA.L \3,A1
 MOVE.L (A0)+,(A1)
 CLR.L D0
 MOVE (A0)+,D0
 ROXR.B #1,D0
 ROR.B #1,D0
 ROXR.B #2,D0
 ANDI.B #%1100000,D0
 OR (A0),D0
 ROR.L #8,D0
 ENDM

EVNT_TIME:MACRO t
 LEA INTIN,A0
 MOVE.L \1,D0
 SWAP D0
 MOVE.L D0,(A0)
 AES $18020100
 ENDM

EVNT_MESS:MACRO b
 MOVE.L \1,ADDRIN
 AES $17000101
 ENDM

EVNT_MULTI:MACRO ei,ci,m,b,i1,a1,i2,a2,t,bf,eo,xy,co
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVE \3,(A0)+
 MOVE \4,(A0)+
 MOVE \5,(A0)+
 MOVEA.L \6,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVE \7,(A0)+
 MOVEA.L \8,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVE.L \9,D0
 SWAP D0
 MOVE.L D0,(A0)
 MOVE.L \a,ADDRIN
 AES $19100701
 LEA INTOUT,A0
 MOVE (A0)+,\b
 MOVEA.L \c,A1
 MOVE.L (A0)+,(A1)
 MOVE (A0)+,D0
 ROXR.B #1,D0
 ROR.B #1,D0
 ROXR.B #2,D0
 ANDI.B #%1100000,D0
 OR (A0)+,D0
 LSL.W #8,D0
 OR.B (A0)+,D0
 SWAP D0
 CLR D0
 MOVE.B (A0)+,D0
 MOVE (A0),\d
 ENDM

; MOUS_DCLICK

RSRC_LOAD:MACRO f
 MOVE.L \1,ADDRIN
 AES $6E000101
 MOVE INTOUT,D0
 ENDM

RSRC_FREE:MACRO
 AES $6F000100
 MOVE INTOUT,D0
 ENDM

RSRC_GADDR:MACRO t,p
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 MOVE #1,CONTRL+8
 AES $70020100
 CLR CONTRL+8
 LEA ADDROUT,A0
 TST INTOUT
 BNE.S .L1
 CLR.L (A0)
.L1 MOVE.L (A0),D0
 ENDM

RSRC_SADDR:MACRO t,p,a
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 MOVE.L \3,ADDRIN
 AES $71020101
 MOVE INTOUT,D0
 ENDM

RSRC_OBJCFIX:MACRO o,t
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $72010101
 MOVE INTOUT,D0
 ENDM

; OBJC_ADD

; OBJC_DELETE

OBJC_CHANGE:MACRO o,a,s,r,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 CLR (A0)+
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVE \3,(A0)+
 MOVE \4,(A0)
 MOVE.L \5,ADDRIN
 AES $2F080101
 MOVE INTOUT,D0
 ENDM

; OBJC_ORDER

OBJC_OFFSET:MACRO o,t,xy
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $2C010301
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVE.L (A0),\3
 ENDM

OBJC_DRAW:MACRO o,d,a,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVEA.L \3,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 MOVE.L \4,ADDRIN
 AES $2A060101
 MOVE INTOUT,D0
 ENDM

OBJC_FIND:MACRO o,d,xy,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVEA.L \3,A1
 MOVE.L (A1),(A0)
 MOVE.L \4,ADDRIN
 AES $2B040101
 MOVE INTOUT,D0
 ENDM

; OBJC_EDIT

MENU_BAR:MACRO f,t
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $1E010101
 MOVE INTOUT,D0
 ENDM

MENU_TITLE:MACRO o,f,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 MOVE.L \3,ADDRIN
 AES $21020101
 MOVE INTOUT,D0
 ENDM

; MENU_TEXT

MENU_CHECK:MACRO o,f,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 MOVE.L \3,ADDRIN
 AES $1F020101
 MOVE INTOUT,D0
 ENDM

MENU_ENABLE:MACRO o,f,t
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 MOVE.L \3,ADDRIN
 AES $20020101
 MOVE INTOUT,D0
 ENDM

MENU_ACC:MACRO m
 MOVE GLOBAL+4,INTIN
 MOVE.L \1,ADDRIN
 AES $23010101
 MOVE INTOUT,D0
 ENDM

WIND_CREATE:MACRO f,a
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $64050100
 MOVE INTOUT,D0
 ENDM

WIND_DELETE:MACRO h
 MOVE \1,INTIN
 AES $67010100
 MOVE INTOUT,D0
 ENDM

WIND_NEW:MACRO
 AES $6D000000
 ENDM

WIND_OPEN:MACRO h,a
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $65050100
 MOVE INTOUT,D0
 ENDM

WIND_CLOSE:MACRO h
 MOVE \1,INTIN
 AES $66010100
 MOVE INTOUT,D0
 ENDM

WIND_GET:MACRO h,f,p
 MOVE \1,INTIN
 MOVE \2,INTIN+2
 AES $68020500
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVEA.L \3,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 ENDM

WIND_SET:MACRO h,f,p
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVEA.L \3,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $69060100
 MOVE INTOUT,D0
 ENDM

WIND_CALC:MACRO t,f,ai,ao
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVEA.L \3,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $6C060500
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVEA.L \4,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 ENDM

WIND_FIND:MACRO xy
 MOVE.L \1,INTIN
 AES $6A020100
 MOVE INTOUT,D0
 ENDM

WIND_UPDATE:MACRO f
 MOVE \1,INTIN
 AES $6B010100
 MOVE INTOUT,D0
 ENDM

FORM_CENTER:MACRO t,a
 MOVE.L \1,ADDRIN
 AES $36000501
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVEA.L \2,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 ENDM

FORM_DIAL:MACRO f,as,af
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVEA.L \3,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 CLR.L ADDRIN
 AES $33090101
 MOVE INTOUT,D0
 ENDM

FORM_DO:MACRO o,t
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $32010101
 MOVE INTOUT,D0
 ENDM

FORM_ALERT:MACRO d,b
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $34010101
 MOVE INTOUT,D0
 ENDM

FORM_ERROR:MACRO e
 MOVE \1,INTIN
 AES $35010100
 ENDM

GRAF_HANDLE:MACRO
 AES $4D000500
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVE.L (A0)+,SCR_CELLW
 MOVE.L (A0),SCR_CELLW
 ENDM

GRAF_MSKBSTATE:MACRO xy
 AES $4F000500
 LEA INTOUT+2,A0
 MOVE.L (A0)+,\1
 CLR.L D0
 MOVE (A0)+,D0
 ROXR.B #1,D0
 ROR.B #1,D0
 ROXR.B #2,D0
 ANDI.B #%1100000,D0
 OR (A0),D0
 ROR.L #8,D0
 ENDM

GRAF_MOUSE:MACRO f,d
 MOVE \1,INTIN
 MOVE.L \2,ADDRIN
 AES $4E010101
 ENDM

; GRAF_WATCHBOX

; GRAF_SLIDEBOX

GRAF_RUBBERBOX:MACRO a,xy
 MOVEA.L \1,A0
 LEA INTIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 AES $46040300
 LEA INTOUT,A0
 MOVE (A0)+,D0
 MOVE.L (A0),\2
 ENDM

; GRAF_DRAGBOX

; GRAF_MOVEBOX

GRAF_GROWBOX:MACRO as,af
 LEA INTIN,A0
 MOVEA.L \1,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $49080100
 ENDM

GRAF_SHRINKBOX:MACRO as,af
 LEA INTIN,A0
 MOVEA.L \2,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)+
 MOVEA.L \1,A1
 MOVE.L (A1)+,(A0)+
 MOVE.L (A1),(A0)
 AES $4A080100
 ENDM
