;*** VDI ***

VDI:MACRO
 MOVE.L #VDIAA,D1
 MOVEQ #115,D0
 TRAP #2
 ENDM

VDIF:MACRO f
 MOVE.L #\1,D0
 LEA CONTRL,A0
 MOVEP.L D0,1(A0)
 VDI
 ENDM

VDIF2:MACRO f,f2
 MOVE.L #\1,D0
 LEA CONTRL,A0
 MOVEP.L D0,1(A0)
 MOVE #\2,10(A0)
 VDI
 ENDM

GRPH_OPEN:MACRO
 LEA INTIN,A0
 MOVE.L #$10001,D0
 MOVE.L D0,(A0)+
 MOVE.L D0,(A0)+
 MOVE.L D0,(A0)+
 MOVE.L D0,(A0)+
 MOVE.L D0,(A0)+
 MOVE #2,(A0)
 VDIF $6400000B
 LEA INTOUT,A0
 MOVE (A0),D0
 ADDQ #1,D0
 MOVE D0,SCR_W
 MOVE 2(A0),D0
 ADDQ #1,D0
 MOVE D0,SCR_H
 MOVE 26(A0),D0
 MOVE D0,SCR_REG
 CMPI #256,D0
 BNE.S .L1
 MOVE #1,INTIN
 VDIF $66000001
 TST INTOUT+10
 BNE.S .L1
 CLR SCR_REG
.L1 NOP
 ENDM

GRPH_CLOSE:MACRO
 VDIF $65000000
 ENDM

GRPH_SCLIP:MACRO xy,f
 MOVEA.L \1,A0
 LEA PTSIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 MOVE \2,INTIN
 VDIF $81020001
 ENDM

GRPH_SMODE:MACRO me,ms
 MOVE \1,INTIN
 VDIF $20000001
 MOVE INTOUT,\2
 ENDM

COLR_GATTRIB:MACRO ce,f,cs,r,g,b
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 VDIF $1A000002
 LEA INTOUT,A0
 MOVE (A0)+,\3
 MOVE (A0)+,\4
 MOVE (A0)+,\5
 MOVE (A0),\6
 ENDM

GRPH_SCOLOR:MACRO c,r,g,b
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)+
 MOVE \3,(A0)+
 MOVE \4,(A0)
 VDIF $0E000004
 ENDM

POIN_GATTRIB:MACRO wh,t,c,m
 VDIF $24000000
 MOVE.L PTSOUT,\1
 LEA INTOUT,A0
 MOVE (A0)+,\2
 MOVE (A0)+,\3
 MOVE (A0),\4
 ENDM

POIN_STYPE:MACRO te,ts
 MOVE \1,INTIN
 VDIF $12000001
 MOVE INTOUT,\2
 ENDM

POIN_SHEIGHT:MACRO h,wh
 LEA PTSIN,A0
 CLR (A0)+
 MOVE \1,(A0)
 VDIF $13010000
 MOVE.L PTSOUT,\2
 ENDM

POIN_SCOLOR:MACRO c
 MOVE \1,INTIN
 VDIF $14000001
 ENDM

POIN_GET:MACRO xy,r,c
 MOVE.L \1,PTSIN
 VDIF $69010000
 LEA INTOUT,A0
 MOVE (A0)+,\2
 MOVE (A0),\1
 ENDM

GRPH_POINTS:MACRO xy,n
 LEA CONTRL,A0
 MOVE #7,(A0)+
 MOVE \2,D0
 MOVE D0,(A0)+
 CLR.L (A0)
 MOVEA.L \1,A0
 SUBQ #1,D0
 LEA PTSIN,A1
.L1 MOVE.L (A0)+,(A1)+
 DBRA D0,.L1
 VDI
 ENDM

LINE_GATTRIB:MACRO w,t,c,m,b,e
 VDIF $23000000
 MOVE PTSOUT,\1
 LEA INTOUT,A0
 MOVE (A0)+,\2
 MOVE (A0)+,\3
 MOVE (A0)+,\4
 MOVE (A0)+,\5
 MOVE (A0),\6
 ENDM

LINE_STYPE:MACRO te,ts
 MOVE \1,INTIN
 VDIF $0F000001
 MOVE INTOUT,\2
 ENDM

LINE_SUSDFTYPE:MACRO t
 MOVE \1,INTIN
 VDIF $71000001
 ENDM

LINE_SENDS:MACRO b,e
 LEA INTIN,A0
 MOVE \1,(A0)+
 MOVE \2,(A0)
 VDIF $6C000002
 ENDM

LINE_SWIDTH:MACRO we,ws
 LEA PTSIN,A0
 MOVE \1,(A0)
 CLR (A0)
 VDIF $10010000
 MOVE PTSOUT,\2
 ENDM

LINE_SCOLOR:MACRO ce,cs
 MOVE \1,INTIN
 VDIF $11000001
 MOVE INTOUT,\2
 ENDM

GRPH_LINES:MACRO xy,n
 LEA CONTRL,A0
 MOVE #6,(A0)+
 MOVE \2,D0
 MOVE D0,(A0)+
 CLR.L (A0)
 MOVEA.L \1,A0
 SUBQ #1,D0
 LEA PTSIN,A1
.L1 MOVE.L (A0)+,(A1)+
 DBRA D0,.L1
 VDI
 ENDM

GRPH_RECTANGLE:MACRO xy
 MOVEA.L \1,A0
 LEA PTSIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 VDIF2 $0B020000,1
 ENDM

GRPH_RRECT:MACRO xy
 MOVEA.L \1,A0
 LEA PTSIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 VDIF2 $0B020000,8
 ENDM

GRPH_CIRCLE:MACRO xy,r
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 CLR.L (A0)+
 MOVE \2,(A0)+
 CLR (A0)
 VDIF2 $0B030000,4
 ENDM

GRPH_ARC:MACRO xy,r,b,e
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 CLR.L (A0)+
 CLR.L (A0)+
 MOVE \2,(A0)+
 CLR (A0)
 LEA INTIN,A0
 MOVE \3,(A0)+
 MOVE \4,(A0)
 VDIF2 $0B040002,2
 ENDM

GRPH_ELLIPSE:MACRO xy,rr
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 MOVE.L \2,(A0)
 VDIF2 $0B020000,5
 ENDM

GRPH_EARC:MACRO xy,rr,b,e
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 MOVE.L \2,(A0)
 LEA INTIN,A0
 MOVE \3,(A0)+
 MOVE \4,(A0)
 VDIF2 $0B020002,6
 ENDM

FILL_GATTRIB:MACRO t,c,s,m,p
 VDIF $25000000
 LEA INTOUT,A0
 MOVE (A0)+,\1
 MOVE (A0)+,\2
 MOVE (A0)+,\3
 MOVE (A0)+,\4
 MOVE (A0),\5
 ENDM

FILL_STYPE:MACRO te,ts
 MOVE \1,INTIN
 VDIF $17000001
 MOVE INTOUT,\2
 ENDM

FILL_SSUBTYPE:MACRO se,ss
 MOVE \1,INTIN
 VDIF $18000001
 MOVE INTOUT,\2
 ENDM

FILL_SUSDFTYPE:MACRO f,p
 LEA CONTRL,A0
 MOVE #112,(A0)+
 CLR.L (A0)+
 MOVE \2,D0
 MOVE D0,(A0)
 SUBQ #1,D0
 MOVEA.L \1,A0
 LEA INTIN,A1
.L2 MOVEQ #7,D1
.L1 MOVE.L (A0)+,(A1)+
 DBRA D1,.L1
 DBRA D0,.L2
 VDI
 ENDM

FILL_SPERIMETER:MACRO pe,ps
 MOVE \1,INTIN
 VDIF $68000001
 MOVE INTOUT,\2
 ENDM

FILL_SCOLOR:MACRO ce,cs
 MOVE \1,INTIN
 VDIF $19000001
 MOVE INTOUT,\2
 ENDM

GRPH_CFILL:MACRO xy,c
 MOVE.L \1,PTSIN
 MOVE \2,INTIN
 VDIF $67010001
 ENDM

GRPH_LFILL:MACRO xy,n
 LEA CONTRL,A0
 MOVE #9,(A0)+
 MOVE \2,D0
 MOVE D0,(A0)+
 CLR.L (A0)
 MOVEA.L \1,A0
 SUBQ #1,D0
 LEA PTSIN,A1
.L1 MOVE.L (A0)+,(A1)+
 DBRA D0,.L1
 VDI
 ENDM

GRPH_FRECT:MACRO xy
 MOVEA.L \1,A0
 LEA PTSIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 VDIF $72020000
 ENDM

GRPH_FRRECT:MACRO xy
 MOVEA.L \1,A0
 LEA PTSIN,A1
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 VDIF2 $0B020000,9
 ENDM

GRPH_SECTOR:MACRO xy,r,b,e
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 CLR.L (A0)+
 CLR.L (A0)+
 MOVE \2,(A0)+
 CLR (A0)
 LEA INTIN,A0
 MOVE \3,(A0)+
 MOVE \4,(A0)
 VDIF2 $0B040002,3
 ENDM

GRPH_ESECTOR:MACRO xy,rr,b,e
 LEA PTSIN,A0
 MOVE.L \1,(A0)+
 MOVE.L \2,(A0)
 LEA INTIN,A0
 MOVE \3,(A0)+
 MOVE \4,(A0)
 VDIF2 $0B020002,7
 ENDM

GRPH_BLKPIXEL:MACRO s,d,xys,xyd,m
 MOVE.L \1,CONTRL+14
 MOVE.L \2,CONTRL+18
 LEA PTSIN,A1
 MOVEA.L \3,A0
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)+
 MOVE.L \4,A0
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 MOVE \5,INTIN
 VDIF $6D040001
 ENDM

GRPH_BLKBIT:MACRO s,d,xys,xyd,m,b,f
 MOVE.L \1,CONTRL+14
 MOVE.L \2,CONTRL+18
 LEA PTSIN,A1
 MOVEA.L \3,A0
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)+
 MOVE.L \4,A0
 MOVE.L (A0)+,(A1)+
 MOVE.L (A0),(A1)
 LEA INTIN,A0
 MOVE \5,(A0)+
 MOVE \6,(A0)+
 MOVE \7,(A0)
 VDIF $79040003
 ENDM

GRPH_BLKTRANS:MACRO s,d
 MOVE.L \1,CONTRL+14
 MOVE.L \2,CONTRL+18
 VDIF $6E000000
 ENDM

; TEXT_SFONTS

; TEXT_CFONTS

; TEXT_GFONT

; TEXT_SFONT

; TEXT_GINFO

; TEXT_GCHAR

; TEXT_SHEIGHTPIX

; TEXT_SHEIGHTPNT

; TEXT_GATTRIB

; TEXT_SBASE

; TEXT_SALIGN

; TEXT_STYPE

; TEXT_SCOLOR

; TEXT_GEXTENT

; GRPH_JTEXT

; GRPH_TEXT

SCRN_SLOFFSET:MACRO l
 MOVE \1,INTIN
 VDIF2 $05000001,101
 ENDM

SCRN_SFONT:MACRO f
 MOVE.L \1,INTIN
 VDIF2 $05000002,102
 ENDM

SCRN_GCELLS:MACRO c,l
 VDIF2 $05000000,1
 LEA INTOUT,A0
 MOVE (A0)+,\2
 MOVE (A0),\1
 ENDM

SCRN_GPOSITION:MACRO c,l
 VDIF2 $05000000,15
 LEA INTOUT,A0
 MOVE (A0)+,\2
 MOVE (A0),\1
 ENDM

SCRN_TEXT:MACRO s,n,m
 LEA CONTRL,A0
 MOVE #5,(A0)+
 CLR.L (A0)+
 IFEQ \3
 CLR (A0)+
 ELSE
 MOVE \2,(A0)+
 ENDC
 CLR (A0)+
 MOVE #12,(A0)
 IFEQ \3
 MOVEA.L \1,A0
 LEA INTIN,A1
 MOVEQ #127,D0
 CLR D1
 MOVE #128,D2
 MOVE.B (A0)+,D1
 MOVE D1,(A1)+
 DBEQ D0,#-6
 ADDQ #1,D0
 SUB D0,D2
 MOVE D2,CONTRL+6
 MOVE.L #VDIAA,D1
 ELSE
 MOVE.L \1,.AA+4
 MOVE.L #.AA,D1
 _D
.AA: DC.L CONTRL,0,PTSIN,INTOUT,PTSOUT
 _P
 ENDC
 MOVEQ #115,D0
 TRAP #2
 IFNE \3
 CLR CONTRL+6
 ENDC
 ENDM

MOUS_GET:MACRO xy
 VDIF $7C000100
 MOVEA.L \1,A0
 MOVE.L PTSOUT,(A0)
 CLR.L D0
 MOVE INTOUT,D0
 ROXR #1,D0
 ROR.L #1,D0
 ROXR.L #2,D0
 ENDM

MOUS_SHOW:MACRO c
 MOVE \1,INTIN
 VDIF $7A000001
 ENDM

MOUS_HIDE:MACRO c
 MOVE \1,INTIN
 VDIF $7B000001
 ENDM

; MOUS_SFORM

MOUS_SVBUTT:MACRO v
 MOVE.L \1,CONTRL+14
 VDIF $7D000000
 MOVE.L CONTRL+18,D0
 ENDM

MOUS_SVMOVT:MACRO v
 MOVE.L \1,CONTRL+14
 VDIF $7E000000
 MOVE.L CONTRL+18,D0
 ENDM

MOUS_SVCHGT:MACRO v
 MOVE.L \1,CONTRL+14
 VDIF $7F000000
 MOVE.L CONTRL+18,D0
 ENDM

TIME_SV:MACRO v
 MOVE.L \1,CONTRL+14
 VDIF $76000000
 MOVE.L CONTRL+18,D0
 ENDM
