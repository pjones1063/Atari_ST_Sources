'
' On reserve de la place pour le Malloc
'
RESERVE 32000
CLS
'
' On cree un tableau de nombre aleatoires
'
DIM nombre%(100)
FOR i%=0 TO 99
  nombre%(i%)=RAND(&HFFFF)
NEXT i%
'
' On demande une ability
'
ability%=FN dsp_requestuniqueability
'
' On reserve de la place pour le code DSP
'
adr%=MALLOC(1024*3)
'
' Converti le .LOD en code binaire DSP
' dsp_lodtobinary semble bugg‚e, elle ajoute 3 mots DSP
' au debut du code binaire, il faut donc les supprimer
' car ils font planter dsp_loadsubroutine !
'
size%=FN dsp_lodtobinary("RELOGE.LOD",adr%)
adr%=adr%+3*3
'
' Si le DSP est libre
'
IF size%>=0
  IF FN dsp_lock=0
    '
    ' On regarde si notre routine ne serait pas deja l…
    '
    handle%=FN dsp_inqsubrability(ability%)
    '
    ' sinon, il faut la recharger
    '
    IF hanlde%<=0
      handle%=FN dsp_loadsubroutine(adr%,size%,ability%)
    ENDIF
    '
    ' Maintenant qu'on a un handle, on peut ex‚cuter la subroutine
    '
    IF handle%>0
      status%=FN dsp_runsubroutine(handle%)
      ~FN dsp_blkunpacked(V:nombre%(0),100,V:nombre%(0),100)
      '
      ' On affiche le resultat
      '
      FOR i%=0 TO 99
        PRINT nombre%(i%),
      NEXT i%
    ENDIF
    ~FN dsp_unlock
  ELSE
    PRINT "Le dsp n'est pas libre!"
  ENDIF
ELSE
  PRINT "Le fichier RELOGE.LOD est introuvable."
ENDIF
'
PRINT "Appuyez sur une touche."
~INP(2)
~MFREE(adr%)
'
' Fonctions DSP
'
> FUNCTION dsp_lock
RETURN XBIOS(104)
ENDFUNC
> FUNCTION dsp_unlock
RETURN XBIOS(105)
ENDFUNC
> FUNCTION dsp_lodtobinary(file$,codeptr%)
RETURN XBIOS(111,L:V:file$,L:codeptr%)
ENDFUNC
> FUNCTION dsp_requestuniqueability
RETURN XBIOS(113)
ENDFUNC
> FUNCTION dsp_blkunpacked(data_in%,size_in%,data_out%,size_out%)
RETURN XBIOS(98,L:data_in%,L:size_in%,L:data_out%,L:size_out%)
ENDFUNC
> FUNCTION dsp_loadsubroutine(ptr%,size%,ability%)
RETURN XBIOS(116,L:ptr%,L:size%,W:ability%)
ENDFUNC
> FUNCTION dsp_inqsubrability(ability%)
RETURN XBIOS(117,W:ability%)
ENDFUNC
> FUNCTION dsp_runsubroutine(handle%)
RETURN XBIOS(118,W:handle%)
ENDFUNC
> FUNCTION dsp_flushsubroutines
RETURN XBIOS(115)
ENDFUNC
