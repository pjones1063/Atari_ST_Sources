;----------------------------------------------------------------------
;----------------------------------------------------------------------
; Texture mapping dsp subroutine.
; By Martin Griffiths, August 1993
;----------------------------------------------------------------------
;----------------------------------------------------------------------

IPR		EQU	$ffff
BCR		EQU	$fffe
MYPBC		EQU	$ffe0
PCC		EQU	$ffe1
PCDDR		EQU	$ffe3
PCD		EQU	$ffe5
RX		EQU	$FFEF
TX		EQU	$FFEF
CRA		EQU	$FFEC
CRB		EQU	$FFED
HSR		EQU	$ffe9
HRX		EQU	$ffeb

xgrad		EQU 	1
ygrad		EQU 	2
xsave		EQU 	3
ysave		EQU 	4

gradtab		EQU	$100

	org	p:$0
	jmp	start

	org	p:$40
start

	movep	#0,x:BCR
	movep	#1,x:MYPBC
	andi	#$fc,mr

loop	jclr	#0,x:HSR,loop		; wait for cpu...
	movep	x:HRX,x1		; fetch dummy.

	jsr	make_gradtab

; Mandelbrot...

texture	move	#>gradtab,r2
get_dx	jclr	#0,x:HSR,get_dx		
	movep	x:HRX,n2	; dx
get_w	jclr	#0,x:HSR,get_w		
	movep	x:HRX,x0	; sx1
get_h	jclr	#0,x:HSR,get_h		
	movep	x:HRX,y0	; sy1
get_x	jclr	#0,x:HSR,get_x		
	movep	x:HRX,a		; sx2
get_y	jclr	#0,x:HSR,get_y		
	movep	x:HRX,b	 	; sy2 
	sub	x0,a	x:(r2+n2),y1	  ; sdx
	sub	y0,b		a,x1	  ; sdy
	mpy	y1,x1,a		b,x1 	  ; sdx / dx
	mpy	y1,x1,b		a,l:xgrad ; sdy / dy
	move	b,l:ygrad
	move	x0,a
	move	y0,b
	move	l:xgrad,x
	do	n2,text_lp
	move	b1,y0
	clr 	b		b,l:ysave
	move	a,b0		
	asl	b		#>256,y1
	mac	y1,y0,b		l:ygrad,y
sen	jclr	#1,x:HSR,sen
	movep	b0,x:HRX
	add	x,a		l:ysave,b
	add	y,b		
text_lp

	jmp 	texture

; Create 1/x table

make_gradtab
	move	#>gradtab,r0
	clr	a	#>1,r1
	move	a,x:(r0)+
	do	#640,make_tab_lp
	move	#>1,a
	move	r1,y0
	and	#$fe,ccr
	rep 	#24
	div	y0,a
	add	y0,a		(r1)+
	move	a0,x:(r0)+	
make_tab_lp
	rts

	end	