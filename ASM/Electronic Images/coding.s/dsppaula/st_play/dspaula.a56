;----------------------------------------------------------------------
; DSP 4 channel Player.
; 56001 source. (very fast)
; By Martin Griffiths, August 1993
; uses $6000-$8000 in both X and Y memory...
;----------------------------------------------------------------------

IPR		EQU	$FFFF
BCR		EQU	$FFFE
PBC		EQU	$FFE0
PCC		EQU	$FFE1
PCDDR		EQU	$FFE3		; Port C data direction
PCD		EQU	$FFE5		; Port C data register
RX		EQU	$FFEF
TX		EQU	$FFEF
CRA		EQU	$FFEC
CRB		EQU	$FFED
HCR		EQU	$FFE8		; host command register
HSR		EQU	$FFE9		; host status register
HRX		EQU	$FFEB
HTX		EQU	$FFEB

; Voice Structure.

frequency       equ 0   ; frequency fraction for voice. (24bit fraction) 
curr_fraction   equ 1   ; current fraction part. 
last_sample	equ 2   ; 
			; 


cbufsize	EQU $1FC0
bufsize		EQU $800-16

; Sample buffer X: memory address for each voice.

		org x:$4000
chn1buf		ds bufsize
chn2buf		ds bufsize
chn3buf		ds bufsize
chn4buf		ds bufsize
chn5buf		ds bufsize
chn6buf		ds bufsize
chn7buf		ds bufsize
chn8buf		ds bufsize
vc1_data	ds 4
vc2_data	ds 4
vc3_data	ds 4
vc4_data	ds 4
vc5_data	ds 4
vc6_data	ds 4
vc7_data	ds 4
vc8_data	ds 4
vces_8_flag	dc 0



; Circular buffer for dac output in Y: memory.

		org y:$4000
circbuf 	ds cbufsize

	org	p:$0
	jmp	start			; -> start

; DAC xmit FAST interrupt.

	org	p:$10			; ssi transmit data
xmit_int
	movep	y:(r7)+,x:RX		; output sample word

; Music routine, host command 2.

	org	p:$26
	jsr	musicplayer_int		; host command 2
	
	org	p:$40
start	movep	#0,x:BCR
	movep	#1,x:PBC
	bset	#2,x:HCR		; enable host commands	
	movep	#$0c00,x:IPR		; Set the IPR for HOST COMMANDS 
	jsr	setup_player
	andi	#$f0,mr			; unmask interrupts
	nop
forever	nop
	nop
	nop
	jmp 	forever

;
; Music routines.  
;

; Add a voice to the buffer (NO interpolation)
; r0 -> sample buffer to apply volume and frequency shift too.
; r1 -> voice structure for this voice
; n2 -> no. of sample words to do.

add_voice_ni
	clr	a	R4,R6		; r6 -> fill
	nop
	do	n2,end_lni
	add	x,b	x:(r0)+,a	y:(r6),y1	
	add	y1,a	
	move	a,y:(r6)+n6	
end_lni
	nop
	nop
	rts

; Add a voice to the buffer (interpolate)
; r0 -> sample buffer to apply volume and frequency shift too.
; r1 -> voice structure for this voice
; n2 -> no. of sample words to do.

add_voice_int
	rts

; The music player interrupt

musicplayer_int
	andi	#$f0,mr		; unmask interrupts

	move	#>add_voice_ni,r2	; r2 -> non-interpolated add voice
	move	#>add_voice_int,r3	; r3 -> interpolated add voice
waitcpu	jclr	#0,x:HSR,waitcpu	; wait for cpu...
	movep	x:HRX,a			; fetch mix routine
	tst	a			; 0  = no interpolation
	tne	x1,a	r3,r2		; >0 = interpolation
wait8fl	jclr	#0,x:HSR,wait8fl	; wait for 8 voice flag
	movep	x:HRX,a			; fetch flag
	move	a,y:vces_8_flag
waite	move	r7,b			; current pos in circular buffer
	jset	#0,b1,waite		;
	move	r6,x0			; last position in buffer 
	sub	x0,b			; difference 
	jpl	ok
	move	#cbufsize,x0
	add 	x0,b
ok	asr	b	r6,r4		; /2 , R4=r6 (-> fill start)
	move	b1,n2			; store no of samples to do.
wait2	jclr	#1,x:HSR,wait2		; wait..
	movep	n2,x:HRX		; send frequency fraction * no. of samples to do. 

	move	#chn1buf,r0		;
	move	#>vc1_data,r1			;
	jsr	getchannel		; get channel 1
	move	#chn2buf,r0		;
	move	#>vc2_data,r1			;
	jsr	getchannel		; get channel 2
	move	#chn3buf,r0		;
	move	#>vc3_data,r1		;
	jsr	getchannel		; get channel 3
	move	#chn4buf,r0		;
	move	#>vc4_data,r1		;
	jsr	getchannel		; get channel 4

	move	y:vces_8_flag,a
	tst	a
	jeq	not8_1

	move	#chn5buf,r0		;
	move	#>vc5_data,r1			;
	jsr	getchannel		; get channel 1
	move	#chn6buf,r0		;
	move	#>vc6_data,r1			;
	jsr	getchannel		; get channel 2
	move	#chn7buf,r0		;
	move	#>vc7_data,r1		;
	jsr	getchannel		; get channel 3
	move	#chn8buf,r0		;
	move	#>vc8_data,r1		;
	jsr	getchannel		; get channel 4
not8_1

clrbuf	
	clr	a	r4,r6
	move	#2,n6			; interleaved stereo voices 
	do	n2,end_clearlp
	move	a,y:(r6)+
	move	a,y:(r6)+
end_clearlp
	move	#>chn1buf,r0		
	move	#>vc1_data,r1
	jsr	(r2)			; add voice 1
	move	#>chn2buf,r0	
	move	y:(R4)+,x0
	move	#>vc2_data,r1
	jsr	(r2)			; add voice 2
	move	#>chn3buf,r0
	move	#>vc3_data,r1
	jsr	(r2)			; add voice 3
	move	#>chn4buf,r0	
	move	y:(R4)-,x0
	move	#>vc4_data,r1
	jsr	(r2)			; add voice 4

	move	y:vces_8_flag,a
	tst	a
	jeq	not8_2

	move	#>chn5buf,r0		
	move	#>vc5_data,r1
	jsr	(r2)			; add voice 5
	move	#>chn6buf,r0	
	move	y:(R4)+,x0
	move	#>vc6_data,r1
	jsr	(r2)			; add voice 6
	move	#>chn7buf,r0
	move	#>vc7_data,r1
	jsr	(r2)			; add voice 7
	move	#>chn8buf,r0	
	move	y:(R4)-,x0
	move	#>vc8_data,r1
	jsr	(r2)			; add voice 8
not8_2

wait_E1	jclr	#0,x:HSR,wait_E1	; wait..
	movep	x:HRX,a			; 
	tst	a
	jeq	nowait
wait_E2	jclr	#1,x:HSR,wait_E2	; wait..
	movep	b,x:HRX			; tell cpu "finished"!
nowait	
	rti


; Fetch a channel, including volume,frequency data.

getchannel
	jclr	#0,x:HSR,getchannel	; wait...
	movep	x:HRX,y1		; receive volume
	move	#>$8000,y0
	do	n2,end_gc		; - for x no. of samples
wait4   jclr    #0,X:HSR,wait4          ; - wait...
	movep   x:HRX,x1                ; 
	mpy     y0,x1,a        
	move	a0,x1
	mpy	y1,x1,b	
	move	b,x:(r0)+
end_gc	
	nop
	nop
	rts

setup_player
	clr	a			; set all
	move	#>vc1_data,r6		; voice
	rep	#8*4			; variables
	move	a,x:(r6)+		; to zero
	move	#circbuf,r6		; circular buffer pointers 
	movec	#cbufsize-1,m6		; and modulos.. 
	move	r6,r7			; 
	movec	m6,m7			; 
	move 	#cbufsize,n0		; setup the dac output 
	nop
	rep	n0			; and clear the buffer
	move	a,y:(r6)+		;
; Setup DAC output.	
	movep	#$3c00,x:IPR		; Set the IPR for ssi interrupts, and ENABLE HOST COMMANDS HPL0,HPL1
	movep	#$4100,x:CRA		; set wordlength to 16bits and 2 words per "frame".
	movep	#$5800,x:CRB		; transmit int enable | transmitter enable | enable network mode(word request)
	movep	#$1f8,x:PCC		; turn on SSI port(Port C control reg)
	movep	#0,x:PCDDR		; 
	rts

	end	