;----------------------------------------------------------------------
;----------------------------------------------------------------------
; Gouraud dsp subroutine.(moire shaded :))
; By Martin Griffiths, August 1993
;----------------------------------------------------------------------
;----------------------------------------------------------------------

IPR		EQU	$ffff
BCR		EQU	$fffe
MYPBC		EQU	$ffe0
PCC		EQU	$ffe1
PCDDR		EQU	$ffe3
PCD		EQU	$ffe5
RX		EQU	$FFEF
TX		EQU	$FFEF
CRA		EQU	$FFEC
CRB		EQU	$FFED
HSR		EQU	$ffe9
HRX		EQU	$ffeb

xgrad		EQU 	1
ygrad		EQU 	2
xsave		EQU 	3
ysave		EQU 	4

gradtab		EQU	$100

	org	p:$0
	jmp	start

	org	p:$40
start

	movep	#0,x:BCR
	movep	#1,x:MYPBC
	andi	#$fc,mr

loop	jclr	#0,x:HSR,loop		; wait for cpu...
	movep	x:HRX,x1		; fetch dummy.

	jsr	make_gradtab

gouraud_tc
	move	#>gradtab,r2
get_gdx	jclr	#0,x:HSR,get_gdx		
	movep	x:HRX,n2		; dx
	move	#>$80,y1
get_gi1	jclr	#0,x:HSR,get_gi1	
	movep	x:HRX,x0		; i1
	mpy	y1,x0,a	  		; (18places right)
	move	a0,a			; 24bit signed intensity i1
get_gi2	jclr	#0,x:HSR,get_gi2		
	movep	x:HRX,y0		; i2
	mpy	y1,y0,b
	move	b0,b			; 24bit signed intensity i2
	sub	a,b	 x:(r2+n2),y1	; Di , get 1/dx
	move	b,x1	  	
	mpy	y1,x1,b	 #>$20,y1	; Di * 1/dx
	move	b,x1
	mpy	y1,x1,b	  a,x1		; 18 places right
	mpy	y1,x1,a	  b1,x1		; 18 places right	
	clr	b	  b0,x0
	do	n2,gour_lp
	add	a,b
sen_g	jclr	#1,x:HSR,sen_g
	movep	b,x:HRX
	add	x,a	#>0,b1
gour_lp

	jmp 	gouraud_tc

; Make gradient table for 1/x calculations.

make_gradtab
	move	#>gradtab,r0
	clr	a
	move	a,x:(r0)+
	move	#>1,r1
	do	#640,make_tab_lp
	move	#>1,a
	move	r1,y0
	and	#$fe,ccr
	rep 	#24
	div	y0,a
	add	y0,a		(r1)+
	move	a0,x:(r0)+	
make_tab_lp
	rts
	

	end	