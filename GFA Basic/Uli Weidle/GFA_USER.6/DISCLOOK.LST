' DiScLook,LST
' (c) Dietmar Schell
' ------------------
' Offset for PRINT in LR
pt%=-20*(XBIOS(4)=0)
' Offset for graphic in x-direction in LR
xt%=pt%*8
' Divider for graphic in y-direction in LR and MR
yt%=2+(XBIOS(4)=2)
'
' was program started as application?
select$=STRING$(128,0)
BMOVE BASEPAGE+129,VARPTR(select$),128
i%=INSTR(select$,CHR$(13))
IF i%>0
  select$=LEFT$(select$,i%-1)
ELSE
  ' if not, show file selector box
  FILESELECT "\*.*","",select$
ENDIF
' start program only with valid name
IF select$="" OR RIGHT$(select$,1)="\" OR NOT EXIST(select$)
  EDIT
ENDIF
'
BOX 174-xt%,126/yt%,466-xt%,274/yt%
BOX 178-xt%,130/yt%,462-xt%,270/yt%
PRINT AT(27-pt%,10);"This file can only"
PRINT AT(27-pt%,11);"be printed or shown."
PRINT AT(27-pt%,14);"  SHOW      PRINT     CANCEL"
PRINT AT(27-pt%,16);" SHOW BYTES      SHOW PICTURE"
BOX 199-xt%,206/yt%-1,279-xt%,226/yt%+1
BOX 200-xt%,206/yt%,278-xt%,226/yt%
BOX 288-xt%,206/yt%,358-xt%,226/yt%
BOX 368-xt%,206/yt%,440-xt%,226/yt%
BOX 200-xt%,238/yt%,312-xt%,258/yt%
BOX 336-xt%,238/yt%,440-xt%,258/yt%
'
DO
  w$=INKEY$
  MOUSE x%,y%,k%
  ' show
  IF (x%>200-xt% AND y%>206/yt% AND x%<278-xt% AND y%<226/yt% AND k%=1) OR ASC(w$)=13
    @show(1)
  ENDIF
  ' print
  IF x%>288-xt% AND y%>206/yt% AND x%<358-xt% AND y%<226/yt% AND k%=1
    ALERT 2,"|Print with |all control codes,|or only LF + CR? ",2,"All|LF + CR",print%
    ALERT 2,"|Print with |German|or US character set?",1,"D| USA ",country%
    @show(2)
  ENDIF
  ' cancel
  IF x%>368-xt% AND y%>206/yt% AND x%<440-xt% AND y%<226/yt% AND k%=1
    EDIT
  ENDIF
  ' show bytes
  IF x%>200-xt% AND y%>238/yt% AND x%<312-xt% AND y%<258/yt% AND k%=1
    @bytes
  ENDIF
  ' show picture
  IF x%>336-xt% AND y%>238/yt% AND x%<440-xt% AND y%<258/yt% AND k%=1
    @picture
  ENDIF
LOOP
'
PROCEDURE load
  select$=select$+CHR$(0)
  VOID GEMDOS(78,L:VARPTR(select$),1+2+4+16+32)
  length%=LPEEK(GEMDOS(47)+26)
  '
  dat_name$="File : "+select$+" : "+STR$(length%)+" Bytes"
  pos%=INSTR(dat_name$,"\")
  strt$=LEFT$(dat_name$,pos%)
  fin$=MID$(dat_name$,pos%+1)
  REPEAT
    pos%=INSTR(fin$,"\")
    IF pos%>0
      fin$=MID$(fin$,pos%+1)
      PRINT fin$
    ENDIF
  UNTIL pos%=0
  dat_name$=strt$+fin$
RETURN
'
PROCEDURE show(v%)
  CLS
  IF EXIST(select$)
    @load
    a$=SPACE$(32000)
    leng%=length%
    OPEN "I",#1,select$
    WHILE leng%>32000
      BGET #1,VARPTR(a$),32000
      IF v%=1
        @text(32000)
      ELSE
        @print(32000)
      ENDIF
      SUB leng%,32000
    WEND
    a$=SPACE$(32000)
    BGET #1,VARPTR(a$),leng%
    IF v%=1
      @text(leng%)
    ELSE
      @print(leng%)
    ENDIF
    CLOSE #1
  ENDIF
  IF v%=1
    PRINT AT(2,25);" - END OF FILE -";
    VOID INP(2)
  ELSE
    LPRINT
  ENDIF
  EDIT
RETURN
'
PROCEDURE text(fin%)
  a%=VARPTR(a$)
  FOR m%=a% TO a%+fin%-1
    mm%=PEEK(m%)
    IF mm%<>13
      POKE m%,MAX(mm%,32)
      PRINT CHR$(PEEK(m%));
    ELSE
      IF PEEK(m%+1)<>10
        POKE m%,32
        PRINT CHR$(32);
      ELSE
        INC m%
        PRINT CHR$(13);CHR$(10);
      ENDIF
    ENDIF
    IF CRSLIN=24
      PRINT AT(2,25);"- MORE -";
      w%=INP(2) OR 32
      IF w%=113
        EDIT
      ENDIF
      CLS
    ENDIF
  NEXT m%
RETURN
'
PROCEDURE print(fin%)
  ' test if parallel printer is online
  d%=0
  WHILE GEMDOS(17)=0 AND d%<>2
    ALERT 3,"|Switch on printer!",1,"OK|CANCEL",d%
  WEND
  '
  IF d%=2
    EDIT
  ENDIF
  '
  IF country%=1
    LPRINT CHR$(27);"R";CHR$(2);
  ELSE
    LPRINT CHR$(27);"R";CHR$(0);
  ENDIF
  '
  a%=VARPTR(a$)
  FOR m%=a% TO a%+fin%-1
    esc$=INKEY$
    mm%=PEEK(m%)
    IF print%=2
      IF mm%<>13
        POKE m%,MAX(mm%,32)
        LPRINT CHR$(PEEK(m%));
      ELSE
        IF PEEK(m%+1)<>10
          POKE m%,32
          LPRINT CHR$(32);
        ELSE
          INC m%
          LPRINT CHR$(13);CHR$(10);
        ENDIF
      ENDIF
    ELSE
      LPRINT CHR$(mm%);
    ENDIF
    w%=ASC(esc$) OR 32
    IF w%=113
      LPRINT CHR$(27);"R";CHR$(0)
      EDIT
    ENDIF
  NEXT m%
  LPRINT CHR$(27);"R";CHR$(0)
RETURN
'
PROCEDURE bytes
  DEFTEXT 1,0,0,13/yt%
  DEFFILL 1,2,8
  IF EXIST(select$)
    GET 0,0,7,15/yt%,m$
    @load
    leng%=length%
    a$=SPACE$(32000)
    OPEN "I",#1,select$
    CLS
    WHILE leng%>32000
      BGET #1,VARPTR(a$),32000
      IF XBIOS(4)>0
        @source_code(32000)
      ELSE
        @source_code_lr(32000)
      ENDIF
      SUB leng%,32000
    WEND
    a$=SPACE$(32000)
    BGET #1,VARPTR(a$),leng%
    IF XBIOS(4)>0
      @source_code(leng%)
    ELSE
      @source_code_lr(leng%)
    ENDIF
    CLOSE #1
  ENDIF
  EDIT
RETURN
'
PROCEDURE source_code(leng%)
  fin%=-32000*(leng%>32000)-leng%*(leng%<=32000)
  FOR m%=1 TO fin% STEP 1000
    PRINT AT(18,3)
    TEXT 72,37/yt%,dat_name$
    TEXT 64,57/yt%,"Q=Quit   Byte-Nr.:"
    FOR n%=0 TO 999
      IF n% MOD 50=0
        PRINT
        aa$=RIGHT$("      "+STR$(n%+m%+zz%),7)+" :"
        PRINT TAB(7);aa$'
      ENDIF
      a%=ASC(MID$(a$,n%+m%,1))
      OUT 5,a%
      IF a%=0
        xpos%=(n% MOD 50)*8+137
        ypos%=(n% DIV 50)*16+66
        BOX xpos%,ypos%/yt%,xpos%+5,(ypos%+11)/yt%
      ENDIF
      IF n%+m%+zz%>length%
        xpos%=(n% MOD 50)*8+137
        ypos%=(n% DIV 50)*16+66
        PBOX xpos%,ypos%/yt%,xpos%+5,(ypos%+11)/yt%
      ENDIF
    NEXT n%
    BOX 50,20/yt%,537,385/yt%
    BOX 50,41/yt%,537,62/yt%
    LINE 132,62/yt%,132,385/yt%
    LPOKE XBIOS(14,1)+6,0
    REPEAT
      MOUSE x%,y%,k%
      xp%=(x% DIV 8)*8
      yp%=(y% DIV (16/yt%))*16/yt%
      IF x%>135 AND y%>63/yt% AND x%<536 AND y%<384/yt%
        HIDEM
        PUT xp%,yp%,m$,10
        p$=RIGHT$("      "+STR$(m%+zz%+xp%/8-17+yp%*50/16*yt%-200),7)
        TEXT 224,57/yt%,p$
        @pause
        PUT xp%,yp%,m$,10
      ELSE
        w$=INKEY$
        SHOWM
      ENDIF
    UNTIL LEN(w$)
    SHOWM
    CLS
    IF w$="q" OR w$="Q"
      CLOSE #1
      EDIT
    ENDIF
  NEXT m%
  ADD zz%,32000
RETURN
'
PROCEDURE source_code_lr(leng%)
  fin%=MIN(leng%,32000)
  FOR m%=1 TO fin% STEP 500
    PRINT AT(18,3)
    TEXT 32,16,dat_name$
    TEXT 24,27,"Q=Quit   Byte-Nr.:"
    FOR n%=0 TO 499
      IF n% MOD 25=0
        PRINT
        aa$=RIGHT$("      "+STR$(n%+m%+zz%),7)+" :"
        PRINT TAB(2);aa$'
      ENDIF
      a%=ASC(MID$(a$,n%+m%,1))
      OUT 5,a%
      IF a%=0
        xpos%=(n% MOD 25)*8+97
        ypos%=(n% DIV 25)*8+33
        BOX xpos%,ypos%,xpos%+5,ypos%+5
      ENDIF
      IF n%+m%+zz%>length
        xpos%=(n% MOD 25)*8+97
        ypos%=(n% DIV 25)*8+33
        PBOX xpos%,ypos%,xpos%+5,ypos%+5
      ENDIF
    NEXT n%
    BOX 13,8,297,192
    BOX 13,19,297,30
    LINE 92,31,92,192
    LPOKE XBIOS(14,1)+6,0
    REPEAT
      MOUSE x%,y%,k%
      xp%=(x% DIV 8)*8
      yp%=(y% DIV 8)*8
      IF x%>96 AND y%>31 AND x%<296 AND y%<192
        HIDEM
        PUT xp%,yp%,m$,10
        p$=RIGHT$("      "+STR$(m%+zz%+xp%/8-12+yp%*25/8-100),7)
        TEXT 184,28,p$
        @pause
        PUT xp%,yp%,m$,10
      ELSE
        w$=INKEY$
        SHOWM
      ENDIF
    UNTIL LEN(w$)
    SHOWM
    CLS
    IF w$="q" OR w$="Q"
      CLOSE #1
      EDIT
    ENDIF
  NEXT m%
  ADD zz%,32000
RETURN
'
PROCEDURE pause
  REPEAT
    w$=INKEY$
    MOUSE xw%,yw%,k%
    xw%=(xw% DIV 8)*8
    yw%=(yw% DIV (16/yt%))*16/yt%
  UNTIL xp%<>xw% OR yp%<>yw% OR LEN(w$)
RETURN
'
PROCEDURE picture
  CLS
  IF EXIST(select$)
    @load
    IF length%=32034
      a$=SPACE$(32034)
      BLOAD select$,VARPTR(a$)
      a$=RIGHT$(a$,32000)
      SPUT a$
      VOID INP(2)
      EDIT
    ENDIF
    IF length%=32000
      a$=SPACE$(32000)
      BLOAD select$,VARPTR(a$)
      SPUT a$
      VOID INP(2)
      EDIT
    ENDIF
  ENDIF
  EDIT
RETURN
'
