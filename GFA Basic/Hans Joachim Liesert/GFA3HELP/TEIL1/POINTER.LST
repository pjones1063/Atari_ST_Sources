'
' * - Beispiel 1:
'
a%=12
b%=*a%
*b%=33
PRINT "Variablenadresse 'A%' (Åber Pointer): ";*a%
PRINT "Variablenadresse 'A%' (Åber VARPTR) : ";VARPTR(a%)
PRINT "Variablenadresse 'A%' (in 'B%')     : ";b%
PRINT "Variableninhalt 'A%'  (direkt)      : ";a%
PRINT "Variableninhalt 'A%'  (indirekt)    : ";LPEEK(b%)
'
' * - Beispiel 2:
'
var$="GFA-BASIC"
var%=5
GOSUB proc(*var$,*var%)
PRINT var$'var%
PROCEDURE proc(para1%,para2%)
  LOCAL lvar$,lvar,lvar%,i%
  FOR i%=0 TO DPEEK(para1%+4)-1
    z%=PEEK(LPEEK(para1%)+i%)
    lvar$=lvar$+CHR$(z%)
  NEXT i%
  IF TYPE(para2%)=0
    BMOVE para2%,VARPTR(lvar),6
    *para1%=STRING$(3,lvar$)+" / 2^"+STR$(lvar)+" = "
    *para2%=2^lvar
  ENDIF
  IF TYPE(para2%)=2
    LPOKE VARPTR(lvar%),LPEEK(para2%)
    *para1%=STRING$(3,lvar$)+" / 2^"+STR$(lvar%)+" = "
    *para2%=2^lvar%
  ENDIF
RETURN
'
' * -  Beispiel 3:
'
DIM a$(2)
a$(0)="String 1"
a$(1)="String 2"
a$(2)="String 3"
@xyz(*a$())
FOR i%=0 TO 2
  PRINT a$(i%)
NEXT i%
PROCEDURE xyz(feldpointer%)
  DIM dummyfeld$(1)
  SWAP *feldpointer%,dummyfeld$()
  FOR i%=0 TO 2
    CLR x$
    FOR j%=LEN(dummyfeld$(i%)) DOWNTO 1
      x$=x$+MID$(dummyfeld$(i%),j%,1)
    NEXT j%
    dummyfeld$(i%)=x$
  NEXT i%
  SWAP *feldpointer%,dummyfeld$()
  ERASE dummyfeld$()
RETURN
