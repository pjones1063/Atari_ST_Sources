DEFTEXT ,0,,13
TEXT 100,50,"Alter Font"
@fontset(3,"SYSTEM.FNT","",*nfont%)
TEXT 100,100,"Neuer Font"
IF nfont%>BASEPAGE
  @original
  TEXT 100,150,"Wieder alter Font"
ENDIF
PROCEDURE fontset(typ%,font1$,font2$,font%)
  LOCAL a$,a%,fnt%,i%,j%,k%
  IF typ%=1 OR typ%=2 OR typ%=3
    IF EXIST(font1$)
      a$=MKI$(&HA000)+MKL$(&H20094E75)
      a%=VARPTR(a$)
      fnt%=LPEEK(&H607E)
      orig%=LPEEK(LPEEK(C:a%()+8)+76)
      IF fnt%=orig%
        RESERVE FRE(0)-5000
        fnt%=HIMEM+602
        IF typ%<3
          BMOVE orig%-602,HIMEM,602
        ENDIF
      ENDIF
      IF typ%<3
        FOR k%=0 TO 1
          b$=STRING$(2048*typ%,0)
          OPEN "I",#1,font1$
          BGET #1,VARPTR(b$),2048*typ%
          CLOSE
          FOR j%=0 TO (128*typ%)-1
            FOR i%=0 TO 15
              POKE k%*128+fnt%+j%+i%*256,PEEK(VARPTR(b$)+i%+j%*16)
            NEXT i%
          NEXT j%
          IF k%=0
            IF typ%=1
              IF EXIST(font2$)=0
                k%=1
              ENDIF
            ELSE
              k%=1
            ENDIF
          ENDIF
        NEXT k%
      ENDIF
      IF typ%=3
        BLOAD font1$,fnt%-602
      ENDIF
      *font%=fnt%
      LPOKE &H2924,fnt%
      LPOKE &H607E,fnt%
    ENDIF
  ENDIF
RETURN
'
PROCEDURE original
  LOCAL a$,a%,orig%
  a$=MKI$(&HA000)+MKL$(&H20094E75)
  a%=VARPTR(a$)
  orig%=LPEEK(LPEEK(C:a%()+8)+76)
  LPOKE &H2924,orig%
  LPOKE &H607E,orig%
  RESERVE XBIOS(2)-HIMEM-16384+FRE(0)
RETURN
'
PROCEDURE make_sys(font%,fname$)
  IF fname$>"" AND fname$<>"\" AND fname$<>"\.FNT"
    BSAVE fname$,font%-602,4698
  ENDIF
RETURN
'
PROCEDURE make_deg(font%,fname$)
  LOCAL b$,i%,j%
  IF fname$>"" AND fname$<>"\"
    b$=STRING$(2048,0)
    FOR j%=0 TO 127
      FOR i%=0 TO 15
        POKE VARPTR(b$)+i%+j%*16,PEEK(font%+j%+i%*256)
      NEXT i%
    NEXT j%
    b$=b$+MKI$(1)
    BSAVE fname$,VARPTR(b$),2050
  ENDIF
RETURN
'     '
PROCEDURE make_stad(font%,fname$)
  IF fname$>"" AND fname$<>"\"
    b$=STRING$(4096,0)
    FOR j%=0 TO 255
      FOR i%=0 TO 15
        POKE VARPTR(b$)+i%+j%*16,PEEK(font%+j%+i%*256)
      NEXT i%
    NEXT j%
    BSAVE fname$,VARPTR(b$),4096
  ENDIF
RETURN
