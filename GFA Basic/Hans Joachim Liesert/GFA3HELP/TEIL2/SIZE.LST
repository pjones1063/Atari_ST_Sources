' Beispiel (nur HIRES):
'
FOR i%=1 TO 67
  READ a%
  a$=a$+MKI$(a%)
NEXT i%
DEFTEXT ,16,,12
TEXT 100,25,"PUT-VerknÅpfung"
@size(100,2,400,30,540,28,*b$)
PUT 80,2,b$
DEFTEXT ,0,,12
PUT 8,2,a$
PUT 600,2,a$
@size(8,2,40,34,64,64,*a$)
PRINT AT(10,10);"Bild mit Maus bewegen! (Ende durch Mausklick)"
REPEAT
  MOUSE mx%,my%,mk%
  PUT mx%,my%,a$,6
  PUT mx%,my%,a$,6
UNTIL mk%
DEFFILL ,2,2
PBOX 6,36,633,393
GRAPHMODE 2
FOR i%=0 TO 3
  FOR j%=0 TO 3
    PUT 36+i%*166,46+j%*88,a$,j%*4+i%
    TEXT 36+i%*166+18,46+j%*88+80,STR$(j%*4+i%)
  NEXT j%
NEXT i%
DATA 31,31,1,0,0,7,49152,15,57344,24,12288,32,2048,230
DATA 52736,266,41216,324,17664,322,34048,292,18688,227
DATA 36352,40,10240,23,53248,8,8192,255,64512,373,22016
DATA 683,43776,1365,21888,1771,43648,1365,23936,1771,44928
DATA 2525,30272,2223,60480,2775,54592,3756,44480,2004,55168
DATA 168,27648,248,31744,136,17408,132,33792,252,64512,0,0
PROCEDURE size(xl%,yo%,xr%,yu%,xd%,yd%,buf%)
  LOCAL bg$,xsz,ysz,i%,j%,xcnt%,ycnt%,md%,xt%,yt%,buf$
  yt%=MIN(2,3-XBIOS(4))
  xt%=2-SGN(XBIOS(4))
  GRAPHMODE 1
  DEFFILL ,0,0
  yu%=MAX(0,MIN((399/yt%),yu%))
  xr%=MAX(0,MIN((639/xt%),xr%))
  ysz=yd%/(yu%-yo%)
  IF ysz<1
    md%=7
  ELSE
    md%=3
  ENDIF
  '  Dpoke Vdibase+34,0   ! fÅr V2.xx
  BOUNDARY 0              ! fÅr V3.0
  DIM v.buf$(yu%-yo%)
  DIM h.buf$(xr%-xl%)
  GET 0,0,MIN((639/xt%),MAX(xr%-xl%,xd%+10)),MIN((399/yt%),MAX(yu%-yo%,yd%+10)),bg$
  FOR i%=0 TO (yu%-yo%)
    GET xl%,yo%+i%,xr%,yo%+i%,v.buf$(ycnt%)
    INC ycnt%
  NEXT i%
  PBOX 0,0,MAX(xd%,xr%-xl%),yd%
  FOR i%=0 TO ycnt%-1
    PUT 0,i%*ysz,v.buf$(i%),md%
    IF ysz>1 AND i%<ycnt%-1
      FOR j%=0 TO ysz
        PUT 0,i%*ysz+j%,v.buf$(i%),md%
      NEXT j%
    ENDIF
  NEXT i%
  xsz=xd%/(xr%-xl%)
  IF xsz<1
    md%=7
  ELSE
    md%=3
  ENDIF
  FOR i%=0 TO (xr%-xl%)
    GET i%,0,i%,yd%,h.buf$(xcnt%)
    INC xcnt%
  NEXT i%
  PBOX 0,0,xd%,yd%
  FOR i%=0 TO xcnt%-1
    PUT i%*xsz,0,h.buf$(i%),md%
    IF xsz>1 AND i%<xcnt%-1
      FOR j%=0 TO xsz
        PUT i%*xsz+j%,0,h.buf$(i%),md%
      NEXT j%
    ENDIF
  NEXT i%
  GET 0,0,MIN((639/xt%),xd%),MIN((399/yt%),yd%),buf$
  PUT 0,0,bg$
  ERASE v.buf$()
  ERASE h.buf$()
  '  Dpoke Vdibase+34,1    ! fÅr V2.xx
  BOUNDARY 1               ! fÅr V3.0
  *buf%=buf$
RETURN
