DIM fontbuff%(25000)
f.name$="SUPER.FON"
IF EXIST(f.name$)
  @fontload(f.name$,VARPTR(fontbuff%(0)),*platz%)
  DIM font1%(INT(platz%/4)+1)
  SWAP fontbuff%(),font1%()
  ERASE fontbuff%()
  f.adr1%=VARPTR(font1%(0))
  DEFFILL ,2,4
  PBOX 0,0,639,399
  FOR i%=1 TO 10
    a$=a$+CHR$(i%)
  NEXT i%
  FOR i%=16 TO 22
    a$=a$+CHR$(i%)+CHR$(i%+32)
  NEXT i%
  FOR i%=65 TO 72
    a$=a$+CHR$(i%)+CHR$(i%+32)
  NEXT i%
  FOR i%=1 TO 14
    @ptext(2,i%*28,636,a$,f.adr1%,i%)
  NEXT i%
ENDIF
PROCEDURE ptext(xtxt%,ytxt%,plen%,txt$,c_bf%,gmod%)
  LOCAL i%,s_bf$,plc%,ldiv,pt_w%,pt_h%,plane%
  IF LEN(txt$)>0
    pt_w%=PEEK(c_bf%)
    pt_h%=PEEK(c_bf%+1)
    plane%=2^(2-XBIOS(4))
    plc%=((INT((pt_w%/16)+SGN(pt_w% MOD 16)))*2*pt_h%)*plane%
    s_bf$=MKI$(pt_w%-1)+MKI$(pt_h%-1)+MKI$(plane%)+SPACE$(plc%)
    IF plen%>0
      ldiv=plen%/LEN(txt$)+(plen%/LEN(txt$)-pt_w%)/(LEN(txt$)-1)
    ELSE
      ldiv=pt_w%
    ENDIF
    FOR i%=0 TO LEN(txt$)-1
      BMOVE c_bf%+2+ASC(MID$(txt$,i%+1,1))*plc%,VARPTR(s_bf$)+6,plc%
      PUT xtxt%+i%*ldiv,ytxt%-pt_h%,s_bf$,gmod%
    NEXT i%
  ENDIF
RETURN
PROCEDURE fontload(sel$,adr%,plc%)
  OPEN "I",#99,sel$
  BGET #99,adr%,LOF(#99)
  *plc%=LOF(#99)
  CLOSE #99
RETURN
