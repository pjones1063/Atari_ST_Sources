LET tree1&=0     ! RSRC_TREE
LET t1_txt&=1    ! Text-Objekt in Baum 0
LET t1_ok&=3     ! OKAY-Objekt in Baum 0
LET t1_can&=4    ! Cancel-Objekt in Baum 0
'
LET tree2&=1     ! RSRC_TREE
LET t2_txt&=1    ! Text-Objekt in Baum 1
LET t2_but1&=4   ! Objekt in Baum 1
LET t2_but2&=5   ! Objekt in Baum 1
LET t2_but3&=6   ! Objekt in Baum 1
LET t2_but4&=7   ! Objekt in Baum 1
LET t2_but5&=8   ! Objekt in Baum 1
LET t2_but6&=9   ! Objekt in Baum 1
LET t2_but7&=10  ! Objekt in Baum 1
LET t2_but8&=11  ! Objekt in Baum 1
LET t2_but9&=12  ! Objekt in Baum 1
LET t2_but10&=13 ! Objekt in Baum 1
LET t2_ok&=14    ! OKAY-Objekt in Baum 1
LET t2_can&=15   ! Cancel-Objekt in Baum 1
'
RESERVE FRE(0)-10000
~RSRC_LOAD("formular.rsc")
~RSRC_GADDR(0,tree1&,form1%)
~FORM_CENTER(form1%,x,y,w,h)
~RSRC_GADDR(0,tree2&,form2%)
~FORM_CENTER(form2%,x2,y2,w2,h2)
CHAR{{OB_SPEC(form2%,t2_txt&)}}="00"
'
DEFFILL ,2,4
PBOX 120,85,515,325
PRINT " Baum 0 mit linker Maustaste"
PRINT " Baum 1 mit rechter Maustaste"
PRINT " Abbruch mit beliebiger <Taste>"
DO
  EXIT IF INKEY$<>""
  IF MOUSEK=1
    ~FORM_DIAL(1,0,0,0,0,x,y,w,h)
    ~OBJC_DRAW(form1%,0,3,x,y,w,h)
    ~FORM_DIAL(0,0,0,0,0,x,y,w,h)
    ret_ob&=FORM_DO(form1%,t1_txt&)
    ~FORM_DIAL(3,0,0,0,0,x,y,w,h)
    ~FORM_DIAL(2,0,0,0,0,x,y,w,h)
    ~OBJC_CHANGE(form1%,ret_ob&,0,x,y,w,h,0,0)
    PRINT AT(1,10);"Ihr Alter: ";CHAR{{OB_SPEC(form1%,t1_txt&)}}'
  ENDIF
  IF MOUSEK=2
    FOR objc&=t2_but1& TO t2_but10&
      EXIT IF AND(OB_STATE(form2%,objc&),1)
    NEXT objc&
    old_objc&=objc&
    old_str$=CHAR{{OB_SPEC(form2%,t2_txt&)}}
    ~FORM_DIAL(1,0,0,0,0,x2,y2,w2,h2)
    ~OBJC_DRAW(form2%,0,3,x2,y2,w2,h2)
    ~FORM_DIAL(0,0,0,0,0,x2,y2,w2,h2)
    REPEAT
      ret_ob&=FORM_DO(form2%,0)
      EXIT IF ret_ob&=t2_ok& OR ret_ob&=t2_can&
      FOR objc&=t2_but1& TO t2_but10&
        EXIT IF BTST(OB_STATE(form2%,objc&),0)
      NEXT objc&
      string$=STR$((objc&-t2_but1&)*5+10)
      CHAR{{OB_SPEC(form2%,t2_txt&)}}=string$
      ~OBJC_DRAW(form2%,t2_txt&,1,x2,y2,w2,h2)
    UNTIL ret_ob&=t2_ok& OR ret_ob&=t2_can&
    ~FORM_DIAL(3,0,0,0,0,x2,y2,w2,h2)
    ~FORM_DIAL(2,0,0,0,0,x2,y2,w2,h2)
    ~OBJC_CHANGE(form2%,ret_ob&,0,x,y,w,h,0,0)
    IF ret_ob&=t2_can&
      ~OBJC_CHANGE(form2%,objc&,0,x2,y2,w2,h2,0,0)
      ~OBJC_CHANGE(form2%,old_objc&,0,x2,y2,w2,h2,1,0)
      CHAR{{OB_SPEC(form2%,t2_txt&)}}=old_str$
    ENDIF
  ENDIF
LOOP
~RSRC_FREE()
RESERVE FRE(0)+10000
