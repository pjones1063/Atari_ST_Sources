' Programm zum Anzeigen eines Files
' Filenamen ermitteln
fs_iinpath$="C:\*.*"
fs_ireturn=FSEL_INPUT(fs_iinpath$,fs_insel$,fs_iexbutton)
IF fs_ireturn<>0 AND fs_iexbutton=1
  pathnr|=RINSTR(fs_iinpath$,"\")
  path$=LEFT$(fs_iinpath$,pathnr|)
  filename$=path$+fs_insel$
  OPEN "U",#1,filename$
  dateilaenge%=LOF(#1)
  PRINT "Filename ";filename$,"DateilÑnge = ";dateilaenge%
  PRINT
  PRINT "Flieûtext oder Zeilenmodus? (f/z)"
  KEYGET ab|
  IF ab|=ASC("f") THEN
    ' Anzeige im Flieûtextmodus'
    PRINT
    PRINT "Space = nÑchste Seite, Return = nÑchste Zeile, Esc = Stop";
    PRINT
    PRINT "--------------------------------------------------------------------------------"
    ' ZunÑchst Bildschirm fÅllen
    i%=1
    WHILE (i%<25) AND (NOT EOF(#1))
      GOSUB zeile
      INC i%
    WEND
    ' Schleife zur weiteren Anzeige der Datei
    WHILE (NOT EOF(#1)) AND (ab|<>27)
      KEYGET ab|
      IF ab|=32 THEN
        ' nÑchste Seite anzeigen
        i%=1
        WHILE (i%<24) AND (NOT EOF(#1))
          GOSUB zeile
          INC i%
        WEND
      ELSE
        IF ab|=13 THEN
          ' nÑchste Zeile anzeigen
          GOSUB zeile
        ENDIF
      ENDIF
    WEND
    IF EOF(#1) THEN
      ' Wenn Dateiende erreicht, Programm abbrechen
      PRINT "Ende der Datei [Taste]";
      VOID INP(2)
    ENDIF
  ELSE
    ' Datei im Zeilenmodus anzeigen
    PRINT
    PRINT "Auf und Ab durch Cursortasten, Ende mit ESC!"
    PRINT
    PRINT "--------------------------------------------------------------------------------"
    PRINT
    ' Bildschirm fÅllen
    i%=1
    WHILE (i%<=1920) AND NOT EOF(#1)
      byte%=INP(#1)
      OUT 5,byte%
      INC i%
    WEND
    IF EOF(#1) THEN
      PRINT "## Dateiende ##"
    ENDIF
    ' Tastatur abfragen und Kommandos ausfÅhren
    REPEAT
      a%=INP(2)
      IF (a%=200) AND (LOC(#1)>=2000) THEN
        ' Eine Zeile nach oben scrollen
        ' Wenn keine ganze Zeile (am Fileende), dann zunÑchst eine Zeile zurÅck
        r%=LOC(#1) MOD 80
        RELSEEK #1,-r%
        RELSEEK #1,-2000
        ' Scrolling durch VT-52-Sequenz
        PRINT AT(1,24);"                                                                                ";
        PRINT AT(1,1);CHR$(27);"I";
        ' Zeile ausgeben
        i%=1
        WHILE (i%<=80) AND NOT EOF(#1)
          byte%=INP(#1)
          OUT 5,byte%
          INC i%
        WEND
        ' Filepointer wieder auf Bildschirmende setzen
        RELSEEK #1,1840
      ELSE
        IF (a%=208) AND LOC(#1)<=dateilaenge% THEN
          ' Eine Zeile nach unten scrollen
          PRINT AT(1,25);
          i%=1
          WHILE (i%<=80) AND NOT EOF(#1)
            byte%=INP(#1)
            OUT 5,byte%
            INC i%
          WEND
          IF EOF(#1) THEN
            PRINT "## Dateiende ##";
          ENDIF
        ENDIF
      ENDIF
    UNTIL a%=27
  ENDIF
ENDIF
CLOSE #1
' Prozedur zum Anzeigen einer Zeile (Flieûtext)
PROCEDURE zeile
  '  Local Z%,J%
  j%=1
  z%=0
  WHILE (j%<=80) AND (z%<>13) AND NOT EOF(#1)
    ' Zeichen aus Datei lesen
    INC j%
    z%=INP(#1)
    OUT 5,z%
  WEND
  IF z%=13 THEN
    OUT 5,INP(#1)
  ENDIF
  IF j%<80 THEN
    PRINT
  ENDIF
RETURN
