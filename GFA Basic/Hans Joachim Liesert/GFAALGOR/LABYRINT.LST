Dim Lab(5,5)
' Daten fÅr das Labyrinth
Data 1,2,5,3,5
Data 9,3,12,9,9
Data 11,12,3,12,9
Data 11,6,12,3,12
Data 8,2,6,14,4
For Z=1 To 5
  For S=1 To 5
    Read Lab(Z,S)
  Next S
Next Z
' Suche starten
Rueck=False
Gosub Laby(5,5)
'
Procedure Laby(Zeile,Spalte)
  Print Zeile,Spalte
  Void Inp(2)
  If (Zeile=1) And (Spalte=1) Then
    ' Eingang gefunden!!!!
    Rueck=True
  Else
    If ((Lab(Zeile,Spalte) And 1)=1) And (Rueck=False) Then
      ' Bewegung nach SÅden mîglich
      ' RÅckwÑrtslaufen verhindern
      Lab(Zeile+1,Spalte)=Lab(Zeile+1,Spalte) And 7
      ' Weitersuchen
      Gosub Laby(Zeile+1,Spalte)
      If Rueck=True Then
        Print "Norden"
      Endif
    Endif
    If ((Lab(Zeile,Spalte) And 2)=2) And (Rueck=False) Then
      ' Bewegung nach Osten mîglich
      ' RÅckwÑrtslaufen verhindern
      Lab(Zeile,Spalte+1)=Lab(Zeile,Spalte+1) And 11
      ' Weitersuchen
      Gosub Laby(Zeile,Spalte+1)
      If Rueck=True Then
        Print "Westen"
      Endif
    Endif
    If ((Lab(Zeile,Spalte) And 4)=4) And (Rueck=False) Then
      ' Bewegung nach Westen mîglich
      ' RÅckwÑrtslaufen verhindern
      Lab(Zeile,Spalte-1)=Lab(Zeile,Spalte-1) And 13
      ' Weitersuchen
      Gosub Laby(Zeile,Spalte-1)
      If Rueck=True Then
        Print "Osten"
      Endif
    Endif
    If ((Lab(Zeile,Spalte) And 8)=8) And (Rueck=False) Then
      ' Bewegung nach Norden mîglich
      ' RÅckwÑrtslaufen verhindern
      Lab(Zeile-1,Spalte)=Lab(Zeile-1,Spalte) And 14
      ' Weitersuchen
      Gosub Laby(Zeile-1,Spalte)
      If Rueck=True Then
        Print "SÅden"
      Endif
    Endif
  Endif
Return
