Dim S(13),Code(9),Satz_a(9),Satz_b(9),Satz_c(9)
For I=0 To 9
  Read Satz_a(I)
  Satz_c(I)=Not Satz_a(I)
Next I
For I=0 To 9
  Read Satz_b(I)
Next I
For I=0 To 9
  Read Code(I)
Next I
Procedure Ean_13(F1,F2,An)
  ' schreibt einen dreizehnstelligen Barcode auf den Bildschirm
  '
  ' in einzelne Stellen auflîsen
  S(1)=F1
  S(2)=F2
  For I=0 To 9
    S(12-I)=(An Div (10^I)) Mod 10
  Next I
  ' PrÅfsumme berechnen
  P=0
  For I=1 To 11 Step 2
    P=P+S(I)
  Next I
  For I=2 To 12 Step 2
    P=P+3*S(I)
  Next I
  S(13)=P Mod 10
  If S(13)<>0 Then
    S(13)=10-S(13)
  Endif
  ' Drucken der Codes
  Spalte%=400
  ' Randzeichen
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  Inc Spalte%
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  ' linkes Segment
  For I=2 To 7
    If (Code(S(1)) And 2^(7-I))<>0 Then
      Zeichencode=Satz_b(S(I))
    Else
      Zeichencode=Satz_a(S(I))
    Endif
    For J=1 To 7
      If (Zeichencode And 2^(7-J))<>0 Then
        Draw Spalte%,100 To Spalte%,200
      Endif
      Inc Spalte%
    Next J
  Next I
  ' Trennzeichen
  Inc Spalte%
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  Inc Spalte%
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  Inc Spalte%
  ' rechtes Segment drucken
  For I=8 To 13
    Zeichencode=Satz_c(S(I))
    For J=1 To 7
      If (Zeichencode And 2^(7-J))<>0 Then
        Draw Spalte%,100 To Spalte%,200
      Endif
      Inc Spalte%
    Next J
  Next I
  ' Randzeichen
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  Inc Spalte%
  Draw Spalte%,100 To Spalte%,210
  Inc Spalte%
  For I=1 To 13
    Print S(I);
  Next I
  Print
Return
' Daten fÅr Satz A
Data 13,25,19,61,35,49,47,59,55,11
' Daten fÅr Satz B
Data 39,51,27,33,29,57,5,17,9,23
' Daten fÅr Spezialcode
Data 0,11,13,14,19,25,28,21,22,26
