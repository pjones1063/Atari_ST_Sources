' Programm zum Anzeigen eines Files
' Filenamen ermitteln
Fileselect "A:\*.*","",F$
If (F$<>"") And (Exist(F$)) Then
  Open "U",#1,F$
  Dateilaenge%=Lof(#1)
  Print "Filename ";F$,"DateilÑnge = ";Dateilaenge%
  Print
  Print "Flieûtext oder Zeilenmodus? (f/z)"
  A%=Inp(2)
  If A%=Asc("f") Then
    ' Anzeige im Flieûtextmodus'
    Print
    Print "Space = nÑchste Seite, Return = nÑchste Zeile, Esc = Stop";
    Print
    Print "--------------------------------------------------------------------------------"
    ' ZunÑchst Bildschirm fÅllen
    I%=1
    While (I%<25) And (Not Eof(#1))
      Gosub Zeile
      Inc I%
    Wend
    ' Schleife zur weiteren Anzeige der Datei
    While (Not Eof(#1)) And (A%<>27)
      A%=Inp(2)
      If A%=32 Then
        ' nÑchste Seite anzeigen
        I%=1
        While (I%<24) And (Not Eof(#1))
          Gosub Zeile
          Inc I%
        Wend
      Endif
      If A%=13 Then
        ' nÑchste Zeile anzeigen
        Gosub Zeile
      Endif
    Wend
    If Eof(#1) Then
      ' Wenn Dateiende erreicht, Programm abbrechen
      Print "Ende der Datei [Taste]";
      Void Inp(2)
    Endif
  Else
    ' Datei im Zeilenmodus anzeigen
    Print
    Print "Auf und Ab durch Cursortasten, Ende mit ESC!"
    Print
    Print "--------------------------------------------------------------------------------"
    Print
    ' Bildschirm fÅllen
    I%=1
    While (I%<=1920) And Not Eof(#1)
      Byte%=Inp(#1)
      Out 5,Byte%
      Inc I%
    Wend
    If Eof(#1) Then
      Print "## Dateiende ##"
    Endif
    ' Tastatur abfragen und Kommandos ausfÅhren
    Repeat
      A%=Inp(2)
      If (A%=200) And (Loc(#1)>=2000) Then
        ' Eine Zeile nach oben scrollen
        ' Wenn keine ganze Zeile (am Fileende), dann zunÑchst eine Zeile zurÅck
        R%=Loc(#1) Mod 80
        Relseek #1,-R%
        Relseek #1,-2000
        ' Scrolling durch VT-52-Sequenz
        Print At(1,24);"                                                                                ";
        Print At(1,1);Chr$(27);"I";
        ' Zeile ausgeben
        I%=1
        While (I%<=80) And Not Eof(#1)
          Byte%=Inp(#1)
          Out 5,Byte%
          Inc I%
        Wend
        ' Filepointer wieder auf Bildschirmende setzen
        Relseek #1,1840
      Endif
      If (A%=208) And Loc(#1)<=Dateilaenge% Then
        ' Eine Zeile nach unten scrollen
        Print At(1,25);
        I%=1
        While (I%<=80) And Not Eof(#1)
          Byte%=Inp(#1)
          Out 5,Byte%
          Inc I%
        Wend
        If Eof(#1) Then
          Print "## Dateiende ##";
        Endif
      Endif
    Until A%=27
  Endif
Endif
Close #1
' Prozedur zum Anzeigen einer Zeile (Flieûtext)
Procedure Zeile
  '  Local Z%,J%
  J%=1
  Z%=0
  While (J%<=80) And (Z%<>13) And Not Eof(#1)
    ' Zeichen aus Datei lesen
    Inc J%
    Z%=Inp(#1)
    Out 5,Z%
  Wend
  If Z%=13 Then
    Out 5,Inp(#1)
  Endif
  If J%<80 Then
    Print
  Endif
Return
