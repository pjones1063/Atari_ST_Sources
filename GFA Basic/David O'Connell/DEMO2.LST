Clear
' this program was given free in ST.FORMAT
' All Changes Made to include sound are marked with ***
' always exit program from menu "quit" or key click will be dissabled
Spoke &H484,Peek(&H484) And Not 1 !turns of key click
For A%=0 To 9                    !***
  Read B%                        !***
  Music_off$=Music_off$+Chr$(B%) !***
Next A%                          !***
Data 8,0,9,0,10,0,7,255,255,0,0  !***
Dim A$(22)
A$(0)=" DALLAS "
A$(1)="  About GFA BASIC  "
A$(2)="-------------------"
A$(3)="1"
A$(4)="2"
A$(5)="3"
A$(6)="4"
A$(7)="5"
A$(8)="6"
A$(9)=""
A$(10)=" Options "
A$(11)="  Cosine  "
A$(12)="  Fill    "
A$(13)="  Swiggle "
A$(14)="----------"
A$(15)="  Quit  "
A$(16)=""
A$(17)=" Rectangles "
A$(18)="  GFA       "
A$(19)="  Boxes     "
A$(20)="  2001"
A$(21)=""
Menu A$()
Menu Off
On Menu  Gosub Info
Do
  On Menu
Loop
Procedure Info
  Menu Off
  Cls
  Value=Menu(0)
  If Value=1
    F$="dallas.xbs"
    @Music                                !***
    Alert 2,"GFA BASIC DEMO | Tim Purves|MUSIC DEMO:D.O'Connell",1,"  OK  ",B
  Endif
  If Value>10 And Value<20
    If Value<>18
      @Infobox
    Endif
  Endif
  If Value=11
    Gosub Cosine
  Endif
  If Value=12
    Gosub Fill
  Endif
  If Value=13
    Gosub Lisa
  Endif
  If Value=15
    Spoke &H484,Peek(&H484) Or 1
    End
  Endif
  If Value=18
    Gosub Rect2
  Endif
  If Value=19
    Gosub Rect1
  Endif
  If Value=20
    @2001
  Endif
  Cls
  Menu A$()
Return
Procedure Cosine
  F$="inthemod.xbs"                     !***
  @Music                                !***
  Q=1.2
  P=0.03
  '  Target=Timer+4000
  Repeat
    For F=0 To 4
      Color F
      I=I+P
      S=Sin(I)+1
      C=Cos(I*Q)+1
      Line 320*S,100*C,640-320*C,100*S
    Next F
    M%=Xbios(28,255,7)                  !***
    Exit If Inkey$<>""                  !***
  Until M%=255                          !***
Return
Procedure Fill
  F$="popcorn.xbs"                         !***
  @Music                                !***
  Ellipse 320,200,320,200,-7200,0
  Print "Click on Area to fill"
  Target=Timer+20
  Repeat
    I=(I+1) Mod 36
    If Mousek
      Deffill Random(3)+1,3+(I<24),I Mod 24+1
      Fill Mousex,Mousey
    Endif
    M%=Xbios(28,255,7)                 !***
    Exit If Inkey$<>""                 !***
  Until M%=255                         !***
Return
Procedure Rect1
  F$="gostbust.xbs"                    !***
  @Music                               !***
  Graphmode 3
  '  Target=Timer+12000
  Repeat
    Box I Mod 640,I Mod 200,639-I Mod 640,199-I Mod 200
    Add I,8
    M%=Xbios(28,255,7)                  !***
    Exit If Inkey$<>""                  !***
  Until M%=255                          !***
Return
Procedure Rect2
  F$="music.xbs"                     !***
  @Music                                !***
  Deftext 2,16,,32
  Text 200,50,"GFA BASIC Interpreter"
  Deftext 3,4,,13
  Text 250,80,"GFA Data Media (UK) Ltd"
  Text 250,100,"Box 121"
  Text 250,120,"Wokingham"
  Text 250,140,"Berkshire"
  Text 250,160,"RG11 9LP"
  Graphmode 3
  '  Target=Timer+12000
  Repeat
    For A=3 To 99
      For I=0 To 150 Step A
        Pbox I,I,I+A,I+A
      Next I
    Next A
    For A=99 Downto 3
      For I=0 To 150 Step A
        Pbox I,I,I+A,I+A
      Next I
    Next A
    M%=Xbios(28,255,7)          !***
  Until M%=255                  !***
  Deftext 1,0,0,13
Return
Procedure Lisa
  F$="rockarou.xbs"                 !***
  @Music                        !***
  ' Lissa
  ' P und Q variieren
  Q=3/4
  P=0.1
  Color 2
  Plot 320,199
  I=0
  '  Target=Timer+12000
  Repeat
    I=I+P
    S=Sin(I)+1
    C=Cos(I*Q)+1
    Draw  To 320*S,100*C
    Exit If Inkey$<>""          !***
    M%=Xbios(28,255,7)          !***
  Until M%=255                  !***
Return
'
'
' ******************************************
'                  new procedures
' *******************************************
Procedure Music                 !***
  W!=W! Xor True
  Open "I",#1,"a:\xbs.fld\"+F$
  Len%=Lof(#1)
  If W!=False
    Erase Music%()                        !***
    Dim Music%(Len%/4)
    Bload "a:\xbs.fld\"+F$,Varptr(Music%(0))
    @M_2
    Void Xbios(32,L:Varptr(Music%(0)))
  Else
    Erase Music1%()
    Dim Music1%(Len%/4)
    Bload "a:\xbs.fld\"+F$,Varptr(Music1%(0))
    @M_2
    Void Xbios(32,L:Varptr(Music1%(0)))
  Endif
  Close #1
Return
Procedure M_2
  Void Xbios(32,L:Varptr(Music_off$))
  X%=Xbios(28,254,128+7)
Return
Procedure Infobox
  Box 250,300,400,390
  Box 253,303,397,387
  Text 260,350,"HIT A KEY TO EXIT"
Return
Procedure 2001
  F$="2001spac.xbs"
  @Music                                !***
Return
