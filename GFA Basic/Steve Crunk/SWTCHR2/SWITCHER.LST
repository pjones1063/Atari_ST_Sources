'
' SWITCHER   rev 2
' automatic desktop resolution selector
'
' S.CRUNK 12-14-88
'
Cls
'
' setup printer
M$="XXXX&X000100"
M%=Val(Right$(M$,8))
A%=Xbios(33,M%)
'
Chdir "\"
Print Chr$(27)+"p"
Print At(3,8);"~~~ Steve's handy Desktop Selector ~~~"
Print Chr$(27)+"q"
Print At(5,10);" Configuring printer with "+Right$(M$,8)
Print At(5,12);" The default Resolution is Medium."
Print At(5,13);" You have 5 seconds to hit a key"
Print At(5,14);" to change to Low Resolution."
Print At(5,17);" installing...";
T=Timer
Repeat
  Z%=Int((Timer-T)/2)/100
  Z$=Inkey$
  Print At(19,17);"in ";5-Z%;" seconds   "
Until Z$<>"" Or Z%>4
Rname$="MEDIUM RESOLUTION!    "
If Exist("DESKTOP.MED") Or Exist("DESKTOP.LOW")
  Setup!=True
Else
  Setup!=False
Endif
If (Not Exist("DESKTOP.INF")) Or (Not Setup!)
  Cls
  Print Chr$(27)+"p"
  Print At(1,7);"+-------------------------------------+"
  Print At(1,8);"|   YOU MUST HAVE A DESKTOP.MED OR    |"
  Print At(1,9);"|   DESKTOP.LOW FILE ON DISK WITH A   |"
  Print At(1,10);"| PROPER DESKTOP.INF FOR THIS PROGRAM |"
  Print At(1,11);"|  TO WORK PROPERLY. LOADING DEFAULT. |"
  Print At(1,12);"+-------------------------------------+"
  Print Chr$(27)+"q"
  Pause 100
Else
  If Z$<>""
    Rname$="LOW RESOLUTION!     "
    If Exist("DESKTOP.LOW") And Exist("DESKTOP.INF")
      Name "DESKTOP.INF" As "DESKTOP.MED"
      Name "DESKTOP.LOW" As "DESKTOP.INF"
    Else
      If Exist("DESKTOP.LOW")
        Name "DESKTOP.LOW" As "DESKTOP.INF"
      Endif
    Endif
  Else
    If Exist("DESKTOP.INF") And Exist("DESKTOP.MED")
      Name "DESKTOP.INF" As "DESKTOP.LOW"
      Name "DESKTOP.MED" As "DESKTOP.INF"
    Else
      If Exist("DESKTOP.MED")
        Name "DESKTOP.MED" As "DESKTOP.INF"
      Endif
    Endif
  Endif
  Print At(19,17);Rname$
Endif
Pause 50
Cls
End
