' Nach dem Patchen des Compilers mit GFA_PTCH sollte der GFA-eigene
' Befehl FILESELECT nicht mehr benutzt werden. Er verwendet Line-A
' und erzeugt daher 'M„usedreck' beim Aufruf. Benutzen Sie lieber
' die folgende Routine...
'
a$=@fileselect$("Datei ausw„hlen...","C:\*.*","DATEI.GFA")
' ------------------------------------------------------------------------------
' Aufruf von FSEL_EXINPUT...
'
> FUNCTION fileselect$(a$,pfad$,file$)
LOCAL f&,b&
'
~WIND_UPDATE(1)                           ! BEG_UPDATE
'
IF INT{ADD({ADD(GB,4)},0)}<&H140          ! Altes GEM/TOS...
  f&=FSEL_INPUT(pfad$,file$,b&)
  '
ELSE                                      ! Sonst mit Titelzeile...
  f&=@fsel_exinput(a$,pfad$,file$,b&)
ENDIF
'
~WIND_UPDATE(0)                           ! END_UPDATE
'
IF f&=0 OR b&=0                           ! Abbruch oder Error...
  RETURN ""
ENDIF
'
RETURN LEFT$(pfad$,RINSTR(pfad$,"\"))+file$
ENDFUNC
> FUNCTION fsel_exinput(a$,VAR pfad$,file$,b&)
$F%
'
a$=a$+CHR$(0)         ! Titel
pfad$=pfad$+CHR$(0)+SPACE$(400)
file$=file$+CHR$(0)+SPACE$(150)
'
GCONTRL(0)=91
GCONTRL(1)=0
GCONTRL(2)=2
GCONTRL(3)=3
GCONTRL(4)=0
'
ADDRIN(0)=V:pfad$     ! Pfad
ADDRIN(1)=V:file$     ! Datei
ADDRIN(2)=V:a$        ! Titel
'
GEMSYS
'
pfad$=CHAR{V:pfad$}   ! Pfad
file$=CHAR{V:file$}   ! Dateiname
'
b&=GINTOUT(1)         ! Abbruch=0, OK=1
'
RETURN GINTOUT(0)     ! Fehlercode
ENDFUNC
' ------------------------------------------------------------------------------
