> FUNCTION xrsrc_init
LOCAL a%,tl%,dl%,bl%,p$
' -----------------------------------------------------------
INLINE xrsrc%,9423
INLINE relo%,94
'
f.base%=GEMDOS(75,5,L:0,L:0,L:0)     ! CREATE BASEPAGE
'
IF f.base%<=0
  RETURN FALSE                       ! ERROR!
ENDIF
'
tl%={xrsrc%+2}                       ! L„nge TEXT
dl%={xrsrc%+6}                       ! L„nge DATA
bl%={xrsrc%+10}                      ! L„nge BSS
'
{f.base%+8}=f.base%+256              ! Adresse TEXT
{f.base%+16}={f.base%+8}+{xrsrc%+2}  ! Adresse DATA
{f.base%+24}={f.base%+16}+{xrsrc%+6} ! Adresse BSS
'
{f.base%+12}=tl%                     ! L„nge TEXT
{f.base%+20}=dl%                     ! L„nge DATA
{f.base%+28}=bl%                     ! L„nge BSS
'
' {f.base%+32}=dta%                  ! Addresse DTA
'
BMOVE xrsrc%+28,f.base%+256,9423-28  ! Relozieren...
~C:relo%(L:f.base%+256,L:tl%,L:dl%,L:INT{xrsrc%+14})
'
~MSHRINK(f.base%,256+tl%+dl%+bl%)
' -----------------------------------------------------------
f.init%={f.base%+8}
'
p$=STRING$(100,0)
{f.init%+4}=V:p$
'
$C+
a%=C:f.init%(L:f.base%)              ! Initialisierung
$C-
'
IF a%=0                              ! Hat geklappt...
  f.scalc%={f.init%+42}
  '
  WORD{f.init%+4+36}=128             ! Nummer der Function
  $C+
  r%=C:f.scalc%()
  $C-
  '
  RETURN TRUE
ENDIF
'
RETURN FALSE                         ! ERROR!
ENDFUNC
> PROCEDURE xrsrc_exit
'
WORD{f.init%+4+36}=129          ! Nummer der Funktion
$C+
r%=C:f.scalc%()
$C-
'
~MFREE(f.base%)
'
RETURN
> FUNCTION xrsrc_load(file$)
LOCAL r%
'
file$=file$+CHR$(0)
'
WORD{f.init%+4+36}=1          ! Nummer der Funktion
'
{f.init%+4+16}=V:file$
'
$C+
r%=C:f.scalc%()
$C-
'
RETURN r%
ENDFUNC
> FUNCTION xrsrc_free
LOCAL r%
'
WORD{f.init%+4+36}=2          ! Nummer der Function
'
$C+
r%=C:f.scalc%()
$C-
'
RETURN r%
ENDFUNC
> FUNCTION xrsrc_gaddr(re_gtype&,re_gindex&,VAR re_gaddr%)
LOCAL r%
'
WORD{f.init%+4+36}=3          ! Nummer der Funktion
'
WORD{f.init%+4+0}=re_gtype&
WORD{f.init%+4+2}=re_gindex&
'
{f.init%+4+16}=V:re_gaddr%
'
$C+
r%=C:f.scalc%()
$C-
'
RETURN r%
ENDFUNC
> FUNCTION xrsrc_saddr(re_gtype&,re_gindex&,VAR re_gaddr%)
LOCAL r%
'
WORD{f.init%+4+36}=4          ! Nummer der Funktion
'
WORD{f.init%+4+0}=re_gtype&
WORD{f.init%+4+2}=re_gindex&
'
{f.init%+4+16}=V:re_gaddr%
'
$C+
r%=C:f.scalc%()
$C-
'
RETURN r%
ENDFUNC
> FUNCTION xrsrc_obfix(re_gaddr%,re_obj&)
LOCAL r%
'
WORD{f.init%+4+36}=5          ! Nummer der Funktion
'
WORD{f.init%+4+0}=re_obj&
'
{f.init%+4+16}=re_gaddr%
'
$C+
r%=C:f.scalc%()
$C-
'
RETURN r%
ENDFUNC
