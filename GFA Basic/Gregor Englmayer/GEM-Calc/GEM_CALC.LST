'
'
'  ***************************
'  * GEM-CALC 1.92    Ω 1988 *
'  * Dr. Gregor Englmayer    *
'  * A-8813 St. Lambrecht    *
'  * Weiûenbach 30/2         *
'  ***************************
'
On Break Gosub Ende
On Error Gosub Fehler
Gosub Aufloesung
Gosub Icon
Gosub Titel
Defmouse 2
Gosub Init_variablen
Gosub Leiste
Gosub Init
Gosub Grafik_init
Cls
'
Menu Leiste$()
Menu 33,1
Menu 34,1
For I%=76 To 83
  Menu I%,2
Next I%
If Aufloesung%=1
  Menu 85,2
Endif
Defmouse 0
On Menu  Gosub Menue
On Menu Key Gosub Taste
On Menu Ibox 1,36,80*Dvs,586-17*Test%,302*Dvs Gosub Cursor.inbox
On Menu Ibox 2,623,57*Dvs,16,335*Dvs Gosub Schieber
'
Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
'
Fehlerlabel:
Close
On Error Gosub Fehler
Do
  On Menu
  If Sflag!=True
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Sflag!=False
  Endif
Loop
'
Procedure Schieber
  Sflag!=False
  Deftext 1,0,0,6+2*Test%
  Repeat
    While Mousek<>0
      Delta_x=26/Breite%
      Ym%=Mousey
      If Ym%>88*Dvs And Ym%<115*Dvs
        Sflag!=True
        X%=(Ym%-88*Dvs)/Dvs/Delta_x
        X%=Max(1,X%)
        Cx%=X%
        If Ox%<>X%
          Ox%=X%
          Gosub Zeichne_schieber1(X%)
        Endif
      Endif
      If Ym%<=(168+(Y%+Lng%)*182/Hoehe%)*Dvs-Test% And Ym%>=(168+Y%*182/Hoehe%)*Dvs
        Sflag!=True
        Oy%=Y%
        Dy=(168+Y%*182/Hoehe%)*Dvs-Ym%
        Repeat
          Yym%=Mousey+Dy
          If Yym%>167*Dvs And Yym%<352*Dvs
            Y%=(Yym%/Dvs-167)/182*Hoehe%
            Y%=Min(Y%,Hoehe%-Lng%)
            If ((168+Y%*182/Hoehe%)*Dvs)>166*Dvs And (168+(Y%+Lng%)*182/Hoehe%)*Dvs-Test%<352*Dvs And Oy%<>Y%
              Oy%=Y%
              Gosub Zeichne_schieber3(Y%)
            Endif
          Endif
        Until Mousek=0
        Ym%=Mousey
      Endif
      If Ym%>(168+(Y%+Lng%)*182/Hoehe%)*Dvs-Test% And Ym%<352*Dvs
        Sflag!=True
        Add Y%,10
        Y%=Min(Y%,Hoehe%)
        Gosub Zeichne_schieber3(Y%)
        Pause 8
      Endif
      If Ym%>167*Dvs And Ym%<(168+Y%*182/Hoehe%)*Dvs
        Sflag!=True
        Sub Y%,10
        Y%=Max(Y%,1)
        Gosub Zeichne_schieber3(Y%)
        Pause 8
      Endif
      '
      If Ym%>57*Dvs And Ym%<73*Dvs And X%>1
        Sflag!=True
        Dec X%
        Cx%=X%
        Gosub Zeichne_schieber1(X%)
        Pause 5
      Endif
      If Ym%>115*Dvs And Ym%<131*Dvs And X%<Breite%
        Sflag!=True
        Inc X%
        Cx%=X%
        Gosub Zeichne_schieber1(X%)
        Pause 5
      Endif
      If Ym%>135*Dvs And Ym%<151*Dvs And Y%>1
        Sflag!=True
        Dec Y%
        Gosub Zeichne_schieber3(Y%)
        Pause 5
      Endif
      If Ym%>367*Dvs And Ym%<383*Dvs And Y%<Hoehe%-Lng%
        Sflag!=True
        Inc Y%
        Gosub Zeichne_schieber3(Y%)
        Pause 5
      Endif
      If Ym%>151*Dvs And Ym%<167*Dvs And Y%>1
        Sflag!=True
        Sub Y%,100
        Y%=Max(Y%,1)
        Gosub Zeichne_schieber3(Y%)
        Pause 8
      Endif
      If Ym%>351*Dvs And Ym%<367*Dvs And Y%<Hoehe%-Lng%
        Sflag!=True
        Add Y%,100
        Y%=Min(Y%,Hoehe%-Lng%)
        Gosub Zeichne_schieber3(Y%)
        Pause 8
      Endif
    Wend
  Until Mousex<622 Or Mousey<51*Dvs Or Mousey>382*Dvs
  Cy%=Y%
  Deftext 1,,,13+7*Test%
Return
'
Procedure Cursor.inbox
  If Mousek<>0 And Grafik!=False
    Ym%=(Mousey-64*Dvs)/(8*Aufloesung%)+Y%-1
    If Ym%<=Y%+Lng%
      Gosub Invert(0)
      Cy%=Ym%
      Cy%=Max(Cy%,1)
      Xm%=(Mousex-28)/8
      Br%=0
      I%=X%
      Repeat
        Add Br%,Breite%(I%)
        Inc Br%
        If Xm%<=Br%
          Cx%=I%
          I%=Spalten%
        Endif
        Inc I%
      Until I%>=Spalten%+1
      Gosub Invert(1)
      Text 0,398*Dvs,"    "
      H$=Chr$(Cx%+64)+Str$(Cy%)+"  "
      Graphmode 4
      Text 0,398*Dvs,H$
      Graphmode 1
    Endif
  Endif
Return
'
Procedure Taste
  Menu Off
  Aa%=Menu(14)/256
  Ab%=Menu(14)-256*Aa%
  Bb%=Menu(13)
  '
  If (Aa%=75 Or Aa%=77 Or Aa%=72 Or Aa%=80) And (Bb% And 3)<>0 And Grafik!=False !SHIFT+CURSOR
    Gosub Invert(0)
    If Aa%=72 !RAUF
      Sub Y%,20
      Y%=Max(Y%,1)
      Sub Cy%,20
      Cy%=Max(Cy%,1)
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
    If Aa%=80 !RUNTER
      Add Cy%,19
      Cy%=Min(Cy%,Hoehe%-1)
      Gosub Runter(0)
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
    If Aa%=75 !LINKS
      Sub X%,5
      X%=Max(X%,1)
      Sub Cx%,5
      Cx%=Max(Cx%,1)
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
    If Aa%=77 !RECHTS
      Add Cx%,4
      Cx%=Min(Cx%,Breite%-1)
      Gosub Rechts(0)
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
    Clr Aa%
  Else
    '
    If Ab%=0 And Grafik!=False And Aa%<>3 And Aa%<>110
      If Aa%=71 !CLR-HOME
        Gosub Invert(0)
        Gosub Gehe.a1
      Endif
      '
      If Aa%=119 !CONTROL+CLR-HOME
        Gosub Invert(0)
        X%=1
        Cx%=Xmax%-1
        Gosub Rechts(0)
        If Y%<>Ymax%
          Y%=1
          Cy%=Ymax%-1
          Gosub Runter(0)
        Endif
        Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Endif
      '
      If Aa%=72 And Bb%=4 !CONTROL+RAUF
        Gosub Invert(0)
        Y%=1
        Cy%=1
        Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Endif
      If Aa%=80 And Bb%=4 !CONTROL+RUNTER
        Gosub Invert(0)
        If Y%<>Ymax%
          Y%=1
          Cy%=Ymax%-1
          Gosub Runter(0)
        Endif
        Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Endif
      '
      If Aa%=115 And Bb%=4 !CONTROL+LINKS
        Gosub Invert(0)
        X%=1
        Cx%=1
        Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Endif
      '
      If Aa%=116 And Bb%=4 !CONTROL+RECHTS
        Gosub Invert(0)
        X%=1
        Cx%=Xmax%-1
        Gosub Rechts(0)
        Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Endif
      '
      If Aa%=98
        Gosub Help.screen
      Endif
      '
      If Bb%<>4
        Gosub Beweg.cursor
      Endif
      '
      ' FUNKTIONSTASTE
      '
      On Aa%-58 Gosub Edit,Block.ru,Laden,Breite,Komma,Copy.abs,Move.abs,Gehe.zelle,Drucken,Loesche.zelle
      '
      ' SHIFT-FUNKTIONSTASTE
      '
      On Aa%-83 Gosub Recalc,Block.lo,Speichern,Breite.all,Komma.all,Copy.rel,Move.rel,Kopf,Ende,Loeschen
      '
    Else
      '
      H$="ÑéîôÅöû"+Chr$(126)+Chr$(124)+"^"
      If ((Ab%>31 And Ab%<123) Or Instr(H$,Chr$(Aa%))<>0) And Grafik!=False
        Gosub Eingabe
      Else
        If Grafik!=False And Aa%>11
          '
          If Ab%=30 And Aa%=12
            Gosub Help.screen
          Endif
          '
          If Ab%=13 And Aa%=50
            Gosub Merken
          Endif
          '
          If Ab%=1 And Aa%=30
            Gosub Copy.cel.abs
          Endif
          '
          If Ab%=18 And Aa%=19
            Gosub Copy.cel.rel
          Endif
          '
          If Ab%=25 And Aa%=44
            Gosub Widmung
          Endif
          '
          If Ab%=3 And Aa%=46
            Gosub Invers
          Endif
          '
          If Ab%=23 And Aa%=17
            Gosub Degas
          Endif
          '
          If Ab%=16 And Aa%=25 !LINEFEED
            If Lfeed!=True
              Alert 2," | Carr. Return mit Linefeed ?",1,"  ja  | nein ",H%
              If H%=2
                Gespeichert!=False
                Lfeed!=False
                Menu 34,0
              Endif
            Else
              Alert 2," | Carr. Return mit Linefeed ?",2,"  ja  | nein ",H%
              If H%=1
                Gespeichert!=False
                Lfeed!=True
                Menu 34,1
              Endif
            Endif
          Endif
          '
          If Ab%=20 And Aa%=20 !TRENNZEICHEN
            If Trennz!=True
              Alert 2," | Trennzeichen beim Ausdruck ?",1,"  ja  | nein ",H%
              If H%=2
                Gespeichert!=False
                Trennz!=False
                Menu 35,0
              Endif
            Else
              Alert 2," | Trennzeichen beim Ausdruck ?",2,"  ja  | nein ",H%
              If H%=1
                Gespeichert!=False
                Trennz!=True
                Menu 35,1
              Endif
            Endif
          Endif
          '
          If Ab%=2 And Aa%=48 !BOGENMAû
            If Grd=1
              Alert 2," | Bogenmaû ?",1,"  ja  | nein ",H%
              If H%=2
                Gespeichert!=False
                Grd=Pi/180
                Menu 67,0
              Endif
            Else
              Alert 2," | Bogenmaû ?",2,"  ja  | nein ",H%
              If H%=1
                Gespeichert!=False
                Grd=1
                Menu 67,1
              Endif
            Endif
          Endif
          '
          If Ab%=14 And Aa%=49 !NACHKOMMA-0
            If Null!=True
              Alert 2," | Nachkommastellen mit '0' | auffÅllen ?",1,"  ja  | nein ",H%
              If H%=2
                Gespeichert!=False
                Null!=False
                Menu 33,0
                Gosub Nachkomma
              Endif
            Else
              Alert 2," | Nachkommastellen mit '0' | auffÅllen ?",2,"  ja  | nein ",H%
              If H%=1
                Gespeichert!=False
                Null!=True
                Menu 33,1
                Gosub Nachkomma
              Endif
            Endif
          Endif
          '
          If Ab%=26 And Aa%=21 !AUSDRUCK ZELLINHALT
            Gosub Druck.zellinhalt
          Endif
          '
          If Ab%=11 And Aa%=37
            Gosub Hi.resol
          Endif
          '
          If Ab%=21 And Aa%=22
            Gosub Daten.uebernahme
          Endif
          '
          If Ab%=24 And Aa%=45
            Gosub Sortieren
          Endif
          '
          If Ab%=12 And Aa%=38
            Gosub Zahl_spalten
          Endif
          '
          If Ab%=15 And Aa%=24
            Gosub Such.fehler
          Endif
          '
          If Ab%=22 And Aa%=47 !BLOCK-ZELLEN IN WERTE éNDERN
            Gosub Block.werte
          Endif
          '
        Endif
        '
        If Ugrafik!=True And Aa%>11
          If Ab%=4 And Spa<=14 And Offs>=0 And Aa%=32 !KUCHENGRAFIK
            Gosub Kuchen
          Endif
          '
          If Ab%=5 And Aa%=18 !LINIENGRAFIK
            Hilf$=" E Linien"
            Gosub Grafik.2d
          Endif
          '
          If Ab%=6 And Aa%=33 And (Spa<=14 Or Rei<=2) !BALKENGRAFIK
            Hilf$=" F Balken"
            Gosub Grafik.2d
          Endif
          '
          If Ab%=7 And Aa%=34 And Offs>=0 !STAPELGRAFIK
            Gosub Stapel
          Endif
          '
          If Ab%=8 And Aa%=35 And Offs>=0 !SéULENGRAFIK
            Hilf$=" H SÑulen"
            Gosub Grafik.3d
          Endif
          '
          If Ab%=9 And Aa%=23 And Spa<=14 And Offs>=0 !BLOCKGRAFIK
            Hilf$=" I Blîcke"
            Gosub Grafik.3d
          Endif
          '
          If Ab%=10 And Aa%=36 And Spa<=14 And Offs>=0 !FLéCHENGRAFIK
            Hilf$=" J FlÑchen"
            Gosub Grafik.3d
          Endif
        Endif
        '
        If Ab%=17 And Aa%=16
          Gosub Hardcpy
        Endif
        '
        If Ab%=19 And Aa%=31 !BILDSCHIRMAUFBAU nach Grafik oder Accessory
          Gosub Spreadsheet
        Endif
        '
        If Aa%<=11
          If Ab%=17 And Aa%=2
            Gosub Zeile.einfugen
          Endif
          '
          If Ab%=0 And Aa%=3
            Gosub Zeile.loschen
          Endif
          '
          If Ab%=19 And Aa%=4
            Gosub Spalte.einfugen
          Endif
          '
          If Ab%=20 And Aa%=5
            Gosub Spalte.loschen
          Endif
          '
          If Ab%=21 And Aa%=6
            Gosub Save.gdaten
          Endif
          '
          If Ab%=30 And Aa%=7
            Gosub Load.gdaten
          Endif
          '
          If Ab%=23 And Aa%=8
            ' control-7
          Endif
          '
          If Ab%=24 And Aa%=9
            ' control-8
          Endif
          '
          If Ab%=25 And Aa%=10
            Gosub Anpassen_max
          Endif
          '
          If Ab%=16 And Aa%=11
            Gosub Titelzeilen
          Endif
        Endif
        '
      Endif
    Endif
  Endif
  Repeat
  Until Inkey$=""
Return
'
' UNTERPROGRAMME
'
Procedure Zahl_spalten
  Local X1%,Y1%
  Gosub Invert(0)
  A$="Zahl Spalten"
  Gosub Anzeige
  H$="(Esc) Zahl an Spalten (5 - 26) = "
  H%=Breite%
  Gosub Ein.zahl(H$,H%,5,26,2,3)
  If Val(Aus$)<>-1
    Br_neu%=Val(Aus$)
    If Br_neu%<>Breite%
      H_neu%=Min(5200/Br_neu%,999)
      H%=1
      If Br_neu%<Xmax% Or H_neu%<Ymax%
        Alert 3," |Benutzter Bereich grîûer als|neue Blattgrîûe. Ein Teil der|Daten geht VERLOREN !",2," okay |oh nein",H%
      Endif
      If H%=1
        Defmouse 2
        Swap Breite%,Br_neu%
        Swap Hoehe%,H_neu%
        Swap Hlp$(),Ein$()
        Erase Ein$()
        Erase Aus$()
        Erase Erg()
        Dim Ein$(Breite%,Hoehe%),Aus$(Breite%,Hoehe%),Erg(Breite%,Hoehe%)
        Arrayfill Erg(),0
        For I%=1 To Breite%
          If Kopf$(I%)=""
            Kopf$(I%)=Chr$(I%+64)
          Endif
          For J%=1 To Hoehe%
            Aus$(I%,J%)=Space$(Breite%(I%)+1)
          Next J%
        Next I%
        For I%=77 To 83
          Menu I%,2
        Next I%
        Gosub Variablen.init
        Gosub Grafik_init
        Xmax%=1
        Ymax%=1
        Cd%=2
        Ugrafik!=False
        Grafik!=False
        Gosub Breite.block
        Sget Screen$
        Xmax%=Breite%
        Ymax%=Hoehe%
        Gosub Adjust.screen
        Gosub Invert(0)
        A$="Zahl Spalten"
        Gosub Anzeige
        Defmouse 2
        If Breite%<Br_neu%
          For X1%=1 To Breite%
            For Y1%=1 To H_neu%
              Swap Ein$(X1%,Y1%),Hlp$(X1%,Y1%)
              If Left$(Ein$(X1%,Y1%),1)="="
                Gosub Rechne.zelle(X1%,Y1%)
              Endif
              Gosub Format.zelle(X1%,Y1%)
            Next Y1%
          Next X1%
        Else
          For Y1%=1 To Hoehe%
            For X1%=1 To Br_neu%
              Swap Ein$(X1%,Y1%),Hlp$(X1%,Y1%)
              If Left$(Ein$(X1%,Y1%),1)="="
                Gosub Rechne.zelle(X1%,Y1%)
              Endif
              Gosub Format.zelle(X1%,Y1%)
            Next X1%
          Next Y1%
        Endif
        Erase Hlp$()
        Dim Hlp$(Breite%,Hoehe%)
        Gespeichert!=False
        Gosub Anpassen_max
        Gosub Recalc
      Else
        Gosub Adjust.screen
      Endif
    Else
      Gosub Adjust.screen
    Endif
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Save.gdaten
  A$="Spch Grafikdaten"
  Gosub Select2
  If File1$<>"" And File1$<>"\"
    Open "O",#1,File1$
    Print #1;Reih%
    Print #1;Ueb1$
    Print #1;Ueb2$
    Print #1;Offs
    Print #1;Rei
    Print #1;Beginn%
    Print #1;Bezrei%
    Print #1;Spa
    Print #1;Beginn2%
    Print #1;Bez%
    Close #1
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Load.gdaten
  A$="Laden Grafikdaten"
  Gosub Select2
  If File1$<>"" And File1$<>"\"
    Open "I",#1,File1$
    Input #1,Reih%
    Input #1,Ueb1$
    Input #1,Ueb2$
    Input #1,Offs
    Input #1,Rei
    Input #1,Beginn%
    Input #1,Bezrei%
    Input #1,Spa
    Input #1,Beginn2%
    Input #1,Bez%
    Close #1
    Gosub Daten.uebernahme
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Select2
  Gosub Invert(0)
  Gosub Anzeige
  H$="\*.GFK"
  Sget Screen$
  Fileselect H$,File$,File1$
  If Len(File1$)>4
    If Right$(File1$,4)<>".GFK"
      Ai%=Instr(File1$,".")
      If Ai%>0
        File1$=Left$(File1$,Ai%)+"GFK"
      Else
        File1$=File1$+".GFK"
      Endif
    Endif
  Endif
  If File1$<>"" And File1$<>"\"
    Ai%=Instr(File1$,".")
    If Ai%>0
      File1$=Left$(File1$,Ai%)+"GFK"
    Else
      File1$=File1$+".GFK"
    Endif
  Endif
  If File1$<>"" And File1$<>"\"
    File$=Mid$(File1$,3)
    Ai%=Instr(File$,".")
    File$=Left$(File$,Ai%-1)
  Endif
  Sput Screen$
Return
'
Procedure Such.fehler
  Gosub Invert(0)
  Clr X1%,Y1%
  E1flag!=False
  For L_x%=Cx% To Xmax%
    For L_y%=Cy% To Ymax%
      If Left$(Aus$(L_x%,L_y%),3)="*FE"
        X1%=L_x%
        Y1%=L_y%
        E1flag!=True
      Endif
      Exit If E1flag!=True
    Next L_y%
    Exit If E1flag!=True
  Next L_x%
  If X1%=0 And X2%=0
    Out 2,7
    Alert 0,"Keine Fehler gefunden ",1," okay ",In%
    Gosub Invert(1)
  Else
    X%=X1%
    Y%=Y1%
    Cx%=X1%
    Cy%=Y1%
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
Return
'
Procedure Block.werte
  If B1x%>B2x% Or B1y%>B2y%
    Alert 3," |BLOCK-Koordinaten falsch|      gewÑhlt !!",1,"Abbruch",H%
    H%=4
  Else
    Gosub Invert(0)
    A$="Block -> Werte"
    Gosub Anzeige
    Alert 1," |Zellinhalte im Blockbereich|in die absoluten Werte|Ñndern ?",2," okay | nein ",In%
    If In%=1
      Gespeichert!=False
      Defmouse 2
      For L_x%=B1x% To B2x%
        For L_y%=B1y% To B2y%
          If Left$(Ein$(L_x%,L_y%),1)="=" Or (Asc(Ein$(L_x%,L_y%))>=48 And Asc(Ein$(L_x%,L_y%))<=57)
            Ein$(L_x%,L_y%)=Str$(Erg(L_x%,L_y%))
            Aus$(L_x%,L_y%)=Ein$(L_x%,L_y%)
            Gosub Format.zelle(L_x%,L_y%)
          Endif
        Next L_y%
      Next L_x%
      Defmouse 0
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Else
      Gosub Inv_anzeige
    Endif
  Endif
Return
'
Procedure Zeile.einfugen
  Gosub Invert(0)
  A$="Zeile einfÅgen"
  Gosub Anzeige
  If Ymax%=Hoehe%
    Alert 3," |Daten in Zeile 200|gehen verloren !",2," okay |Abbruch",In%
  Else
    In%=1
    Inc Ymax%
  Endif
  If In%=1
    Defmouse 2
    Gespeichert!=False
    For L_y%=Ymax% Downto Cy%+1
      For L_x%=1 To Xmax%
        Ein$(L_x%,L_y%)=Ein$(L_x%,L_y%-1)
        If Left$(Ein$(L_x%,L_y%),1)="="
          Clr A%
          Repeat
            A%=Instr(Ein$(L_x%,L_y%),Chr$(126),A%+1)
            If A%<>0
              Gosub Endern.koord2(1)
            Endif
          Until A%=0
          Restore Funktion
          Do
            Read F$
            Exit If F$="END"
            Repeat
              A%=Instr(Ein$(L_x%,L_y%),F$,A%+1)
              If A%<>0
                Add A%,4
                Gosub Endern.koord2(1)
                A%=Instr(Ein$(L_x%,L_y%),":",A%)
                Gosub Endern.koord2(1)
              Endif
            Until A%=0
          Loop
        Endif
        Aus$(L_x%,L_y%)=Aus$(L_x%,L_y%-1)
        Erg(L_x%,L_y%)=Erg(L_x%,L_y%-1)
      Next L_x%
    Next L_y%
    For L_x%=1 To Xmax%
      Ein$(L_x%,Cy%)=""
      Aus$(L_x%,Cy%)=Space$(Breite%(L_x%)+1)
      Erg(L_x%,Cy%)=0
    Next L_x%
    Gosub Recalc
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Zeile.loschen
  Gosub Invert(0)
  A$="Zeile lîschen"
  Gosub Anzeige
  Alert 3," |ZEILE wirklich LôSCHEN ??",2," okay | nein ",In%
  If In%=1
    Defmouse 2
    Gespeichert!=False
    If Ymax%>=Cy%
      Dec Ymax%
      For L_y%=Cy% To Ymax%
        For L_x%=1 To Xmax%
          Ein$(L_x%,L_y%)=Ein$(L_x%,L_y%+1)
          If Left$(Ein$(L_x%,L_y%),1)="="
            Clr A%
            Repeat
              A%=Instr(Ein$(L_x%,L_y%),Chr$(126),A%+1)
              If A%<>0
                Gosub Endern.koord2(-1)
              Endif
            Until A%=0
            Restore Funktion
            Do
              Read F$
              Exit If F$="END"
              Repeat
                A%=Instr(Ein$(L_x%,L_y%),F$,A%+1)
                If A%<>0
                  Add A%,4
                  Gosub Endern.koord2(-1)
                  A%=Instr(Ein$(L_x%,L_y%),":",A%)
                  Gosub Endern.koord2(-1)
                Endif
              Until A%=0
            Loop
          Endif
          Aus$(L_x%,L_y%)=Aus$(L_x%,L_y%+1)
          Erg(L_x%,L_y%)=Erg(L_x%,L_y%+1)
        Next L_x%
      Next L_y%
      For L_x%=1 To Xmax%
        Ein$(L_x%,Ymax%+1)=""
        Aus$(L_x%,Ymax%+1)=Space$(Breite%(L_x%)+1)
        Erg(L_x%,Ymax%+1)=0
      Next L_x%
    Endif
    Gosub Recalc
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Spalte.einfugen
  Gosub Invert(0)
  A$="Spalte einfÅgen"
  Gosub Anzeige
  If Xmax%=Breite%
    Alert 3," |Daten in Spalte Z|gehen verloren !",2," okay |Abbruch",In%
  Else
    In%=1
    Inc Xmax%
  Endif
  If In%=1
    Gespeichert!=False
    Defmouse 2
    For L_x%=Breite% Downto Cx%+1
      If Kopf$(L_x%-1)<>Chr$(L_x%+63)
        Kopf$(L_x%)=Kopf$(L_x%-1)
      Endif
    Next L_x%
    For L_x%=Breite% Downto Cx%+1
      Breite%(L_x%)=Breite%(L_x%-1)
      Komma%(L_x%)=Komma%(L_x%-1)
      For L_y%=1 To Hoehe%
        Ein$(L_x%,L_y%)=Ein$(L_x%-1,L_y%)
        If Left$(Ein$(L_x%,L_y%),1)="="
          Clr A%
          Repeat
            A%=Instr(Ein$(L_x%,L_y%),Chr$(126),A%+1)
            If A%<>0
              Gosub Endern.koord3(1)
            Endif
          Until A%=0
          Restore Funktion
          Do
            Read F$
            Exit If F$="END"
            Repeat
              A%=Instr(Ein$(L_x%,L_y%),F$,A%+1)
              If A%<>0
                Add A%,4
                Gosub Endern.koord3(1)
                A%=Instr(Ein$(L_x%,L_y%),":",A%)
                Gosub Endern.koord3(1)
              Endif
            Until A%=0
          Loop
        Endif
        Aus$(L_x%,L_y%)=Aus$(L_x%-1,L_y%)
        Erg(L_x%,L_y%)=Erg(L_x%-1,L_y%)
      Next L_y%
    Next L_x%
    Kopf$(Cx%)=Chr$(Cx%+64)
    Breite%(Cx%)=Breite%(0)
    Komma%(Cx%)=Komma%(0)
    For L_y%=1 To Hoehe%
      Ein$(Cx%,L_y%)=""
      Aus$(Cx%,L_y%)=Space$(Breite%(Cx%)+1)
      Erg(Cx%,L_y%)=0
    Next L_y%
    Gosub Recalc
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Spalte.loschen
  Gosub Invert(0)
  A$="Spalte lîschen"
  Gosub Anzeige
  Alert 3," |SPALTE wirklich LôSCHEN ??",2," okay | nein ",In%
  If In%=1
    Defmouse 2
    Gespeichert!=False
    If Xmax%>=Cx%
      Dec Xmax%
      For L_x%=Cx% To Breite%-1
        If Kopf$(L_x%+1)<>Chr$(L_x%+65)
          Kopf$(L_x%)=Kopf$(L_x%+1)
        Endif
        Breite%(L_x%)=Breite%(L_x%+1)
        Komma%(L_x%)=Komma%(L_x%+1)
        For L_y%=1 To Hoehe%
          Ein$(L_x%,L_y%)=Ein$(L_x%+1,L_y%)
          If Left$(Ein$(L_x%,L_y%),1)="="
            Clr A%
            Repeat
              A%=Instr(Ein$(L_x%,L_y%),Chr$(126),A%+1)
              If A%<>0
                Gosub Endern.koord3(-1)
              Endif
            Until A%=0
            Restore Funktion
            Do
              Read F$
              Exit If F$="END"
              Repeat
                A%=Instr(Ein$(L_x%,L_y%),F$,A%+1)
                If A%<>0
                  Add A%,4
                  Gosub Endern.koord3(-1)
                  A%=Instr(Ein$(L_x%,L_y%),":",A%)
                  Gosub Endern.koord3(-1)
                Endif
              Until A%=0
            Loop
          Endif
          Aus$(L_x%,L_y%)=Aus$(L_x%+1,L_y%)
          Erg(L_x%,L_y%)=Erg(L_x%+1,L_y%)
        Next L_y%
      Next L_x%
      Flag!=False
      For L_x%=Breite% Downto Cx%
        If Kopf$(L_x%)<>Chr$(L_x%+64)
          Flag!=True
          Kopf$(L_x%)=Chr$(L_x%+64)
        Endif
        Exit If Flag!=True
      Next L_x%
      Breite%(Xmax%+1)=Breite%(0)
      Komma%(Xmax%+1)=Komma%(0)
      For L_y%=1 To Hoehe%
        Ein$(Xmax%+1,L_y%)=""
        Aus$(Xmax%+1,L_y%)=Space$(Breite%(0)+1)
        Erg(Xmax%+1,L_y%)=0
      Next L_y%
    Endif
    Gosub Recalc
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Endern.koord2(Dy%)
  E1$=Left$(Ein$(L_x%,L_y%),A%+1)
  A1%=Val(Mid$(Ein$(L_x%,L_y%),A%+2))
  If A1%>=Cy%
    A1%=Val?(Mid$(Ein$(L_x%,L_y%),A%+2))
    Ein$=Mid$(Ein$(L_x%,L_y%),A%+2)
    Ein$=Str$(Val(Ein$)+Dy%)+Mid$(Ein$,1+A1%)
    If Left$(Ein$,1)="0"
      Mid$(Ein$,1,1)="1"
    Endif
    Ein$(L_x%,L_y%)=E1$+Ein$
  Endif
Return
'
Procedure Endern.koord3(Dx%)
  E1$=Left$(Ein$(L_x%,L_y%),A%)
  A1%=Asc(Mid$(Ein$(L_x%,L_y%),A%+1))-64
  If A1%>=Cx%
    Ein$=Mid$(Ein$(L_x%,L_y%),A%+1)
    Ein$=Chr$(Asc(Left$(Ein$,1))+Dx%)+Mid$(Ein$,2)
    If Left$(Ein$,1)=Chr$(64)
      Mid$(Ein$,1,1)="A"
    Endif
    Ein$(L_x%,L_y%)=E1$+Ein$
  Endif
Return
'
Procedure Nachkomma
  Local X1%,Y1%
  Defmouse 2
  For X1%=1 To Xmax%
    For Y1%=1 To Ymax%
      Gosub Format.zelle(X1%,Y1%)
    Next Y1%
  Next X1%
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Defmouse 0
Return
'
Procedure Merken
  Local H$
  Mx%=Cx%
  My%=Cy%
  Text 48,398*Dvs,"    "
  H$=Chr$(Mx%+64)+Str$(My%)+"  "
  Graphmode 4
  Text 48,398*Dvs,H$
  Graphmode 1
Return
'
Procedure Copy.cel.abs
  Ein$(Cx%,Cy%)=Ein$(Mx%,My%)
  Erg(Cx%,Cy%)=Erg(Mx%,My%)
  Gosub Format.zelle(Cx%,Cy%)
  Gosub Invert(1)
  Gosub C.minimax
Return
'
Procedure Copy.cel.rel
  Dx%=Cx%-Mx%
  Dy%=Cy%-My%
  X1%=1
  Y1%=1
  Hlp$(X1%,Y1%)=Ein$(Mx%,My%)
  If Left$(Hlp$(X1%,Y1%),1)="="
    Clr A%
    Repeat
      A%=Instr(Hlp$(X1%,Y1%),Chr$(126),A%+1)
      If A%<>0
        Gosub Endern.koord
      Endif
    Until A%=0
    Restore Funktion
    Do
      Read F$
      Exit If F$="END"
      Repeat
        A%=Instr(Hlp$(X1%,Y1%),F$,A%+1)
        If A%<>0
          Add A%,4
          Gosub Endern.koord
          A%=Instr(Hlp$(X1%,Y1%),":",A%)
          Gosub Endern.koord
        Endif
      Until A%=0
    Loop
  Endif
  Ein$(Cx%,Cy%)=Hlp$(X1%,Y1%)
  Hlp$(X1%,Y1%)=""
  If Left$(Ein$(Cx%,Cy%),1)="="
    Gosub Rechne.zelle(Cx%,Cy%)
  Endif
  Gosub Format.zelle(Cx%,Cy%)
  Gosub Invert(1)
  Gosub C.minimax
Return
'
Procedure Line.feed
  Gespeichert!=False
  If Lfeed!=True
    Lfeed!=False
    Menu 34,0
  Else
    Lfeed!=True
    Menu 34,1
  Endif
Return
'
Procedure Trennzeichen
  Gespeichert!=False
  If Trennz!=True
    Trennz!=False
    Menu 35,0
  Else
    Trennz!=True
    Menu 35,1
  Endif
Return
'
Procedure Bogenmass
  Gespeichert!=False
  If Grd<>1
    Grd=1
    Menu 67,1
  Else
    Grd=Pi/180
    Menu 67,0
  Endif
Return
'
Procedure Nullen
  Gespeichert!=False
  If Null!=True
    Null!=False
    Menu 33,0
  Else
    Null!=True
    Menu 33,1
  Endif
  Gosub Nachkomma
Return
'
Procedure Help.screen
  Cls
  Print At(12,2);Chr$(27);"p F U N K T I O N E N ";Chr$(27);"q"
  Print At(9,4);"#PI -- Wert fÅr Pi (3.14...)"
  Print At(46,4);"#DAT -- Datum"
  Print At(6,6);"#SQR() -- Wurzel"
  Print At(6,7);"#LOG() -- Nat. Logarithmus"
  Print At(44,7);"#CLG() -- dek. Logarithmus"
  Print At(44,6);"#EXP() -- Exponentiation"
  Print At(6,8);"#SIN() -- Sinus"
  Print At(44,8);"#COS() -- Cosinus"
  Print At(6,9);"#TAN() -- Tangens"
  Print At(44,9);"#ATN() -- Arcustangens"
  Print At(6,10);"#INT() -- Integer"
  Print At(44,10);"#ABS() -- Absolutwert"
  Print At(6,11);"#RND() -- Runden auf Ganzzahl"
  Print At(44,11);"#FAK() -- FakultÑt"
  Print At(12,13);"#SUM(A1:Z100) -- Summe"
  Print At(12,14);"#MUL(A1:Z100) -- Produkt"
  Print At(12,15);"#AVE(A1:Z100) -- Durchschnitt"
  Print At(12,16);"#STA(A1:Z100) -- Standardabweichung (div. n-1)"
  Print At(12,17);"#STD(A1:Z100) -- Standardabweichung (div. n)"
  Print At(12,18);"#QWN(A1:Z100) -- Quadratischer Mittelwert (div. n)"
  Print At(12,19);"#QMW(A1:Z100) -- Quadratischer Mittelwert (div. n-1)"
  Print At(12,20);"#MIN(A1:Z100) -- Minimum"
  Print At(12,21);"#MAX(A1:Z100) -- Maximum"
  Print At(12,23);"#NUN(N:n)     -- N Åber n  =  N!/(n!*(N-n)!)"
  Repeat
    A=Mousek
  Until A Or Inkey$<>""
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Infozeile
  Local H$,A$
  H$=" frei="+Str$(Fre(0)-100000)+" "
  A$=Space$(80)
  Mid$(A$,1)=Chr$(Cx%+64)+Str$(Cy%)
  Mid$(A$,7)=Chr$(Mx%+64)+Str$(My%)
  Mid$(A$,13)="Block lo="+Chr$(B1x%+64)+Str$(B1y%)
  Mid$(A$,27)="ru="+Chr$(B2x%+64)+Str$(B2y%)
  Mid$(A$,36)="Breite="+Str$(Bl.breite%)+"/"+Str$(All.breite%)
  Mid$(A$,60)=Str$(Breite%)+"*"+Str$(Hoehe%)
  Mid$(A$,81-Len(H$))=H$
  H$=Space$(80)
  Text 0,398*Dvs,H$
  Graphmode 4
  Text 0,398*Dvs,A$
  Graphmode 1
Return
'
Procedure Beweg.cursor
  If (Aa%=72 Or Aa%=200) And Cy%>1
    Gosub Hoch(1)
  Endif
  If (Aa%=80 Or Aa%=208) And Cy%<Hoehe%
    Gosub Runter(1)
  Endif
  If (Aa%=77 Or Aa%=205) And Cx%<Breite%
    Gosub Rechts(1)
  Endif
  If (Aa%=75 Or Aa%=203) And Cx%>1
    Gosub Links(1)
  Endif
  Text 0,398*Dvs,"    "
  H$=Chr$(Cx%+64)+Str$(Cy%)+"  "
  Graphmode 4
  Text 0,398*Dvs,H$
  Graphmode 1
Return
'
Procedure Aufbau.screen(X%,Sy%,Cx%,Cy%)
  Color 1
  If Grafik!=False
    Defmouse 0
    Gosub Zahl.spalten(73)
    If Sy%>Hoehe%-Lng%
      Y%=Hoehe%-Lng%
      Sy%=Y%
    Endif
    Cls
    Put 0,12-12*Test%,Copyright$
  Endif
  Deftext 1,0,0,13+7*Test%
  H$="    "+Chr$(14)+Chr$(15)+"    IN/OUT    FORMAT    BLOCK    ZELLE    SPEZIAL    GRAFIK"
  Text 0,16+8*Test%,H$
  Defline 1
  Line 0,18+8*Test%,639,18+8*Test%
  If Grafik!=False
    Breit%=6
    For Xs%=X% To Spalten%
      Print At(Breit%,5);Left$(Kopf$(Xs%),Breite%(Xs%))
      Deftext 1,,,6+2*Test%
      Text Breit%*8-8-2*Test%,60+31*Test%,Chr$(Xs%+64)
      Add Breit%,(Breite%(Xs%)+1)
    Next Xs%
    Deftext 1,,,13+7*Test%
    Tief%=6
    For Ys%=Sy% To Sy%+Lng%
      Print At(2,Tief%);Using "### ",Ys%;
      For Xs%=X% To Spalten%
        Print Aus$(Xs%,Ys%);
      Next Xs%
      Inc Tief%
    Next Ys%
    X1%=36
    Y1%=95+48*Test%+Lng%*16*Dvs
    Line X1%,63+23*Test%,X1%,Y1%
    Defline -&X101010101010101
    For Xs%=X% To Spalten%
      Add X1%,Breite%(Xs%)*8+8
      If Xs%<>Spalten%
        Line X1%,63+23*Test%,X1%,Y1%
      Endif
    Next Xs%
    Clr Zl%
    For Ys%=63*Dvs To Y1%-16*Dvs Step 16*Dvs
      Inc Zl%
      If Zl%<3
        Defline 1
      Else
        Defline -&X101010101010101
      Endif
      Line 4,Ys%,X1%,Ys%
    Next Ys%
    Defline 1
    Line X1%,63*Dvs,X1%,Y1%
    Line 4,Y1%,X1%,Y1%
    Line 3,63*Dvs,3,Y1%
    Gosub Zeichne_schieber1(X%)
    Gosub Zeichne_schieber2(Sy%)
    Gosub Infozeile
    Gosub Invert(1)
  Endif
  Repeat
  Until Inkey$=""
Return
'
Procedure Zeichne_schieber1(X%)
  Deffill 1,0
  Pbox 622,57*Dvs,638,131*Dvs
  Deftext 1,0,0,13+9*Test%
  Text 626-2*Test%,71*Dvs+2*Test%,Chr$(4)
  Text 627-2*Test%,128*Dvs+Test%,Chr$(3)
  Deftext 1,,,6+2*Test%
  Text 626-2*Test%,84*Dvs+Test%,Chr$(X%+64)
  Deftext 1,,,13+7*Test%
  Line 623,73*Dvs+Test%,638,73*Dvs+Test%
  Line 623,115*Dvs-Test%,638,115*Dvs-Test%
  Line 623,88*Dvs+Test%,638,88*Dvs+Test%
  Defline -&X101010101010101
  Delta_x=26/Breite%
  Line 623,(88+X%*Delta_x)*Dvs,637,(88+X%*Delta_x)*Dvs
  Defline 1
Return
'
Procedure Zeichne_schieber2(Y%)
  Deffill 1,0
  Pbox 622,135*Dvs,638,383*Dvs
  Deftext 1,0,0,13+9*Test%
  Text 626-2*Test%,149*Dvs+2*Test%,Chr$(1)
  Text 626-2*Test%,380*Dvs+Test%,Chr$(2)
  Deftext 1,,,6+2*Test%
  Text 624-2*Test%,161*Dvs+Test%,13+3*Test%,Chr$(1)+Chr$(1)
  Text 624-2*Test%,361*Dvs-Test%,13+3*Test%,Chr$(2)+Chr$(2)
  Deftext 1,,,13+7*Test%
  Line 623,151*Dvs+Test%,638,151*Dvs+Test%
  Line 623,367*Dvs-Test%,638,367*Dvs-Test%
  Defline 1
  Gosub Zeichne_schieber3(Y%)
Return
'
Procedure Zeichne_schieber3(Y%)
  If Y%=1
    Deffill 1,0
    Pbox 622,167*Dvs+Test%,638,(167+Lng%*182/Hoehe%)*Dvs
    Deffill 2,2,4
    Pbox 622,(167+Lng%*182/Hoehe%)*Dvs,638,351*Dvs-Test%
  Else
    If Y%=Hoehe%-Lng%
      Deffill 1,0
      Pbox 622,(351-Lng%*182/Hoehe%)*Dvs,638,351*Dvs-Test%
      Deffill 2,2,4
      Pbox 622,167*Dvs+Test%,638,(351-Lng%*182/Hoehe%)*Dvs
    Else
      Deffill 1,0
      Pbox 622,(168+Y%*182/Hoehe%)*Dvs,638,(168+(Y%+Lng%)*182/Hoehe%)*Dvs-Test%
      Deffill 3,2,4
      Pbox 622,167*Dvs+Test%,638,(168+Y%*182/Hoehe%)*Dvs
      Pbox 622,(168+(Y%+Lng%)*182/Hoehe%)*Dvs-Test%,638,351*Dvs-Test%
    Endif
  Endif
Return
'
Procedure Hoch(Dy%)
  Gosub Invert(0)
  Sub Cy%,Dy%
  If Cy%<Y%
    Sub Y%,Dy%
    Y%=Min(Y%,Cy%)
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Gosub Invert(1)
  Endif
Return
'
Procedure Runter(Flag%)
  If Flag%
    Gosub Invert(0)
  Endif
  Inc Cy%
  If Cy%>Y%+Lng%
    While Cy%>Y%+Lng%
      Inc Y%
    Wend
    If Flag%
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
  Else
    If Flag%
      Gosub Invert(1)
    Endif
  Endif
Return
'
Procedure Rechts(Flag%)
  If Flag%
    Gosub Invert(0)
  Endif
  Inc Cx%
  Gosub Zahl.spalten(73)
  If Cx%>Spalten%
    Repeat
      Inc X%
      Gosub Zahl.spalten(73)
    Until Spalten%>=Cx%
    If Flag%
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
  Else
    If Flag%
      Gosub Invert(1)
    Endif
  Endif
Return
'
Procedure Links(Dx%)
  Gosub Invert(0)
  Sub Cx%,Dx%
  If Cx%<X%
    Sub X%,Dx%
    X%=Min(X%,Cx%)
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Gosub Invert(1)
  Endif
Return
'
Procedure Zahl.spalten(B%)
  Clr Br%
  For Spalten%=X% To Breite%
    Add Br%,Breite%(Spalten%)+1
    Exit If Br%>B%
  Next Spalten%
  Dec Spalten%
Return
'
Procedure Invert(Inv%)
  If Inv%=1
    Inv$=Ron$
  Else
    Inv$=Roff$
  Endif
  Breit%=6
  If Cx%>X%
    For Xs%=X% To Cx%-1
      Add Breit%,(Breite%(Xs%)+1)
    Next Xs%
  Endif
  Tief%=6+Cy%-Y%
  H$=Left$(Aus$(Cx%,Cy%),Len(Aus$(Cx%,Cy%))-1)
  Print At(Breit%,Tief%);Inv$;H$;Roff$
  If Left$(H$,3)="*FE" And Inv%=1
    Print Chr$(7);
  Endif
  If Inv%=0
    Breit%=Breit%*8-8
    Tief%=Tief%*16/(1-(Aufloesung%=1))-1
    If Cy%=Y%+Lng%
      Defline 1
    Else
      Defline -&X101010101010101
    Endif
    Line Breit%,Tief%,Breit%+Breite%(Cx%)*8,Tief%
    Defline 1
  Endif
  Print At(2,3);Ein$(Cx%,Cy%);" ";Chr$(27);"K"
Return
'
Procedure Format.zelle(X%,Y%)
  Local H%,A%
  H$=Ein$(X%,Y%)
  If Left$(H$,5)="=#DAT"
    H$=" "+Date$
    Erg(X%,Y%)=0
  Else
    If Left$(H$,1)="="
      If Left$(Aus$(X%,Y%),3)<>"*FE"
        H$=Str$(Erg(X%,Y%))
      Else
        H$=Aus$(X%,Y%)
        Erg(X%,Y%)=0
      Endif
    Else
      A1%=Instr(H$," !")
      If A1%<>0
        H$=Left$(H$,A1%)
      Endif
    Endif
  Endif
  If Val?(Left$(H$,1))>0 Or (Left$(H$,1)="-" And Val?(Mid$(H$,2,1))>0)
    If Val(H$)>1.0E-10 Or Val(H$)<-1.0E-10
      H=Trunc(Val(H$)*10^Komma%(X%))
      H1$=Str$(H/10^Komma%(X%))
    Else
      H=Val(H$)
      H1$=H$
    Endif
    H%=Instr(H1$,".")
    If H%=0 And Komma%(X%)>0
      If Null!=True
        H1$=H1$+"."
      Endif
      H%=Len(H1$)
    Endif
    A%=Instr(H1$,"E")
    If A%=0
      If Len(H1$)-H%<Komma%(X%)
        If Null!=True
          H1$=H1$+String$(Komma%(X%)-(Len(H1$)-H%),"0")
        Endif
      Endif
    Else
      H%=Instr(H1$,".")
      If A%-H%>=Komma%(X%)
        H1$=Left$(H1$,H%+Komma%(X%))+Mid$(H1$,A%)
      Else
        If Null!=True
          H1$=Left$(H1$,A%-1)+String$(Komma%(X%)-A%+H%+1,"0")+Mid$(H1$,A%)
        Endif
      Endif
    Endif
    H$=Space$(Breite%(X%))
    If Len(H1$)>Breite%(X%)
      H$=String$(Breite%(X%),"*")
    Else
      Rset H$=H1$
    Endif
  Else
    H%=Len(H$)
    If H%<Breite%(X%)
      H$=H$+Space$(Breite%(X%)-H%)
    Else
      H$=Left$(H$,Breite%(X%))
    Endif
  Endif
  Aus$(X%,Y%)=H$+" "
Return
'
'
Procedure Edit
  Gosub C.minimax
  A$="éndern"
  Gosub Anzeige
  Form Input 77 As Ein$(Cx%,Cy%)
  If Left$(Ein$(Cx%,Cy%),1)="="
    A%=Instr(Ein$(Cx%,Cy%)," !")
    If A%<>0
      Ein$(Cx%,Cy%)=Upper$(Left$(Ein$(Cx%,Cy%),A%))+Right$(Ein$(Cx%,Cy%),Len(Ein$(Cx%,Cy%))-A%)
    Else
      Ein$(Cx%,Cy%)=Upper$(Ein$(Cx%,Cy%))
    Endif
    Gosub Rechne.zelle(Cx%,Cy%)
  Else
    Erg(Cx%,Cy%)=Val(Ein$(Cx%,Cy%))
  Endif
  Gosub Format.zelle(Cx%,Cy%)
  Gespeichert!=False
  Gosub Inv_anzeige
Return
'
Procedure C.minimax
  If Cx%>Xmax%
    Xmax%=Cx%
    Gosub Breite.block
    Gosub Infozeile
  Endif
  If Cy%>Ymax%
    Ymax%=Cy%
    Gosub Breite.block
    Gosub Infozeile
  Endif
Return
'
Procedure Test.zelle(Cx%,Cy%)
  Local A%,A1!
  A1!=False
  Repeat
    A%=Instr(Ein$(Cx%,Cy%),",")
    If A%<>0
      Mid$(Ein$(Cx%,Cy%),A%,1)="ˇ"
      A1!=True
    Endif
  Until A%=0
  If A1!=True
    Repeat
      A%=Instr(Aus$(Cx%,Cy%),",")
      If A%<>0
        Mid$(Aus$(Cx%,Cy%),A%,1)="ˇ"
      Endif
    Until A%=0
  Endif
Return
'
Procedure Test.zelle2(Cx%,Cy%)
  Local A%,A1!
  A1!=False
  Repeat
    A%=Instr(Ein$(Cx%,Cy%),"ˇ")
    If A%<>0
      Mid$(Ein$(Cx%,Cy%),A%,1)=","
      A1!=True
    Endif
  Until A%=0
  If A1!=True
    Repeat
      A%=Instr(Aus$(Cx%,Cy%),"ˇ")
      If A%<>0
        Mid$(Aus$(Cx%,Cy%),A%,1)=","
      Endif
    Until A%=0
  Endif
Return
'
Procedure Recalc
  Local X1%,Y1%
  Gosub Invert(0)
  Defmouse 2
  A$="Berechnung"
  Gosub Anzeige
  For Y1%=1 To Ymax%
    For X1%=1 To Xmax%
      If Left$(Ein$(X1%,Y1%),1)="="
        Gosub Rechne.zelle(X1%,Y1%)
        Gosub Format.zelle(X1%,Y1%)
      Endif
    Next X1%
  Next Y1%
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Block.ru
  B2x%=Cx%
  B2y%=Cy%
  Gosub Breite.block
  Gosub Infozeile
  If Cx%<B1x% Or Cy%<B1y%
    Print Chr$(7)
  Endif
Return
'
Procedure Block.lo
  B1x%=Cx%
  B1y%=Cy%
  Gosub Breite.block
  Gosub Infozeile
  If Cx%>B2x% Or Cy%>B2y%
    Print Chr$(7)
  Endif
Return
'
Procedure Laden
  Gosub Invert(0)
  Local X1%,Y1%
  H$="Laden von"
  Gosub Select(1)
  If H%<>3 And File1$<>""
    Defmouse 2
    Open "I",#1,File1$
    If H%=1
      Input #1,Ein$
      If Ein$="B"
        Input #1,Breite%
        Input #1,Tit_zl%
        If Tit_zl%
          Menu 66,1
        Else
          Menu 66,0
        Endif
        Input #1,Tit_fl!
        Input #1,Ein$
        Hoehe%=Min(5200/Breite%,999)
        Erase Hlp$()
        Gosub Array_loeschen
        Dim Hlp$(Breite%,Hoehe%)
      Endif
      If Ein$="N" Or Ein$="C"
        If Ein$="C"
          Input #1,Code$
          Print At(2,3);"Bitte CODE eingeben (6 Zeichen) : ";Chr$(7);
          Ein$=Input$(6)
          Print At(2,3);Chr$(27);"K"
          Code!=True
          Cd%=3
          If Ein$<>Code$
            Code!=False
            Cd%=2
          Endif
        Else
          Code$=""
          Code!=True
        Endif
        If Code!=True
          Input #1,Null!
          If Null!=True
            Menu 33,1
          Else
            Menu 33,0
          Endif
          Input #1,Lfeed!
          If Lfeed!=True
            Menu 34,1
          Else
            Menu 34,0
          Endif
          Input #1,Trennz!
          If Trennz!=True
            Menu 35,1
          Else
            Menu 35,0
          Endif
          Input #1,Grd
          If Grd=1
            Menu 67,1
          Else
            Menu 67,0
          Endif
        Else
          Print At(2,3);"CODE FALSCH !";Chr$(7);
          Close #1
          Pause 50
        Endif
      Endif
    Else
      Code$=""
      Cd%=2
      Code!=True
    Endif
    If Code!=True
      Input #1,S1x%
      Input #1,S1y%
      Input #1,S2x%
      Input #1,S2y%
      If H%=1
        Xmax%=S2x%
        Ymax%=S2y%
        For X1%=1 To Breite%
          Input #1,Breite%(X1%)
          Input #1,Komma%(X1%)
          Input #1,Kopf$(X1%)
          H$=Space$(Breite%(X1%)+1)
          For Y1%=1 To Hoehe%
            Aus$(X1%,Y1%)=H$
            Ein$(X1%,Y1%)=""
          Next Y1%
        Next X1%
        Gespeichert!=True
        Gosub Breite.block
      Endif
      If H%=2
        S1x%=S1x%+Cx%-1
        S1y%=S1y%+Cy%-1
        S2x%=S2x%+Cx%-1
        S2y%=S2y%+Cy%-1
        Gespeichert!=False
      Endif
      If S2x%>Breite% Or S2y%>Hoehe%
        Alert 3," | Block zu breit ! ",1,"   ok   ",H%
      Else
        For X1%=S1x% To S2x%
          For Y1%=S1y% To S2y%
            Input #1;Ein$(X1%,Y1%)
            Input #1;Aus$(X1%,Y1%)
            Gosub Test.zelle2(X1%,Y1%)
            Erg(X1%,Y1%)=Val(Ein$(X1%,Y1%))
            If Left$(Ein$(X1%,Y1%),1)="="
              Input #1;Erg(X1%,Y1%)
            Endif
            If H%=2
              Gosub Format.zelle(X1%,Y1%)
            Endif
          Next Y1%
        Next X1%
        Xmax%=Max(X1%-1,Xmax%)
        Ymax%=Max(Y1%-1,Ymax%)
        Close #1
        Gosub Breite.block
      Endif
    Endif
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
  Gosub Inv_anzeige
  Defmouse 0
Return
'
Procedure Speichern
  Gosub Invert(0)
  Local X1%,Y1%
  Gosub Select(0)
  If H%<>3 And E_flag!=False And File1$<>""
    Defmouse 2
    Open "O",#1,File1$
    If H%=1
      Print #1;"B"
      Print #1;Breite%
      Print #1;Tit_zl%
      Print #1;Tit_fl!
      If Cd%=1
        Ein$=""
        Print At(2,3);Chr$(7);"Bitte CODE eingeben (6 Zeichen) : ";
        Form Input 6 As Ein$
        Ein$=Ein$+"      "
        Ein$=Left$(Ein$,6)
        Print #1;"C"
        Print #1;Ein$
      Else
        If Cd%=3
          Print #1;"C"
          Print #1;Code$
        Else
          Print #1;"N"
        Endif
      Endif
      Print #1;Null!
      Print #1;Lfeed!
      Print #1;Trennz!
      Print #1;Grd
      Print #1;S1x%
      Print #1;S1y%
      Print #1;S2x%
      Print #1;S2y%
    Else
      Print #1;1
      Print #1;1
      Print #1;S2x%-S1x%+1
      Print #1;S2y%-S1y%+1
    Endif
    If H%=1
      For X1%=1 To Breite%
        Print #1;Breite%(X1%)
        Print #1;Komma%(X1%)
        Print #1;Kopf$(X1%)
      Next X1%
      Gespeichert!=True
    Endif
    For X1%=S1x% To S2x%
      For Y1%=S1y% To S2y%
        Gosub Test.zelle(X1%,Y1%)
        Print #1;Ein$(X1%,Y1%)
        Print #1;Aus$(X1%,Y1%)
        Gosub Test.zelle2(X1%,Y1%)
        If Left$(Ein$(X1%,Y1%),1)="="
          Print #1;Erg(X1%,Y1%)
        Endif
      Next Y1%
    Next X1%
    Close #1
  Endif
  Gosub Inv_anzeige
  Defmouse 0
Return
'
Procedure Breite
  Local Y1%
  Gosub Invert(0)
  A$="Breite stellen"
  Gosub Anzeige
  H$="(Esc) Breite Spalte (1 - 72) "+Chr$(Cx%+64)+" = "
  H%=Breite%(Cx%)
  Gosub Ein.zahl(H$,H%,1,72,2,3)
  If Val(Aus$)<>-1
    Defmouse 2
    Breite%(Cx%)=Val(Aus$)
    For Y1%=1 To Hoehe%
      Gosub Format.zelle(Cx%,Y1%)
    Next Y1%
    Gosub Adjust.screen
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Breite.all
  Local Y1%
  Gosub Invert(0)
  A$="Breite stellen"
  Gosub Anzeige
  H$="(Esc) Breite aller Spalten (1 - 72) = "
  H%=Breite%(0)
  Gosub Ein.zahl(H$,H%,1,72,2,3)
  If Val(Aus$)<>-1
    Defmouse 2
    Arrayfill Breite%(),Val(Aus$)
    For Sx%=1 To Breite%
      For Y1%=1 To Hoehe%
        Gosub Format.zelle(Sx%,Y1%)
      Next Y1%
    Next Sx%
    Gosub Adjust.screen
    Defmouse 0
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Adjust.screen
  Gosub Breite.block
  Gespeichert!=False
  Dec X%
  Repeat
    Inc X%
    Gosub Zahl.spalten(73)
  Until Spalten%>=Cx%
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Komma
  Local Y1%
  Gosub Invert(0)
  A$="Komma stellen"
  Gosub Anzeige
  H$="(Esc) Nachkommastellen Spalte "+Chr$(Cx%+64)+" = "
  H%=Komma%(Cx%)
  Gosub Ein.zahl(H$,H%,0,9,2,3)
  If Val(Aus$)<>-1
    Defmouse 2
    Komma%(Cx%)=Val(Aus$)
    Gespeichert!=False
    For Y1%=1 To Ymax%
      Gosub Format.zelle(Cx%,Y1%)
    Next Y1%
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Komma.all
  Local Y1%
  Gosub Invert(0)
  A$="Komma stellen"
  Gosub Anzeige
  H$="(Esc) Nachkommastellen fÅr alle Spalten = "
  H%=Komma%(0)
  Gosub Ein.zahl(H$,H%,0,9,2,3)
  If Val(Aus$)<>-1
    Defmouse 2
    Arrayfill Komma%(),Val(Aus$)
    For Sx%=1 To Xmax%
      For Y1%=1 To Ymax%
        Gosub Format.zelle(Sx%,Y1%)
      Next Y1%
    Next Sx%
    Gespeichert!=False
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Inv_anzeige
  Gosub Invert(1)
  A$=""
  Gosub Anzeige
Return
'
Procedure Copy.abs
  H2$="Kopieren"
  Gosub Block.abs(0)
Return
'
Procedure Copy.rel
  H2$="Kopieren rel."
  Gosub Block.rel(0)
Return
'
Procedure Move.abs
  H2$="Verschieben"
  Gosub Block.abs(1)
Return
'
Procedure Move.rel
  H2$="Verschieben rel."
  Gosub Block.rel(1)
Return
'
Procedure Gehe.zelle
  Gosub Invert(0)
  Print At(2,3);Chr$(27);"l"
  Print At(2,3);"Gehe Zelle ";Chr$(27);"p ";Chr$(27);"q";
  Repeat
    K$=Inkey$
  Until (K$>="A" And K$<="Z") Or (K$>="a" And K$<="z") Or (K$=Chr$(13))
  If K$<>Chr$(13)
    X%=Asc(K$)+32*(K$>"Z")-64
    Cx%=X%
    H$="Gehe Feld "+Chr$(X%+64)
    Gosub Ein.zahl(H$,1,1,Hoehe%,2,3)
    Y%=Val(Aus$)
    Cy%=Y%
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Print At(2,3);Chr$(27);"l ";Ein$(Cx%,Cy%)
    Gosub Invert(1)
  Endif
Return
'
Procedure Gehe.a1
  If X%=1 And Y%=1
    Gosub Invert(0)
    Cx%=1
    Cy%=1
    Text 0,398*Dvs,"    "
    H$=Chr$(Cx%+64)+Str$(Cy%)+"  "
    Graphmode 4
    Text 0,398*Dvs,H$
    Graphmode 1
    Gosub Invert(1)
  Else
    X%=1
    Y%=1
    Cx%=1
    Cy%=1
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
Return
'
Procedure Drucken
  Gosub Invert(0)
  A$="Ausdruck"
  Gosub Anzeige
  Sget Screen$
  Gosub Draw.ausdruck
  Gosub Control.ausdruck
  Sput Screen$
  If H%<>3
    Gosub Koordinaten(H%)
    If E_flag!=False
      Defmouse 2
      If A1%=1
        Open "O",#2,"PRN:"
        Print #2;Chr$(27);"R";Chr$(2);Chr$(27);"l";Chr$(0);
      Else
        Sget Screen$
        Fileselect "\*.TXT","",Dfile$
        Sput Screen$
        If Dfile$<>""
          Open "O",#2,Dfile$
        Endif
      Endif
      If A1%=1 Or Dfile$<>""
        If H%=1
          A%=All.breite%
        Else
          A%=Bl.breite%
        Endif
        If A1%=1
          Links$=Space$(6)
          Print #2;Chr$(18);Chr$(27);"P";
          If A%>74+4*(Z!=True)
            If A%>88+4*(Z!=True)
              If A%>122+4*(Z!=True)
                Print #2;Chr$(27);"M";Chr$(15);
                Links$=Space$(12)
              Else
                Print #2;Chr$(15);
                Links$=Space$(10)
              Endif
            Else
              Print #2;Chr$(27);"M";
              Links$=Space$(8)
            Endif
          Endif
        Endif
        Clr Zeilen%
        If T!=True
          Gosub Dr_kopf
        Endif
        If Ymax%>Tit_zl%
          For D_y%=S1y%+Tit_zl% To S2y%
            Gosub Dr_zeile
            If Zeilen%>64 And A1%=1
              Print #2;Chr$(12);
              Clr Zeilen%
              If T!=True
                Gosub Dr_kopf
              Endif
            Endif
          Next D_y%
        Endif
        If A1%=1
          Print #2;Chr$(12);
        Endif
        Close #2
      Endif
    Endif
  Endif
  Gosub Inv_anzeige
  Defmouse 0
Return
'
Procedure Dr_kopf
  Local X%,D_y%
  Print #2;Links$;
  If Z!=True
    Print #2;"    ";
  Endif
  For X%=S1x% To S2x%
    H$=Kopf$(X%)
    If Len(H$)>=Breite%(X%)
      H$=Left$(H$,Breite%(X%))
    Else
      H$=H$+Space$(Breite%(X%)-Len(H$))
    Endif
    Gosub Test.umlaut
    If Trennz!=False
      Print #2;H$;" ";
    Else
      Print #2;H$;"!";
    Endif
  Next X%
  Zeilen%=2
  If Lfeed!=True
    Print #2
  Else
    Print #2;Chr$(13);
  Endif
  If Tit_zl%>0 And Ymax%>=Tit_zl%
    If Tit_fl!=False
      For D_y%=1 To Tit_zl%
        Gosub Dr_zeile
      Next D_y%
    Else
      For D_y%=1 To Tit_zl%
        H$=Left$(Ein$(1,D_y%),75)
        Gosub Test.umlaut
        Print #2;Links$;H$;
        Inc Zeilen%
        If Lfeed!=True
          Print #2
        Else
          Print #2;Chr$(13);
        Endif
      Next D_y%
    Endif
  Endif
  If Lfeed!=True
    Print #2
  Else
    Print #2;Chr$(13);
  Endif
Return
'
Procedure Dr_zeile
  Local X%
  Print #2;Links$;
  If Z!=True
    Print #2;Using "###_ ",D_y%;
  Endif
  For X%=S1x% To S2x%
    H$=Aus$(X%,D_y%)
    Gosub Test.umlaut
    If Trennz!=False
      Print #2;H$;
    Else
      Print #2;Left$(H$,Len(H$)-1);"!";
    Endif
  Next X%
  Inc Zeilen%
  If Lfeed!=True
    Print #2
  Else
    Print #2;Chr$(13);
  Endif
Return
'
Procedure Draw.ausdruck
  Graphmode 1
  Gosub Shadow.box(170,60*Dvs,480,305*Dvs,2,4)
  Graphmode 4
  Deffill ,2,8
  Pbox 120,70*Dvs,120,70*Dvs
  Pbox 120,70*Dvs,520,90*Dvs
  Graphmode 1
  Deffill ,2,2
  Pbox 190,70*Dvs,460,110*Dvs
  Graphmode 2
  Deftext ,0-Test%,,13+7*Test%
  Text 260,95*Dvs," A U S D R U C K "
  Deftext ,0
  Graphmode 1
  Deffill ,2,8
  Pbox 190,120*Dvs,310,150*Dvs
  Pbox 190,200*Dvs,310,230*Dvs
  Deffill ,0,0
  Pbox 340,120*Dvs,460,150*Dvs
  Pbox 190,160*Dvs,310,190*Dvs
  Pbox 340,160*Dvs,460,190*Dvs
  Pbox 340,200*Dvs,460,230*Dvs
  Text 380,140*Dvs,"BLOCK"
  Text 215,180*Dvs,"mit TITEL"
  Text 365,180*Dvs,"mit ZEILE"
  Text 370,220*Dvs,"DISKETTE"
  Graphmode 3
  Text 230,140*Dvs,"BLATT"
  Text 220,220*Dvs,"DRUCKER"
  Graphmode 1
  Gosub Frame.box(190,250*Dvs,315,290*Dvs,0,0)
  Gosub Frame.box(340,250*Dvs,460,290*Dvs,0,0)
  Text 245,275*Dvs-Test%,"OK"
  Text 375,275*Dvs-Test%,"ABBRUCH"
Return
'
Procedure Control.ausdruck
  Ex!=False
  H%=1
  A1%=1
  T!=False
  Z!=False
  Do
    Mouse X,Y,K
    Y=Y/Dvs
    If K=1
      If X>191 And X<309 And Y>120 And Y<150
        H%=1
        Deffill ,2,8
        Pbox 190,120*Dvs,310,150*Dvs
        Deffill ,0,0
        Pbox 340,120*Dvs,460,150*Dvs
        Text 380,140*Dvs,"BLOCK"
        Graphmode 3
        Text 230,140*Dvs,"BLATT"
        Graphmode 1
      Endif
      If X>340 And X<460 And Y>120 And Y<150
        H%=2
        Deffill ,2,8
        Pbox 340,120*Dvs,460,150*Dvs
        Deffill ,0,0
        Pbox 190,120*Dvs,310,150*Dvs
        Text 230,140*Dvs,"BLATT"
        Graphmode 3
        Text 380,140*Dvs,"BLOCK"
        Graphmode 1
      Endif
      If X>190 And X<310 And Y>160 And Y<190
        If T!=False
          T!=True
          Deffill ,2,8
          Pbox 190,160*Dvs,310,190*Dvs
          Graphmode 3
          Text 215,180*Dvs,"mit TITEL"
          Graphmode 1
        Else
          T!=False
          Deffill ,0,0
          Pbox 190,160*Dvs,310,190*Dvs
          Text 215,180*Dvs,"mit TITEL"
        Endif
        Pause 10
      Endif
      If X>340 And X<460 And Y>160 And Y<190
        If Z!=False
          Z!=True
          Deffill ,2,8
          Pbox 340,160*Dvs,460,190*Dvs
          Graphmode 3
          Text 365,180*Dvs,"mit ZEILE"
          Graphmode 1
        Else
          Z!=False
          Deffill ,0,0
          Pbox 340,160*Dvs,460,190*Dvs
          Text 365,180*Dvs,"mit ZEILE"
        Endif
        Pause 10
      Endif
      If X>190 And X<310 And Y>200 And Y<230
        A1%=1
        Deffill ,2,8
        Pbox 190,200*Dvs,310,230*Dvs
        Deffill ,0,0
        Pbox 340,200*Dvs,460,230*Dvs
        Graphmode 3
        Text 220,220*Dvs,"DRUCKER"
        Graphmode 1
        Text 370,220*Dvs,"DISKETTE"
      Endif
      If X>340 And X<460 And Y>200 And Y<230
        A1%=2
        Deffill ,2,8
        Pbox 340,200*Dvs,460,230*Dvs
        Deffill ,0,0
        Pbox 190,200*Dvs,310,230*Dvs
        Text 220,220*Dvs,"DRUCKER"
        Graphmode 3
        Text 370,220*Dvs,"DISKETTE"
        Graphmode 1
      Endif
      If X>195 And X<310 And Y>255 And Y<285
        Ex!=True
      Endif
      If X>345 And X<455 And Y>255 And Y<285
        H%=3
        Ex!=True
      Endif
    Endif
    Exit If Ex!=True
  Loop
  Repeat
  Until Mousek=0
Return
'
Procedure Test.umlaut
  If H$<>"" And A1%=1
    Restore Umlaute
    Do
      Read H1$
      Exit If H1$="*"
      Read H%
      Do
        A%=Instr(H$,H1$)
        Exit If A%=0
        Mid$(H$,A%,1)=Chr$(H%)
      Loop
    Loop
  Endif
  Umlaute:
  Data Ñ,123,î,124,Å,125,é,91,ô,92,ö,93,û,126,*
Return
'
Procedure Kopf
  Local X1%
  For X1%=1 To Breite%
    Print At(2,3);"Kopf Spalte ";Chr$(X1%+64);" : ";Chr$(27);"K";
    Form Input 77 As Kopf$(X1%)
    Print At(2,3);Chr$(27);"K"
  Next X1%
  Gespeichert!=False
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Ende
  Gosub Test.gesp
  Alert 2," |Willst Du wirklich das|Programm verlassen ?",2,"  ja  | nein ",H%
  If H%=1
    Menu Kill
    Edit
  Endif
Return
'
Procedure Loesche.zelle
  Gespeichert!=False
  Ein$(Cx%,Cy%)=""
  Erg(Cx%,Cy%)=0
  Aus$(Cx%,Cy%)=Space$(Breite%(Cx%)+1)
  Gosub Invert(1)
Return
'
Procedure Loeschen
  Alert 1," |    LôSCHEN von           ",3," allem | Block | nichts ",H%
  If H%<>3
    If H%=1
      Gosub Loesche.all
    Else
      Gosub Loesche.block
    Endif
  Endif
Return
'
Procedure Loesche.all
  Alert 1," |LôSCHEN des ARBEITSBLATTES",2,"   ja   | nein ",H%
  If H%=1
    Defmouse 2
    Gosub Test.gesp
    Erase Hlp$()
    Gosub Array_loeschen
    Clr Tit_zl%
    Tit_fl!=False
    Gosub Grafik_init
    Dim Hlp$(Breite%,Hoehe%)
    Defmouse 1
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
Return
'
Procedure Loesche.block
  If B1x%>B2x% Or B1y%>B2y%
    Alert 3," |BLOCK-Koordinaten falsch|      gewÑhlt !!",1,"Abbruch",H%
  Else
    Alert 1," |LôSCHEN des BLOCKES",2,"   ja   | nein ",H%
    If H%=1
      For X1%=B1x% To B2x%
        H$=Space$(Breite%(X1%)+1)
        For Y1%=B1y% To B2y%
          Ein$(X1%,Y1%)=""
          Erg(X1%,Y1%)=0
          Aus$(X1%,Y1%)=H$
        Next Y1%
      Next X1%
      Gespeichert!=False
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
  Endif
Return
'
Procedure Ein.zahl(H$,K%,Min%,Max%,X%,Y%)
  Local Lng%
  H$=H$+" "
  Lng%=Len(Str$(Max%))
  Ein$=Str$(K%)
  Print Chr$(27);"e";
  Repeat
    Aus$=""
    If Y%=3
      Print At(X%,Y%);Chr$(27);"l"
    Endif
    Print At(X%,Y%);H$;Ein$
    Print At(X%-1+Len(H$),Y%);" ";
    Repeat
    Until Inkey$=""
    Repeat
      In$=Chr$(Inp(2))
      If In$=Chr$(8) And Len(Aus$)>=1
        Aus$=Left$(Aus$,Len(Aus$)-1)
      Endif
      If In$>="0" And In$<="9" And Len(Aus$)<Lng%
        Aus$=Aus$+In$
      Endif
      Print At(X%+Len(H$),Y%);Aus$;Spc(Lng%-Len(Aus$))
      Print At(X%+Len(H$)+Len(Aus$),Y%);
    Until In$=Chr$(27) Or In$=Chr$(13)
    If In$=Chr$(13) And Aus$=""
      Aus$=Ein$
    Endif
    If In$=Chr$(27)
      Aus$="-1"
    Endif
    K%=Val(Aus$)
    If (K%<Min% Or K%>Max%) And K%<>-1
      Print Chr$(7)
      Print At(X%+Len(H$),Y%);Ein$;Spc(Lng%-Len(Ein$))
    Endif
  Until (K%>=Min% And K%<=Max%) Or K%=-1
  Print Chr$(27);"f"
Return
'
Procedure Select(Flag%)
  If Flag%=0
    A$="Speichern"
    Gosub Anzeige
    Sget Screen$
    Gosub Draw.speicher
    Gosub Control.speicher
  Else
    A$="Laden"
    Gosub Anzeige
    Sget Screen$
    Alert 2," |       Laden von        ",3," allem | Block | nichts ",H%
  Endif
  If H%<>3
    If H%=1
      If Flag%=1
        Gosub Test.gesp
        A$="Laden"
        Gosub Anzeige
      Endif
      H$="\*.CLC"
    Else
      H$="\*.BLK"
    Endif
    Gosub Koordinaten(H%)
    If E_flag!=False
      Fileselect H$,File$,File1$
      If Len(File1$)>4
        If H%=1
          If Right$(File1$,4)<>".CLC"
            Ai%=Instr(File1$,".")
            If Ai%>0
              File1$=Left$(File1$,Ai%)+"CLC"
            Else
              File1$=File1$+".CLC"
            Endif
          Endif
        Else
          If Right$(File1$,4)<>".BLK"
            Ai%=Instr(File1$,".")
            If Ai%>0
              File1$=Left$(File1$,Ai%)+"BLK"
            Else
              File1$=File1$+".BLK"
            Endif
          Endif
        Endif
      Else
        If File1$<>"" And File1$<>"\"
          If H%=1
            Ai%=Instr(File1$,".")
            If Ai%>0
              File1$=Left$(File1$,Ai%)+"CLC"
            Else
              File1$=File1$+".CLC"
            Endif
          Else
            Ai%=Instr(File1$,".")
            If Ai%>0
              File1$=Left$(File1$,Ai%)+"BLK"
            Else
              File1$=File1$+".BLK"
            Endif
          Endif
        Endif
      Endif
      If File1$<>"" And File1$<>"\"
        File$=Mid$(File1$,3)
        Ai%=Instr(File$,".")
        File$=Left$(File$,Ai%-1)
      Endif
      Gosub Koordinaten(H%)
    Endif
  Endif
  Sput Screen$
Return
'
Procedure Draw.speicher
  Graphmode 1
  Gosub Shadow.box(160,64*Dvs,480,296*Dvs,2,4)
  Deffill ,2,2
  Pbox 180,74*Dvs,460,115*Dvs
  Deffill ,0,0
  Pbox 325,130*Dvs,460,166*Dvs
  Deffill ,2,8
  Pbox 180,130*Dvs,315,166*Dvs
  Graphmode 2
  Deftext ,0-Test%,,13+7*Test%
  Text 246,100*Dvs," S P E I C H E R N "
  Deftext ,0
  Graphmode 1
  Text 370,152*Dvs,"BLOCK"
  Graphmode 3
  Text 225,152*Dvs,"BLATT"
  Gosub Draw.codebox
  Gosub Frame.box(180,240*Dvs,315,286*Dvs,0,0)
  Gosub Frame.box(325,240*Dvs,460,285*Dvs,0,0)
  Text 240,268*Dvs,"OK"
  Text 365,268*Dvs,"ABBRUCH"
Return
'
Procedure Control.speicher
  Ex!=False
  H%=1
  Do
    Mouse X,Y,K
    Y=Y/Dvs
    If K=1
      If X>180 And X<315 And Y>130 And Y<165
        H%=1
        Deffill ,2,8
        Pbox 180,130*Dvs,315,166*Dvs
        Deffill ,0,0
        Pbox 325,130*Dvs,460,166*Dvs
        Graphmode 3
        Text 225,152*Dvs,"BLATT"
        Graphmode 1
        Text 370,152*Dvs,"BLOCK"
        Gosub Draw.codebox
      Endif
      If X>325 And X<459 And Y>130 And Y<165
        H%=2
        Deffill ,0,0
        Pbox 180,130*Dvs,315,166*Dvs
        Pbox 325,180*Dvs,460,216*Dvs
        Deffill ,2,8
        Pbox 325,130*Dvs,460,166*Dvs
        Pbox 180,180*Dvs,315,216*Dvs
        Text 225,152*Dvs,"BLATT"
        Text 360,202*Dvs,"mit CODE"
        Graphmode 3
        Text 370,152*Dvs,"BLOCK"
        Text 210,202*Dvs,"ohne CODE"
        Graphmode 1
      Endif
      If X>180 And X<315 And Y>180 And Y<215 And Cd%<3
        Cd%=2
        Deffill ,2,8
        Pbox 180,180*Dvs,315,216*Dvs
        Deffill ,0,0
        Pbox 325,180*Dvs,460,216*Dvs
        Text 360,202*Dvs,"mit CODE"
        Graphmode 3
        Text 210,202*Dvs,"ohne CODE"
        Graphmode 1
      Endif
      If X>325 And X<459 And Y>180 And Y<215 And Cd%<3 And H%=1
        Cd%=1
        Deffill ,0,0
        Pbox 180,180*Dvs,315,216*Dvs
        Deffill ,2,8
        Pbox 325,180*Dvs,460,216*Dvs
        Graphmode 3
        Text 360,202*Dvs,"mit CODE"
        Graphmode 1
        Text 210,202*Dvs,"ohne CODE"
      Endif
      If X>185 And X<310 And Y>245 And Y<280
        Ex!=True
      Endif
      If X>330 And X<455 And Y>245 And Y<280
        H%=3
        Ex!=True
      Endif
    Endif
    Exit If Ex!=True
  Loop
  Repeat
  Until Mousek=0
Return
'
Procedure Draw.codebox
  Graphmode 1
  If Cd%<>1 And Cd%<>3
    Deffill ,2,8
    Pbox 180,180*Dvs,315,216*Dvs
    Deffill ,0,0
    Pbox 325,180*Dvs,460,216*Dvs
    Graphmode 1
    Text 360,202*Dvs,"mit CODE"
    Graphmode 3
    Text 210,202*Dvs,"ohne CODE"
  Else
    Deffill ,0,0
    Pbox 180,180*Dvs,315,216*Dvs
    Deffill ,2,8
    Pbox 325,180*Dvs,460,216*Dvs
    Graphmode 3
    Text 360,202*Dvs,"mit CODE"
    Graphmode 1
    Text 210,202*Dvs,"ohne CODE"
  Endif
  Graphmode 1
Return
'
Procedure Shadow.box(X%,Y%,W%,H%,Fstyle%,Findex%)
  If X%>W%
    Swap X%,W%
  Endif
  If Y%>H%
    Swap Y%,H%
  Endif
  Deffill 1,1
  Pbox X%+2,H%,W%+2,H%+2
  Pbox W%,Y%+2,W%+2,H%
  If Fstyle%=4
    Box X%,Y%,W%,H%
  Else
    Deffill 2,Fstyle%,Findex%
    Pbox X%,Y%,W%,H%
  Endif
  Deffill 1
Return
'
Procedure Frame.box(X%,Y%,W%,H%,Fstyle%,Findex%)
  If X%>W%
    Swap X%,W%
  Endif
  If Y%>H%
    Swap Y%,H%
  Endif
  If Fstyle%=4
    Box X%,Y%,W%,H%
    Box X%+5,Y%+5,W%-5,H%-5
    Line X%,Y%,X%+5,Y%+5
    Line W%,Y%,W%-5,Y%+5
    Line W%,H%,W%-5,H%-5
    Line X%,H%,X%+5,H%-5
  Else
    Deffill 1,0
    Pbox X%,Y%,W%,H%
    Line X%,Y%,X%+5,Y%+5
    Line W%,Y%,W%-5,Y%+5
    Line W%,H%,W%-5,H%-5
    Line X%,H%,X%+5,H%-5
    Deffill 1,Fstyle%,Findex%
    Pbox X%+5,Y%+5,W%-5,H%-5
  Endif
Return
'
Procedure Koordinaten(H%)
  E_flag!=False
  If H%=1
    S1x%=1
    S1y%=1
    S2x%=Xmax%
    S2y%=Ymax%
  Else
    If B1x%>B2x% Or B1y%>B2y% And Left$(A$,17)<>"            Laden"
      Alert 3," |BLOCK-Koordinaten falsch|      gewÑhlt !!",1,"Abbruch",H%
      E_flag!=True
    Else
      S1x%=B1x%
      If Left$(A$,17)="         Ausdruck"
        S1y%=B1y%-Tit_zl%
      Else
        S1y%=B1y%
      Endif
      S2x%=B2x%
      S2y%=B2y%
    Endif
  Endif
Return
'
Procedure Block.abs(Flag%)
  If B1x%>B2x% Or B1y%>B2y%
    Alert 3," |BLOCK-Koordinaten falsch|      gewÑhlt !!",1,"Abbruch",H%
    H%=4
  Else
    Gosub Hol.block
    If Ac%<>27
      Gosub Schreib.block(Flag%,0)
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
    Endif
  Endif
Return
'
Procedure Block.rel(Flag%)
  If B1x%>B2x% Or B1y%>B2y%
    Alert 3," |BLOCK-Koordinaten falsch|      gewÑhlt !!",1,"Abbruch",H%
    H%=4
  Else
    Gosub Hol.block
    If Ac%<>27
      Dx%=Cx%-B1x%
      Dy%=Cy%-B1y%
      For Y1%=B1y% To B2y%
        For X1%=B1x% To B2x%
          If Left$(Hlp$(X1%,Y1%),1)="="
            Clr A%
            Repeat
              A%=Instr(Hlp$(X1%,Y1%),Chr$(126),A%+1)
              If A%<>0
                Gosub Endern.koord
              Endif
            Until A%=0
            Restore Funktion
            Funktion:
            Data #SUM,#AVE,#STA,#STD,#MUL,#MIN,#MAX,#QMN,#QMW,END
            Do
              Read F$
              Exit If F$="END"
              Repeat
                A%=Instr(Hlp$(X1%,Y1%),F$,A%+1)
                If A%<>0
                  Add A%,4
                  Gosub Endern.koord
                  A%=Instr(Hlp$(X1%,Y1%),":",A%)
                  Gosub Endern.koord
                Endif
              Until A%=0
            Loop
          Endif
        Next X1%
      Next Y1%
      Gosub Schreib.block(Flag%,1)
    Endif
  Endif
Return
'
Procedure Endern.koord
  E1$=Left$(Hlp$(X1%,Y1%),A%)
  A1%=Val?(Mid$(Hlp$(X1%,Y1%),A%+2))
  Ein$=Mid$(Hlp$(X1%,Y1%),A%+1)
  Ein$=Chr$(Asc(Left$(Ein$,1))+Dx%)+Mid$(Ein$,2)
  If Left$(Ein$,1)<"A"
    Mid$(Ein$,1,1)="A"
  Endif
  If Left$(Ein$,1)>Chr$(Breite%+64)
    Mid$(Ein$,1,1)=Chr$(Breite%+64)
  Endif
  H%=Val(Mid$(Ein$,2))+Dy%
  H%=Min(H%,Hoehe%)
  H%=Max(H%,1)
  Ein$=Left$(Ein$,1)+Str$(H%)+Mid$(Ein$,2+A1%)
  Hlp$(X1%,Y1%)=E1$+Ein$
Return
'
Procedure Hol.block
  A$=H2$
  Gosub Anzeige
  Print At(2,3);Chr$(7);Chr$(27);"l"
  Print At(2,3);"(Esc) Bewege Cursor an die linke obere Ecke, dann 'Return' oder Mausklick re";
  Repeat
    Clr Mo_k%,Ac%
    Repeat
    Until Inkey$=""
    Repeat
      Mo_k%=Mousek
    Until Inp?(2) Or Mo_k%=2
    If Mo_k%=2
      Ac%=13
    Else
      Ac%=Inp(2)
    Endif
    If Ac%<>13 And Ac%<>27
      Swap Aa%,Ac%
      Gosub Beweg.cursor
      Swap Aa%,Ac%
    Endif
  Until Ac%=13 Or Ac%=27
  If Ac%=13
    If (Cx%+(B2x%-B1x%)<=Breite%) And (Cy%+(B2y%-B1y%)<=Hoehe%)
      Defmouse 2
      For X1%=B1x% To B2x%
        For Y1%=B1y% To B2y%
          Hlp$(X1%,Y1%)=Ein$(X1%,Y1%)
        Next Y1%
      Next X1%
      Gespeichert!=False
    Endif
  Else
    Print Chr$(7)
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
Return
'
Procedure Schreib.block(Flag%,Rel%)
  If (Cx%+(B2x%-B1x%)<=Breite%) And (Cy%+(B2y%-B1y%)<=Hoehe%)
    If Flag%=1
      For X1%=B1x% To B2x%
        H$=Space$(Breite%(X1%)+1)
        For Y1%=B1y% To B2y%
          Ein$(X1%,Y1%)=""
          Erg(X1%,Y1%)=0
          Aus$(X1%,Y1%)=H$
        Next Y1%
      Next X1%
    Endif
    For X1%=Cx% To Cx%+(B2x%-B1x%)
      For Y1%=Cy% To Cy%+(B2y%-B1y%)
        Ein$(X1%,Y1%)=Hlp$(X1%-Cx%+B1x%,Y1%-Cy%+B1y%)
        Hlp$(X1%-Cx%+B1x%,Y1%-Cy%+B1y%)=""
        If Left$(Ein$(X1%,Y1%),1)="=" And Rel%=0
          Gosub Rechne.zelle(X1%,Y1%)
        Else
          Erg(X1%,Y1%)=Val(Ein$(X1%,Y1%))
        Endif
        Gosub Format.zelle(X1%,Y1%)
      Next Y1%
    Next X1%
    Xmax%=Max(X1%-1,Xmax%)
    Ymax%=Max(Y1%-1,Ymax%)
    Gosub Breite.block
    If Rel%=1
      Gosub Recalc
    Endif
  Else
    Alert 3," | Block zu breit ! ",1,"   ok   ",H%
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Endif
Return
'
Procedure Rechne.zelle(X%,Y%)
  Eflag%=False
  If Left$(Ein$(X%,Y%),5)<>"=#DAT"
    Gosub Berechnung(Mid$(Ein$(X%,Y%),2)+" ")
    If Eflag%=False
      Aus$(X%,Y%)=""
      Erg(X%,Y%)=Stack(1)
    Else
      Aus$(X%,Y%)="*FEHLER*"
      Erg(X%,Y%)=0
      A$=" | "+Fehler$(Eflag%)+" | in Zelle "+Chr$(X%+64)+Str$(Y%)
      Alert 3,A$,1," weiter ",H%
    Endif
  Endif
Return
'
Procedure Berechnung(Ein$)
  Local A%,A1%,A2%,I%,J%,J1%,J2%,H%,H1%,H2%,H$
  Clr Zexp%,Zstack%,Klammer%
  A%=Instr(Ein$," !")
  If A%<>0
    Ein$=Left$(Ein$,A%)
  Endif
  Do
    Flag!=False
    A%=Instr(Ein$,"#")
    Exit If A%=0 Or Eflag%<>0
    '
    ' SUM
    '
    A%=Instr(Ein$,"#SUM")
    If A%<>0
      Gosub Summe(A%,0)
    Endif
    '
    ' STA
    '
    A%=Instr(Ein$,"#STA")
    If A%<>0
      Gosub Summe(A%,2)
    Endif
    '
    ' STD
    '
    A%=Instr(Ein$,"#STD")
    If A%<>0
      Gosub Summe(A%,3)
    Endif
    '
    ' AVE
    '
    A%=Instr(Ein$,"#AVE")
    If A%<>0
      Gosub Summe(A%,1)
    Endif
    '
    ' MUL
    '
    A%=Instr(Ein$,"#MUL")
    If A%<>0
      Gosub Multipl
    Endif
    '
    ' MIN
    '
    A%=Instr(Ein$,"#MIN")
    If A%<>0
      Gosub Minimax(A%,0)
    Endif
    '
    ' MAX
    '
    A%=Instr(Ein$,"#MAX")
    If A%<>0
      Gosub Minimax(A%,1)
    Endif
    '
    ' QMN
    '
    A%=Instr(Ein$,"#QMN")
    If A%<>0
      Gosub Summe(A%,4)
    Endif
    '
    ' QMW
    '
    A%=Instr(Ein$,"#QMW")
    If A%<>0
      Gosub Summe(A%,5)
    Endif
    '
    ' PI
    '
    A%=Instr(Ein$,"#PI")
    If A%<>0
      Flag!=True
      Ein$=Left$(Ein$,A%-1)+Str$(Pi)+Mid$(Ein$,A%+3)
    Endif
    '
    Exit If Flag!=False
  Loop
  '
  ' ZELLEN
  '
  Do
    A%=Max(Instr(Ein$,Chr$(126)),Instr(Ein$,Chr$(64)))
    If A%>1
      A1%=Instr("+-*/^(",Mid$(Ein$,A%-1,1))
      If A1%=0
        Eflag%=1
      Endif
    Endif
    Exit If A%=0 Or Eflag%<>0
    J1%=Asc(Mid$(Ein$,A%+1,1))-64
    H1%=Val(Mid$(Ein$,A%+2))
    H2%=Val?(Mid$(Ein$,A%+2))
    If J1%>0 And J1%<=Breite% And H1%>0 And H1%<=Hoehe%
      If Left$(Ein$(J1%,H1%),1)="="
        H=Erg(J1%,H1%)
      Else
        H=Val(Ein$(J1%,H1%))
      Endif
    Else
      Clr H
      Eflag%=9
    Endif
    If H<0 And A%>2
      If Mid$(Ein$,A%-1,1)="+"
        Ein$=Left$(Ein$,A%-2)+Str$(H)+Mid$(Ein$,A%+2+H2%)+" "
      Else
        If Mid$(Ein$,A%-1,1)="-"
          Ein$=Left$(Ein$,A%-2)+"+"+Str$(-H)+Mid$(Ein$,A%+2+H2%)+" "
        Else
          If Mid$(Ein$,A%-1,1)="*" Or Mid$(Ein$,A%-1,1)="/"
            If Left$(Ein$,1)<>"-"
              Ein$="-"+Left$(Ein$,A%-1)+Str$(-H)+Mid$(Ein$,A%+2+H2%)+" "
            Else
              Ein$=Left$(Ein$,A%-1)+Str$(-H)+Mid$(Ein$,A%+2+H2%)+" "
              Ein$=Mid$(Ein$,2)
            Endif
          Endif
        Endif
      Endif
    Else
      Ein$=Left$(Ein$,A%-1)+Str$(H)+Mid$(Ein$,A%+2+H2%)
    Endif
  Loop
  '
  If Left$(Ein$,1)="-"
    A%=Val?(Ein$)
    Ein$="(0"+Left$(Ein$,A%)+")"+Mid$(Ein$,A%+1)
  Endif
  '
  Do
    A%=Instr(Ein$,"(-")
    Exit If A%=0
    Ein$=Left$(Ein$,A%)+"0"+Mid$(Ein$,A%+1)
  Loop
  '
  ' NUN
  '
  Do
    A%=Instr(Ein$,"#NUN")
    Exit If A%=0 Or Eflag%<>0
    If A%<>0
      Flag!=True
      Gosub N_uber_n
    Endif
  Loop
  '
  '
  Do
    A%=Instr(Ein$,"#")
    Exit If A%=0 Or Eflag%>0
    H$=Mid$(Ein$,A%+1,3)
    Token=(Instr("SQRSINCOSTANATNLOGCLGEXPRNDABSINTFAK",H$)+2)/3
    If Token=0 Or Int(Token)<>Token
      Eflag%=1
    Else
      Ein$=Left$(Ein$,A%-1)+Chr$(Token+5)+Mid$(Ein$,A%+4)
    Endif
  Loop
  '
  Do
    A%=Instr(Ein$,"+")
    Exit If A%=0
    Mid$(Ein$,A%,1)=Chr$(1)
  Loop
  '
  A$="E"+Chr$(1)
  Do
    A%=Instr(Ein$,A$)
    Exit If A%=0
    Mid$(Ein$,A%+1,1)="+"
  Loop
  '
  Do
    A%=Instr(Ein$,"-")
    Exit If A%=0
    Mid$(Ein$,A%,1)=Chr$(2)
  Loop
  '
  A$="E"+Chr$(2)
  Do
    A%=Instr(Ein$,A$)
    Exit If A%=0
    Mid$(Ein$,A%+1,1)="-"
  Loop
  '
  Do
    A%=Instr(Ein$,"*")
    Exit If A%=0
    Mid$(Ein$,A%,1)=Chr$(3)
  Loop
  '
  Do
    A%=Instr(Ein$,"/")
    Exit If A%=0
    Mid$(Ein$,A%,1)=Chr$(4)
  Loop
  '
  Do
    A%=Instr(Ein$,"^")
    Exit If A%=0
    Mid$(Ein$,A%,1)=Chr$(5)
  Loop
  '
  While Len(Ein$)>1 And Eflag%=0 And Left$(Ein$,1)<>" "
    If Val?(Ein$)>0
      Inc Zexp%
      Exp$(Zexp%)=Str$(Val(Ein$))
      Ein$=Mid$(Ein$,Val?(Ein$)+1)
    Else
      If Left$(Ein$,1)="("
        Inc Klammer%
        Inc Zstack%
        Stack$(Zstack%)=Left$(Ein$,1)
      Else
        If Left$(Ein$,1)=")"
          If Klammer%>0
            Dec Klammer%
            While Stack$(Zstack%)<>"(" And Zstack%>1
              Inc Zexp%
              Exp$(Zexp%)=Stack$(Zstack%)
              Dec Zstack%
            Wend
            Dec Zstack%
          Else
            Eflag%=2
          Endif
        Else
          If Left$(Ein$,1)<Chr$(18)
            While Prior%(Asc(Ein$))<=Prior%(Asc(Stack$(Zstack%))) And Zstack%>0
              Inc Zexp%
              Exp$(Zexp%)=Stack$(Zstack%)
              Dec Zstack%
            Wend
            Inc Zstack%
            Stack$(Zstack%)=Left$(Ein$,1)
          Else
            Eflag%=3
          Endif
        Endif
      Endif
      Ein$=Mid$(Ein$,2)
    Endif
  Wend
  While Zstack%>0
    Inc Zexp%
    Exp$(Zexp%)=Stack$(Zstack%)
    Dec Zstack%
  Wend
  If Klammer%>0
    Eflag%=6
  Endif
  '
  ' AUSWERTUNG
  '
  Clr Zstack%,I%
  Repeat
    Inc I%
    If Val?(Exp$(I%))>0
      Inc Zstack%
      Stack(Zstack%)=Val(Exp$(I%))
    Else
      On Asc(Exp$(I%)) Gosub Plus,Minus,Mal,Durch,H.och,Sqr,Sin,Cos,Tan,Atn,Log,Clg,Exp,Rnd,Abs,Int,Fak
    Endif
  Until I%>=Zexp% Or Eflag%>0
Return
'
Procedure Summe(A%,Flag%)
  Flag!=True
  Clr Sum,Qsum,Zeler%
  Gosub Decode.zellen
  If Mid$(Ein$,A2%,1)=")"
    If J1%>0 And J1%<=Breite% And H1%>0 And H1%<=Hoehe% And J2%>0 And J2%<=Breite% And H2%>0 And H2%<=Hoehe% And J1%<=J2% And H1%<=H2%
      J%=J1%
      Repeat
        H%=H1%
        Repeat
          If Left$(Ein$(J%,H%),1)="=" Or Val?(Left$(Ein$(J%,H%),1))>0 Or (Val?(Mid$(Ein$(J%,H%),2,1))>0 And Left$(Ein$(J%,H%),1)="-")
            Add Sum,Erg(J%,H%)
            Add Qsum,Erg(J%,H%)^2
            Inc Zeler%
          Endif
          Inc H%
        Until H%>H2%
        Inc J%
      Until J%>J2%
      If Flag%=1 And Zeler%
        Div Sum,Zeler%
      Endif
      If Flag%=2 And Zeler%
        If Zeler%>1
          Div Sum,Zeler%
          Sum=Sqr(Abs((Qsum-Sum*Sum*Zeler%)/(Zeler%-1)))
        Else
          Eflag%=5
        Endif
      Endif
      If Flag%=3 And Zeler%
        If Zeler%>1
          Div Sum,Zeler%
          Sum=Sqr(Abs((Qsum-Sum*Sum*Zeler%)/Zeler%))
        Else
          Eflag%=5
        Endif
      Endif
      If Flag%=4 And Zeler%
        If Zeler%>1
          Sum=Sqr(Qsum/Zeler%)
        Else
          Eflag%=15
        Endif
      Endif
      If Flag%=5 And Zeler%
        If Zeler%>1
          Sum=Sqr(Qsum/(Zeler%-1))
        Else
          Eflag%=15
        Endif
      Endif
      H%=Len(Str$(Sum))
      Ein$=Left$(Ein$,A%-1)+Str$(Sum)+Right$(Ein$,Len(Ein$)-A2%)
    Else
      Eflag%=13
    Endif
  Else
    Eflag%=6
  Endif
Return
'
Procedure Multipl
  On Error Gosub Ueberlauf
  Flag!=True
  Sum=1
  Gosub Decode.zellen
  If Mid$(Ein$,A2%,1)=")"
    If J1%>0 And J1%<=Breite% And H1%>0 And H1%<=Hoehe% And J2%>0 And J2%<=Breite% And H2%>0 And H2%<=Hoehe% And J1%<=J2% And H1%<=H2%
      J%=J1%
      Repeat
        H%=H1%
        Repeat
          If Left$(Ein$(J%,H%),1)="=" Or Val?(Left$(Ein$(J%,H%),1))>0 Or (Val?(Mid$(Ein$(J%,H%),2,1))>0 And Left$(Ein$(J%,H%),1)="-")
            Mul Sum,Erg(J%,H%)
          Endif
          Inc H%
        Until H%>H2%
        Inc J%
      Until J%>J2%
      H%=Len(Str$(Sum))
      Ein$=Left$(Ein$,A%-1)+Str$(Sum)+Right$(Ein$,Len(Ein$)-A2%)
    Else
      Eflag%=11
    Endif
  Else
    Eflag%=6
  Endif
  On Error Gosub Fehler
Return
'
Procedure Ueberlauf
  If Err=1
    Alert 3," |   öBERLAUF !!!   ",1," weiter ",H%
  Endif
  Clr Sum
  Eflag%=11
  On Error Gosub Fehler
  Resume Next
Return
'
Procedure Minimax(A%,Flag%)
  Flag!=True
  Clr Zeler%
  Min=1.0E+99
  Max=-1.0E+99
  Gosub Decode.zellen
  If Mid$(Ein$,A2%,1)=")"
    If J1%>0 And J1%<=Breite% And H1%>0 And H1%<=Hoehe% And J2%>0 And J2%<=Breite% And H2%>0 And H2%<=Hoehe% And J1%<=J2% And H1%<=H2%
      J%=J1%
      Repeat
        H%=H1%
        Repeat
          If Left$(Ein$(J%,H%),1)="=" Or Val?(Left$(Ein$(J%,H%),1))>0 Or (Val?(Mid$(Ein$(J%,H%),2,1))>0 And Left$(Ein$(J%,H%),1)="-")
            Inc Zeler%
            Min=Min(Erg(J%,H%),Min)
            Max=Max(Erg(J%,H%),Max)
          Endif
          Inc H%
        Until H%>H2%
        Inc J%
      Until J%>J2%
      If Flag%=1
        Min=Max
      Endif
      H%=Len(Str$(Min))
      Ein$=Left$(Ein$,A%-1)+Str$(Min)+Right$(Ein$,Len(Ein$)-A2%)
    Else
      Eflag%=10
    Endif
    If Zeler%=0 And Eflag%=0
      Eflag%=12
    Endif
  Else
    Eflag%=6
  Endif
Return
'
Procedure N_uber_n
  On Error Gosub Ueberlauf
  Local I%
  N1%=Val(Mid$(Ein$,A%+5))
  A1%=Instr(Ein$,":",A%)+1
  N2%=Val(Mid$(Ein$,A1%))
  Nun=1
  If N1%>=N2%
    If N2%>0
      For I%=N1% Downto N1%-N2%+1
        Mul Nun,I%
        If N2%>0
          Div Nun,N2%
          Dec N2%
        Endif
      Next I%
      If N2%>1
        For I%=2 To N2%
          Div Nun,I%
        Next I%
      Endif
    Else
      Nun=1
    Endif
    A1%=Instr(Ein$,")",A%)+1
    Ein$=Left$(Ein$,A%-1)+Str$(Nun)+Mid$(Ein$,A1%)
  Else
    Nun=0
    Eflag%=14
  Endif
  On Error Gosub Fehler
Return
'
Procedure Plus
  If Zstack%>1
    Add Stack(Zstack%-1),Stack(Zstack%)
    Dec Zstack%
  Else
    Eflag%=4
  Endif
Return
'
Procedure Minus
  If Zstack%>1
    Sub Stack(Zstack%-1),Stack(Zstack%)
    Dec Zstack%
  Else
    Eflag%=4
  Endif
Return
'
Procedure Mal
  If Zstack%>1
    Mul Stack(Zstack%-1),Stack(Zstack%)
    Dec Zstack%
  Else
    Eflag%=4
  Endif
Return
'
Procedure Durch
  If Zstack%>1
    If Stack(Zstack%)<>0
      Div Stack(Zstack%-1),Stack(Zstack%)
      Dec Zstack%
    Else
      Eflag%=5
    Endif
  Else
    Eflag%=4
  Endif
Return
'
Procedure H.och
  If Zstack%>1
    Stack(Zstack%-1)=Stack(Zstack%-1)^Stack(Zstack%)
    Dec Zstack%
  Else
    Eflag%=4
  Endif
Return
'
Procedure Int
  If Zstack%>0
    Stack(Zstack%)=Int(Stack(Zstack%))
  Else
    Eflag%=4
  Endif
Return
'
Procedure Abs
  If Zstack%>0
    Stack(Zstack%)=Abs(Stack(Zstack%))
  Else
    Eflag%=4
  Endif
Return
'
Procedure Rnd
  If Zstack%>0
    Stack(Zstack%)=Trunc(Stack(Zstack%)+0.5*Sgn(Stack(Zstack%)))
  Else
    Eflag%=4
  Endif
Return
'
Procedure Log
  If Zstack%>0
    If Stack(Zstack%)>0
      Stack(Zstack%)=Log(Stack(Zstack%))
    Else
      Stack(Zstack%)=0
      Eflag%=7
    Endif
  Else
    Eflag%=4
  Endif
Return
'
Procedure Exp
  If Zstack%>0
    Stack(Zstack%)=Exp(Stack(Zstack%))
  Else
    Eflag%=4
  Endif
Return
'
Procedure Sqr
  If Zstack%>0
    If Stack(Zstack%)>=0
      Stack(Zstack%)=Sqr(Stack(Zstack%))
    Else
      Stack(Zstack%)=0
      Eflag%=8
    Endif
  Else
    Eflag%=4
  Endif
Return
'
Procedure Clg
  If Zstack%>0
    If Stack(Zstack%)>0
      Stack(Zstack%)=Log10(Stack(Zstack%))
    Else
      Stack(Zstack%)=0
      Eflag%=7
    Endif
  Else
    Eflag%=4
  Endif
Return
'
Procedure Sin
  If Zstack%>0
    Stack(Zstack%)=Sin(Stack(Zstack%)*Grd)
  Else
    Eflag%=4
  Endif
Return
'
Procedure Cos
  If Zstack%>0
    Stack(Zstack%)=Cos(Stack(Zstack%)*Grd)
  Else
    Eflag%=4
  Endif
Return
'
Procedure Tan
  If Zstack%>0
    Stack(Zstack%)=Tan(Stack(Zstack%)*Grd)
  Else
    Eflag%=4
  Endif
Return
'
Procedure Atn
  If Zstack%>0
    Stack(Zstack%)=Atn(Stack(Zstack%))/Grd
  Else
    Eflag%=4
  Endif
Return
'
Procedure Fak
  If Zstack%>0
    Gosub Fakultet(Stack(Zstack%))
    Stack(Zstack%)=Fak
  Else
    Eflag%=4
  Endif
Return
'
Procedure Fakultet(N%)
  Local I%
  Fak=1
  If N%>=0 And N%<94
    For I%=1 To N%
      Mul Fak,I%
    Next I%
  Else
    Eflag%=14
    Fak=0
  Endif
Return
'
Procedure Widmung
  If Wid!=False
    Alert 1,"Dieses Programm ist meiner|Gattin IRIS gewidmet, die|immer viel VerstÑndnis fÅr|mein Hobby aufbringt !",1,"Klick...",H%
    Wid!=True
  Else
    Alert 2,"  Ich grÅûe Dich herzlich !|Sieh' bitte gnÑdig Åber meine|und  die  Fehler  im  Programm|hinweg.                 Danke",2," gern | na gut |  nie  ",H%
    Sget Screen$
    Deffill 1,0
    Prbox 10,10*Dvs,630,390*Dvs
    Rbox 11,11*Dvs,629,389*Dvs
    Rbox 14,14*Dvs,626,386*Dvs
    Deftext 1,17,,32-19*Test%
    Out 2,7
    If H%=1
      Put 50,25*Dvs,Icon$(0)
      Put 425,25*Dvs,Icon$(0)
      Graphmode 1
      Deftext ,16,,27+14*Test%
      Text 225-45*Test%,185*Dvs,"Vielen Dank"
      Deftext ,0,,13+7*Test%
      Text 240,205*Dvs,"und ein dickes Bussi!"
      Text 194,245*Dvs,"Der zweite Teil gilt natÅrlich"
      Text 194,265*Dvs,"nur fÅr einen weiblichen User!"
      Pause 60
    Else
      If H%=2
        Text 120,190*Dvs,400,"Sehr nett von Dir !"
        Deftext ,0,,13+7*Test%
        Text 120,220*Dvs,400,"(Du kennst das Programm wohl noch nicht so genau?)"
      Else
        Deftext ,0,,13+7*Test%
        Text 120,190*Dvs,400,"Da ist doch irgendwo der MistkÅbel ?"
        Text 120,220*Dvs,400,"(da kannst Du zumindest das Programm hineinwerfen)"
      Endif
    Endif
    Wid!=False
    Pause 120
    Prbox 16,16*Dvs,624,394*Dvs
    For H%=18 To 198 Step 4
      Rbox H%,H%*Dvs,640-H%,(400-H%)*Dvs
    Next H%
    Sput Screen$
  Endif
Return
'
Procedure Decode.zellen
  J1%=Asc(Mid$(Ein$,A%+5,1))-64
  H1%=Val(Mid$(Ein$,A%+6))
  A1%=Instr(Ein$,":",A%)
  J2%=Asc(Mid$(Ein$,A1%+1,1))-64
  H2%=Val(Mid$(Ein$,A1%+2))
  A2%=A1%+2+Val?(Mid$(Ein$,A1%+2))
Return
'
Procedure Breite.block
  Local X1%
  Clr Bl.breite%
  For X1%=B1x% To B2x%
    Add Bl.breite%,Breite%(X1%)+1
  Next X1%
  Clr All.breite%
  For X1%=1 To Xmax%
    Add All.breite%,Breite%(X1%)+1
  Next X1%
Return
'
Procedure Test.gesp
  Local H%
  If Gespeichert!=False
    Alert 3," | Arbeitsblatt ist| |NICHT GESPEICHERT!",2,"   OK   |Sichern",H%
    If H%=2
      Gosub Speichern
    Endif
  Endif
Return
'
Procedure Eingabe
  Ein$=Chr$(Ab%)
  A$="Eingabe"
  Gosub Anzeige
  Zeler%=1
  H1$=Chr$(200)+Chr$(203)+Chr$(205)+Chr$(208)+Chr$(27)+Chr$(225)
  Repeat
    Print At(2,3);Ein$;Ron$;" ";Roff$;Chr$(27);"K";
    A%=Inp(2)
    H$="ÑéîôÅöû^"+Chr$(126)+Chr$(124)+Chr$(95)+Chr$(200)+Chr$(203)+Chr$(205)+Chr$(208)+Chr$(27)+Chr$(8)+Chr$(225)
    If (A%>31 And A%<123) Or Instr(H$,Chr$(A%))
      If A%=8 And Zeler%>0
        Dec Zeler%
        Ein$=Left$(Ein$,Zeler%)
      Else
        If A%<>27 And A%<200 And A%<>8
          Ein$=Ein$+Chr$(A%)
          Inc Zeler%
        Endif
      Endif
    Endif
  Until A%=13 Or Instr(H1$,Chr$(A%)) Or Zeler%>76
  If Zeler%>76
    A%=13
    Print Chr$(27);"D";Chr$(7);
    Pause 30
    Repeat
    Until Inkey$=""
  Endif
  If A%<>27 And A%<>225
    Gosub C.minimax
    Gosub Breite.block
    Ein$(Cx%,Cy%)=Ein$
    If Left$(Ein$(Cx%,Cy%),1)="="
      A1%=Instr(Ein$(Cx%,Cy%)," !")
      If A1%<>0
        Ein$(Cx%,Cy%)=Upper$(Left$(Ein$(Cx%,Cy%),A1%))+Right$(Ein$(Cx%,Cy%),Len(Ein$(Cx%,Cy%))-A1%)
      Else
        Ein$(Cx%,Cy%)=Upper$(Ein$(Cx%,Cy%))
      Endif
      Gosub Rechne.zelle(Cx%,Cy%)
    Else
      Erg(Cx%,Cy%)=Val(Ein$(Cx%,Cy%))
    Endif
    Gosub Format.zelle(Cx%,Cy%)
    Gespeichert!=False
  Endif
  H$=Chr$(200)+Chr$(203)+Chr$(205)+Chr$(208)
  If Instr(H$,Chr$(A%))
    Aa%=A%
    Gosub Beweg.cursor
  Endif
  Gosub Inv_anzeige
Return
'
Procedure Anzeige
  If A$=""
    A$=Str$(Breite%)+"*"+Str$(Hoehe%)+" "
  Else
    A$=A$+" "
  Endif
  A$=Space$(18-Len(A$))+A$
  Text 392,398*Dvs,Space$(18)
  Graphmode 4
  Text 392,398*Dvs,A$
  Graphmode 1
  Print At(2,3);
Return
'
Procedure Aufloesung
  Aufloesung%=Xbios(4)
  Test%=(Aufloesung%=1)
  If Test%=True
    Dvs=0.5
  Else
    Dvs=1
  Endif
  If Aufloesung%=0
    Alert 3," |Dieses Programm lÑuft|nicht in niedriger|Bildschirmauflîsung",1," OK ",A%
    Quit
  Endif
  Copyright$=Space$(1526)
  If Aufloesung%=1
    Col%=275
    Col1%=1635
    Col2%=85
    Col3%=1280
    Setcolor 0,Col1%
    Setcolor 1,Col2%
    Setcolor 2,Col3%
    Setcolor 3,Col%
    Color 2
  Else
    Clr Col%
    Gosub No.copyr
  Endif
Return
'
Procedure Icon
  Dim Icon$(1)
  If Exist("GEM_CALC.DAT")
    Open "I",#1,"GEM_CALC.DAT"
    Input #1,No%
    Input #1,Count%
    Input #1,Len
    Icon$(Count%)=Space$(Len)
    Bget #1,Varptr(Icon$(Count%)),Len
    Close #1
  Else
    Icon$(0)=""
  Endif
Return
'
Procedure Init_variablen
  Ron$=Chr$(27)+"p"
  Roff$=Chr$(27)+"q"
  Breite%=26
  Hoehe%=200
  Grd=Pi/180
  Lng%=18
  Fak=1
  Rei=1
  Spa=12
  Wid!=False
  Trennz!=False
  Lfeed!=True
  Null!=True
  Clr Tit_zl%
  Tit_fl!=False
  Dim Kopf$(Breite%),Breite%(Breite%),Komma%(Breite%),Stack$(100),Exp$(100),Stack(100),Prior%(100),Fehler$(15)
  Dim W(5,50),Nsa$(50),X(50),Y(50),N$(5)
  Dim Hlp$(Breite%,Hoehe%)
  Arrayfill Prior%(),0
  Restore Prior
  For I%=1 To 17
    Read Prior%(I%)
  Next I%
  Prior:
  Data 1,1,2,2,3,3,4,4,4,4,4,4,4,4,4,4,4
  '
  For I%=1 To 14
    Read Fehler$(I%)
  Next I%
  Fehler$(9)=Fehler$(9)+Chr$(126)
  Data unbekannte Funktion,'(' fehlt,unbekannter Operator,zu wenig Argumente
  Data Division durch Null,')' fehlt,negative Zahl bei LOG,negative Zahl bei SQR
  Data falsche Koordinaten bei ,Fehler bei #MIN/MAX,Fehler bei #MUL
  Data keine numerischen Daten| bei #MIN/MAX,Fehler bei #SUM/AVE/STA/QMW
  Data Fehler bei #FAK/NUN
Return
'
Procedure Leiste
  Dim Leiste$(100)
  For I%=0 To 100
    Read Leiste$(I%)
    Exit If Leiste$(I%)="***"
  Next I%
  Leiste$(I%)=""
  Leiste$(I%+1)=""
  Data     ,  Ω GEM-CALC,--------------------, , , , , , ,""
  Data         ,  Laden           F3,  Speichern      ^F3,---------------------,  Drucken         F9,  Zellinhalt     C-Z,---------------------,  Blatt lîschen ^F10,  Ende           ^F9,""
  Data         ,  Zahl der Spalten C-L,  Breite Spalte     F4,  Breite Alle      ^F4,-----------------------,  Komma Spalte      F5,  Komma Alle       ^F5,-----------------------,  Zeile einfÅgen   C-1,  Zeile lîschen    C-2,  Spalte einfÅgen  C-3
  Data   Spalte lîschen   C-4,-----------------------,  Nachkomma-0      C-N,  Return mit LF    C-P,  Trennzeichen     C-T,""
  Data        ,  links oben   ^F2,  rechts unten  F2,-------------------,  Move absolut  F7,  Move relativ ^F7,  Copy absolut  F6,  Copy relativ ^F6,-------------------,  --> Werte    C-V,  Lîschen     ^F10,""
  Data        ,  éndern        F1,-------------------,  Merken       C-M,  Copy absolut C-A,  Copy relativ C-R,-------------------,  Lîschen      F10,""
  Data          ,  Berechnung    ^F1,  Fehler suchen C-O,  Sortieren     C-X,  Gehe Zelle     F8,  Invert        C-C,  Hi Resolution C-K,  Kopf          ^F8,  Titelzeilen   C-0,  Bogenmaû      C-B,  max. Bereich  C-9
  Data --------------------,  Hilfe    Help/C-û,""
  Data         ,  Datentransfer   C-U,  Daten laden     C-6,  Daten speichern C-5,----------------------,  Kuchen          C-D,  Linien          C-E,  Balken          C-F,  Stapel          C-G,  SÑulen          C-H,  Blîcke          C-I
  Data   FlÑchen         C-J,----------------------,  Hardcopy        C-Q,  Degas           C-W,----------------------,  Spreadsheet     C-S,""
  Data ***
Return
'
Procedure Init
  Gespeichert!=True
  Dim Ein$(Breite%,Hoehe%),Aus$(Breite%,Hoehe%),Erg(Breite%,Hoehe%)
  Arrayfill Komma%(),2
  Arrayfill Breite%(),9
  Arrayfill Erg(),0
  '
  H$=Space$(10)
  For I%=1 To Breite%
    Kopf$(I%)=Chr$(I%+64)
    For J%=1 To Hoehe%
      Aus$(I%,J%)=H$
    Next J%
  Next I%
  For I%=77 To 83
    Menu I%,2
  Next I%
  Gosub Variablen.init
  If Tit_zl%
    Menu 66,1
  Else
    Menu 66,0
  Endif
  Xmax%=1
  Ymax%=1
  Cd%=2
  Ugrafik!=False
  Grafik!=False
  Gosub Breite.block
  Sget Screen$
Return
'
Procedure Variablen.init
  X%=1
  Y%=1
  Cx%=1
  Cy%=1
  B1x%=1
  B1y%=1
  B2x%=1
  B2y%=1
  Mx%=1
  My%=1
Return
'
Procedure Grafik_init
  Ueb1$=""
  Ueb2$=""
  Clr Offs
  Fak=1
  Rei=1
  Spa=12
  Reih%=1
  Beginn%=1
  Beginn2%=1
  Bez%=1
  Bezrei%=1
Return
'
Procedure Hi.resolution(X%,Y%,Z%)
  Cls
  Deftext 1,0,,4
  Gosub Zahl.spalten(103)
  Y%=Min(Y%,Hoehe%-55)
  Breit%=23
  Deffill 1,1
  Pbox 0,0,639,9
  Graphmode 3
  For Xs%=X% To Spalten%
    Text Breit%,8,Left$(Kopf$(Xs%),Breite%(Xs%))
    Add Breit%,Breite%(Xs%)*6+6
  Next Xs%
  Graphmode 1
  Tief%=18
  For Ys%=Y% To Y%+Z%
    A$="   "
    Rset A$=Str$(Ys%)
    A$=A$+" "
    For Xs%=X% To Spalten%
      A$=A$+Aus$(Xs%,Ys%)
    Next Xs%
    Text 0,Tief%,A$
    Add Tief%,7
  Next Ys%
  Deftext 1,,,13+7*Test%
  Repeat
  Until Inkey$<>"" Or Mousek
Return
'
Procedure Fehler
  Close
  If Err=-33
    H$=" | Datei nicht gefunden ! "
  Else
    H$="Es ist ein Fehler aufgetreten:|Fehler Nr. "+Str$(Err)+"|(Klartext gibts erst in|GFA-BASIC Version 3.04)"
  Endif
  Alert 3,H$,1," Mist ",H%
  Defmouse 0
  Resume Fehlerlabel
Return
'
Procedure No.copyr
  Local I%,H$
  Deftext 1,16,,6
  H$="GEM-CALC  ** **  Version 1.92 - 02.03.1989  ** **  GEM-CALC"
  For I%=1 To Len(H$)
    If Asc(Mid$(H$,I%,1))>47 And Asc(Mid$(H$,I%,1))<58
      Mid$(H$,I%,1)=Chr$(Asc(Mid$(H$,I%,1))-32)
    Endif
  Next I%
  Text 24,40,H$
  Deftext 1,0,,6
  Get 0,24,639,42,Copyright$
Return
'
Procedure Menue
  Aa%=Menu(0)
  Menu Off
  If Aa%=1
    Gosub Titel
    Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
  Else
    Sub Aa%,10
    On Aa% Gosub Laden,Speichern,A,Drucken,Druck.zellinhalt,A,Loesche.all,Ende
    Sub Aa%,10
    On Aa% Gosub Zahl_spalten,Breite,Breite.all,A,Komma,Komma.all,A,Zeile.einfugen,Zeile.loschen,Spalte.einfugen,Spalte.loschen,A,Nullen,Line.feed,Trennzeichen
    Sub Aa%,17
    On Aa% Gosub Block.lo,Block.ru,A,Move.abs,Move.rel,Copy.abs,Copy.rel,A,Block.werte,Loesche.block
    Sub Aa%,12
    On Aa% Gosub Edit,A,Merken,Copy.cel.abs,Copy.cel.rel,A,Loesche.zelle
    Sub Aa%,9
    On Aa% Gosub Recalc,Such.fehler,Sortieren,Gehe.zelle,Invers,Hi.resol,Kopf,Titelzeilen,Bogenmass,Anpassen_max,A,Help.screen
    Sub Aa%,14
    Hilf$=Leiste$(Menu(0))
    On Aa% Gosub Daten.uebernahme,Load.gdaten,Save.gdaten,A,Kuchen,Grafik.2d,Grafik.2d,Stapel,Grafik.3d,Grafik.3d,Grafik.3d,A,Hardcpy,Degas,A,Spreadsheet
  Endif
Return
'
Procedure Hi.resol
  If Aufloesung%=2
    Z%=54
  Else
    Z%=25
  Endif
  Gosub Hi.resolution(X%,Y%,Z%)
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Invers
  Setcolor 0,Col%
  If Aufloesung%=2
    Col%=1911-Col%
    Setcolor 1,Col%
  Else
    Swap Col%,Col1%
    Setcolor 0,Col1%
    Setcolor 3,Col%
  Endif
Return
'
Procedure Titel
  Local X%,Y%
  Cls
  Put 0,24-12*Test%,Copyright$
  Deffill 3,4,1
  Pbox 0,62*Dvs,639,399
  Deffill 1,0
  Prbox 160,80*Dvs,479,150*Dvs
  Prbox 160,144*Dvs,479,318*Dvs
  Box 170,200*Dvs,282,248*Dvs
  For I%=208 To 240 Step 8
    Line 170,I%*Dvs,282,I%*Dvs
  Next I%
  For I%=184 To 268 Step 14
    Line I%,200*Dvs,I%,248*Dvs
  Next I%
  Deftext 1,0,,32+19*Test%
  Text 190,126*Dvs,"G"
  Text 250,126*Dvs,"E"
  Text 292+6*Test%,126*Dvs,"M"
  Text 360,126*Dvs,90,"-CALC"
  Deftext 1,2,,13+7*Test%
  Text 206+6*Test%,126*Dvs,"regor"
  Text 266+6*Test%,126*Dvs,"ngl"
  Text 308+12*Test%,126*Dvs,"mayer"
  Deftext 1,17,,13+7*Test%
  Text 175,170*Dvs,"GEM-CALC"
  Deftext 1,0,,6+2*Test%
  Text 185,188*Dvs,"Ω 1988"
  Deftext 1,,,13+7*Test%
  Text 278,170*Dvs,"ist ein PD-Programm von"
  Text 302,212*Dvs,"Gregor Englmayer"
  Text 302,228*Dvs,"Weiûenbach 30/2"
  Text 302,244*Dvs,"A-8813 St. Lambrecht"
  Deftext 1,-2*(Test%=0),,13+7*Test%
  Text 170,286*Dvs,"GefÑllt Dir das Programm, dann schicke"
  Text 170,302*Dvs,"mir bitte DM 20.- oder îS 100.-- DANKE"
  Deftext 2,1,,32+19*Test%
  Restore Klick
  For I%=0 To 9
    Pcircle I%*60+45,360*Dvs,25
    Read H$
    Text I%*60+38,372*Dvs,H$
  Next I%
  Klick:
  Data  ,K,L,I,C,K,.,.,.,
  Repeat
  Until Mousek Or Inkey$<>""
Return
'
Procedure Druck.zellinhalt
  Local A%,X%,Y%
  Alert 2," |  Ausdruck der Zellinhalte ?   ",3," alle | Block | nein ",H%
  If H%<>3
    Gosub Koordinaten(H%)
    If E_flag!=False
      Defmouse 2
      Open "O",#2,"PRN:"
      Print #2;Chr$(27);"R";Chr$(2);Chr$(27);"l";Chr$(8);Chr$(27);"M";
      Clr Zeilen%
      For X%=S1x% To S2x%
        For Y%=S1y% To S2y%
          If Ein$(X%,Y%)<>""
            H$=Ein$(X%,Y%)
            A1%=1
            Repeat
              A%=Instr(H$,Chr$(126))
              If A%<>0
                H$=Left$(H$,A%-1)+Mid$(H$,A%+1)
              Endif
            Until A%=0
            Gosub Test.umlaut
            Print #2;Chr$(X%+64);
            Print #2;Str$(Y%);": ";
            Print #2;H$;
            If Lfeed!=True
              Print #2
            Else
              Print #2;Chr$(13);
            Endif
            Inc Zeilen%
            If Zeilen%>64
              Print #2;Chr$(12);
              Clr Zeilen%
            Endif
          Endif
        Next Y%
      Next X%
      Print #2;Chr$(12);
      Close #2
      Defmouse 0
    Endif
  Endif
Return
'
Procedure Hardcpy
  If Aufloesung%<>1
    Hidem
    H%=1
    If Grafik!=False
      Gosub Invert(0)
      Alert 3," |Eine Hardcopy des Arbeits-|blattes bewirkt einen|PROGRAMMABSTURZ  !!",2,"weiter|oh nein",H%
    Endif
    If H%=1
      While Out?(0)=False And H%=1
        Alert 1," |Drucker einschalten",1," okay |Abbruch",H%
      Wend
      If H%=1
        A$=Space$(380)
        G$="        "+Chr$(27)+"*"+Chr$(5)+Chr$(380)+Chr$(380/256)
        Open "",#99,"LST:"
        Print #99;Chr$(27);"3";Chr$(24)
        For S%=Xbios(3) To S%+79
          Dx%=Varptr(A$)
          For Q%=S%+399*80 To S% Step -80
            Poke Dx%,Peek(Q%)
            Inc Dx%
          Next Q%
          Print #99,G$;A$;Chr$(13);
          If Lfeed!=True
            Print #99
          Endif
        Next S%
        Print #99;Chr$(27);"2"
        Print #99;Chr$(12);
        Close #99
      Endif
    Endif
    If Grafik!=False
      Gosub Invert(1)
    Endif
    Showm
  Endif
Return
'
Procedure Spreadsheet
  Grafik!=False
  For I%=1 To 75
    Menu I%,3
  Next I%
  Restore Linien
  Do
    Read I%
    Exit If I%=-1
    Menu I%,2
  Loop
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
Linien:
Data 2,13,16,24,27,32,40,45,51,55,69,76,84,87,-1
'
Procedure Sortieren
  If Ymax%>1
    Gosub Invert(0)
    A$="Sortieren"
    Gosub Anzeige
    Sget Screen$
    Gosub Draw.sortiere
    Gosub Control.sortiere
    Sput Screen$
    If Auf%<>3
      Gespeichert!=False
      Defmouse 2
      If Auf%=1
        For I%=Von% To Bis%-1
          For J%=I%+1 To Bis%
            If Alph%=2
              If Erg(Spa%,I%)>Erg(Spa%,J%)
                Gosub Tauschen
              Endif
            Else
              If Ein$(Spa%,I%)>Ein$(Spa%,J%)
                Gosub Tauschen
              Endif
            Endif
          Next J%
        Next I%
      Else
        For I%=Von% To Bis%-1
          For J%=I%+1 To Bis%
            If Alph%=2
              If Erg(Spa%,I%)<Erg(Spa%,J%)
                Gosub Tauschen
              Endif
            Else
              If Ein$(Spa%,I%)<Ein$(Spa%,J%)
                Gosub Tauschen
              Endif
            Endif
          Next J%
        Next I%
      Endif
      Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
      Defmouse 0
    Endif
    Gosub Inv_anzeige
  Endif
Return
'
Procedure Draw.sortiere
  Graphmode 1
  Gosub Shadow.box(77,62*Dvs,555,370*Dvs,2,4)
  Deffill ,2,2
  Pbox 115,74*Dvs,520,110*Dvs
  Deffill ,0,0
  Pbox 325,124*Dvs,520,160*Dvs
  Pbox 325,170*Dvs,520,206*Dvs
  Pbox 115,214*Dvs,305,250*Dvs
  Pbox 115,260*Dvs,305,296*Dvs
  Pbox 325,260*Dvs,520,296*Dvs
  Deffill ,2,8
  Pbox 115,124*Dvs,305,160*Dvs
  Pbox 115,170*Dvs,305,206*Dvs
  Gosub Frame.box(115,314*Dvs,305,360*Dvs,0,0)
  Gosub Frame.box(330,314*Dvs,520,360*Dvs,0,0)
  Graphmode 2
  Deftext ,0-Test%,,13+7*Test%
  Text 244,98*Dvs," S O R T I E R E N"
  Deftext ,0
  Graphmode 1
  Text 380,146*Dvs,"absteigend"
  Text 385,190*Dvs,"numerisch"
  Text 200,344*Dvs,"OK"
  Text 390,344*Dvs,"ABBRUCH"
  H1$="SPALTE (A-"+Chr$(Xmax%+64)+") : "
  Print At(19,15);H1$+Chr$(Cx%+64)
  H2$="von ZEILE (1-"+Str$(Ymax%-1)+"): "
  Print At(16,18);H2$+"1"
  H3$="bis ZEILE (2-"+Str$(Ymax%)+"): "
  Print At(43,18);H3$+Str$(Ymax%)
  Graphmode 3
  Text 165,146*Dvs,"aufsteigend"
  Text 160,190*Dvs,"alphabetisch"
  Graphmode 1
Return
'
Procedure Control.sortiere
  Ex!=False
  Auf%=1
  Alph%=1
  Spa%=Cx%
  Von%=1
  Bis%=Ymax%
  Do
    Mouse X,Y,K
    Y=Y/Dvs
    If K=1
      If X>114 And X<305 And Y>125 And Y<160
        Auf%=1
        Deffill ,2,8
        Pbox 115,124*Dvs,305,160*Dvs
        Deffill ,0,0
        Pbox 325,124*Dvs,520,160*Dvs
        Graphmode 3
        Text 165,146*Dvs,"aufsteigend"
        Graphmode 1
        Text 380,146*Dvs,"absteigend"
      Endif
      If X>325 And X<520 And Y>125 And Y<160
        Auf%=2
        Deffill ,2,8
        Pbox 325,124*Dvs,520,160*Dvs
        Deffill ,0,0
        Pbox 115,124*Dvs,305,160*Dvs
        Graphmode 3
        Text 380,146*Dvs,"absteigend"
        Graphmode 1
        Text 165,146*Dvs,"aufsteigend"
      Endif
      If X>114 And X<305 And Y>170 And Y<206
        Alph%=1
        Deffill ,2,8
        Pbox 115,170*Dvs,305,206*Dvs
        Deffill ,0,0
        Pbox 325,170*Dvs,520,206*Dvs
        Graphmode 3
        Text 160,190*Dvs,"alphabetisch"
        Graphmode 1
        Text 385,190*Dvs,"numerisch"
      Endif
      If X>325 And X<520 And Y>170 And Y<206
        Alph%=2
        Deffill ,2,8
        Pbox 325,170*Dvs,520,206*Dvs
        Deffill ,0,0
        Pbox 115,170*Dvs,305,206*Dvs
        Graphmode 3
        Text 385,190*Dvs,"numerisch"
        Graphmode 1
        Text 160,190*Dvs,"alphabetisch"
      Endif
      If X>115 And X<305 And Y>215 And Y<250
        Repeat
          Ein$=Chr$(Spa%+64)
          Print At(19,15);H1$;
          Form Input 1 As Ein$
          Spa%=Asc(Upper$(Ein$))-64
          If Spa%<1 Or Spa%>Xmax%
            Out 2,7
          Endif
        Until Spa%>0 And Spa%<=Xmax%
        Print At(19,15);H1$+Chr$(Spa%+64)
      Endif
      If X>115 And X<304 And Y>260 And Y<295
        Gosub Ein.zahl(Left$(H2$,Len(H2$)-1),Von%,1,Ymax%-1,16,18)
        If Val(Aus$)<>-1
          Von%=Val(Aus$)
        Endif
        Print At(16,18);H2$;Von%
      Endif
      If X>330 And X<520 And Y>260 And Y<295
        Gosub Ein.zahl(Left$(H3$,Len(H3$)-1),Bis%,2,Ymax%,43,18)
        If Val(Aus$)<>-1
          Bis%=Val(Aus$)
        Endif
        Print At(43,18);H3$;Bis%
      Endif
      If X>120 And X<300 And Y>320 And Y<355
        Ex!=True
      Endif
      If X>335 And X<515 And Y>320 And Y<355
        Auf%=3
        Ex!=True
      Endif
    Endif
    Exit If Ex!=True
  Loop
  Repeat
  Until Mousek=0
Return
'
Procedure Tauschen
  For K%=1 To Xmax%
    Swap Erg(K%,I%),Erg(K%,J%)
    Swap Ein$(K%,I%),Ein$(K%,J%)
    Swap Aus$(K%,I%),Aus$(K%,J%)
  Next K%
Return
'
Procedure Titelzeilen
  Gespeichert!=False
  Inc Tit_zl%
  Alert 2," |  Wieviele Zeilen sollen als | Titelzeilen gedruckt werden ? ",Tit_zl%,"  0  |  1  |  2  ",Tit_zl%
  Dec Tit_zl%
  If Tit_zl%
    Menu 66,1
    If Tit_fl!=True
      H%=2
    Else
      H%=1
    Endif
    Alert 2,"1. alle Spalten|2. linke Spalten (75 Zeichen) | |   werden gedruckt.",H%,"  1  |  2  ",H%
    If H%=1
      Tit_fl!=False
    Else
      Tit_fl!=True
    Endif
  Else
    Menu 66,0
  Endif
Return
'
Procedure Anpassen_max
  Local X%,Y%,M%
  A$="Bereich max"
  Gosub Anzeige
  Defmouse 2
  M%=1
  If Xmax%>1
    For X%=1 To Xmax%
      For Y%=1 To Ymax%
        If Ein$(X%,Y%)<>""
          M%=X%
        Endif
        Exit If M%=X%
      Next Y%
    Next X%
  Endif
  Xmax%=M%
  M%=1
  If Ymax%>1
    For Y%=1 To Ymax%
      For X%=1 To Xmax%
        If Ein$(X%,Y%)<>""
          M%=Y%
        Endif
        Exit If M%=Y%
      Next X%
    Next Y%
  Endif
  Ymax%=M%
  A$=""
  Gosub Breite.block
  Gosub Infozeile
  Defmouse 0
Return
'
Procedure Degas
  If Aufloesung%=1
    File$="\*.PI2"
  Else
    File$="\*.PI3"
  Endif
  Sget Screen$
  Fileselect File$,"",File1$
  Sput Screen$
  Hidem
  If Len(File1$)>4
    File$=Right$(File$,4)
    If Right$(File1$,4)<>File$
      A%=Instr(File1$,".")
      If A%=0
        File1$=File1$+File$
      Else
        File1$=Left$(File1$,A%-1)+File$
      Endif
    Endif
    Open "O",#1,File1$
    If Aufloesung%=1
      Out #1,0
      Out #1,1
      Out #1,Int(Col%/256)
      Out #1,Col% Mod 256
      Out #1,Int(Col2%/256)
      Out #1,Col2% Mod 256
      Out #1,Int(Col3%/256)
      Out #1,Col3% Mod 256
      Out #1,Int(Col1%/256)
      Out #1,Col1% Mod 256
      For I%=1 To 24
        Out #1,0
      Next I%
    Else
      Out #1,0
      Out #1,2
      Out #1,Int(1911/256)
      Out #1,1911 Mod 256
      Out #1,0
      Out #1,0
      For I%=1 To 28
        Out #1,0
      Next I%
    Endif
    Bput #1,Xbios(2),32000
    Close #1
    File$=""
    Showm
  Endif
Return
'
Procedure Array_loeschen
  Erase Ein$()
  Erase Aus$()
  Erase Erg()
  Gosub Init
Return
'
' GRAFIKROUTINEN  *** Dank an Johannes Fiedler, D-7920 Heidenheim ***
'
Procedure Daten.uebernahme
  Gosub Invert(0)
  Deftext 1,16,,13+7*Test%
  Text 8,46+24*Test%,"Grafik - DatenÅbernahme"+Space$(55)
  Alert 2,"   Darstellung von    | | ",Reih%,"Reihen|Spalten",Reih%
  Hidem
  Deffill 0,0
  Pbox 0,240*Dvs,639,399*Dvs
  Print At(3,17);"öberschrift : "
  Print At(3,18);
  Form Input 35 As Ueb1$
  Print At(3,20);"Untertitel :"
  Print At(3,21);
  Form Input 70 As Ueb2$
  Ein$=Str$(Offs)
  Print At(3,23);"Y-Offset  ";
  Form Input 6 As Ein$
  Offs=Val(Ein$)
  Ein$=Str$(Fak)
  Print At(3,24);"Y-Faktor  ";
  Form Input 7 As Ein$
  Fak=Val(Ein$)
  Fak=Max(0.1,Fak)
  Pbox 0,240*Dvs,639,399*Dvs
  Line 0,380*Dvs,639,380*Dvs
  '
  If Reih%=1
    A$="Reihen"
  Else
    A$="Spalten"
  Endif
  Ein$=Str$(Rei)
  Print At(7,17);"Wieviele "+A$+" hintereinander (1-4) ? ";
  Form Input 1 As Ein$
  Rei=Val(Ein$)
  Rei=Min(4,Rei)
  Rei=Max(1,Rei)
  A$=Left$(A$,Len(A$)-1)
  If Reih%=1
    Ein$=Str$(Beginn%)
    Hilfs$="Eingabe der obersten darzustellenden Zeile:  1 - "+Str$(Hoehe%-Rei+1)
  Else
    Ein$=Chr$(Beginn%+64)
    Hilfs$="Eingabe der Ñuûerst linken darzustellenden Spalte: A - Z"
  Endif
  Print At(3,25);Hilfs$;
  Print At(7,18);"Beginnend mit ";A$;"       ";
  Form Input 3 As Ein$
  If Reih%=1
    Beginn%=Val(Ein$)
    Beginn%=Min(Beginn%,Hoehe%+1-Rei)
  Else
    Beginn%=Asc(Ein$)-64
    If Beginn%>Breite%+1-Rei
      Sub Beginn%,32
    Endif
  Endif
  Beginn%=Max(1,Beginn%)
  Print At(2,25);Space$(78);
  '
  If Reih%<>1
    A$="Spalte(n)"
    Ein$=Str$(Bezrei%)
    Hilfs$="Zeile: 1 - "+Str$(Hoehe%-1)+" | Kopfzeile: 0 | nichts: -1"
  Else
    A$="Reihe(n)"
    Hilfs$="Spalte: A - Z | nichts: -1"
    If Bezrei%<=0
      Ein$=Str$(Bezrei%)
    Else
      Ein$=Chr$(Bezrei%+64)
    Endif
  Endif
  Print At(3,25);Hilfs$;
  Print At(7,19);"Bezeichnung der ";A$;"  ";
  Form Input 3 As Ein$
  If Reih%=1
    If Val?(Ein$)>0
      Bezrei%=-1
      Bezrei%=Min(Bezrei%,Breite%+1-Rei)
    Else
      Bezrei%=Asc(Ein$)-64
      If Bezrei%>Breite%+1-Rei
        Sub Bezrei%,32
      Endif
    Endif
  Else
    Bezrei%=Val(Ein$)
    Bezrei%=Min(Bezrei%,Hoehe%+1-Rei)
  Endif
  Print At(2,25);Space$(78);
  '
  Ein$=Str$(Spa)
  If Reih%=1
    Hilf$="26"
  Else
    Hilf$="28"
  Endif
  Print At(7,21);"Wieviele Werte nebeneinander (2-";Hilf$;") ? ";
  Form Input 2 As Ein$
  Spa=Val(Ein$)
  Spa=Min(Val(Hilf$),Spa)
  Spa=Max(2,Spa)
  If Reih%=1
    Ein$=Chr$(Beginn2%+64)
    A$="Spalte"
    Hilfs$="Eingabe der Ñuûerst linken darzustellenden Spalte: A - Z"
  Else
    Ein$=Str$(Beginn2%)
    A$="Reihe"
    Hilfs$="Eingabe der obersten darzustellenden Zeile:  1 - "+Str$(Hoehe%-Spa+1)
  Endif
  Print At(3,25);Hilfs$;
  Print At(7,22);"Beginnend mit ";A$;"       ";
  Form Input 3 As Ein$
  If Reih%=1
    Beginn2%=Asc(Ein$)-64
    If Beginn2%>Breite%+1-Spa
      Sub Beginn2%,32
    Endif
  Else
    Beginn2%=Min(Hoehe%+1-Spa,Val(Ein$))
  Endif
  Beginn2%=Max(1,Beginn2%)
  Print At(2,25);Space$(78);
  '
  If Reih%<>1
    A$="Reihe(n)"
    Hilfs$="Spalte: A - Z | nichts: -1"
    If Bez%<=0
      Ein$=Str$(Bez%)
    Else
      Ein$=Chr$(Bez%+64)
    Endif
  Else
    A$="Spalte(n)"
    Ein$=Str$(Bez%)
    Hilfs$="Zeile: 1 - "+Str$(Hoehe%-1)+" | Kopfzeile: 0 | nichts: -1"
  Endif
  Print At(3,25);Hilfs$;
  Print At(7,23);"Bezeichnung der ";A$;"  ";
  Form Input 3 As Ein$
  If Reih%=1
    Bez%=Min(Hoehe%+1-Rei,Val(Ein$))
  Else
    If Val?(Ein$)>0
      Bez%=-1
      Bez%=Min(Bez%,Breite%+1-Rei%)
    Else
      Bez%=Asc(Ein$)-64
      If Bez%>Breite%+1-Rei
        Sub Bez%,32
      Endif
    Endif
  Endif
  Print At(2,25);Space$(78);
  If Reih%=1
    If Spa+Beginn2%+1>Breite%
      Spa=Breite%-Beginn2%+1
    Endif
  Else
    If Spa+Beginn%+1>Hoehe%
      Spa=Hoehe%-Beginn%+1
    Endif
  Endif
  '
  For I%=1 To 5
    N$(I%)=""
  Next I%
  For I%=1 To 50
    Nsa$(I%)=""
    W(0,I%)=0
  Next I%
  Clr Maks,Maks2
  For I%=1 To Rei
    For J%=1 To Spa
      If Reih%=1
        W(I%,J%)=Erg(Beginn2%+J%-1,Beginn%+I%-1)
        If Bezrei%=0
          N$(I%)=Kopf$(Beginn%+I%-1)
        Else
          If Bezrei%>0
            N$(I%)=Ein$(Bezrei%,Beginn%+I%-1)
          Endif
        Endif
        If Bez%=0
          Nsa$(J%)=Kopf$(Beginn2%+J%-1)
        Else
          If Bez%>0
            Nsa$(J%)=Ein$(Beginn2%+J%-1,Bez%)
          Endif
        Endif
      Else
        W(I%,J%)=Erg(Beginn%+I%-1,Beginn2%+J%-1)
        If Bezrei%=0
          N$(I%)=Kopf$(Beginn%+I%-1)
        Else
          If Bezrei%>0
            N$(I%)=Ein$(Beginn%+I%-1,Bezrei%)
          Endif
        Endif
        If Bez%>0
          Nsa$(J%)=Ein$(Bez%,Beginn2%+J%-1)
        Endif
      Endif
      While Left$(N$(I%),1)=" "
        N$(I%)=Mid$(N$(I%),2)
      Wend
      N$(I%)=Left$(N$(I%),8)
      While Left$(Nsa$(J%),1)=" "
        Nsa$(J%)=Mid$(Nsa$(J%),2)
      Wend
      If Spa<7
        Nsa$(J%)=Left$(Nsa$(J%),8)
      Endif
      If Spa=7
        Nsa$(J%)=Left$(Nsa$(J%),7)
      Endif
      If Spa=8
        Nsa$(J%)=Left$(Nsa$(J%),6)
      Endif
      If Spa=9
        Nsa$(J%)=Left$(Nsa$(J%),5)
      Endif
      If Spa>9
        Nsa$(J%)=Left$(Nsa$(J%),4)
      Endif
      If Spa>11
        Nsa$(J%)=Left$(Nsa$(J%),3)
      Endif
      If Spa>14
        Nsa$(J%)=Left$(Nsa$(J%),2)
      Endif
      If Spa>24
        Nsa$(J%)=Left$(Nsa$(J%),1)
      Endif
      '
      If W(I%,J%)<Offs
        W(I%,J%)=Offs
      Endif
      Maks=Max(Maks,W(I%,J%))
      Add W(0,J%),W(I%,J%)
      Maks2=Max(Maks2,W(0,J%))
    Next J%
  Next I%
  If Offs>=Maks
    Offs=0
  Endif
  Gosub Bestimme.maks
  Swap Maks,Maks2
  Gosub Bestimme.maks
  Swap Maks,Maks2
  '
  For I%=1 To Rei
    For J%=1 To Spa
      Sub W(I%,J%),Offs
      If W(I%,J%)<0
        W(I%,J%)=0
      Endif
    Next J%
  Next I%
  '
  Ugrafik!=True
  For I%=77 To 83
    Menu I%,3
  Next I%
  If Spa>14
    Menu 77,2
    Menu 82,2
    Menu 83,2
    If Rei>2
      Menu 79,2
    Endif
  Endif
  If Offs<0
    For I%=77 To 83
      Menu I%,2
    Next I%
    Menu 78,3
    Menu 79,3
  Endif
  Showm
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Bestimme.maks
  Sub Maks,Offs
  Flag!=False
  I=0.1
  Repeat
    If Maks<=I
      Maks=I
      Flag!=True
    Else
      Mul I,2
      If Maks<=I
        Maks=I
        Flag!=True
      Else
        Mul I,2.5
        If Maks<=I
          Maks=I
          Flag!=True
        Endif
      Endif
    Endif
    Mul I,2
  Until I>10000000 Or Flag!=True
Return
'
Procedure Grafik.3d
  Gosub Ein.grafik
  Cls
  Color 3
  Deftext 1,0,,32+19*Test%
  Text 20,52*Dvs,Ueb1$
  Deftext 1,,,6
  Text 20,66*Dvs-2*Test%,Ueb2$
  X1=30
  Y1=380
  Vx=25
  Vy=50
  Laenge=470
  Hoehe=250
  Ho=Hoehe
  La=Laenge
  X2=X1+Vx
  Y2=Y1-Vy
  Gosub Um
  X3=X2+La
  Y3=Y2-Ho
  X4=X1+La
  Y4=Y1-Ho
  B=0
  C=B
  D=(X2-X1)/Rei
  E=(Y2-Y1)/Rei
  For A=0 To Rei
    Draw X1+B,(Y1+C)*Dvs To X4+B,(Y1+C)*Dvs
    B=B+D
    C=C+E
  Next A
  B=0
  D=(X4-X1)/Spa
  For A=0 To Spa
    Draw X1+B,Y1*Dvs To X2+B,Y2*Dvs
    Draw X2+B,Y2*Dvs To X2+B,Y3*Dvs
    B=B+D
  Next A
  B=0
  C=B
  D=(Y4-Y1)/Az
  For A=0 To Az
    Draw X1,(Y1+B)*Dvs To X2,(Y2+B)*Dvs
    Draw X2,(Y2+B)*Dvs To X3,(Y2+B)*Dvs
    B=B+D
    St1=X3+6
    St2=Y2+A*D
    If Maks<1
      I%=100
    Else
      I%=10
    Endif
    O=(A*J/Az+Offs)/Fak
    If O<10
      O=Int(O*I%+0.5)/I%
    Else
      O=Int(O)
    Endif
    Te$=Str$(O)
    If A=Az And Fak<>1
      Te$=Te$+" *"+Str$(Fak)
    Endif
    Z=4-2*Test%
    Deftext 1,0,,Z
    Text St1,St2*Dvs-4*Test%,Te$
  Next A
  Draw X1,Y1*Dvs To X1,Y4*Dvs
  D=(X4-X1)/Spa
  E=(Y1-Y2)/Rei
  G=(X2-X1)/Rei
  O=D/4
  C=0
  H=C
  Color 2
  If Hilf$="  SÑulen          C-H"
    Gosub Saeulen
  Else
    Gosub Bloecke
  Endif
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Saeulen
  For Fm=Rei Downto 1
    B=0
    Ft=1
    Deffill 2,2,Fm*2-2
    Te$=N$(Fm)
    For A=1 To Spa
      A1=X2+B+1+O*0.5-H-3*G/4
      A2=A1+D-O
      B2=Y2+C+3*E/4
      B1=B2-W(Fm,A)*U
      Box A1,B1*Dvs,A2,B2*Dvs
      Pbox A1+1,(B1+1)*Dvs,A2-1,(B2-1)*Dvs
      Gosub Seiten
      Gosub Oben
      B=B+D
    Next A
    Z=6+2*Test%
    St1=A1+D+4
    St2=B2+4
    Deftext 1,0,,Z
    Text St1-25*Test%,St2*Dvs,N$(Fm)
    H=H+G
    C=C+E
  Next Fm
  For A=1 To Spa
    St1=X1+D*A-(D/4)*3.5
    St2=Y1+10+K
    Te$=Str$(A)
    Deftext 1,0,,6
    Text St1,St2*Dvs-4*Test%,Nsa$(A)
  Next A
Return
'
Procedure Seiten
  X(1)=A2
  X(2)=A2+2*G/4
  X(3)=A2+2*G/4
  X(0)=A2
  Y(1)=B2*Dvs
  Y(2)=(B2-2*E/4)*Dvs
  Y(3)=(B1-2*E/4)*Dvs
  Y(0)=B1*Dvs
  Polyfill 4,X(),Y()
Return
'
Procedure Oben
  X(1)=A1
  X(2)=A2
  X(3)=A2+2*G/4
  X(0)=A1+2*G/4
  Y(1)=B1*Dvs
  Y(2)=B1*Dvs
  Y(3)=(B1-2*E/4)*Dvs
  Y(0)=(B1-2*E/4)*Dvs
  Polyfill 4,X(),Y()
Return
'
Procedure Bloecke
  For Fm=Rei Downto 1
    Deffill 2,2,Fm*2-2
    Gosub Poly
    Te$=N$(Fm)
    St1=X2+Laenge+5-Fm1*G
    St2=Y2+E+Fm1*E
    Deftext 1,0,,6+2*Test%
    Text St1-25*Test%,St2*Dvs,N$(Fm)
  Next Fm
  For I=0 To Spa-1
    St1=X1+I*D-20
    St2=Y1+12
    Deftext 1,,,6
    Text St1,St2*Dvs-2*Test%,Nsa$(I+1)
  Next I
Return
'
Procedure Poly
  Spa1=Spa-1
  Fm1=Rei-Fm
  For S=0 To Spa1
    P=Spa1-S
    X(S)=X2+S*D-Fm1*G-G
    X(Spa+P)=X(S)+G
    Y(S)=(Y2-U*W(Fm,S+1)+4.2*E/4+Fm1*E)*Dvs
    Y(Spa+P)=Y(S)-E*Dvs
  Next S
  Polyfill Spa*2,X(),Y()
  For S=0 To Spa1
    Draw X(S),Y(S) To X(S)+G,Y(S)-E*Dvs
  Next S
  If Hilf$="  Blîcke          C-I"
    X(Spa)=X(Spa1)
    Y(Spa)=(Y2+E+Fm1*E)*Dvs
    X(Spa+1)=X2-Fm1*G-G
    Y(Spa+1)=Y(Spa)
    Polyfill Spa+2,X(),Y()
    X(1)=X(Spa1)
    Y(1)=Y(Spa1)
    X(2)=X(1)+G
    Y(2)=Y(1)-E*Dvs
    X(3)=X(2)
    Y(3)=(Y2+Fm1*E)*Dvs
    X(0)=X(1)
    Y(0)=(Y2+E+Fm1*E)*Dvs
    Polyfill 4,X(),Y()
  Endif
Return
'
'
Procedure Grafik.2d
  Gosub Raster.2d
  Color 2
  For R=1 To Rei
    X=40
    Y=350
    If Ba=1
      If R=1
        Gosub Balken
      Endif
    Else
      Y=Y-(W(R,1)*Wy)
      Plot X,Y*Dvs
      Defline 1,1,0,0
      If R=2
        Defline -&X101010101010101
      Endif
      If R=3
        Defline -&X110110110110110
      Endif
      If R=4
        Defline 6,1,0,0
      Endif
      For I=2 To Spa
        Ox=X
        Oy=Y+1
        X=X+Wx
        Y=350-(W(R,I)*Wy)
        Draw  To X,Y*Dvs
        Draw Ox,Oy*Dvs To X,Y*Dvs+1 To X,Y*Dvs
      Next I
      Line X+52,(125+R*20)*Dvs,X+80,(125+R*20)*Dvs
      Line X+52,(126+R*20)*Dvs,X+80,(126+R*20)*Dvs
      Deftext 1,0,,6
      Text X+84,(130+R*20)*Dvs,N$(R)
    Endif
  Next R
  Defline 1,1,0,0
  Gosub Text
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
'
Procedure Balken
  Wx=450/Spa
  Wwx=Wx/(Rei+1)
  For I=1 To Rei
    Deffill 1-Test%,2,9-I*2
    For P=1 To Spa
      Xx=40+(P-1)*Wx+(I-1)*Wwx
      Xx2=Xx+Wwx
      If Offs>=0
        Yy2=350-W(I,P)*Wy
        Pbox Xx,350*Dvs,Xx2,Yy2*Dvs
      Else
        Yy2=350+Offs*Wy-(W(I,P)+Offs)*Wy
        Pbox Xx,(350+Offs*Wy)*Dvs,Xx2,Yy2*Dvs
      Endif
    Next P
  Next I
  Gosub Muster.2d
Return
'
Procedure Text
  Deftext 1,0,,32+19*Test%
  Text 20,52*Dvs,Ueb1$
  Deftext 1,,,6
  Text 20,66*Dvs-4*Test%,Ueb2$
  For I=0 To Az
    Deftext 1,,,4-2*Test%
    O=(I*Awy+Offs)/Fak
    If Maks<1
      I%=100
    Else
      I%=10
    Endif
    If O<10
      O=Int(O*I%+0.5)/I%
    Else
      O=Int(O)
    Endif
    Te$=Str$(O)
    If I=Az And Fak<>1
      Te$=Te$+" *"+Str$(Fak)
    Endif
    Text 496,(350-(I*Aby))*Dvs-2*Test%,Te$
  Next I
  Xxx=500-(Ba=1)
  J=0
  For I=25 To Xxx Step Wx
    Ii=I+12
    If Ba=1
      Add Ii,4
    Endif
    Inc J
    Deftext 1,0,,6
    Text Ii,360*Dvs-4*Test%,Nsa$(J)
  Next I
Return
'
Procedure Um
  J=0
  For X=1 To Rei
    For B=1 To Spa
      J=Max(J,W(X,B))
    Next B
  Next X
  J=Max(J,Maks)
  J=Max(J,0.1)
  U=Ho/J
  Az=10
Return
'
'
Procedure Kuchen
  Gosub Ein.grafik
  Cls
  Var=1
  If Aufloesung%<>1
    Alert 2," |Beschriftung der Kuchenteile :",1,"keine|Zahlwert|% -wert",Var
  Endif
  For P=1 To Rei
    Su=0
    For I=1 To Spa
      Su=Su+W(P,I)+Offs
    Next I
    If Su<=0
      Su=1
    Endif
    W1=3600/Su
    We=0
    If Rei=1
      E1=240
      E2=215
      E3=135
      E4=115
    Endif
    If Rei=2
      E3=90
      E4=77
      E2=215
      If P=1
        E1=130
      Else
        E1=380
      Endif
    Endif
    If Rei=3
      E3=75
      E4=68
      E1=105
      E2=160
      If P=2
        E1=400
        E2=160
      Endif
      If P=3
        E1=250
        E2=285
      Endif
    Endif
    If Rei=4
      E3=65
      E4=59
      E1=95
      E2=144
      If P=2
        E1=320
      Endif
      If P=3
        E1=200
        E2=300
      Endif
      If P=4
        E1=430
        E2=300
      Endif
    Endif
    Ellipse E1,E2*Dvs,E3,E4*Dvs
    For I=1 To Spa
      Deftext 1,0,,4
      Wa=We
      We=We+(W(P,I)+Offs)*W1
      Deffill I>1,2,I
      Pellipse E1,E2*Dvs,E3,E4*Dvs,Wa,We
      Ellipse E1,E2*Dvs,E3,E4*Dvs,Wa,We
      If Var>1
        Wx=((Wa+We)/2)*Pi/1800
        W2=0
        If Wx>Pi/2
          If Wx<5.3
            W2=8
          Endif
        Endif
        Wk=W(P,I)+Offs
        If Var=3
          Wk=Int(1000*Wk/Su)/10
        Endif
        Text E1-4-W2+E3*(1.15-(Rei=4)/20)*Sin(Wx+0.5*Pi),(E2+E4*(1.15-(Rei=4)/20)*Cos(Wx+0.5*Pi))*Dvs,Wk
      Endif
    Next I
    Deftext 1,0,,6+2*Test%
    Su$="Summe="+Str$(Su)
    Text E1-E3/2,(E2+5*E4/4+18)*Dvs-2*Test%,Su$
    Deftext 1,1,,6+2*Test%
    Text E1-E3/2,(E2+5*E4/4+9)*Dvs,N$(P)
  Next P
  Spp=(12-Spa)*10+56
  Circle 550,(Spp+24)*Dvs-10*Test%,12
  For A=2 To Spa
    Deffill A>1,2,A
    Pcircle 550,(Spp+A*25)*Dvs-10*Test%,12
  Next A
  For A=1 To Spa
    Deftext 1,0,,6+2*Test%
    Text 570,(Spp+2+A*25)*Dvs-10*Test%,Nsa$(A)
  Next A
  Deftext 1,,,32+19*Test%
  Text 20,48*Dvs,Left$(Ueb1$,32)
  Deftext 1,,,6
  Text 20,60*Dvs-4*Test%,Left$(Ueb2$,64)
  Deftext 1,,,13+7*Test%
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Ein.grafik
  Grafik!=True
  For I%=1 To 75
    Menu I%,2
  Next I%
Return
'
Procedure Stapel
  Swap Maks,Maks2
  Gosub Raster.2d
  Wx=450/Spa
  For P=1 To Spa
    Xx=40+(P-1)*Wx
    Xx2=Xx+Wx*0.75
    Yy2=350-((W(0,P)-Offs)*Wy)
    Deffill 2,0
    Oy=350*Dvs
    Pbox Xx,Oy,Xx2,Yy2*Dvs
    For I=1 To Rei
      Clr W
      For K%=1 To I
        W=W+W(K%,P)+Offs
      Next K%
      Sub W,Offs
      Yy2=(350-(W*Wy))*Dvs
      If Oy>Yy2+2
        Deffill 2,2,9-I*2
        Pbox Xx,Yy2,Xx2,Oy
      Else
        Line Xx,Yy2,Xx2,Yy2
      Endif
      Oy=Yy2
    Next I
  Next P
  Ba=1
  Gosub Muster.2d
  Defline 1,1,0,0
  Gosub Text
  Swap Maks,Maks2
  Gosub Aufbau.screen(X%,Y%,Cx%,Cy%)
Return
'
Procedure Muster.2d
  Spp=(12-Spa)*10
  Deftext 1,0,,6
  For A=1 To Rei
    Deffill 2,2,9-A*2
    Pcircle 555,(120+A*25)*Dvs,12
    Text 571,(122+A*25)*Dvs,N$(A)
  Next A
Return
'
Procedure Raster.2d
  Gosub Ein.grafik
  Cls
  Gosub Um
  Aby=260/Az
  Abx=450/(Spa-1)
  Wx=450/(Spa-1)
  Spb=Spa-1
  Ba=0
  If Hilf$="  Balken          C-F"
    Ba=1
    Spb=Spa
    Abx=450/Spa
    Wx=Abx
  Endif
  Awy=J/Az
  Wy=260/J
  X=40
  Y=350
  Color 3
  Draw X,Y*Dvs To X+450,Y*Dvs
  For I=1 To Az
    Y=350-I*Aby
    Draw X,Y*Dvs To 490,Y*Dvs
  Next I
  Y=350
  For I=0 To Spb
    If Hilf$="  Linien          C-E" Or I=0 Or I=Spb Or Offs<0
      X=40+I*Abx
      Draw X,Y*Dvs To X,(Y-260)*Dvs
    Endif
  Next I
Return
