Left%=True
Right%=False
'
' 32*32 Sprite Designer
' by Robert Warnestam, 1988-04
' changes 1990
'
Gosub Init
Do
  Mouse X%,Y%,B%
  If B% Then
    If X%>192 Then
      X%=(X%-192) Div 16
      Div Y%,16
      Gosub On_control(X%,Y%,B%)
    Else
      Div X%,6
      Div Y%,6
      If X%<32 And Y%<32 Then
        Poke Mc_ed%,Mode%
        Poke Mc_ed%+1,X%
        Poke Mc_ed%+2,Y%
        Poke Mc_ed%+3,Col%
        If Mode%=1 Then
          If Lin% Then
            Lin%=False
            Call Mc_edit%
          Else
            Lin%=True
            Poke Mc_ed%,0
            Call Mc_edit%
            Poke Mc_ed%+4,X%
            Poke Mc_ed%+5,Y%
          Endif
          While Mousek=1
          Wend
        Else
          Call Mc_edit%
        Endif
      Endif
    Endif
  Endif
  If Cycle% Then
    Gosub Cycle
  Endif
  If Anim%
    Gosub Animate
  Endif
  Vsync
Loop
End
'
' **************************************************************************
' On Control Board
'
Procedure On_control(X%,Y%,B%)
  Hidem
  If X%>0 And X%<5 And Y%<4 Then
    If B%=1 Then
      Gosub Set_color(4*Y%+X%-1)
    Else
      Gosub Soft_color(4*Y%+X%-1)
    Endif
  Endif
  If X%=0 Then
    If Y%<3 Then
      Gosub Set_mode(Y%)
    Endif
    If Y%=6 Then
      Gosub Toggle_animation
    Endif
    If Y%=7 Then
      Gosub Toggle_anim_dir
    Endif
    If Y%=8 Then
      Gosub Rotate_left
    Endif
    If Y%=9 Then
      Gosub Choose_sprite
      Sprit%=Choose_sprite%
      Gosub Change_sprite(0)
    Endif
    If Y%=10 Then
      Gosub Clear
    Endif
    If Y%=11 Then
      Gosub Load_sprite
    Endif
  Endif
  If X%=1 Then
    If Y%=4 Then
      Gosub Change_anim_from(-1)
    Endif
    If Y%=5 Then
      Gosub Change_anim_to(-1)
    Endif
    If Y%=6 Then
      Gosub Change_anim_speed(-1)
    Endif
    If Y%=7 Then
      Gosub Swap_sprites
    Endif
    If Y%=8 Then
      Gosub Rotate_right
    Endif
    If Y%=9 Then
      Gosub Change_sprite(-1)
    Endif
    If Y%=11 Then
      Gosub Save_sprite
    Endif
  Endif
  If X%=2 Then
    If Y%=7 Then
      Gosub Copy_sprite_from
    Endif
    If Y%=8 Then
      Gosub Rotate_up
    Endif
  Endif
  If X%=3 Then
    If Y%=4 Then
      Gosub Change_anim_from(1)
    Endif
    If Y%=5 Then
      Gosub Change_anim_to(1)
    Endif
    If Y%=6 Then
      Gosub Change_anim_speed(1)
    Endif
    If Y%=7 Then
      Gosub Copy_sprite_to
    Endif
    If Y%=8 Then
      Gosub Rotate_down
    Endif
    If Y%=9 Then
      Gosub Change_sprite(1)
    Endif
    If Y%=11 Then
      Gosub Copyright
    Endif
  Endif
  If X%=4 Then
    If Y%=6 Then
      Gosub Toggle_cycle
    Endif
    If Y%=7 Then
      Gosub Toggle_cycle_dir
    Endif
    If Y%=8 Then
      Gosub Mirror
    Endif
    If Y%=11 Then
      Gosub Help
    Endif
  Endif
  If X%=5 Then
    If Y%=0 Then
      Gosub Change_rgb(-1,0,0)
    Endif
    If Y%=1 Then
      Gosub Change_rgb(0,-1,0)
    Endif
    If Y%=2 Then
      Gosub Change_rgb(0,0,-1)
    Endif
    If Y%=4 Then
      Gosub Change_cycle_from(-1)
    Endif
    If Y%=5 Then
      Gosub Change_cycle_to(-1)
    Endif
    If Y%=6 Then
      Gosub Change_cycle_speed(-1)
    Endif
    If Y%=7 Then
      Gosub Swap_color
    Endif
    If Y%=8 Then
      Gosub Up_down
    Endif
    If Y%=11 Then
      Gosub Test
    Endif
  Endif
  If X%=6 Then
    If Y%=7 Then
      Gosub Copy_color_from
    Endif
    If Y%=8 Then
      Gosub Turn270
    Endif
  Endif
  If X%=7 Then
    If Y%=0 Then
      Gosub Change_rgb(1,0,0)
    Endif
    If Y%=1 Then
      Gosub Change_rgb(0,1,0)
    Endif
    If Y%=2 Then
      Gosub Change_rgb(0,0,1)
    Endif
    If Y%=3 Then
      Gosub Palette
    Endif
    If Y%=4 Then
      Gosub Change_cycle_from(1)
    Endif
    If Y%=5 Then
      Gosub Change_cycle_to(1)
    Endif
    If Y%=6 Then
      Gosub Change_cycle_speed(1)
    Endif
    If Y%=7 Then
      Gosub Copy_color_to
    Endif
    If Y%=8 Then
      Gosub Turn90
    Endif
    If Y%=11 Then
      Gosub Quit
    Endif
  Endif
  Showm
  While Mousek=1
  Wend
Return
'
' **************************************************************************
' Copyright, Help, Test and Quit
'
Procedure Copyright
  Gosub Restore_palette
  While Mousek=1
  Wend
  Alert 0,"   SPRITE EDITOR       |by Robert Warnestam|       1988",1,"Amazing",Dummy%
Return
'
Procedure Help
  Gosub Restore_palette
  While Mousek=1
  Wend
  Alert 0,"         HELP             | There is a TXT file! ",1,"OK",Dummy%
Return
'
Procedure Test
  Local X%,Y%,Ox%,Oy%,P%
  While Mousek<>0
  Wend
  Alert 2," Test mode... ",0,"Sprite|Paint",P%
  Hidem
  Gosub Get_screen
  If P%=2 Then
    Gosub Cls
  Endif
  Ox%=0
  Oy%=0
  Gosub Show_sprite(Mousex,Mousey,Sprit%)
  While Mousek=0
    Mouse X%,Y%,Dummy%
    Vsync
    Gosub Get_next_anim
    If X%<>Ox% Or Y%<>Oy% Or Anim_changed% Then
      Ox%=X%
      Oy%=Y%
      If P%=1 Then
        Gosub Move_sprite(X%,Y%,Anim_nr%)
      Else
        Gosub Show_sprite(X%,Y%,Anim_nr%)
      Endif
    Endif
    If Cycle% Then
      Gosub Cycle
    Endif
  Wend
  Gosub Hide_sprite
  Gosub Restore_screen
  Showm
  While Mousek<>0
  Wend
Return
'
Procedure Quit
  Local A%
  Gosub Restore_palette
  While Mousek=1
  Wend
  Alert 2,"| Quit now? ",2,"Yes|No",A%
  If A%=1 Then
    End
  Endif
Return
'
' **************************************************************************
' Save & Load
'
Procedure Save_sprite
  Local Flag%,E%,I%,A1%,A2%,J%,A%,B%
  While Mousek<>0
  Wend
  Gosub Get_screen
  Gosub Cls
  Text 80,20,"Saving sprites..."
  Alert 1,"|    Save what?      ",1,"SPRITE|ANIM.",Flag%
  If Flag%=1 Then
    A1%=Sprit%
    A2%=Sprit%
  Else
    A1%=Anim_from%
    A2%=Anim_to%
    If A1%>A2% Then
      Swap A1%,A2%
    Endif
  Endif
  For I%=A1% To A2%
    Gosub Put_number(I%,0,I%)
  Next I%
  Gosub Get_filename
  If Exist% And Not Cancel% Then
    Alert 1," Sprite already exist! | overwrite file? ",1,"SAVE|CANCEL",E%
    If E%=2 Then
      Cancel%=True
    Endif
  Endif
  If Not Cancel% Then
    For I%=0 To 15
      Dpoke Buffer%+2*I%,Dpeek(Varptr(Palette$)+2*I%)
    Next I%
    Dpoke Buffer%+32,A1%
    Dpoke Buffer%+34,A2%
    A%=Image%+512*A1%
    B%=Buffer%+36
    For I%=A1% To A2%
      A%=Image%+512*I%
      For J%=0 To 511
        Poke B%,Peek(A%)
        Inc B%
        Inc A%
      Next J%
    Next I%
    Bsave File$,Buffer%,B%-Buffer%
  Endif
  Gosub Restore_screen
Return
'
Procedure Load_sprite
  Local A%,B%,I%,J%
  While Mousek<>0
  Wend
  Gosub Get_screen
  Gosub Cls
  Text 80,20,"Loading sprites..."
  Gosub Get_filename
  If Not Cancel% Then
    If Not Exist% Then
      Alert 1,"| File doesn't exist ",1,"CANCEL",Dymmu%
    Else
      Bload File$,Buffer%
      For I%=0 To 15
        Sdpoke &HFF8240+2*I%,Dpeek(Buffer%+2*I%)
      Next I%
      Gosub Get_palette
      Anim_from%=Dpeek(Buffer%+32)
      Anim_to%=Dpeek(Buffer%+34)
      If Anim_from%=Anim_to% Then
        Anim_from%=Sprit%
        Anim_to%=Sprit%
      Endif
      A%=Image%+512*Anim_from%
      B%=Buffer%+36
      For I%=Anim_from% To Anim_to%
        For J%=0 To 511
          Poke A%,Peek(B%)
          Inc A%
          Inc B%
        Next J%
      Next I%
    Endif
  Endif
  Gosub Restore_screen
  Gosub Change_anim_from(0)
  Gosub Change_anim_to(0)
  Gosub Change_rgb(0,0,0)
  Gosub Choose_sprite
  Sprit%=Choose_sprite%
  Gosub Change_sprite(0)
Return
'
Procedure Get_filename
  ' out: file$, cancel%,exist%
  Showm
  Fileselect "\*.SPR","",File$
  If Right$(File$,1)="\" Then
    File$=""
  Endif
  Cancel%=(File$="")
  Exist%=Exist(File$)
Return
'
' **************************************************************************
' Sprite routines
'
Procedure Change_sprite(S%)
  Add Sprit%,S%
  If Sprit%<0 Then
    Sprit%=0
  Endif
  If Sprit%>15 Then
    Sprit%=15
  Endif
  Gosub Put_number(14,9,Sprit%)
  Poke Mc_ed%+6,Sprit%
  Poke Mc_ed%,19
  Call Mc_edit%
Return
'
Procedure Swap_sprites
  Poke Mc_ed%,17
  Poke Mc_ed%+7,Anim_from%
  Call Mc_edit%
  Gosub Change_sprite(0)
  Gosub Put_anim(Anim_from%)
Return
'
Procedure Copy_sprite_from
  Poke Mc_ed%,18
  Poke Mc_ed%+6,Anim_from%
  Poke Mc_ed%+7,Sprit%
  Call Mc_edit%
  Poke Mc_ed%+6,Sprit%
  Gosub Change_sprite(0)
Return
'
Procedure Copy_sprite_to
  Poke Mc_ed%,18
  Poke Mc_ed%+7,Anim_from%
  Call Mc_edit%
  Gosub Put_anim(Anim_from%)
Return
'
Procedure Choose_sprite
  Local I%,X%,Y%
  Showm
  Gosub Get_screen
  Gosub Cls
  While Mousek<>0
  Wend
  For I%=0 To 15
    X%=2*(I% Mod 8)
    Y%=1+3*(I% Div 8)
    Gosub Put_number(X%,Y%,I%)
    X%=31+32*(I% Mod 8)
    Y%=63+48*(I% Div 8)
    Gosub Show_sprite(X%,Y%,I%)
  Next I%
  I%=-1
  While Mousek=0 Or I%=-1
    X%=Mousex Div 16
    Y%=Mousey Div 16
    If X% Mod 2=0 And X%<15 And (Y%=1 Or Y%=4) Then
      I%=X% Div 2-8*(Y%=4)
    Else
      I%=-1
    Endif
    If Cycle% Then
      Gosub Cycle
      Vsync
    Endif
  Wend
  Gosub Restore_screen
  While Mousek<>0
  Wend
  Choose_sprite%=I%
Return
'
' **************************************************************************
' Mode changing:  0=Plot, 1=Line, 2=Fill
'
Procedure Set_mode(M%)
  Local I%
  Mode%=M%
  For I%=0 To 2
    Gosub Put_image(12,I%,54+I%-7*(M%<>I%))
    Lin%=False
  Next I%
Return
'
' **************************************************************************
' Color and RGB changing
'
Procedure Set_color(C%)
  Local I%,R%,G%,B%
  Gosub Restore_palette
  Col%=C%
  I%=Dpeek(&HFF8240+2*C%)
  R%=(I% Div 256) And 7
  G%=(I% Div 16) And 7
  B%=I% And 7
  Gosub Put_number(17,3,C%)
  Gosub Put_color(18,3,C%)
  Gosub Put_number(18,0,R%)
  Gosub Put_number(18,1,G%)
  Gosub Put_number(18,2,B%)
Return
'
Procedure Soft_color(C2%)
  Local C1%,Dc%,I%,J%,R2%,G2%,B2%,R,G,B,Dr,Dg,Db
  Gosub Restore_palette
  C1%=Col%
  If C1%>C2% Then
    Swap C1%,C2%
  Endif
  Dc%=C2%-C1%
  If Dc%<>0 Then
    I%=Dpeek(&HFF8240+2*C1%)
    R=(I% Div 256) And 7
    G=(I% Div 16) And 7
    B=I% And 7
    I%=Dpeek(&HFF8240+2*C2%)
    R2%=(I% Div 256) And 7
    G2%=(I% Div 16) And 7
    B2%=I% And 7
    Dr=(R2%-R)/Dc%
    Dg=(G2%-G)/Dc%
    Db=(B2%-B)/Dc%
    For I%=C1% To C2%
      J%=256*Trunc(R)+16*Trunc(G)+Trunc(B)
      Sdpoke &HFF8240+2*I%,J%
      Add R,Dr
      Add G,Dg
      Add B,Db
    Next I%
    Gosub Get_palette
  Endif
Return
'
Procedure Change_rgb(Rs%,Gs%,Bs%)
  Local I%,R%,G%,B%
  Gosub Restore_palette
  I%=Dpeek(&HFF8240+2*Col%)
  R%=(I% Div 256) And 7
  G%=(I% Div 16) And 7
  B%=I% And 7
  Add R%,Rs%
  Add G%,Gs%
  Add B%,Bs%
  If R%<0 Then
    R%=0
  Endif
  If G%<0 Then
    G%=0
  Endif
  If B%<0 Then
    B%=0
  Endif
  If R%>7 Then
    R%=7
  Endif
  If G%>7 Then
    G%=7
  Endif
  If B%>7 Then
    B%=7
  Endif
  I%=256*R%+16*G%+B%
  Sdpoke &HFF8240+2*Col%,I%
  Gosub Put_number(18,0,R%)
  Gosub Put_number(18,1,G%)
  Gosub Put_number(18,2,B%)
  Gosub Get_palette
Return
'
Procedure Swap_color
  Local Temp%
  Gosub Restore_palette
  Temp%=Dpeek(&HFF8240+2*Cycle_from%)
  Sdpoke &HFF8240+2*Cycle_from%,Dpeek(&HFF8240+2*Cycle_to%)
  Sdpoke &HFF8240+2*Cycle_to%,Temp%
  Gosub Get_palette
Return
'
Procedure Copy_color_from
  Gosub Restore_palette
  Sdpoke &HFF8240+2*Cycle_from%,Dpeek(&HFF8240+2*Cycle_to%)
  Gosub Get_palette
Return
'
Procedure Copy_color_to
  Gosub Restore_palette
  Sdpoke &HFF8240+2*Cycle_to%,Dpeek(&HFF8240+2*Cycle_from%)
  Gosub Get_palette
Return
'
Procedure Get_palette
  Local I%
  For I%=0 To 15
    Dpoke Varptr(Palette$)+2*I%,Dpeek(&HFF8240+2*I%)
  Next I%
Return
'
Procedure Restore_palette
  ' xbios 6 makes the program 'hang up'
  Local I%
  For I%=0 To 15
    Sdpoke &HFF8240+2*I%,Dpeek(Varptr(Palette$)+2*I%)
  Next I%
Return
'
' **************************************************************************
' Macro Palette
Procedure Palette
  Local