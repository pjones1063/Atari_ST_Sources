' ------------------------------------
'               BALL 
' ------------------------------------
'
'     a nintendo LCD game simulator
'            for atari ST
'
'           florent coste 
'             mars 2002
'
'        (c) atari.games 2002
'      http://atari.games.free.fr
'
' ------------------------------------
'
HIDEM
CLS
'
' ------- tables initialisation ------
'
DIM x(50), y(50), x1(50), x2(50), y1(50), y2(50), sp$(50), spb$(50)
'
DIM b_max(3), b_index(3)
DIM b_line(3), b_dir(3), b_state(3), old_b_state(3), b_timer(3), b_taken(3), snd_timer(3)
'
DIM led$(10)
DIM thefont$(46)
'
DIM blue_palette&(16)
DIM clr_palette&(16)
DIM orig_palette(16)
DIM palette&(16)
DIM r%(16),g%(16),b%(16)
'
DIM scores(10)
DIM snames$(10)
'
@save_palette
@to_blue
'
RESERVE 35000
'
LET screen%=MALLOC(32000)
IF screen%=0
  EDIT
ENDIF
'
' --------- sprites indices ----------
'
b1_index=0
b2_index=8
b3_index=18
'
' bras et jambes gauches et droites
'
bg_index=30
bd_index=33
pg_index=36
pd_index=39
'
c1_index=42
c2_index=43
'
lb_index=44
rb_index=45
'
nb_sprites=45
'
b_max(0)=7
b_max(1)=9
b_max(2)=11
'
b_index(0)=0
b_index(1)=8
b_index(2)=18
'
leftkey$="w"
rightkey$="x"
pauskey$="p"
'
' ------- game initialisation  --------
'
ecran%=XBIOS(2)
'
COLOR 16
DEFFILL 16,1,8
RANDOMIZE VAL(MID$(TIME$,7,8))
'
@load_scores
@load_gfx
@load_sprites
@load_config
'
' ---------------------------
' --                       --
' --       main loop       --
' --                       --
' ---------------------------
'
pres_timer=0
game_paused=0
playmode=-1
page=-1
'
DO
'
   if playmode=-1
     DEC pres_timer
'
     if pres_timer<=0
       INC page
       if page=5
         page=0
       ENDIF
       ON page+1 GOSUB pres1,pres2,pres3,pres4,halloffame
       pres_timer=page_timer
     ENDIF
   ENDIF
'
   key=ASC(INKEY$)
   EXIT IF key==27
     
   IF key<>0
     IF playmode=-1
       @game_help
       playmode=1
     ELSE IF key=65 OR key=97
       playmode=0
       @play_game
       page=3
       pres_timer=0
       playmode=-1
     ELSE IF key=66 OR key=98
       playmode=1
       @play_game
       page=3
       pres_timer=0
       playmode=-1
     ENDIF
   ENDIF
'
LOOP
'
@prg_end
'
'
' ---------------------------
' --                       --
' --     program end       --
' --                       --
' ---------------------------
PROCEDURE prg_end
  SHOWM
  CLS
  @restore_palette
  ~MFREE(screen%)
  RESERVE
  EDIT
RETURN
'
' ---------------------------
' --                       --
' --       play loop       --
' --                       --
' ---------------------------
'
PROCEDURE play_game
'
  @to_blue
  BMOVE screen%, ecran%,32000
  @set_clr_palette
  @fade_to_color(2,3,4)
'
' default config
'
  balls_timer=1000
'
  @load_config
'
  nb_balls        = playmode+1
  game_paused     = 0
  score%          = -1
  end             = 0
'
  ju_state = -1
  ju_disp  = 0
'
  FOR i=0 TO 2
    b_state(i) = -1
    b_dir(i)= 0
    b_taken(i)=0
  NEXT i
'
  lb_state        = -1
  rb_state        = -1
'
  b_line(0)=RANDOM(3)
  b_dir(0)=1
  b_state(0)=0
  b_timer(0)=balls_timer
'
  if nb_balls>0
    b_line(1)=(b_line(0)+2)MOD3
    b_dir(1)=-1
    b_state(1)=b_max(b_line(1))
   b_timer(1)=balls_timer
  ENDIF
'
  if nb_balls=2
    b_line(2)=3-b_line(0)-b_line(1)
    b_dir(2)=1
    b_state(2)=0
    b_timer(2)=balls_timer
  ENDIF
'
  @score
'
  snd_timer(0)=0
  snd_timer(1)=0
  snd_timer(2)=0
  
  DO
'
    old_ju_state   = ju_state
    old_b_state(0) = b_state(0)
    old_b_state(1) = b_state(1)
    old_b_state(2) = b_state(2)
    old_lb_state   = lb_state
    old_rb_state   = rb_state
'
    @test_keyboard
'
    IF game_paused=0
'
      @play_juggler    
'
      FOR i=0 TO nb_balls
        IF snd_timer(i)>0
          DEC snd_timer(i)
          IF snd_timer(i)=0
            SOUND i+1,0,0,0
          ENDIF
        ENDIF
        @play_ball(i)
      NEXT i
'
' dessine les bras gauche et droit
'
      @put_sprite(bg_index,2-ju_state,2-old_ju_state)
      @put_sprite(bd_index,ju_state,old_ju_state)
'
' dessine les jambes gauche et droite
'
      @put_sprite(pg_index, ju_state, old_ju_state)
      @put_sprite(pd_index, ju_state, old_ju_state)
'
' dessine les balles
'
      FOR i=0 TO nb_balls
        @put_sprite(b_index(b_line(i)),b_state(i),old_b_state(i))
      NEXT i
'
' dessine les boutons
'
      put_sprite(lb_index,lb_state,old_lb_state)
      put_sprite(rb_index,rb_state,old_rb_state)
'
' collisions
'
      FOR i=0 TO nb_balls
        IF b_taken(i)=0
          IF (b_state(i)=0 AND b_dir(i)<0 AND b_line(i)=2-ju_state) OR (b_state(i)=b_max(b_line(i)) AND b_dir(i)>0 AND b_line(i)=ju_state)
            SOUND 1,0,0,0
            SOUND 1,15,11,7
            snd_timer(1)=5
            @score
            b_taken(i) = 1
            b_timer(i) = MIN(50,b_timer(i))
          ENDIF
        ENDIF
      NEXT i
    ENDIF
'
    EXIT IF end=1
  LOOP
'
  @end_of_game
'
RETURN
'
' ---------------------------
' --                       --
' --   game intelligence   --
' --                       --
' ---------------------------
'
' ----- choose new dir ------
'
PROCEDURE choose_new_dir( which )
'
  ok = 1
'
  newline = RANDOM(3)
  IF b_line(which)=0 AND newline=2
    newline=1
  ENDIF
  IF b_line(which)=2 AND newline=0
    newline=1
  ENDIF
  first_newline = newline
'
  DO
     FOR ii=0 TO nb_balls
        IF which <> ii
           IF b_line(ii) = newline
             IF b_dir(ii) <> b_dir(which)
               ok=0
             ELSE
               IF b_dir(ii)>0 AND b_state(ii)<2
                 ok=0
               ELSE IF b_dir(ii)<0 AND b_state(ii)>(b_max(newline)-2)
                 ok=0
               ENDIF
             ENDIF
           ENDIF
        ENDIF
     NEXT ii
     EXIT IF ok=1
     newline = (newline+1) MOD 3
     IF b_line(which)=0 AND newline=2
       newline=1
     ENDIF
     IF b_line(which)=2 AND newline=0
       newline=1
     ENDIF
     exit if first_newline = newline
     ok = 1
  LOOP
'
  @put_sprite(b_index(b_line(which)),-1,b_state(which))

  b_line(which)=newline
  old_b_state(which)=-1
  IF b_dir(which)<0
    b_state(which)=b_max(newline)-1
  ELSE
    b_state(which)=1
  ENDIF
'
  @put_sprite(b_index(b_line(which)),b_state(which),-1)
'
RETURN
'
' ----- juggler ----------
'
PROCEDURE play_juggler
'
  IF ju_state<0
    ju_state = 1
  ELSE if ju_disp != 0
    ADD ju_state, ju_disp
    IF ju_state<0
      ju_state=0
    ELSE IF ju_state>2
      ju_state=2
    ENDIF
  ENDIF
'  
RETURN
'
' ------ miss a ball --------
'
> PROCEDURE miss_it(which)
'
  IF b_state(which) < 0
    PUT 106,106,sp$(42)
  ELSE
    PUT 184,106,sp$(43)
  ENDIF      
'
  @put_sprite(b_index(b_line(which)),-1,old_b_state(which))
  @put_sprite(bg_index,2-ju_state,-1)
  @put_sprite(bd_index,ju_state,-1)

  FOR i=1 To 15 STEP 2
    SOUND 1,15,i,2
    PAUSE 1
    SOUND 2,15,15-i,3
    PAUSE 1
  NEXT i
    
  PAUSE 50
  end=1
  SOUND 1,0
  SOUND 2,0
  SOUND 3,0
'
RETURN
'
'
' ----- play balls ----------
'
> PROCEDURE play_ball(which)
'
  IF b_state(which) >= 0
    DEC b_timer(which)
    IF b_timer(which) = 0
      IF b_taken(which)=1
        b_dir(which)= -b_dir(which)
        @choose_new_dir(which)
        b_taken(which)=0
        b_timer(which)=balls_timer
      ELSE
        b_timer(which)=balls_timer
        ADD b_state(which), b_dir(which)
        SOUND which+1,15,7,6
        snd_timer(which)=5
        IF b_state(which)<0 OR b_state(which)>b_max(b_line(which))
          miss_it(which)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
'  
RETURN

'
' ---------------------------
' --                       --
' --  keyboard & joystick  --
' --                       --
' ---------------------------
'
' --------- keyboard ----------
'
> PROCEDURE test_keyboard
'
  ju_disp = 0
'
  old_joy = joy
  old_but = but
'
  joy=Peek(3593)
  but=MOUSEK
'  
' joystick
'
  IF old_joy<>joy
    IF joy=4                      ! LEFT JOYSTICK
      ju_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF joy=8                 ! RIGHT JOYSTICK
      ju_disp=1
      rb_state=0
      rb_timer=40
    ENDIF
'  
' mouse buts
'
  ELSE IF but<>old_but
    IF but=1                       ! LEFT MOUSE BUTTON
      ju_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF but=2                 ! RIGHT MOUSE BUTTON
      ju_disp=1
      rb_state=0
      rb_timer=40
    ENDIF
'  
' keyboard
'
  ELSE
    k$=INKEY$
'
    IF LEN(k$)==2
      IF CVI(k$)=59               ! F1 KEY
        end=1
        score%=0
      ELSE IF CVI(k$)=75          ! LEFT ARROW
        ju_disp=-1
        lb_state=0
        lb_timer=40
      ELSE IF CVI(k$)=77          ! RIGHT ARROW
        ju_disp=1
        rb_state=0
        rb_timer=40
      ENDIF
    ELSE IF ASC(k$)=27            ! ESC KEY
      end=1
      score%=0
    ELSE IF k$=rightkey$          ! RIGHT KEY
      ju_disp=1
      rb_state=0
      rb_timer=40
    ELSE IF k$=leftkey$           ! LEFT KEY
      ju_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF k$=pauskey$ OR ASC(k$)=32       ! PAUSE OR SPACE KEY
      IF playmode=0 OR playmode=1
        game_paused=1-game_paused
        @set_clr_palette
        if game_paused=1
           FOR i=0 TO 16
           SETCOLOR i,((palette&(i)/256) AND 7)/2,((palette&(i)/16) AND 7)/2,(palette&(i) AND 7)/2
           NEXT i
        ELSE
           VOID XBIOS(6, L:VARPTR(palette&(0)))
        ENDIF
      ENDIF
    ENDIF
  ENDIF
'
' buttons state & drawing
'
  IF lb_timer>0
    DEC lb_timer
    IF lb_timer=0
      lb_state=-1
    ENDIF
  ENDIF

  IF rb_timer>0
    DEC rb_timer
    IF rb_timer=0
      rb_state=-1
    ENDIF
  ENDIF
RETURN
'
' ---------------------------
' --                       --
' --   drawing & loading   --
' --     graphic data      --
' --                       --
' ---------------------------
'
' ------------------------------
' ---- draw player score -------
' ------------------------------
'
> PROCEDURE score
  INC score%
  a%=score%/1000
  b%=(score%-(a%*1000))/100
  c%=(score%-(a%*1000)-(b%*100))/10
  d%=(score%-(a%*1000)-(b%*100)-(c%*10))
'
  IF a%>0
    PUT 184,53,led$(a%)
  ENDIF
  IF b%>0
    PUT 195,53,led$(b%)
  ENDIF
  IF c%>0
    PUT 209,53,led$(c%)
  ENDIF
  PUT 219,53,led$(d%)
'  
  IF score% > 0
    balls_timer = MAX( balls_timer_min, INT(balls_timer*29/30) )
  ENDIF
'
RETURN
'
' ------------------------------
' ---- sprite drawing func -----
' ------------------------------
'
PROCEDURE put_sprite(index,state,old)
  IF old <> state
    IF old>-1
      PUT x1(old+index), y1(old+index), spb$(old+index)
    ENDIF
    IF state>-1
      PUT x1(state+index), y1(state+index), sp$(state+index)
    ENDIF
  ENDIF
RETURN
'
' ------------------------------------------
' palette save and restore
' ------------------------------------------
'
PROCEDURE save_palette
  LOCAL i
  FOR i=0 TO 15
    orig_palette(i)=XBIOS(7,W:i,W:-1)
  NEXT i
RETURN
'
PROCEDURE restore_palette
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i,orig_palette(i)
  NEXT i
RETURN
'
' -------------------------
' ----- fade to colors ----
' -------------------------
'
> PROCEDURE fade_to_color(rr,gg,bb)
  LOCAL i, j, ii
'
  FOR i=0 TO 15
    v% = palette&(i)
    r%(i) = (v%/256) AND 7
    g%(i) = (v%/16) AND 7
    b%(i) = v% AND 7
  NEXT i
'
  ii=7
  FOR i=0 TO 7
    VSYNC
    FOR j=0 TO 15
       SETCOLOR j, (r%(j)*i+rr*ii)/7, (g%(j)*i+gg*ii)/7, (b%(j)*i+bb*ii)/7
    NEXT j
    DEC ii
'    PAUSE 1
  NEXT i

RETURN

> PROCEDURE set_blue_palette
  BMOVE VARPTR(blue_palette&(0)), VARPTR(palette&(0)), 32
RETURN
'
> PROCEDURE set_clr_palette
  BMOVE VARPTR(clr_palette&(0)), VARPTR(palette&(0)), 32
RETURN
'
' -------------------------
' ----- fade to black -----
' -------------------------
'
> PROCEDURE fade_to_black(rr,gg,bb)
  LOCAL i, j, ii
'
  FOR i=0 TO 15
    v% = palette&(i)
    r%(i) = (v%/256) AND 7
    g%(i) = (v%/16) AND 7
    b%(i) = v% AND 7
  NEXT i
'
  ii = 7
  FOR i=0 TO 7
    VSYNC
    FOR j=0 TO 15
      SETCOLOR j, (r%(j)*ii+rr*i)/7, (g%(j)*ii+gg*i)/7, (b%(j)*ii+bb*i)/7
    NEXT j
    DEC ii
'   PAUSE 1
  NEXT i
'
RETURN
'
' -----------------------------
' -- set all colors to black --
' -----------------------------
'
> PROCEDURE to_black
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i, 0,0,0
  NEXT i
RETURN
'
> PROCEDURE to_blue
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i, 2,3,4
  NEXT i
RETURN
'
' ----------------------------
' -- text drawing procedure --
' ----------------------------
'
> PROCEDURE draw_text(text$,xx,yy)
   l=LEN(text$)
   if xx<40 AND yy<25
     xx = xx*8
     yy = yy*8
   ENDIF
   if xx<0
      xx=(320-l*8)/2
   ENDIF
   FOR ii=0 TO l
      v=ASC( MID$(text$,ii+1,1) )
      SELECT v
      CASE 65 TO 90
         PUT xx+ii*8,yy,thefont$(v-65)
      CASE 97 TO 122
         PUT xx+ii*8,yy,thefont$(v-97)
      CASE 48 TO 57
         PUT xx+ii*8,yy,thefont$(v-20)
      CASE 38
         PUT xx+ii*8,yy,thefont$(45)
      CASE 39
         PUT xx+ii*8,yy,thefont$(44)
      CASE 46
         PUT xx+ii*8,yy,thefont$(26)
      CASE 45
         PUT xx+ii*8,yy,thefont$(27)
      CASE 95
         PUT xx+ii*8,yy,thefont$(27)
      CASE 64
         PUT xx+ii*8,yy,thefont$(39)
      CASE 58
         PUT xx+ii*8,yy,thefont$(40)
      CASE 92
         PUT xx+ii*8,yy,thefont$(41)
      CASE 60
         PUT xx+ii*8,yy,thefont$(42)
      CASE 62
         PUT xx+ii*8,yy,thefont$(43)
      DEFAULT
         PUT xx+ii*8,yy,thefont$(38)
      ENDSELECT
   NEXT ii
RETURN
'
' ---------------------------
' --                       --
' --  menu & intermediate  --
' --        screens        --
' --                       --
' ---------------------------
'
' -----------------------
' ---- premier titre ----
' -----------------------
'
> PROCEDURE pres1
  @fade_to_black(2,3,4)
  CLS
  @set_blue_palette
'
   PUT 120,50,logo$
   PUT 110,135,logo2$
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- deuxieme  titre ---
' -----------------------
'
> PROCEDURE pres2
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("-----------------", -1,3)
  @draw_text("LCD.revival.games", -1,4)
  @draw_text("-----------------", -1,5)
  PUT 131,70, title$
  @draw_text("------",-1,19)
  @draw_text("ball",-1,20)
  @draw_text("------",-1,21)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- troisieme titre ---
' -----------------------
'
> PROCEDURE pres3
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("by florent.coste", -1,12)
  @draw_text("@ - atari.games - 2002", -1,18)
  @draw_text("http:\\atari.games.free.fr",-1,21)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- quatrieme titre ---
' -----------------------
'
> PROCEDURE pres4
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("Original" , -1,4)
  @draw_text("'Game & Watch'", -1,6)
  @draw_text("by", -1, 8)
  PUT 107,90, nintendo$
  @draw_text("@ - nintendo - 1980",-1, 19)
'
  @fade_to_color(2,3,4)
RETURN
'
' --------------------
' -- hall of fame ----
' --------------------
'
> PROCEDURE halloffame
'
  @fade_to_black(2,3,4)
  CLS
'  
  @draw_text("------------",-1,3)
  @draw_text("HALL OF FAME",-1,4)
  @draw_text("------------",-1,5)
'
  FOR i=0 TO 9
    @draw_text( snames$(i), 14,9+i )
    @draw_text( ".....",   18,9+i )
    @draw_text( STR$(scores(i)), 24,9+i )
  NEXT i
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' - get the player name -
' -----------------------
'
PROCEDURE end_of_game
'
  fade_to_black(0,0,0)
  CLS
  @set_blue_palette
'
  PUT 131, 20, title$
  @draw_text("---------",-1,13)
  @draw_text("game over",-1,14)
  @draw_text("---------",-1,15)
  fade_to_color(0,0,0)
'  
  nbl=0
  place=-1
'
  FOR i=0 TO 9
     IF score% >= scores(i)
       place=i
     ENDIF
     EXIT IF place>=0
  NEXT i
'
  IF place >= 0
    FOR j=9 TO place+1 STEP -1
      scores(j) = scores(j-1)
      snames$(j) = snames$(j-1)
    NEXT j
'
' clears keyboard buffer
'
    DO
    LOOP UNTIL INKEY$=""
'
    nam$=">>> ... <<<"
    @draw_text( "enter your name",-1,18)
    @draw_text( nam$,-1,20)
    DO
      d$=INPUT$(1)
      a%=ASC(d$)
      IF a%>0
        IF a%==13 OR a%=27 
          nbl=3
        ELSE IF a%==8 AND nbl>0
          MID$(nam$,nbl+4)="."
          DEC nbl
        ELSE IF d$<>","
          INC nbl
          MID$(nam$,nbl+4)=d$
        ENDIF
        @draw_text( nam$,-1,20)
      ENDIF
      EXIT IF nbl=3
    LOOP
'
    snames$(place) = MID$(nam$,5,3)
    scores(place) = score%
    @save_scores
  ENDIF
'
  score%=0
  PAUSE 25
'
RETURN
'
'
' -----------------------
' ---- game help page ---
' -----------------------
'
PROCEDURE game_help
  @fade_to_black(2,3,4)
  CLS
'  
  PUT 131, 20, title$

  @draw_text("keybd ...... <" + leftkey$ + "> <" + rightkey$ +">          ",-1,13)
  @draw_text("      ...... <ARROWS> <ESC>   ",-1,14)
  @draw_text("mouse ...... left & right buts", -1, 16 )
  @draw_text("joystick ... left & right     ", -1, 17 )
  @draw_text("press -A- for easy mode",-1,21)
  @draw_text("press -B- for hard mode",-1,22)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------------------------------
' ----- load image & sprites --------------------
' -----------------------------------------------
'
> PROCEDURE load_sprites
'
  BLOAD "A:\BALL\A_G.PAL", VARPTR(blue_palette&(0))
  BLOAD "A:\BALL\GAME.PAL", VARPTR(clr_palette&(0))
'
  BLOAD "A:\BALL\A_G.DAT", ecran%
'
  FOR i=0 TO 38
    GET i*8, 0, 7+(i*8), 7, thefont$(i)
  NEXT i
  FOR i=0 TO 6
    GET i*8, 8, 7+(i*8), 15, thefont$(i+39)
  NEXT i
'
  GET 0,17,60,77,    title$
  GET 123,33,229,60, nintendo$
  GET 230,10,311,87, logo$
  GET 132,10,229,33, logo2$
  CLS
'
RETURN
'
' -----------------------------------------------
' ----- load image & sprites --------------------
' -----------------------------------------------
'
> PROCEDURE load_gfx
'
  RESTORE piece
  FOR i=0 TO nb_sprites
     READ x1(i), y1(i), x2(i), y2(i), x(i), y(i)
  NEXT i
'
  BLOAD "A:\BALL\SPRITES.DAT", ecran%
'
  FOR i=0 TO nb_sprites
    IF x(i)>-1 AND y(i)>-1
       GET x(i), y(i), x(i)+x2(i)-x1(i), y(i)+y2(i)-y1(i), sp$(i)
    ENDIF
  NEXT i
'
  FOR i=0 TO 9
    GET i*8, 15, 7+(i*8), 28, led$(i)
  NEXT i 
'
  BLOAD "A:\BALL\GAME.DAT", ecran%
      
  FOR i=0 TO nb_sprites
    IF x(i)>-1 AND y(i)>-1
       GET x1(i), y1(i), x2(i), y2(i), spb$(i)
    ELSE
       GET  x1(i), y1(i), x2(i), y2(i), sp$(i)
       PBOX x1(i), y1(i), x2(i), y2(i)
       GET  x1(i), y1(i), x2(i), y2(i), spb$(i)
    ENDIF
  NEXT i
  sp$(41)=sp$(40)
  sp$(37)=sp$(36)
'
  BMOVE ecran%,screen%,32000
'
RETURN
'
piece:
'
' ball 1
'
DATA 140,95,147,101,-1,-1
DATA 140,85,148,93,-1,-1
DATA 144,76,151,84,-1,-1
DATA 152,70,159,78,-1,-1
DATA 162,70,170,78,-1,-1
DATA 171,76,179,84,-1,-1
DATA 174,84,182,93,-1,-1
DATA 175,95,182,101,-1,-1
'
' ball 2
'
DATA 130,94,138,101,-1,-1
DATA 130,84,138,91,-1,-1
DATA 134,73,140,81,-1,-1
DATA 140,65,149,72,-1,-1
DATA 151,60,159,67,-1,-1
DATA 163,60,171,67,-1,-1
DATA 174,63,182,72,-1,-1
DATA 180,73,188,81,-1,-1
DATA 182,84,191,91,-1,-1
DATA 185,95,192,101,-1,-1
'
' ball 3
'
DATA 121,95,129,101,-1,-1
DATA 122,83,129,90,-1,-1
DATA 125,72,132,79,-1,-1
DATA 131,60,138,68,-1,-1
DATA 140,54,147,61,-1,-1
DATA 151,50,158,55,-1,-1
DATA 163,49,170,55,-1,-1
DATA 175,55,181,60,-1,-1
DATA 185,60,191,68,-1,-1
DATA 191,70,198,78,-1,-1
DATA 193,83,200,89,-1,-1
DATA 194,95,200,101,-1,-1
'
' bras gauche
'
DATA 122,102,146,115,0,0
DATA 122,102,146,115,26,0
DATA 122,102,146,115,52,0
'
' bras droit
'
DATA 176,102,201,115,78,0
DATA 176,102,201,115,105,0
DATA 176,102,201,115,132,0
'
' jambe gauche
'
DATA 150,123,159,130,-1,-1
DATA 150,123,159,130,-1,-1
DATA 144,117,152,123,-1,-1
'
' jambe droite
'
DATA 171,118,180,123,-1,-1
DATA 164,123,173,130,-1,-1
DATA 164,123,173,130,-1,-1
'
' crash gauche et droit
'
DATA 100,101,139,129,159,0
DATA 186,109,222,129,201,0
'
' boutons gauche et droit
'
DATA 16, 131,52,163, 81,15
DATA 267,129,303,161,81,15
'
DATA -1,-1,-1,-1,-1,-1
'
DATA END

'
' ------------------------------------------
' ----- load scores table  -----------------
' ------------------------------------------
'
PROCEDURE load_scores
'
  OPEN "i",#1,"A:\BALL\SCORES.DAT"
'   
  FOR i=0 TO 9
    INPUT #1,snames$(i),scores(i)
  NEXT i
'
  CLOSE #1
'
RETURN
'
' ------------------------------------------
' ----- save scores table  -----------------
' ------------------------------------------
'
PROCEDURE save_scores
'
  OPEN "o",#1,"A:\BALL\SCORES.DAT"
'
  FOR i=0 TO 9
     WRITE #1,snames$(i),scores(i)
  NEXT i
'
  CLOSE #1
'
RETURN
'
' ------------------------------------------
' ----- load keys --------------------------
' ------------------------------------------
'
PROCEDURE load_config
'
  OPEN "i",#1,"A:\BALL\CONFIG.DAT"
'
  DO
    INPUT #1,keyword$,val$
    EXIT IF keyword$="---"
    IF keyword$="left_key"
      leftkey$=val$
    ELSE IF keyword$="right_key"
      rightkey$=val$
    ELSE IF keyword$="pause_key"
      pauskey$=val$
    ELSE IF keyword$="balls_timer"
      balls_timer=VAL(val$)
    ELSE IF keyword$="balls_timer_min"
      balls_timer_min=VAL(val$)
    ELSE IF keyword$="page_timer"
      page_timer=VAL(val$)
    ENDIF
  LOOP
'
  CLOSE #1
'
RETURN
'
'