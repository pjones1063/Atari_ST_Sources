' ------------------------------------
'              PARACHUTE 
' ------------------------------------
'
'     a nintendo LCD game simulator
'            for atari ST
'
'           florent coste 
'            april 2002
'
'        (c) atari.games 2002
'      http://atari.games.free.fr
'
' ------------------------------------
'
HIDEM
CLS
'
' ------- sprites & coords tables declaration ------
'
DIM x(60), y(60), x1(60), x2(60), y1(60), y2(60), sp$(60), spb$(60)
'
DIM p_max(6), p_index(6)
DIM p_line(4), p_state(4), old_p_state(4), p_timer(4)
'
DIM led$(10)
DIM thefont$(46)
'
DIM blue_palette&(16)
DIM clr_palette&(16)
DIM orig_palette(16)
DIM palette&(16)
DIM r%(16),g%(16),b%(16)
'
DIM scores(10)
DIM snames$(10)
'
@save_palette
@to_blue
'
RESERVE 50000
'
LET screen%=MALLOC(32000)
IF screen%=0
  EDIT
ENDIF
'
' --------- sprites indices ----------
'
' les paras
'
p_max(0)=7
p_max(1)=6
p_max(2)=5
p_max(3)=8
p_max(4)=10
p_max(5)=12
'
p_index(0)=0                          ! 8 sprites  (7+1)
p_index(1)=p_index(0)+p_max(0)+1      ! 7 sprites  (6+1)
p_index(2)=p_index(1)+p_max(1)+1      ! 6 sprites  (5+1)
p_index(3)=p_index(2)+p_max(2)+1      ! 9 sprites  (5+1+3)
p_index(4)=p_index(3)+p_max(3)+1      ! 11 sprites (5+1+5)
p_index(5)=p_index(4)+p_max(4)+1      ! 13 sprites (5+1+7)
'
' le bateau
'
boat_index=p_index(5)+p_max(5)+1      ! 3 sprites
'
' les boutons
'
lb_index=boat_index+3
rb_index=lb_index+1
'
nb_sprites=rb_index
'
leftkey$="w"
rightkey$="x"
pauskey$="p"
'
' ------- game initialisation  --------
'
ecran%=XBIOS(2)
'
COLOR 5
DEFFILL 5,1,8
RANDOMIZE VAL(MID$(TIME$,7,8))
'
@load_scores
'setcolor 16,0,0,0
setcolor 15,0,0,0
@load_gfx
@load_sprites
'@load_config
'
' ---------------------------
' --                       --
' --       main loop       --
' --                       --
' ---------------------------
'
pres_timer=0
game_paused=0
playmode=-1
page=-1

 playmode=0
 @play_game
       
'
DO
'
   EXIT IF end=1
   if playmode=-1
     DEC pres_timer
'
     if pres_timer<=0
       INC page
       if page=5
         page=0
       ENDIF
       ON page+1 GOSUB pres1,pres2,pres3,pres4,halloffame
       pres_timer=page_timer
     ENDIF
   ENDIF
'
   key=ASC(INKEY$)
   EXIT IF key==27
     
   IF key<>0
     IF playmode=-1
       @game_help
       playmode=1
     ELSE IF key=65 OR key=97
       playmode=0
       @play_game
       page=3
       pres_timer=0
       playmode=-1
     ELSE IF key=66 OR key=98
       playmode=1
       @play_game
       page=3
       pres_timer=0
       playmode=-1
     ENDIF
   ENDIF
'
LOOP
'
@prg_end
'
'
' ---------------------------
' --                       --
' --     program end       --
' --                       --
' ---------------------------
PROCEDURE prg_end
  SHOWM
  CLS
  @restore_palette
  ~MFREE(screen%)
  RESERVE
  EDIT
RETURN
'
' ---------------------------
' --                       --
' --       play loop       --
' --                       --
' ---------------------------
'
PROCEDURE play_game
'
  @to_blue
  BMOVE screen%, ecran%,32000
  @set_clr_palette
  @fade_to_color(2,3,4)
'
' default config
'
  paras_timer=1000
'
  @load_config
'
  nb_paras        = playmode+1
  nb_paras        = playmode
  game_paused     = 0
  score%          = -1
  end             = 0
'
  boat_state = -1
  boat_disp  = 0
'
  FOR i=0 TO 3
    p_state(i) = -1
  NEXT i
'
  lb_state        = -1
  rb_state        = -1
'
  @score
'
  nb_paras_max    = 3
  nb_misses       = 0
'
  DO
'
    old_boat_state = boat_state
    old_p_state(0) = p_state(0)
    old_p_state(1) = p_state(1)
    old_p_state(2) = p_state(2)
    old_lb_state   = lb_state
    old_rb_state   = rb_state
'
    @test_keyboard
'
    IF game_paused=0
'
      @play_boat
'
      @play_paras
'
' dessine le bateau
'
      @put_sprite(boat_index,boat_state,old_boat_state)
'
' dessine les paras
'
      FOR i=0 TO nb_paras
PRINT AT(1,i+1); p_line(i);" ";p_state(i);"/";p_max(p_line(i));"  "
        @put_sprite(p_index(p_line(i)),p_state(i),old_p_state(i))
      NEXT i
'
' dessine les boutons
'
      put_sprite(lb_index,lb_state,old_lb_state)
      put_sprite(rb_index,rb_state,old_rb_state)
'
' collisions
'
      FOR i=0 TO nb_paras
        IF boat_state=p_line(i) AND p_state(i)=p_max(p_line(i))-1
          DEC nb_paras
          p_line(j)=0
          p_state(j)=0
          p_timer(j)=0
          FOR j=i TO nb_paras
            p_line(j)=p_line(j+1)
            p_state(j)=p_state(j+1)
            p_timer(j)=p_timer(j+1)
          NEXT j
          @score
        ENDIF
      NEXT i
    ENDIF
'
    EXIT IF end=1
  LOOP
'
  @end_of_game
'
RETURN
'
' ---------------------------
' --                       --
' --   game intelligence   --
' --                       --
' ---------------------------
'
' ----- boat movement ----------
'
PROCEDURE play_boat
'
  IF boat_state<0
    boat_state = 0
  ELSE if boat_disp != 0
    ADD boat_state, boat_disp
    IF boat_state<0
      boat_state=0
    ELSE IF boat_state>2
      boat_state=2
    ENDIF
  ENDIF
'  
RETURN
'
' ------ miss a para --------
'
> PROCEDURE miss_it(which)
'
  FOR i=1 To 15 STEP 2
    SOUND 1,15,i,2
    PAUSE 1
    SOUND 2,15,15-i,3
    PAUSE 1
  NEXT i
'
  IF p_state(which) < 0
    PUT 106,106,sp$(42)
  ELSE
    PUT 184,106,sp$(43)
  ENDIF      
'
  @put_sprite(p_index(p_line(which)),-1,old_p_state(which))
  @put_sprite(bg_index,2-boat_state,-1)
  @put_sprite(bd_index,boat_state,-1)
'
  PAUSE 80
  INC nb_misses
  IF nb_misses=3
     end = 1
  ENDIF
  SOUND 1,0
  SOUND 2,0
  SOUND 3,0
'
RETURN
'
'
' ----- play paras ----------
'
PROCEDURE play_paras
'
  IF nb_paras<nb_paras_max
'     IF nb_paras<2 OR RANDOM(200)=0
     IF nb_paras=0
       IF playmode=0
         vvv = RANDOM(4)
       ELSE IF playmode=0
         vvv = RANDOM(6)
       ENDIF
       ok = 1
       FOR i=0 TO nb_paras
         IF p_line(i)=vvv AND  p_state(i)<2
           ok = 0
         ENDIF
       NEXT i
       IF ok=1
         p_line(nb_paras)=vvv
         p_state(nb_paras)=0
         p_timer(nb_paras)=paras_timer
         INC nb_paras
       ENDIF
     ENDIF
  ENDIF
'
  FOR i=0 TO nb_paras
    @play_para(i)
  NEXT i
'
RETURN
'
' ----- play para ----------
'
PROCEDURE play_para(which)
'
  IF p_state(which) >= 0
    DEC p_timer(which)
    IF p_timer(which) = 0
      p_timer(which)=paras_timer
      INC p_state(which)
      IF p_state(which)=p_max(p_line(which))
        @miss_it(which)
      ENDIF
    ENDIF
  ENDIF
'  
RETURN

'
' ---------------------------
' --                       --
' --  keyboard & joystick  --
' --                       --
' ---------------------------
'
' --------- keyboard ----------
'
> PROCEDURE test_keyboard
'
  boat_disp = 0
'
  old_joy = joy
  old_but = but
'
  joy=Peek(3593)
  but=MOUSEK
'  
' joystick
'
  IF old_joy<>joy
    IF joy=4                      ! LEFT JOYSTICK
      boat_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF joy=8                 ! RIGHT JOYSTICK
      boat_disp=1
      rb_state=0
      rb_timer=40
    ENDIF
'  
' mouse buts
'
  ELSE IF but<>old_but
    IF but=1                       ! LEFT MOUSE BUTTON
      boat_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF but=2                 ! RIGHT MOUSE BUTTON
      boat_disp=1
      rb_state=0
      rb_timer=40
    ENDIF
'  
' keyboard
'
  ELSE
    k$=INKEY$
'
    IF LEN(k$)==2
      IF CVI(k$)=59               ! F1 KEY
        end=1
        score%=0
      ELSE IF CVI(k$)=75          ! LEFT ARROW
        boat_disp=-1
        lb_state=0
        lb_timer=40
      ELSE IF CVI(k$)=77          ! RIGHT ARROW
        boat_disp=1
        rb_state=0
        rb_timer=40
      ENDIF
    ELSE IF ASC(k$)=27            ! ESC KEY
      end=1
      score%=0
    ELSE IF k$=rightkey$          ! RIGHT KEY
      boat_disp=1
      rb_state=0
      rb_timer=40
    ELSE IF k$=leftkey$           ! LEFT KEY
      boat_disp=-1
      lb_state=0
      lb_timer=40
    ELSE IF k$=pauskey$ OR ASC(k$)=32       ! PAUSE OR SPACE KEY
      IF playmode=0 OR playmode=1
        game_paused=1-game_paused
        @set_clr_palette
        if game_paused=1
           FOR i=0 TO 16
           SETCOLOR i,((palette&(i)/256) AND 7)/2,((palette&(i)/16) AND 7)/2,(palette&(i) AND 7)/2
           NEXT i
        ELSE
           VOID XBIOS(6, L:VARPTR(palette&(0)))
        ENDIF
      ENDIF
    ENDIF
  ENDIF
'
' buttons state & drawing
'
  IF lb_timer>0
    DEC lb_timer
    IF lb_timer=0
      lb_state=-1
    ENDIF
  ENDIF

  IF rb_timer>0
    DEC rb_timer
    IF rb_timer=0
      rb_state=-1
    ENDIF
  ENDIF
RETURN
'
' ---------------------------
' --                       --
' --   drawing & loading   --
' --     graphic data      --
' --                       --
' ---------------------------
'
' ------------------------------
' ---- draw player score -------
' ------------------------------
'
> PROCEDURE score
  INC score%
  a%=score%/1000
  b%=(score%-(a%*1000))/100
  c%=(score%-(a%*1000)-(b%*100))/10
  d%=(score%-(a%*1000)-(b%*100)-(c%*10))
'
  IF a%>0
    PUT 184,53,led$(a%)
  ENDIF
  IF b%>0
    PUT 195,53,led$(b%)
  ENDIF
  IF c%>0
    PUT 209,53,led$(c%)
  ENDIF
  PUT 219,53,led$(d%)
'  
  IF score% > 0
    paras_timer = MAX( paras_timer_min, INT(paras_timer*29/30) )
  ENDIF
'
RETURN
'
' ------------------------------
' ---- sprite drawing func -----
' ------------------------------
'
PROCEDURE put_sprite(index,state,old)
  IF old <> state
    IF old>-1
      PUT x1(old+index), y1(old+index), spb$(old+index)
    ENDIF
    IF state>-1
      PUT x1(state+index), y1(state+index), sp$(state+index)
    ENDIF
  ENDIF
RETURN
'
' ------------------------------------------
' palette save and restore
' ------------------------------------------
'
PROCEDURE save_palette
  LOCAL i
  FOR i=0 TO 15
    orig_palette(i)=XBIOS(7,W:i,W:-1)
  NEXT i
RETURN
'
PROCEDURE restore_palette
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i,orig_palette(i)
  NEXT i
RETURN
'
' -------------------------
' ----- fade to colors ----
' -------------------------
'
> PROCEDURE fade_to_color(rr,gg,bb)
  LOCAL i, j, ii
'
  FOR i=0 TO 15
    v% = palette&(i)
    r%(i) = (v%/256) AND 7
    g%(i) = (v%/16) AND 7
    b%(i) = v% AND 7
  NEXT i
'
  ii=7
  FOR i=0 TO 7
    VSYNC
    FOR j=0 TO 15
       SETCOLOR j, (r%(j)*i+rr*ii)/7, (g%(j)*i+gg*ii)/7, (b%(j)*i+bb*ii)/7
    NEXT j
    DEC ii
'    PAUSE 1
  NEXT i

RETURN

> PROCEDURE set_blue_palette
  BMOVE VARPTR(blue_palette&(0)), VARPTR(palette&(0)), 32
RETURN
'
> PROCEDURE set_clr_palette
  BMOVE VARPTR(clr_palette&(0)), VARPTR(palette&(0)), 32
RETURN
'
' -------------------------
' ----- fade to black -----
' -------------------------
'
> PROCEDURE fade_to_black(rr,gg,bb)
  LOCAL i, j, ii
'
  FOR i=0 TO 15
    v% = palette&(i)
    r%(i) = (v%/256) AND 7
    g%(i) = (v%/16) AND 7
    b%(i) = v% AND 7
  NEXT i
'
  ii = 7
  FOR i=0 TO 7
    VSYNC
    FOR j=0 TO 15
      SETCOLOR j, (r%(j)*ii+rr*i)/7, (g%(j)*ii+gg*i)/7, (b%(j)*ii+bb*i)/7
    NEXT j
    DEC ii
'   PAUSE 1
  NEXT i
'
RETURN
'
' -----------------------------
' -- set all colors to black --
' -----------------------------
'
> PROCEDURE to_black
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i, 0,0,0
  NEXT i
RETURN
'
> PROCEDURE to_blue
  LOCAL i
  FOR i=0 TO 15
    SETCOLOR i, 2,3,4
  NEXT i
RETURN
'
' ----------------------------
' -- text drawing procedure --
' ----------------------------
'
> PROCEDURE draw_text(text$,xx,yy)
   l=LEN(text$)
   if xx<40 AND yy<25
     xx = xx*8
     yy = yy*8
   ENDIF
   if xx<0
      xx=(320-l*8)/2
   ENDIF
   FOR ii=0 TO l
      v=ASC( MID$(text$,ii+1,1) )
      SELECT v
      CASE 65 TO 90
         PUT xx+ii*8,yy,thefont$(v-65)
      CASE 97 TO 122
         PUT xx+ii*8,yy,thefont$(v-97)
      CASE 48 TO 57
         PUT xx+ii*8,yy,thefont$(v-20)
      CASE 38
         PUT xx+ii*8,yy,thefont$(45)
      CASE 39
         PUT xx+ii*8,yy,thefont$(44)
      CASE 46
         PUT xx+ii*8,yy,thefont$(26)
      CASE 45
         PUT xx+ii*8,yy,thefont$(27)
      CASE 95
         PUT xx+ii*8,yy,thefont$(27)
      CASE 64
         PUT xx+ii*8,yy,thefont$(39)
      CASE 58
         PUT xx+ii*8,yy,thefont$(40)
      CASE 92
         PUT xx+ii*8,yy,thefont$(41)
      CASE 60
         PUT xx+ii*8,yy,thefont$(42)
      CASE 62
         PUT xx+ii*8,yy,thefont$(43)
      DEFAULT
         PUT xx+ii*8,yy,thefont$(38)
      ENDSELECT
   NEXT ii
RETURN
'
' ---------------------------
' --                       --
' --  menu & intermediate  --
' --        screens        --
' --                       --
' ---------------------------
'
' -----------------------
' ---- premier titre ----
' -----------------------
'
> PROCEDURE pres1
  @fade_to_black(2,3,4)
  CLS
  @set_blue_palette
'
   PUT 120,50,logo$
   PUT 110,135,logo2$
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- deuxieme  titre ---
' -----------------------
'
> PROCEDURE pres2
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("-----------------", -1,3)
  @draw_text("LCD.revival.games", -1,4)
  @draw_text("-----------------", -1,5)
  PUT 131,70, title$
  @draw_text("---------",-1,19)
  @draw_text("PARACHUTE",-1,20)
  @draw_text("---------",-1,21)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- troisieme titre ---
' -----------------------
'
> PROCEDURE pres3
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("by florent.coste", -1,12)
  @draw_text("@ - atari.games - 2002", -1,18)
  @draw_text("http:\\atari.games.free.fr",-1,21)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' --- quatrieme titre ---
' -----------------------
'
> PROCEDURE pres4
  @fade_to_black(2,3,4)
  CLS
'
  @draw_text("Original" , -1,4)
  @draw_text("'Game & Watch'", -1,6)
  @draw_text("by", -1, 8)
  PUT 107,90, nintendo$
  @draw_text("@ - nintendo - 1981",-1, 19)
'
  @fade_to_color(2,3,4)
RETURN
'
' --------------------
' -- hall of fame ----
' --------------------
'
> PROCEDURE halloffame
'
  @fade_to_black(2,3,4)
  CLS
'  
  @draw_text("------------",-1,3)
  @draw_text("HALL OF FAME",-1,4)
  @draw_text("------------",-1,5)
'
  FOR i=0 TO 9
    @draw_text( snames$(i), 14,9+i )
    @draw_text( ".....",   18,9+i )
    @draw_text( STR$(scores(i)), 24,9+i )
  NEXT i
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------
' - get the player name -
' -----------------------
'
PROCEDURE end_of_game
'
  fade_to_black(0,0,0)
  CLS
  @set_blue_palette
'
  PUT 131, 20, title$
  @draw_text("---------",-1,13)
  @draw_text("game over",-1,14)
  @draw_text("---------",-1,15)
  fade_to_color(0,0,0)
'  
  nbl=0
  place=-1
'
  FOR i=0 TO 9
     IF score% >= scores(i)
       place=i
     ENDIF
     EXIT IF place>=0
  NEXT i
'
  IF place >= 0
    FOR j=9 TO place+1 STEP -1
      scores(j) = scores(j-1)
      snames$(j) = snames$(j-1)
    NEXT j
'
' clears keyboard buffer
'
    DO
    LOOP UNTIL INKEY$=""
'
    nam$=">>> ... <<<"
    @draw_text( "enter your name",-1,18)
    @draw_text( nam$,-1,20)
    DO
      d$=INPUT$(1)
      a%=ASC(d$)
      IF a%>0
        IF a%==13 OR a%=27 
          nbl=3
        ELSE IF a%==8 AND nbl>0
          MID$(nam$,nbl+4)="."
          DEC nbl
        ELSE IF d$<>","
          INC nbl
          MID$(nam$,nbl+4)=d$
        ENDIF
        @draw_text( nam$,-1,20)
      ENDIF
      EXIT IF nbl=3
    LOOP
'
    snames$(place) = MID$(nam$,5,3)
    scores(place) = score%
    @save_scores
  ENDIF
'
  score%=0
  PAUSE 25
'
RETURN
'
'
' -----------------------
' ---- game help page ---
' -----------------------
'
PROCEDURE game_help
  @fade_to_black(2,3,4)
  CLS
'  
  PUT 131, 20, title$

  @draw_text("keybd ...... <" + leftkey$ + "> <" + rightkey$ +">          ",-1,13)
  @draw_text("      ...... <ARROWS> <ESC>   ",-1,14)
  @draw_text("mouse ...... left & right buts", -1, 16 )
  @draw_text("joystick ... left & right     ", -1, 17 )
  @draw_text("press -A- for easy mode",-1,21)
  @draw_text("press -B- for hard mode",-1,22)
'
  @fade_to_color(2,3,4)
RETURN
'
' -----------------------------------------------
' ----- load image & sprites --------------------
' -----------------------------------------------
'
> PROCEDURE load_sprites
'
  BLOAD "PARA\A_G.PAL", VARPTR(blue_palette&(0))
  BLOAD "PARA\GAME.PAL", VARPTR(clr_palette&(0))
'
  BLOAD "PARA\A_G.DAT", ecran%
'
  FOR i=0 TO 38
    GET i*8, 0, 7+(i*8), 7, thefont$(i)
  NEXT i
  FOR i=0 TO 6
    GET i*8, 8, 7+(i*8), 15, thefont$(i+39)
  NEXT i
'
  GET 0,17,60,77,    title$
  GET 123,33,229,60, nintendo$
  GET 230,10,311,87, logo$
  GET 132,10,229,33, logo2$
  CLS
'
RETURN
'
' -----------------------------------------------
' ----- load image & sprites --------------------
' -----------------------------------------------
'
> PROCEDURE load_gfx
'
  RESTORE piece
  FOR i=0 TO nb_sprites
     READ x1(i), y1(i), x2(i), y2(i), x(i), y(i)
'     PRINT y2(i)-y1(i); "  ";x2(i)-x1(i)
  NEXT i
'
  BLOAD "PARA\GAME2.DAT", ecran%
'
  FOR i=0 TO nb_sprites
    IF x(i)>-1 AND y(i)>-1
       GET x(i), y(i), x(i)+x2(i)-x1(i), y(i)+y2(i)-y1(i), sp$(i)
    ENDIF
  NEXT i
'
  BLOAD "PARA\SPRITES.DAT", ecran%
'
  FOR i=0 TO 9
    GET i*8, 15, 7+(i*8), 28, led$(i)
  NEXT i 
'
  BLOAD "PARA\GAME.DAT", ecran%
      
  FOR i=0 TO nb_sprites
    IF x(i)>-1 AND y(i)>-1
       GET x1(i), y1(i), x2(i), y2(i), spb$(i)
    ELSE
       GET  x1(i), y1(i), x2(i), y2(i), sp$(i)
       PBOX x1(i), y1(i), x2(i), y2(i)
       GET  x1(i), y1(i), x2(i), y2(i), spb$(i)
    ENDIF
  NEXT i
'
  BMOVE ecran%,screen%,32000
'
RETURN
'
piece:
DATA 185,54,195,63,78,34
DATA 167,58,183,67,60,38
DATA 154,59,165,72,47,39
DATA 140,64,152,79,33,44
DATA 126,77,140,90,19,57
DATA 115,89,132,104,8,69
DATA 108,105,124,123,0,85
DATA 105,128,132,136,8,23
'
' para 2
'
DATA 190,63,200,71,192,0
DATA 180,65,189,75,182,2
DATA 169,65,179,83,169,2
DATA 151,78,166,91,153,15
DATA 142,89,157,105,144,26
DATA 138,106,155,121,139,42
DATA 135,128,154,136,30,24
'
' para 3
'
DATA 202,70,212,77,238,0
DATA 191,68,201,82,225,0
DATA 176,78,192,90,212,9
DATA 171,90,186,105,207,22
DATA 169,105,187,121,205,37
DATA 157,128,183,137,61,24
'
' para 4
'
DATA 202,70,212,77,-1,-1
DATA 191,68,201,82,-1,-1
DATA 191,85,203,100,227,17
DATA 203,85,217,100,240,17
DATA 191,85,203,100,227,17
DATA 176,78,192,90,-1,-1
DATA 171,90,186,105,-1,-1
DATA 169,105,187,121,-1,-1
DATA 157,128,183,137,-1,-1
'
' para 5
'
DATA 202,70,212,77,-1,-1
DATA 191,68,201,82,-1,-1
DATA 191,85,203,100,-1,-1
DATA 203,85,217,100,-1,-1
DATA 191,85,203,100,-1,-1
DATA 203,85,217,100,-1,-1
DATA 191,85,203,100,-1,-1
DATA 176,78,192,90,-1,-1
DATA 171,90,186,105,-1,-1
DATA 169,105,187,121,-1,-1
DATA 157,128,183,137,-1,-1
'
' para 6
'
DATA 202,70,212,77,-1,-1
DATA 191,68,201,82,-1,-1
DATA 191,85,203,100,-1,-1
DATA 203,85,217,100,-1,-1
DATA 191,85,203,100,-1,-1
DATA 203,85,217,100,-1,-1
DATA 191,85,203,100,-1,-1
DATA 203,85,217,100,-1,-1
DATA 191,85,203,100,-1,-1
DATA 176,78,192,90,-1,-1
DATA 171,90,186,105,-1,-1
DATA 169,105,187,121,-1,-1
DATA 157,128,183,137,-1,-1
'
' boat
'
DATA 97,113,121,124,0,8
DATA 128,113,151,127,31,8
DATA 158,113,183,125,61,8
'
' boutons gauche et droit
'
DATA 16,131,52,163,-1,-1
DATA 267,129,303,161,-1,-1
'
' miss
'
DATA 196,116,215,122,-1,-1
DATA 188,121,203,133,-1,-1
DATA 201,121,217,131,-1,-1
DATA 217,121,233,133,-1,-1
'
DATA -1,-1,-1,-1,-1,-1
'
DATA END

'
' ------------------------------------------
' ----- load scores table  -----------------
' ------------------------------------------
'
PROCEDURE load_scores
'
  OPEN "i",#1,"PARA\SCORES.DAT"
'   
  FOR i=0 TO 9
    INPUT #1,snames$(i),scores(i)
  NEXT i
'
  CLOSE #1
'
RETURN
'
' ------------------------------------------
' ----- save scores table  -----------------
' ------------------------------------------
'
PROCEDURE save_scores
'
  OPEN "o",#1,"PARA\SCORES.DAT"
'
  FOR i=0 TO 9
     WRITE #1,snames$(i),scores(i)
  NEXT i
'
  CLOSE #1
'
RETURN
'
' ------------------------------------------
' ----- load keys --------------------------
' ------------------------------------------
'
PROCEDURE load_config
'
  OPEN "i",#1,"PARA\CONFIG.DAT"
'
  DO
    INPUT #1,keyword$,val$
    EXIT IF keyword$="---"
    IF keyword$="left_key"
      leftkey$=val$
    ELSE IF keyword$="right_key"
      rightkey$=val$
    ELSE IF keyword$="pause_key"
      pauskey$=val$
    ELSE IF keyword$="paras_timer"
      paras_timer=VAL(val$)
    ELSE IF keyword$="paras_timer_min"
      paras_timer_min=VAL(val$)
    ELSE IF keyword$="page_timer"
      page_timer=VAL(val$)
    ENDIF
  LOOP
'
  CLOSE #1
'
RETURN
'
'