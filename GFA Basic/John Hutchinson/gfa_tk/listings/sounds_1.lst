' 4.01 - 4.12 sound_effects1                                     SOUNDS_1.LST
' ===========================================================================
' This routine consists of twelve separate sound effect procedures which use
' simple SOUND and WAVE commands. The sound effects procedures included are:
'
'   beep, alarm, siren1, tideli, bounce1, bounce2, tong, poof, clang, bam,
'   ting, heartbeat
'
' NOTE: Parameters are not required EXCEPT for the following procedures:
'
'    beep(number)            number=number of beep sounds desired
'    siren1.sound(number)    number=number of siren sounds desired
'    bounce1.sound(pause)    pause=duration of pause between bounces desired
'    bounce2.sound(pause)    pause=duration of pause between bounces desired
' ===========================================================================
'
PROCEDURE beep(number)                  ! 1
  LOCAL i
  FOR i=1 TO number
    OUT 2,7
    PAUSE 10            ! change pause duration as desired
  NEXT i
RETURN
'
'
PROCEDURE alarm.sound                   ! 2
  ' *** ta-ti-ta sound (warning)
  SOUND 1,10,12,4,25
  SOUND 1,10,6,4,25
  SOUND 1,10,12,4,50
  SOUND 1,0
RETURN
'
'
PROCEDURE siren1.sound(number)          ! 3
  ' *** siren (length determined by number)
  LOCAL n,i
  FOR n=1 TO number
    FOR i=1 TO 8
      SOUND 1,15,i,4,1
    NEXT i
    PAUSE 5
    FOR i=1 TO 8
      SOUND 1,15,i,4,1
    NEXT i
    PAUSE 5
  NEXT n
  WAVE 0,0
RETURN
'
'
PROCEDURE tideli.sound                  ! 4
  SOUND 0,15,2,3,5
  SOUND 0,15,2,4,5
  SOUND 0,15,2,3,5
  SOUND 0,15,2,5,5
  SOUND 0,15,2,3,5
  SOUND 0,15,2,6,10
  SOUND 0,0
RETURN
'
'
PROCEDURE bounce1.sound(pause)          ! 5
  LOCAL n
  FOR n=15 DOWNTO 0
    SOUND 1,n,5,1
    SOUND 2,n,12,2
    SOUND 3,n,5,4
    WAVE 7
    PAUSE pause
    SOUND 3,n,5,5
    PAUSE pause
  NEXT n
  WAVE 0,0
RETURN
'
'
PROCEDURE bounce2.sound(pause)          ! 6
  LOCAL n
  FOR n=15 DOWNTO 0
    SOUND 1,n,1,3
    SOUND 2,n,5,3
    SOUND 3,n,1,4
    WAVE 7
    PAUSE pause
    SOUND 3,n,1,5
    PAUSE pause
  NEXT n
  WAVE 0,0
RETURN
'
'
PROCEDURE tong.sound                    ! 7
  SOUND 1,10,1,5
  WAVE 1,1,3,6000,10
RETURN
'
'
PROCEDURE poof.sound                    ! 8
  WAVE 56,7,0,20000,0
RETURN
'
'
PROCEDURE clang.sound                   ! 9
  SOUND 1,14,1,5
  SOUND 2,14,5,7
  SOUND 3,14,8,6
  WAVE 7,7,9,17391
RETURN
'
'
PROCEDURE bam.sound                     ! 10
  SOUND 2,0,0,0
  WAVE 8,1,7,1500
RETURN
'
'
PROCEDURE heartbeat.sound               ! 11
  SOUND 1,9,1,4
  SOUND 2,7,1,4
  SOUND 3,10,1,1
  WAVE 7739,3,8,4964
RETURN
'
'
PROCEDURE ting.sound                    ! 12
  SOUND 1,15,10,6
  SOUND 2,0,1,1
  SOUND 3,0,12,1
  WAVE 1,1,9,12085
RETURN
'
