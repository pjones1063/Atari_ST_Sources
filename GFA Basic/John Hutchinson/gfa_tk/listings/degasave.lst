' 1.12 degas_save
' ===========================================================================
' DEGASAVE.LST
'
' This procedure saves the current physical screen to disk as an
' uncompressed Degas PI* file.
' Parameters:
'      pic$          path and filename of the saved picture file.
' ===========================================================================
'
PROCEDURE degas_save(pic$)
  ' *** Saves the current physical screen to disk as a Degas file.
  LOCAL screen$,i,col%,h_byte%,l_byte%
  SGET screen$                          ! Get a copy of the screen
  OPEN "O",#1,pic$                      ! Open the file for output
  PRINT #1;CHR$(0);                     ! Save resolution high byte
  PRINT #1;CHR$(XBIOS(4));              ! Save resolution low byte
  FOR i=0 TO 15                         ! 15 low, 3 med, 1 high
    col%=XBIOS(7,W:i,w-1) AND &H777     ! Find color word
    h_byte%=INT(col%/256)               ! Split word to high/low bytes
    l_byte%=col%-(h_byte%*256)
    PRINT #1;CHR$(h_byte%);CHR$(l_byte%);  ! Save color bytes to disk
  NEXT i
  PRINT #1;screen$                      ! Save the picture to disk
  FOR i=0 TO 29
    PRINT #1;CHR$(0);                   ! Save null animation bytes
  NEXT i
  CLOSE #1
RETURN
