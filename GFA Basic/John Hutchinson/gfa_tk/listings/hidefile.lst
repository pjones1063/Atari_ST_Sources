' 3.06 hide_file
' ===========================================================================
' HIDEFILE.LST
'
' This procedure will set (hide) or clear (unhide) the hidden file status
' of a specified disk file.
' Parameters:
'       file$           the path/filename to be locked/unlocked
'       hide!           TRUE (-1) to hide file; FALSE (0) to unhide file
' ===========================================================================
'
PROCEDURE hide_file(file$,hide!)
  ' *** Hides/unhides the specified file.
  LOCAL a%,aa%
  IF EXIST(file$)
    aa%=GEMDOS(67,L:V:file$,0,2)        ! read/store attrs
    a%=aa%
    IF a%>=32
      SUB a%,32
    ENDIF
    IF a%>=16
      SUB a%,16
    ENDIF
    IF a%>=8
      SUB a%,8
    ENDIF
    IF a%>=4
      SUB a%,4
    ENDIF
    IF a%<2                             ! file NOT originally hidden
      IF hide!
        ~GEMDOS(67,L:V:file$,1,aa%+2)   ! hide the file
        PRINT "file is now hidden"
      ENDIF
    ELSE IF a%>=2                       ! file originally hidden
      IF NOT hide!
        ~GEMDOS(67,L:V:file$,1,aa%-2)   ! unhide the file
        PRINT "file is now unhidden"
      ENDIF
    ENDIF
  ENDIF
RETURN
