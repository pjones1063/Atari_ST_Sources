' 3.09 copy_file
' ============================================================================
' COPYFILE.LST
'
' Use this procedure to copy a disk file to another filename.
' Parameters:
'               s$              The source path/filename
'               d$              The destination path/filename
'
' NOTE: This procedure protects you from accidentally copying a file to
' itself, which, in older versions of TOS, could wipe out your disk.
' ============================================================================
'
PROCEDURE copy_file(s$,d$)
  ' *** Copy a file to another filename
  LOCAL block%
  IF s$<>d$                             ! prevent errors
    OPEN "I",#1,s$                      ! open source for input
    OPEN "O",#2,d$                      ! open dest for output
    block%=LOF(#1)                      ! find length of source
    WHILE block%>32000
      PRINT #2,INPUT$(32000,#1);        ! copy the data in 32K blocks
      SUB block%,32000
    WEND
    PRINT #2,INPUT$(block%,#1);         ! copy the last bit of data
    CLOSE                               ! close all open channels
  ENDIF
RETURN
