' 1.10 get_elite
' ===========================================================================
' GETELITE.LST
'
' Use this routine to load compressed Degas ELITE format picture files
' of all resolution modes (low PC1, medium PC2 and high PC3) to a specified
' screen address.
' Parameters:
'      pic$          path/filename of picture to load
'      s%            screen address to load picture to
'
' NOTE:  The procedure init_compress must be called prior to calling
' the get_elite procedure.
' ===========================================================================
'
PROCEDURE init_compress
  ' *** Initialize decompression algorithms for get_elite.
  LOCAL i,x%,unpack$,unrave$
  unpack$=SPACE$(60)
  unpac%=VARPTR(unpack$)
  RESTORE unpack
  FOR i=1 TO 15
    READ x%
    LPOKE unpac%,x%
    ADD unpac%,4
  NEXT i
  unpac%=VARPTR(unpack$)
  unrave$=SPACE$(40)
  unrav%=VARPTR(unrave$)
  RESTORE unravel
  FOR i=1 TO 10
    READ x%
    LPOKE unrav%,x%
    ADD unrav%,4
  NEXT i
  unrav%=VARPTR(unrave$)
unpack:
  DATA &h48e740e0,&h206f0014,&h226f0018,&h2449d4ef
  DATA &h001c4241,&h12186b0c,&h12d851c9,&hfffcb3ca
  DATA &h6df0600e,&h44011018,&h12c051c9,&hfffcb3ca
  DATA &h6de02008,&h4cdf0702,&h4e750000
unravel:
  DATA &h48e740c0,&h206f0010,&h226f0014,&h302f0018
  DATA &h322f001a,&he2486004,&h3298d2c1,&h51c8fffa
  DATA &h4cdf0302,&h4e750000
RETURN
'
'
PROCEDURE get_elite(pic$,s%)
  ' *** Loads a compressed Degas ELITE picture file.
  LOCAL temp$,p%,scr%,screen%,temp2$,b%,i,c,k%,q%,kk%,cc,pp,bb,aa
  IF RIGHT$(pic$)="1"                   ! low resolution color
    kk%=200
    cc=4
    pp=40
    bb=8
    aa=160
  ELSE IF RIGHT$(pic$)="2"              ! medium resolution color
    kk%=200
    cc=2
    pp=80
    bb=4
    aa=160
  ELSE                                  ! high resolution mono
    kk%=400
    cc=1
    pp=80
    bb=2
    aa=80
  ENDIF
  temp$=SPACE$(32760)
  p%=VARPTR(temp$)
  BLOAD pic$,p%
  screen%=s%
  temp2$=SPACE$(40)
  b%=VARPTR(temp2$)
  ADD p%,2
  FOR i=0 TO 15
    SETCOLOR i,DPEEK(p%)
    ADD p%,2
  NEXT i
  FOR k%=1 TO kk%                       ! kk%=200 low & med; 400 hi
    scr%=screen%
    FOR c=1 TO cc ! cc=4 low; 2 med; 1 hi
      p%=C:unpac%(L:p%,L:b%,pp)         ! pp=40 low; 80 med; 80 hi
      q%=C:unrav%(L:b%,L:scr%,pp,bb)    ! bb=8 low; 4 med; 2 hi
      ADD scr%,2
    NEXT c
    ADD screen%,aa                      ! 160 low/med; 80 high
  NEXT k%
RETURN
