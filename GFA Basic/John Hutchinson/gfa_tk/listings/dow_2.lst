' 5.10 dow_2
' ============================================================================
' DOW_2.LST
'
' Use this function to find the day of the week from integer parameters.
' Parameters:
'                m%          month number
'                d%          day number
'                y%          year number
'
' Note: Date must be on or after October 15, 1582 (10/15/1582)
' If y%<100 then the routine assumes the year is from 1900-1999.
'
' An example function call would be: PRINT FN dow_2$(2,26,92)
' which will return "Wednesday".
' ============================================================================
'
FUNCTION dow_2$(m%,d%,y%)
  LOCAL offset%,days%
  IF y%<100
    ADD y%,1900
  ENDIF
  IF (m%<1) OR (m%>12)
    RETURN CHR$(7)+"ERROR - month out of range"
  ELSE IF (m%=2) AND (d%>29)
    RETURN CHR$(7)+"ERROR - February never has more than 29 days"
  ELSE IF (m%=2) AND (d%=29)
    IF y% MOD 4<>0
      RETURN CHR$(7)+"ERROR - not a leap year"
    ELSE IF (y% MOD 100=0) AND (y% MOD 400<>0)
      RETURN CHR$(7)+"ERROR - not a leap year"
    ENDIF
  ELSE IF ((m%=4) OR (m%=6) OR (m%=9) OR (m%=11)) AND (d%>30)
    RETURN CHR$(7)+"ERROR - 30 days hath September, April, June, and November"
  ELSE IF (d%<1) OR (d%>31)
    RETURN CHR$(7)+"ERROR - no month exceeds 31 days"
  ELSE IF y%<1582
    RETURN CHR$(7)+"ERROR - year earlier than 1582"
  ELSE IF y%=1582
    IF (m%<10) OR ((m%=10) AND (d%<15))
      IF (m%=10) AND (d%>4)
        ' NOTE:  The next line is too long to fit on a printed page.
        RETURN CHR$(7)+"ERROR - date does not exist in Gregorian or Julian calendar"
      ELSE
        RETURN CHR$(7)+"ERROR - date earlier than October 15, 1582"
      ENDIF
    ENDIF
  ENDIF
  offset%=VAL(MID$("000031059090120151181212243273304334",3*(m%-1)+1,3))
  days%=(y%*365)+offset%+d%+(y%-1)\4+(y%-1)\400-(y%-1)\100
  IF m%>2 AND ((MOD(y%,4)=0 AND MOD(y%,100)<>0) OR MOD(y%,400)=0)
    INC days%
  ENDIF
  ' NOTE:  The next line is too long to fit on a printed page.
  RETURN MID$("SATURDAY SUNDAY   MONDAY   TUESDAY  WEDNESDAYTHURSDAY FRIDAY   ",9*MOD(days%,7)+1,9)
ENDFUNC
