' ** Dialog Box demonstration
' DIAL_SMP.GFA
'
DIM r%(3)
'
form1%=0    ! 0 = Dialog
icon1%=1    ! ICON in tree FORM1
ch_name%=2  ! FTEXT in tree FORM1
sur%=3      ! FTEXT in tree FORM1
str%=4      ! FTEXT in tree FORM1
town%=5     ! FTEXT in tree FORM1
cancel%=6   ! BUTTON in tree FORM1
ok%=7       ! BUTTON in tree FORM1
r%(1)=8     ! BUTTON in tree FORM1
r%(2)=9     ! BUTTON in tree FORM1
r%(3)=10    ! BUTTON in tree FORM1
output%=11  ! STRING in tree FORM1
'
~RSRC_FREE()
~RSRC_LOAD("\DIALOG.RSC")      ! Load the Resource file
~RSRC_GADDR(0,0,tree_adr%)              ! Get address of Object tree
~FORM_CENTER(tree_adr%,x%,y%,w%,h%)     ! Centre the coordinates, depending on
'                                       !  the current resolution
'
' Define initial editable strings
CHAR{{OB_SPEC(tree_adr%,ch_name%)}}="GFA"
CHAR{{OB_SPEC(tree_adr%,sur%)}}="Data Media"
CHAR{{OB_SPEC(tree_adr%,str%)}}="Box 121"
CHAR{{OB_SPEC(tree_adr%,town%)}}="Wokingham"
'
~OBJC_DRAW(tree_adr%,0,1,x%,y%,w%,h%)   ! Draw the Object tree
'
REPEAT
  ex%=FORM_DO(tree_adr%,0)              ! Clicked an object with EXIT status?
  '
  ' Put the texts from the Edit fields into the appropriate strings
  ch_name$=CHAR{{OB_SPEC(tree_adr%,ch_name%)}}
  surname$=CHAR{{OB_SPEC(tree_adr%,sur%)}}
  street$=CHAR{{OB_SPEC(tree_adr%,str%)}}
  town$=CHAR{{OB_SPEC(tree_adr%,town%)}}
  '
  FOR i%=1 TO 3
    IF BTST(OB_STATE(tree_adr%,r%(i%)),0)       ! Which radio-button
      radio%=r%(i%)                             ! was selected?
    ENDIF
  NEXT i%
UNTIL ex%=ok% OR ex%=cancel%
'
~RSRC_FREE()    ! Release reserved memory
'
CLS
PRINT "Ended with:    : ";ex%
PRINT "Christian name : ";ch_name$
PRINT "Surname        : ";surname$
PRINT "Street         : ";street$
PRINT "Town           : ";town$
PRINT "Radio:         : ";radio%
