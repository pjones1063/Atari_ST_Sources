' *****************************************************************
' ********* Beispiel zum Einbinden eines Music-Mon Songs **********
' ********* in Gfa-Basic.                                **********
' ********* Die Songdaten mÅssen mit "SAVE MODUL" abge-  **********
' ********* speichert worden sein.                       **********
' *****************************************************************
'
SPOKE 1156,2 ! Tastaturklick aus
'
routine$=SPACE$(3000) ! ausreichend Platz fÅr die Musikroutine
routine%=VARPTR(routine$)
jump%=routine%+6      ! die Ansprungadresse der Routine liegt 6 Byte weiter,
'                       weil in den ersten 6 Byte die Kontrollbytes liegen,
'                       mit denen man z.B die LautstÑrke verÑndern kann oder
'                       der Soundroutine den Zugriff auf jedes einzelne
'                       Soundchipregister verbieten kann.
'
songdat$=SPACE$(5000) ! genÅgend Platz fÅr den Demosong
songdat%=VARPTR(songdat$)
'
PRINT "Einen Moment bitte..."
'
BLOAD "include.sng\music_qu.inc",routine%  ! die Musikroutine einladen
BLOAD "include.sng\demo.mod",songdat%      ! das Songmodul einladen
'
CLS
PRINT "Nun hîren sie einen Demosong, der vom"
PRINT "Music-Mon aus mit 'SAVE MODUL'"
PRINT "abgespeichert wurde."
PRINT
PRINT "Die Musik lÑût sich wie folgt verÑndern:"
PRINT
PRINT "'0' unterbrechen"
PRINT "'1' fortsetzen"
PRINT "'2' wieder neu beginnen"
PRINT "'+' Musik lauter"
PRINT "'-' Musik leiser"
PRINT "'(' Musik langsamer"
PRINT "')' Musik schneller"
PRINT "'A' Musik abschalten"
'
VOID C:jump%(L:songdat%,W:1) ! der letzte Paramter gibt folgendes an:
'                               -0- Musik abschalten und aus dem Interrupt
'                                   ausklinken
'                               -1- Song im Loop-Modus abspielen (am Ende
'                                   wieder von vorne anfangen)
'                               -2- der Song wird nur einmal gespielt, danach
'                                   wird die Routine aus dem Interrupt
'                                   ausgeklinkt
' Ein nochmaliges Aufrufen der Routine, wÑhrend diese noch lÑuft, bewirkt
' einen Neustart des Songs.
'
REPEAT
  REPEAT
    key$=INKEY$
  UNTIL key$<>""
  '
  SELECT key$
    '
  CASE "0"
    POKE routine%+5,0 ! der Song wird unterbrochen, bis dieser Wert 1 ist.
    '                       die SoundkanÑle werden dabei nicht abgeschaltet !
  CASE "1"
    POKE routine%+5,1 ! der Song wird fortgesetzt.
  CASE ")"
    IF PEEK(routine%+4)>0
      POKE routine%+4,PEEK(routine%+4)-1 ! der Song wird schneller
    ENDIF
  CASE "("
    IF PEEK(routine%+4)<255
      POKE routine%+4,PEEK(routine%+4)+1 ! der Song wird langsamer
    ENDIF
  CASE "+"
    IF PEEK(routine%+3)>0
      POKE routine%+3,PEEK(routine%+3)-1 ! der Song wird lauter, da dieser Wert
      '                                    als LautstÑrkedÑmpfung zu sehen ist.
    ENDIF !                                (0=laut, 15=stumm)
  CASE "-"
    IF PEEK(routine%+3)<15
      POKE routine%+3,PEEK(routine%+3)+1 ! der Song wird leiser
    ENDIF
  CASE "2"
    VOID C:jump%(L:songdat%,W:1) ! der Song beginnt wieder von vorne
    '
  ENDSELECT
  '
UNTIL UPPER$(key$)="A"
'
VOID C:jump%(L:0,W:0) ! der Song wird abgeschaltet und die Routine aus dem
'                       Interrupt ausgeklinkt.
'
END
