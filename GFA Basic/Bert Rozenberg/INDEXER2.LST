Chdir "\"
Buffer$=Space$(513)
Openw 1
Fullw 1
Clearw 1
' On Break Cont
' On Error Gosub Fout
Graphmode 1
Kleur=Xbios(4)
If Kleur=0
  Alert 3," |Dit programma werkt niet|in lage resolutie",1," SORRY ",Dummy
  System
Endif
If Bios(4,2,L:Varptr(Buffer$),0,0,3)<>0
  Clearw 1
  Alert 3,"Dit Programma heeft een|Ramdisk of Harddisk als|drive D: nodig",1," MEER ",Dummy
  Alert 0,"Dit programma is|Meegeleverd.",1," STOPPEN ",Dummy
  System
Endif
' menu_bar_data
Data "Desk",  Over Indexer,-------------------,1,2,3,4,5,6,""
Data "File", Laden, Toevoegen,-------------, Opslaan, Opslaan als ,-------------, Stoppen,""
Data "Index", Drive A  , Drive B  , Drive C  , Drive D  , Drive E  , Drive F  ,""
Data "Sorteren", Op File , Op Extensie , Op Grootte , Op Datum , Op Disknaam, Op Folder,""
Data "Zoeken", Naar File , Naar Disk , Naar Extensie ,""
Data "Onderhoud", File Verwijderen , Disk Verwijderen , Gelijke Verwijderen ,---------------------, File Invoeren ,""
Data "Uitvoer", Printer , Scherm,""
Data ""
Data ***
'
'
'
'
Rem array voor menubar
Dim Men$(55),Regel$(40000),Soort(6),Lengte(6),S%(10000),Copyright$(5)
N%=0
Copyright=0
Filenaam$=""
Emp$=Space$(20)
Regel$(0)=" NAAM            GROOTTE  DATUM   DISKNAAM      FOLDER"
'
Rem lees menubar data
For I%=0 To 55
  Read Men$(I%)
  Exit If Men$(I%)="***"
Next I%
'
For K=0 To 5
  Read Soort(K)
Next K
Data 1,10,15,26,34,48
'
For K=0 To 5
  Read Lengte(K)
Next K
Data 9,5,10,7,13,30
'
For K=0 To 4
  Read Copyright$(K)
  For L=1 To Len(Copyright$(K))
    Add Copyright,Asc(Mid$(Copyright$(K),L,1))
  Next L
Next K
If Copyright<>6316
  Alert 3,"Dit programma is veranderd.| |Koop liever het origineel.| ",1," Ha Ha ",Dummy
  System
Endif
Data Indexer is,geschreven door:,Bert Rozenberg,Kikkerveen 331,Spijkenisse
'
'
'
Defline 1,2,0,0
'
Rem maak menubar
Menu Men$()
'
'
Rem ga door tot het einde
Einde=0
On Menu  Gosub Pull_down
Repeat
  On Menu
Until Einde=1
Edit
'
Procedure Pull_down
  M%=Menu(0)
  Gosub Wopen(Men$(M%))
  Deftext 1,0,0,6
  On M% Gosub About,D,D,D,D,D,D,D,D,D,Load,Merge,D,Save,Save_as,D,Quit,D,D,Lib,Lib,Lib,Lib,Lib,Lib,D,D,Sort1,Sort1,Sort1,Sort1,Sort1,Sort1,D,D,Find_file,Find_disk,Find_ext,D,D,Verw_file,Verw_disk,Gelijke,D,Invoeren,D,D,Printer,Screen
  Gosub Wopen("Hoofd-Menu")
Return
'
'
Procedure Fout
  Fout$="Foutnummer "+Str$(Err)+"|is opgetreden.|Gebruik OPSLAAN snel!!"
  Alert 1,Fout$,1," DANK U ",Dummy
  On Error Gosub Fout
  Resume Next
Return
'
'
'
'
Procedure About
  Deffill 1
  Rbox 180,25*Kleur,460,170*Kleur
  Box 210,25*Kleur,430,100*Kleur
  Rbox 260,120*Kleur,390,170*Kleur
  Rbox 260,120*Kleur,430,170*Kleur
  Pbox 438,34*Kleur,452,43*Kleur
  Box 280,125*Kleur,315,163*Kleur
  Defline 1,6,0,1
  Line 445,145*Kleur,445,160*Kleur
  Defline 1,2,0,0
  Line 210,40*Kleur,430,40*Kleur
  Line 210,55*Kleur,430,55*Kleur
  Line 210,70*Kleur,430,70*Kleur
  Line 210,85*Kleur,430,85*Kleur
  Deffill 2,2,1
  Fill 190,30*Kleur
  Fill 420,165*Kleur
  Fill 290,130*Kleur
  Deftext 1,16,0,8*Kleur
  Text 260,35*Kleur,Copyright$(0)
  Text 230,50*Kleur,Copyright$(1)
  Text 240,65*Kleur,Copyright$(2)
  Text 240,80*Kleur,Copyright$(3)
  Text 260,95*Kleur,Copyright$(4)
  Gosub Get_mouse
Return
'
'
'
Procedure Load
  N%=0
  Gosub Merge
Return
'
'
'
Procedure Merge
  Fileselect "\*.lst","",Filenaam2$
  If Filenaam2$<>""
    If N%=0
      Filenaam$=Filenaam2$
    Endif
    If Exist(Filenaam2$)=0
      Alert 3," Gevraagde file bestaat niet!!| ",1," SORRY ",Dummy
    Else
      Open "I",#1,Filenaam2$
      Defmouse 2
      Deftext 1,16,0,8*Kleur
      Text 250,85*Kleur,"Ogenblik A.U.B."
      Box 250,90*Kleur,420,110*Kleur
      Do
        Exit If Eof(#1)
        Text 310,105*Kleur,N%
        Inc N%
        Input #1,Regel$(N%)
      Loop
      Close #1
      Defmouse 0
    Endif
  Endif
Return
'
'
Procedure Save_as
  Filenaam$=""
  Gosub Save
Return
'
'
Procedure Save
  '  Alert 3," |Het opslaan werkt niet|in deze DEMO-versie.",1," SORRY ",Dummy
  '  N%=0
  If N%>0
    If Filenaam$=""
      Fileselect "\*.lst","",Filenaam$
    Endif
    If Filenaam$<>""
      Filenaam$=Left$(Filenaam$,Len(Filenaam$)-3)+"LST"
      Deftext 1,16,0,8*Kleur
      Text 250,85*Kleur,"Ogenblik A.U.B."
      Box 250,90*Kleur,420,110*Kleur
      Open "O",#1,Filenaam$
      Defmouse 2
      For K%=1 To N%
        Print #1,Regel$(K%)
        Dummy$=Str$(N%-K%)+" "
        Text 310,105*Kleur,Dummy$
      Next K%
      Close #1
      Defmouse 0
    Endif
  Endif
Return
'
'
'
'
Procedure Quit
  Einde=1
Return
'
'
'
Procedure Lib
  Einde%=0
  Drive%=M%-20
  If Bios(4,2,L:Varptr(Buffer$),0,0,Drive%)<>0
    Alert 3,"De aangesproken|  DISKDRIVE|is niet aanwezig.|",1," SORRY ",Dummy
    Einde%=1
  Endif
  If Einde%=0
    Index%=0
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef disknaam:  ";
    Form Input (12),Disknaam$
    If Disknaam$=""
      Disknaam$="unknown"
    Endif
    Disknaam$=Upper$(Left$(Disknaam$+Emp$,12))
    Clearw 1
    If Drive%=0 Or Drive%=1
      If Xbios(8,L:Varptr(Buffer$),L:0,Drive%,1,1,0,1)<>0
        Alert 3,"De aangesproken|  DISKDRIVE|werkt niet goed.|",1," SORRY ",Dummy
        Einde%=1
      Endif
    Endif
    If Einde%=0
      Path$=Chr$(45+M%)+":\"
      Gosub Index
    Endif
  Endif
Return
'
Procedure Index
  Print At(28,17*Kleur);"Opnemen in index?"
  Print At(15,18*Kleur);"JA (linker muis)"
  Print At(42,18*Kleur);"NEE (rechter muis)"
  Print At(8,9*Kleur);"NAAM           GROOTTE      DATUM      FOLDER"
  Line 150,60*Kleur,150,90*Kleur
  Line 250,60*Kleur,250,90*Kleur
  Line 360,60*Kleur,360,90*Kleur
  Box 10,75*Kleur,630,90*Kleur
  Box 10,60*Kleur,630,75*Kleur
  Gosub Suche(Path$,"*.*","CON:")
Return
'
'
'
' ******************************************************************
'
'
'
'
'
'
' SUCHE
'
' @Suche("D:\","*.*","CON:")
'
'
Procedure Suche(Path2$,File$,Out$)
  Oldpath$=Dir$(0)
  Olddrv%=Gemdos(25)+1
  '  Open "O",#1,Out$
  If Instr(Path2$,":")
    Chdrive Asc(Path2$)-64
    Path2$=Mid$(Path2$,Instr(Path2$,":")+1)
  Endif
  Chdir Path2$
  Void Gemdos(26,L:Basepage+128)           !setdta
  File$=File$+Chr$(0)
  Star$="*.*"+Chr$(0)
  Drv$=Chr$(Gemdos(25)+65)+":"
  @Suche1
  '  Close #1
  Chdir "\"+Olddir$
  Chdrive Olddrv%
Return
Procedure Suche1
  Local W%
  @Fsfirst
  While E%=0
    Path$=Dir$(0)
    Gosub Get_respond
    If Respond%=1
      Inc N%
      Regel$(N%)=Naam$+Ext$+"  "+Grootte$+"  "+Right$(Datum$,2)+Mid$(Datum$,4,2)+Left$(Datum$,2)+"  "+Disknaam$+"  "+Path$
    Endif
    '    Print #1,Z$'''Drv$+Dir$(0)+"\"
    @Fsnext
  Wend
  @Fsfirstdir
  Q%=0
  While E%=0
    If T% And 16
      If X$<>"." And X$<>".."
        W%=Q%
        Chdir X$
        @Suche1
        Chdir ".."
        @Fsfirstdir
        Q%=0
        While W%<>Q%
          Void Gemdos(79)
          Inc Q%
        Wend
      Endif
    Endif
    @Fsnext
    Inc Q%
  Wend
Return
Procedure Fsfirst
  E%=Gemdos(78,L:Varptr(File$),&H27)    !fsfirst
  @Getnam
Return
Procedure Fsfirstdir
  E%=Gemdos(78,L:Varptr(Star$),16)
  @Getnam
Return
Procedure Fsnext
  E%=Gemdos(79)
  @Getnam
Return
Procedure Getnam
  If E%
    X$=""
    T%=0
  Else
    X$=Space$(20)
    Bmove Basepage+158,Varptr(X$),14
    X$=Left$(X$,Instr(X$,Chr$(0))-1)
    T%=Peek(Basepage+149)                 !dta+21 Attribute
    '
    '
    '
    '
    '
    '
    Z$=Space$(20)
    Bmove Basepage+158,Varptr(Z$),14
    ' Filename
    Z$=Left$(Z$,Instr(Z$,Chr$(0))-1)
    A=Instr(Z$,".")
    If A>1
      Naam$=Left$(Left$(Z$,A-1)+Space$(10),9)
      Ext$=Mid$(Z$,A,4)
    Else
      Naam$=Left$(Z$+Space$(10),9)
      Ext$="    "
    Endif
    ' AuffÅllen auf gleiche LÑnge
    '    Z$=Left$(Z$+Space$(20),15)
    ' FilelÑnge oder <<DIR>>
    L$=Space$(9)
    A%=Peek(Basepage+149)                 !dta+21 Attribute
    If A% And 16
      Rset L$="<<DIR>>"
    Else
      If A% And 8
        Rset L$="<LABEL>"         !Diskettenname
      Else
        Rset L$=Str$(Lpeek(Basepage+154))  !dta+26=filelÑnge
      Endif
    Endif
    Grootte$=L$
    '    Z$=Z$+L$
    '
    ' Datum               dpeek(dta+24)
    '
    D%=Dpeek(Basepage+152)
    D$=Right$("0"+Str$(D% And 31),2)+"."
    D$=D$+Right$("0"+Str$(D%/32 And 15),2)+"."
    D$=D$+Str$(D% Div 512+80)
    Datum$=D$
  Endif
Return
'
'
'
'
'
'
'
Procedure Get_respond
  Pause 15
  Print At(4,11*Kleur);Naam$;Ext$
  Print At(20,11*Kleur);Grootte$
  Print At(34,11*Kleur);Datum$
  Print At(47,11*Kleur);Path$
  Gosub Get_mouse
  If Respond%=1
    Sound 2,13,3,6,10
    Sound 2,0
  Else
    Sound 2,13,1,3,10
    Sound 2,0
  Endif
  Print At(4,11*Kleur);Space$(15)
  Print At(20,11*Kleur);Space$(12)
  Print At(34,11*Kleur);Space$(12)
  Print At(47,11*Kleur);Space$(32)
Return
'
'
'
'
'
Procedure Sort1
  Deftext 1,16,0,8*Kleur
  Sort%=M%-28
  Gosub Sort
Return
'
'
'
'
'
Procedure Sort
  Defmouse 2
  Deftext 1,16,0,8*Kleur
  Text 250,85*Kleur,"Ogenblik A.U.B."
  Box 250,90*Kleur,420,110*Kleur
  S%(1)=1
  S%(2)=N%
  P%=2
  While P%<>0
    L%=S%(P%)
    Dec P%
    F%=S%(P%)
    Dec P%
    I%=F%
    While F%<L%
      J%=L%
      D$=Mid$(Regel$((F%+L%)/2),Soort(Sort%),Lengte(Sort%))
      While I%<=J%
        While Mid$(Regel$(I%),Soort(Sort%),Lengte(Sort%))<D$
          Inc I%
        Wend
        While Mid$(Regel$(J%),Soort(Sort%),Lengte(Sort%))>D$
          Dec J%
        Wend
        If I%<=J%
          Swap Regel$(I%),Regel$(J%)
          Inc I%
          Dec J%
        Endif
      Wend
      If F%<J%
        Inc P%
        S%(P%)=F%
        Inc P%
        S%(P%)=J%
      Endif
      F%=I%
      Text 310,105*Kleur,Str$(I%)+" "
    Wend
  Wend
  Defmouse 0
Return
'
'
'
Procedure Screen
  Clearw 1
  Print At(2,Kleur);Regel$(0)
  Tab%=3*Kleur+1
  K%=0
  Do
    Exit If K%>=N%
    Inc K%
    Gosub Prints(Regel$(K%))
  Loop
  Print At(32,21*Kleur);"KLIK MUIS_TOETS"
  Gosub Get_mouse
  Sound 2,13,3,6,10
  Sound 2,0
Return
'
'
'
Procedure Printer
  Gosub Check_print
  If Pr<>0
    Defmouse 2
    Print At(1,6*Kleur);Regel$(0)
    Tab%=0
    For K%=1 To N%
      Gosub Printl(Regel$(K%))
    Next K%
    Lprint Chr$(12)
    Defmouse 0
  Endif
Return
'
'
Procedure Printl(Pr$)
  If Tab%=58
    Tab%=0
    Lprint Chr$(12)
  Endif
  If Tab%=0
    Lprint
    Lprint
    Lprint Regel$(0)
    Lprint
  Endif
  Lprint Pr$
  Print At(1,7*Kleur);Space$(80)
  Print At(1,7*Kleur);Pr$
  Inc Tab%
Return
'
'
'
Procedure Prints(Pr$)
  If Tab%=3*Kleur
    Print At(32,21*Kleur);"KLIK MUIS_TOETS"
    Gosub Get_mouse
    Sound 2,13,3,6,10
    Sound 2,0
    Clearw 1
    Print At(2,Kleur);Regel$(0)
  Endif
  Print At(2,Tab%);Pr$
  Inc Tab%
  If Tab%=20*Kleur
    Tab%=3*Kleur
  Endif
Return
'
'
'
'
'
Procedure Get_mouse
  Do
    Mouse Dummy%,Dummy%,Respond%
    Exit If Respond%=1 Or Respond%=2
  Loop
Return
'
'
'
Procedure Invoeren
  Clearw 1
  Inc N%
  Print At(3,9*Kleur);Regel$(0)
  Line 145,60*Kleur,145,90*Kleur
  Line 220,60*Kleur,220,90*Kleur
  Line 285,60*Kleur,285,90*Kleur
  Line 395,60*Kleur,395,90*Kleur
  Box 10,75*Kleur,630,90*Kleur
  Box 10,60*Kleur,630,75*Kleur
  Label2:
  Naam$=""
  Ext$=""
  Print At(3,11*Kleur);
  Form Input 12,Naam$
  If Naam$=""
    Goto Label2
  Endif
  Naam$=Upper$(Naam$)
  A=Instr(Naam$,".")
  If A<>0
    Ext$=Right$(Naam$,3)
    Naam$=Left$(Naam$,A-1)
  Endif
  Naam$=Left$(" "+Naam$+Emp$,9)
  Ext$=Left$("."+Ext$+Emp$,4)
  Label1:
  Print At(20,11*Kleur);
  Form Input 6,Grootte$
  If Val(Grootte$)=0
    Print At(20,11*Kleur);"      "
    Goto Label1
  Endif
  Grootte$=Right$(Emp$+Grootte$,9)
  Datum$=""
  While Len(Datum$)<>6 Or Val(Datum$)=0 Or (Left$(Datum$,1)<>"8" And Left$(Datum$)<>"9")
    Print At(29,11*Kleur);"      "
    Print At(29,11*Kleur);
    Form Input 6,Datum$
  Wend
  Label3:
  Print At(37,11*Kleur);
  Form Input 12,Disknaam$
  If Disknaam$=""
    Goto Label3
  Endif
  Disknaam$=Left$(Disknaam$+Emp$,12)
  Print At(51,11*Kleur);
  Form Input 28,Path$
  Path$=Upper$(Path$)
  If Len(Path$)<>0 And Left$(Path$,1)<>"\"
    Path$="\"+Path$
  Endif
  Regel$(N%)=Naam$+Ext$+"  "+Grootte$+"  "+Datum$+"  "+Disknaam$+"  "+Path$
Return
'
'
'
Procedure Wopen(String$)
  Menu Off
  Titlew 1," "+String$+" "
  Clearw 1
  Menu Men$()
Return
'
'
'
'
Procedure Find_file
  Do
    Clearw 1
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef Filenaam:  ";
    Form Input (8),Filenaam$
    Exit If Len(Filenaam$)<>0
  Loop
  Clearw 1
  Filenaam$=Upper$(Filenaam$)
  A2$=Left$(Filenaam$,Len(Filenaam$)-1)
  A$=Right$(Filenaam$,1)
  Gosub Ask_dev
  If Pr<>0
    Tab%=0
    If Dev=1
      Tab%=3*Kleur+1
      Clearw 1
      Print At(2,Kleur);Regel$(0)
    Endif
    For K%=1 To N%
      P=0
      If A$="*"
        If Instr(Left$(Regel$(K%),10),A2$)<>0
          P=1
        Endif
      Else
        If Mid$(Regel$(K%),2,Len(Filenaam$))=Filenaam$
          P=1
        Endif
      Endif
      If P=1
        If Dev=1
          Gosub Prints(Regel$(K%))
        Else
          Gosub Printl(Regel$(K%))
        Endif
      Endif
    Next K%
    If Dev<>1
      Lprint Chr$(12)
    Else
      Print At(32,21*Kleur);"KLIK MUIS_TOETS"
      Gosub Get_mouse
      Sound 2,13,3,6,10
      Sound 2,0
    Endif
  Endif
Return
'
'
'
'
Procedure Find_disk
  Do
    Clearw 1
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef Disknaam:  ";
    Form Input (12),Disknaam$
    Exit If Len(Disknaam$)<>0
  Loop
  Clearw 1
  Disknaam$=Upper$(Disknaam$)
  A2$=Left$(Disknaam$,Len(Disknaam$)-1)
  A$=Right$(Disknaam$,1)
  Gosub Ask_dev
  If Pr<>0
    Tab%=0
    If Dev=1
      Tab%=3*Kleur+1
      Clearw 1
      Print At(2,Kleur);Regel$(0)
    Endif
    For K%=1 To N%
      P=0
      If A$="*"
        If Instr(Mid$(Regel$(K%),Soort(4),Lengte(4)),A2$)<>0
          P=1
        Endif
      Else
        If Mid$(Regel$(K%),35,Len(Disknaam$))=Disknaam$
          P=1
        Endif
      Endif
      If P=1
        If Dev=1
          Gosub Prints(Regel$(K%))
        Else
          Gosub Printl(Regel$(K%))
        Endif
      Endif
    Next K%
    If Dev<>1
      Lprint Chr$(12)
    Else
      Print At(32,21*Kleur);"KLIK MUIS_TOETS"
      Gosub Get_mouse
      Sound 2,13,3,6,10
      Sound 2,0
    Endif
  Endif
Return
'
'
'
'
Procedure Find_ext
  Do
    Clearw 1
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef Extensie:  ";
    Form Input (3),Ext$
    Exit If Len(Ext$)<>0
  Loop
  Clearw 1
  Ext$=Upper$(Ext$)
  A$=Right$(Ext$,1)
  A2$=Left$(Ext$,Len(Ext$)-1)
  Gosub Ask_dev
  If Pr<>0
    Tab%=0
    If Dev=1
      Tab%=3*Kleur+1
      Clearw 1
      Print At(2,Kleur);Regel$(0)
    Endif
    For K%=1 To N%
      P=0
      If A$="*"
        If Instr(Mid$(Regel$(K%),Soort(1),4),A2$)<>0
          '  If Instr(Left$(Regel$(K%),10),A2$)<>0
          P=1
        Endif
      Else
        If Mid$(Regel$(K%),11,Len(Ext$))=Ext$
          P=1
        Endif
      Endif
      If P=1
        If Dev=1
          Gosub Prints(Regel$(K%))
        Else
          Gosub Printl(Regel$(K%))
        Endif
      Endif
    Next K%
    If Dev<>1
      Lprint Chr$(12)
    Else
      Print At(32,21*Kleur);"KLIK MUIS_TOETS"
      Gosub Get_mouse
      Sound 2,13,3,6,10
      Sound 2,0
    Endif
  Endif
Return
'
'
'
Procedure Ask_dev
  Alert 1," Waar moet de uitvoer naartoe?? ",1," SCHERM | PRINTER ",Dev
  If Dev=2
    Gosub Check_print
  Else
    Pr=1
  Endif
Return
'
'
'
Procedure Check_print
  Pr=Bios(8,L:0)
  If Pr=0
    Alert 3,"De printer reageert niet.| |Controleer de printer en|probeer het nog eens.|",1," SORRY ",Dummy
  Endif
Return
'
'
'
Procedure Verw_file
  Do
    Clearw 1
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef Filenaam:  ";
    Form Input (8),Filenaam$
    Exit If Len(Filenaam$)<>0
  Loop
  Clearw 1
  Filenaam$=Upper$(" "+Filenaam$)
  Print At(28,17*Kleur);"Verwijderen uit index?"
  Print At(15,18*Kleur);"JA (linker muis)"
  Print At(42,18*Kleur);"NEE (rechter muis)"
  Print At(8,9*Kleur);"NAAM           GROOTTE      DATUM      FOLDER"
  Line 150,60*Kleur,150,90*Kleur
  Line 250,60*Kleur,250,90*Kleur
  Line 360,60*Kleur,360,90*Kleur
  Box 10,75*Kleur,630,90*Kleur
  Box 10,60*Kleur,630,75*Kleur
  For K=1 To N%
    Naam$=Mid$(Regel$(K),1,9)
    Ext$=Mid$(Regel$(K),10,4)
    Grootte$=Mid$(Regel$(K),16,9)
    Datum$=Mid$(Regel$(K),27,6)
    Path$=Mid$(Regel$(K),49,28)
    If Mid$(Naam$,1,Len(Filenaam$))=Filenaam$
      Gosub Get_respond
      If Respond%=1
        Regel$(K)=Chr$(255)+Chr$(255)+Chr$(255)
      Endif
    Endif
  Next K
  Gosub Resort
Return
'
'
'
Procedure Verw_disk
  Do
    Clearw 1
    Box 170,85*Kleur,470,100*Kleur
    Print At(25,12*Kleur);"Geef Extensie:  ";
    Form Input (3),Ext$
    Exit If Len(Ext$)<>0
  Loop
Return
'
'
'
Procedure Gelijke
Return
'
'
'
'
Procedure Resort
  Clearw 1
  Sort%=0
  Gosub Sort
  For K=N% To 1 Step -1
    If Left$(Regel$(K),1)=Chr$(255)
      Dec N%
    Endif
  Next K
Return
