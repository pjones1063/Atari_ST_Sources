Scale=Xbios(4)
If Scale=0 Then
  Print "80 column resolution only"
  End
Endif
Cls
Gosub Getdrive(*Dr$)
Sys$=Dr$+":\SYSTEM.DIR\"
K$=Dr$+":\DOWNLOAD.DIR\"
M$=Dr$+"A:\SYSOP.DIR\"
Chdrive Asc(Dr$)-64
Chdir Dr$+":"
Cls
Files
Mkdir "SYSTEM.DIR"
Mkdir "SYSOP.DIR"
Mkdir "BERICHT.DIR"
Mkdir "DOWNLOAD.DIR"
Mkdir "UPLOAD.DIR"
Cls
Print "Het aantal leden/berichten dat je nu intypt is niet het max. aantal"
Print "leden of berichten."
Print "Dus als je bv. 100 berichten initialiseert en in het BBS wordt het 101e"
Print "bericht verstuurd dan maakt GFA-basic de lijst gewoon langer."
Print
Print
Input "HOEVEEL LEDEN :",Aantal
Input "HOEVEEL BERICHTEN :",Aantal2
Rem
Rem
Rem      LEDEN !!!!!
Rem
Gosub Lidnamen1
Gosub Ledenlijst1
Lset Naam$="SYSOP"
Lset Password$="E.L.I.T.E"
Lset Tel$="020-849189"
Lset Woonpl$="AMSTERDAM"
Lset Lastdate$="12.12.1999"
Lset Lasttime$="23.59.59"
Dl$=Mki$(0)
Ul$=Mki$(0)
Level$=Mkl$(-1)
Login$=Mki$(1000)
Put #99,1
Put #98,1
Lset Naam$="***"
Lset Password$="  GFA BBS"
Lset Tel$="------BY-------"
Lset Woonpl$="JURGEN V/D HANDEL"
Level$=Mkl$(0)
Login$=Mki$(0)
For X=2 To Aantal
  Put #99,X
  Put #98,X
  Print At(10,15);"LIDNUMMER :";X;"        "
Next X
Close #99
Close #98
Rem
Rem       BERICHTENLIJST !!!!
Rem
Gosub Berichten
Gosub Berichtenlijst
Lset Aan$="***"
Lset Van$="---"
Lset Subject$="---"
Lset Datum$="12.12.1999"
Siggie$=Mki$(1)
Prive$=Mki$(0)
Reply$=String$(20,0)
Des$=Mki$(0)
Src$=Mki$(0)
For X=1 To Aantal2
  Put #94,X
  Put #95,X
  Print At(10,15);"BERICHTNUMMER :";X;"     "
Next X
Close #94
Close #95
Rem
Rem     SYSTEEM DATA !!!!!
Rem
Gosub Renewnetdata
Print At(10,15);"SYSTEEM DATA ........................"
Open "O",#1,Sys$+"SYSTEM.DAT"
File$=Mki$(1)+Mki$(0)+Mki$(0)+Mki$(1)+Mki$(0)
Print #1,File$;
Close #1
Print At(10,15);"SIG LIJST                                    "
Open "O",#11,Sys$+"SIG.LST"
Print #11,Mki$(0);
Print #11,"TEXT"
Print #11,"TEXT"
Print #11,Mki$(0);
Print #11,"Algemeen"
Print #11,"ALGEMEEN"
Close #11
Print At(10,15);"KLAAR                                  "
Pause 50
Procedure Lidnamen1
  Open "R",#99,Sys$+"LEDEN.REL",20
  Field #99,20 As Naam$
Return
Procedure Ledenlijst1
  Open "R",#98,Sys$+"LEDENDAT.REL",75
  Field #98,10 As Password$,15 As Tel$,20 As Woonpl$,10 As Lastdate$,8 As Lasttime$,2 As Dl$,2 As Ul$,4 As Level$,2 As Login$,2 As Lastmes$
Return
Procedure Berichtenlijst
  Open "R",#95,Sys$+"BERDAT.REL",77
  Field #95,20 As Van$,25 As Subject$,10 As Datum$,2 As Prive$,20 As Reply$
Return
Procedure Berichten
  Open "R",#94,Sys$+"BERICHT.REL",26
  Field #94,20 As Aan$,2 As Siggie$,2 As Des$,2 As Src$
Return
Rem
Rem
Rem
Procedure Getdrive(Dest)
  Cls
  Deftext 1,0,0,13
  Defline 1,4,0,0
  Text 60,30,"Please select a drive"
  Color 1
  Box 120,49*Scale,520,151*Scale
  Defline 1,1
  Deffill 1,1
  Deftext 0,16,0,13
  For X=0 To 3
    Pbox 180+(X*80),70*Scale,220+(X*80),90*Scale
    Pbox 180+(X*80),110*Scale,220+(X*80),130*Scale
    Box 179+(X*80),69*Scale,221+(X*80),91*Scale
    Box 179+(X*80),109*Scale,221+(X*80),131*Scale
    Graphmode 3
    Text 195+(X*80),85*Scale,20,Chr$(X+65)
    Text 195+(X*80),125*Scale,20,Chr$(X+69)
    Graphmode 1
  Next X
  Flag=False
  Repeat
    If Mousek=1 Then
      For User=1 To 4
        X=User-1
        If Mousex>=180+X*80 And Mousex<=220+X*80 And Mousey>=70*Scale And Mousey<=90*Scale
          *Dest=Chr$(X+65)
          Flag=True
        Endif
        If Mousex>=180+X*80 And Mousex<=220+X*80 And Mousey>=110*Scale And Mousey<=130*Scale
          *Dest=Chr$(X+69)
          Flag=True
        Endif
      Next User
    Endif
  Until Flag=True
Return
Procedure Renewnetdata
  Open "O",#1,Sys$+"ELITE.DAT"
  Print #1,Mki$(1);
  Print #1,Chr$(1)+Chr$(1);
  For X=1 To 10
    Print #1,Chr$(0);
  Next X
  Print #1,"05:00:00"
  Close #1
Return
