Rem
Rem ***********************************
Rem ** GESTION DU DIGITALISEUR VIDEO **
Rem **   format 210x128 ou 192x128   **
Rem ***********************************
Debut:
Z%=0
B=Peek(&HFB0040) ! init carte: gel inactif
Dim A%(25000) ! soit 100Koctets r‚serv‚s
Asm1$=Space$(400)
Adr1%=Varptr(Asm1$)
Bload "ram2st.b",Adr1%
Asm2$=Space$(400)
Adr2%=Varptr(Asm2$)
Bload "affpix.b",Adr2%
Rem
Rem ***********************************
Rem **   entete TGA format 210x128   **
Rem ***********************************
Rem
A%(0)=&H200
A%(1)=&H0
A%(2)=&H0
A%(3)=&HD2008000
A%(4)=&H18200000
Q%=209
Rem
Rem ***********************************
Rem **    d‚marrage du programme     **
Rem ***********************************
Rem
Gosub Paletgris
Rem Setcolor 0,0,0,0
T$="controle.pi1"
Gosub Titre
Rem ---------------------------------
Rem ----- traitement de l'image -----
Rem ---------------------------------
Traitement:
While Mousek=0
Wend
If Mousek=2 Then
  Gosub Gel
  Goto Traitement
Else
  Mouse X,Y,K
Endif
If X<292 Or Y>197 Then
  Goto Traitement
Endif
If Y<161 Then
  If Y>7 And Y<23 Then
    Gosub Save_tga
  Else
    If Y>24 And Y<40 Then
      Gosub Load_tga
    Else
      If Y>41 And Y<57 Then
        Gosub Options
      Else
        If Y>58 And Y<74 Then
          Gosub Fin
        Endif
      Endif
    Endif
  Endif
Else
  If X<305 And Y<187 And Y>178 Then
    Gosub Scanner
  Else
    If X>305 And Y<187 And Y>178 Then
      Gosub Scanner_r
    Else
      If X<305 And Y>188 Then
        Gosub Scanner_v
      Else
        If X>305 And Y>188 Then
          Gosub Scanner_b
        Else
          If Y<176 Then
            Gosub Gel
          Endif
        Endif
      Endif
    Endif
  Endif
Endif
Goto Traitement
Rem
Rem ------------------------------------------------
Rem ------- FICHIER DES PALETTES DES COULEURS ------
Rem -------  NIVEAUX DE GRIS/ROUGE/VERT/BLEU  ------
Rem ------------------------------------------------
Procedure Paletgris
  For X%=0 To 15
    Read A,B,C
    Setcolor X%,A,B,C
  Next X%
  Data 0,0,0,1,1,1,2,2,2,3,3,3,4,4,4,5,5,5,6,6,6,7,7,7
  Data 3,3,3,7,0,0,0,7,0,7,7,7,0,0,7,2,2,2,7,7,0,7,4,0
  Restore
Return
Rem ------------------------------------------------
Procedure Paletrouge
  For D=0 To 15
    Read A,B,C
  Next D
  For X%=0 To 15
    Read A,B,C
    Setcolor X%,A,B,C
  Next X%
  Data 0,0,0,2,0,0,4,0,0,6,1,0,7,3,0,7,5,0,7,7,3,7,7,5
  Data 3,3,3,7,0,0,0,7,0,7,7,7,0,0,7,2,2,2,7,7,0,7,4,0
  Restore
Return
Rem ------------------------------------------------
Procedure Paletvert
  For D=0 To 31
    Read A,B,C
  Next D
  For X%=0 To 15
    Read A,B,C
    Setcolor X%,A,B,C
  Next X%
  Data 0,0,0,0,2,0,0,4,0,2,6,0,4,7,0,6,7,0,7,7,3,7,7,4
  Data 3,3,3,7,0,0,0,7,0,7,7,7,0,0,7,2,2,2,7,7,0,7,4,0
  Restore
Return
Rem ------------------------------------------------
Procedure Paletbleu
  For D=0 To 47
    Read A,B,C
  Next D
  For X%=0 To 15
    Read A,B,C
    Setcolor X%,A,B,C
  Next X%
  Data 0,0,0,0,0,2,0,0,4,0,1,6,0,3,7,0,5,7,3,7,7,5,7,7
  Data 3,3,3,7,0,0,0,7,0,7,7,7,0,0,7,2,2,2,7,7,0,7,4,0
  Restore
Return
Rem ----------------------------------------------
Rem -------    CHARGEMENT au format TGA    -------
Rem ----------------------------------------------
Procedure Load_tga
  Fileselect "A:\*.TGA","",I$
  If I$<>"" Then
    Bload I$,Varptr(A%(0))
    If Lpeek(Varptr(A%(3)))=&HD2008000 Then
      Q%=209
    Else
      If Lpeek(Varptr(A%(3)))=&HD2008000 Then
        Q%=191
      Else
        M$=" l'image charg‚e n'est pas | dans un format affichable"
        Alert 3,M$,0,"OK",Q
        A%(0)=&H200
        A%(1)=&H0
        A%(2)=&H0
        A%(3)=&HD2008000
        A%(4)=&H18200000
        Q%=209
        Goto Retour
      Endif
    Endif
    Gosub Clear
    Gosub Paletgris
    S%=4         !affichage de la moyenne des composantes R-V-B
    R%=Varptr(A%(0))                 !adresse de base du buffer
    Void C:Adr2%(L:R%,W:Q%,W:S%)
  Endif
  Retour:
Return
Rem ----------------------------------------------
Rem ------     SCANNER la RAM VIDEO      ---------
Rem ----------------------------------------------
Procedure Scanner
  Rem
  Rem ***************************
  Rem implantation dans le buffer
  Rem ***************************
  Rem
  S%=3
  R%=Varptr(A%(0))
  Void C:Adr1%(L:R%,W:Q%,W:S%) ! acquisition
  Gosub Clear
  Gosub Paletgris
  Void C:Adr2%(L:R%,W:Q%,W:S%) ! + affichage
  B=Peek(&HFB0040)     !suppression GEL
Return
Rem
Procedure Scanner_r
  Rem
  Rem ***************************
  Rem implantation dans le buffer
  Rem ***************************
  Rem
  S%=0
  R%=Varptr(A%(0))
  Void C:Adr1%(L:R%,W:Q%,W:S%)
  Gosub Clear
  Gosub Paletrouge
  Void C:Adr2%(L:R%,W:Q%,W:S%)
  B=Peek(&HFB0040)     !suppression GEL
Return
Rem
Procedure Scanner_v
  Rem
  Rem ***************************
  Rem implantation dans le buffer
  Rem ***************************
  Rem
  S%=1
  R%=Varptr(A%(0))
  Void C:Adr1%(L:R%,W:Q%,W:S%)
  Gosub Clear
  Gosub Paletvert
  Void C:Adr2%(L:R%,W:Q%,W:S%)
  B=Peek(&HFB0040)     !suppression GEL
Return
Rem
Procedure Scanner_b
  Rem
  Rem ***************************
  Rem implantation dans le buffer
  Rem ***************************
  Rem
  S%=2
  R%=Varptr(A%(0))
  Void C:Adr1%(L:R%,W:Q%,W:S%)
  Gosub Clear
  Gosub Paletbleu
  Void C:Adr2%(L:R%,W:Q%,W:S%)
  B=Peek(&HFB0040)     !suppression GEL
Return
Rem
Rem ----------------------------------------------
Rem -------    SAUVEGARDE au format TGA    -------
Rem ----------------------------------------------
Procedure Save_tga
  Rem ************************************
  Rem **     entete de fichier TGA      **
  Rem ************************************
  Rem
  Fileselect "A:\*.TGA","IMAGE.TGA",I$
  If Q%=191 Then
    M%=18+(192*128*3)
  Else
    M%=18+(210*128*3)
  Endif
  If I$<>"" Then
    Bsave I$,Varptr(A%(0)),M%
  Endif
Return
Rem ----------------------------------------------
Rem ----------     fin du programme     ----------
Rem ----------------------------------------------
Procedure Fin
  M$="Avez vous sauvegard‚|    votre image|  avant de quitter?"
  Alert 3,M$,0,"annuler|quitter",Q
  If Q=2 Then
    Setcolor 0,7,7,7
    Setcolor 15,0,0,0
    Setcolor 2,0,7,0
    End
  Endif
Return
Rem ----------------------------------------------
Rem ------  CHARGEMENT de la PRESENTATION  -------
Rem ----------------------------------------------
Procedure Titre
  Dim Amg%(32034)
  Bload T$,Varptr(Amg%(0))
  Bmove Varptr(Amg%(0))+34,Xbios(2),32000
  Erase Amg%()
Return
Rem ----------------------------------------------
Procedure Clear
  Dim Iimg%(37)
  For I=1 To 187
    Bmove Varptr(Iimg%(0)),Xbios(2)+160*I+1287,134
  Next I
  Erase Iimg%()
Return
Rem ----------------------------------------------
Procedure Gel
  If Z%=1 Then
    Z%=0
    B=Peek(&HFB0040) ! gel inactif
  Else
    Z%=1
    B=Peek(&HFB00C0) ! gel activ‚
  Endif
  Pause 25
Return
Rem ----------------------------------------------
Procedure Options
  M$="  Selection du format  |192 pixels: VGA/3,75MHz|210 pixels: STF/4,10MHz"
  Alert 3,M$,0,"192|210",Q
  If Q=1 Then
    Q%=191
    A%(3)=&HC0008000
  Else
    Q%=209
    A%(3)=&HD2008000
  Endif
Return
End
