' programme de scan des r‚pertoires et liste des images JPEG.
' program‚ par MAXILYS.       modifi‚ par BLISS le 20/5/1994.
'                  pas de copyright
'
' OPEN "o",#99,"f:\jpeg_lst.txt"
DO
  FILESELECT #"DOSSIER ¶ SCANNER","\*.JPG","",chem$
  EXIT IF chem$=""
  IF RIGHT$(chem$)<>"\"
    a%=RINSTR(chem$,"\")
    chem$=LEFT$(chem$,a%)
  ENDIF
  PRINT ''chem$
  ' PRINT #99,''chem$
  scan_jpeg
LOOP
' CLOSE #99
'
> PROCEDURE scan_jpeg
  ~FSETDTA(BASEPAGE+128)
  a%=FSFIRST(chem$+"*.JPG",0)
  DO UNTIL a%
    nom$=CHAR{BASEPAGE+158}
    PRINT nom$;TAB(12);"|";
    ' PRINT #99,nom$;TAB(12);"|";
    taille_jpeg
    a%=FSNEXT()
  LOOP
RETURN
> PROCEDURE taille_jpeg
  larg&=0
  haut&=0
  flag&=0
  valu&=0
  OPEN "i",#1,chem$+nom$
  BGET #1,V:flag&,2
  ptr%=2
  DO
    BGET #1,V:flag&,2
    BGET #1,V:valu&,2
    ADD ptr%,4
    EXIT IF (flag& AND 255)=&HC0
    ADD ptr%,valu&-2
    SEEK #1,ptr%
  LOOP
  SEEK #1,ptr%+1
  BGET #1,V:haut&,2
  BGET #1,V:larg&,2
  PRINT STR$(larg&,4);STR$(haut&,4);"|";STR$(LOF(#1),7);"|"
  ' PRINT #99,STR$(larg&,4);STR$(haut&,4);"|";STR$(LOF(#1),7);"|"
  CLOSE #1
RETURN
