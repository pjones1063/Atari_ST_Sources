' Programme de scan des r‚pertoires et liste
'   des modules soundtrack XTracker .DMF
'
'      fait par BLISS le 11/12/1994
'             pas de copyright
'
' merci … Maxilys pour le squelette du programme.
' merci … Simplet pour la documentation.
'
' OPEN "o",#99,"F:\dmf_lst.txt"
DO
  FILESELECT #"DOSSIER ¶ SCANNER","\*.DMF","",chem$
  EXIT IF chem$=""
  IF RIGHT$(chem$)<>"\"
    a%=RINSTR(chem$,"\")
    chem$=LEFT$(chem$,a%)
  ENDIF
  PRINT ''chem$
  ' PRINT #99,''chem$
  scan_dmf
LOOP
' CLOSE #99
'
> PROCEDURE scan_dmf
  ~FSETDTA(BASEPAGE+128)
  a%=FSFIRST(chem$+"*.DMF",0)
  DO UNTIL a%
    nom$=CHAR{BASEPAGE+158}
    PRINT nom$;TAB(12);"|";
    ' PRINT #99,nom$;TAB(12);"|";
    titre_dmf
    a%=FSNEXT()
  LOOP
RETURN
> PROCEDURE titre_dmf
  LOCAL tit$,aut$,nbvoies|,chunk$,chunklen%
  OPEN "i",#1,chem$+nom$
  SEEK #1,13            ! on se place sur le song name.
  tit$=STRING$(31,0)
  BGET #1,V:tit$,30     ! qui fait 30 caractŠres
  tit$=CHAR{V:tit$}
  '
  aut$=STRING$(21,0)  ! juste aprŠs il y a le nom de l'auteur
  BGET #1,V:aut$,20   ! qui fait 20 caractŠres
  aut$=CHAR{V:aut$}
  '
  SEEK #1,66         ! on se place sur le d‚but du premier chunk
  DO
    chunk$=INPUT$(4,#1)
    EXIT IF chunk$="PATT"       ! on cherche le chunk PATT
    '
    BYTE{V:chunklen%+3}=INP(#1) ! tant qu'on l'a pas trouv‚
    BYTE{V:chunklen%+2}=INP(#1) ! on calcule la taille du chunk
    BYTE{V:chunklen%+1}=INP(#1) ! qui est au format Intel
    BYTE{V:chunklen%}=INP(#1)
    RELSEEK #1,chunklen%        ! et on le passe
  LOOP
  '
  RELSEEK #1,6          ! on se place sur le nombre de voies du chunk PATT
  nbvoies|=INP(#1)
  '
  PRINT tit$;TAB(43);"|";aut$;TAB(64);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |"
  ' PRINT #99,tit$;TAB(43);"|";aut$;TAB(64);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |"
  CLOSE #1
RETURN
