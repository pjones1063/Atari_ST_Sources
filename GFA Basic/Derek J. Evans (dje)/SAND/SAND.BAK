'
' ----------------------------------------------------------------------------
' Name: SAND.GFA
' Desc: Optimized version of OMIKRONMAN's Sand Particle Semo
' Auth: Derek J. Evans 2023
'
' Credit: OMIKRONMAN for initial idea & OMIKRON source version
' ----------------------------------------------------------------------------
'
DEFLIST 0
DEFWRD "A-Z"
'
main
'
END
'
PROCEDURE hourglass.shape(y,VAR x1,x2)
  x1=SHR(maxx,1)-5
  x2=maxx-x1
  IF y<topy
    SUB x1,topy-y
    ADD x2,topy-y
  ELSE IF y>boty
    SUB x1,y-boty
    ADD x2,y-boty
  ENDIF
RETURN
'
PROCEDURE draw.line(x1,y,x2,c)
  ARECT SHL(x1,1),SHL(y,1),SHL(x2,1),SHL(y,1)+1,c,1,V:arect%(0),1
  WHILE x1<=x2
    t|(x1,y)=c
    INC x1
  WEND
RETURN
'
PROCEDURE main
  LOCAL maxx,maxy,topy,boty
  LOCAL x,y,x1,x2,y1,y2
  CLS
  HIDEM
  maxx=150
  maxy=98
  topy=SHR(maxy,1)-10
  boty=SHR(maxy,1)+10
  DIM arect%(0),t|(maxx,maxy)
  LET arect%(0)=-1
  y1=10
  y2=maxy-8
  FOR y=y1 TO y2
    hourglass.shape(y,x1,x2)
    draw.line(MAX(x1-3,0),y,x1-1,1)
    draw.line(x2+1,y,MIN(x2+3,maxx),1)
    IF y>y1+2
      IF y<50
        draw.line(x1,y,x2,4+RAND(10))
      ENDIF
    ENDIF
  NEXT y
  draw.line(x1-4,y2+1,x2+4,1)
  draw.line(x1-4,y2+2,x2+4,1)
  REPEAT
    FOR y=y2 DOWNTO y1
      hourglass.shape(y,x1,x2)
      FOR x=x1 TO x2
        IF t|(x,y)>1
          u=PRED(x+RAND(3))
          v=SUCC(y)
          IF t|(u,v)=0
            SWAP t|(x,y),t|(u,v)
            ARECT SHL(x,1),SHL(y,1),SHL(x,1)+1,SHL(y,1)+1,0,1,V:arect%(0),0
            ARECT SHL(u,1),SHL(v,1),SHL(u,1)+1,SHL(v,1)+1,t|(u,v),1,V:arect%(0),0
          ENDIF
        ENDIF
      NEXT x
    NEXT y
  UNTIL LEN(INKEY$)
RETURN
'
' ----------------------------------------------------------------------------
' END OF SOURCE
' ----------------------------------------------------------------------------
