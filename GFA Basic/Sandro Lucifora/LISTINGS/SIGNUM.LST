$m9000                                                  ! Speicher reservieren
'
gl_apid=APPL_INIT()                                     ! Applikationsnummer
~MENU_REGISTER(gl_apid,"  SIGNUM ACC")                  ! ACC in MenÅleiste eintragen
@multi
'
PROCEDURE multi
  DO
    welcher%=EVNT_MULTI(&H10,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,mx%,my%,mb%,k_state%,key%,anz_clicks%)
    SELECT MENU(1)
    CASE 40                                             ! AC_OPEN
      ALERT 1,"Mein Name ist Hase",1,"  ok  ",a%        ! Wenn ACC in MenÅ angewÑhlt wird
    CASE &H4010                                         ! HELLO_ACC
      @communicate                                      ! ACC von Signum!2 angewÑhlt
    ENDSELECT
  LOOP
RETURN
'
PROCEDURE communicate
  sigid%=APPL_FIND("SIGNUM2 ")
  IF sigid%<0
    ALERT 3,"Kein SIGNUM!2 gefunden.",1,"  OK  ",a%
  ELSE
    IF MENU(7)=&H10
      @msg_send(&H4000,0)
      IF @msg_receive AND (MENU(1)=&H4040)
        IF @fix_adress
          @msg_send(&H40FF,0)
          @user_routine
        ENDIF
        @msg_send(&H4020,0)
      ENDIF
    ELSE
      @msg_send(&H4021,0)
    ENDIF
  ENDIF
RETURN
'
PROCEDURE msg_send(command&,attr&)
  DIM msgbout&(8)
  msgbout&(0)=command&
  msgbout&(1)=sigid%
  msgbout&(2)=0
  msgbout&(3)=0
  msgbout&(4)=attr&
  ~APPL_WRITE(sigid%,16,V:msgbout&(0))
  ERASE msgbout&()
RETURN
'
FUNCTION msg_receive
  LOCAL w%,d%
  w%=EVNT_MULTI(16+32,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1000,d%,d%,d%,d%,d%,d%)
  RETURN (w%=16)
ENDFUNC
'
FUNCTION fix_adress
  LOCAL adr%
  DIM dum&(1)
  dum&(0)=MENU(5)
  dum&(1)=MENU(6)
  adr%=LPEEK(V:dum&(0))
  sigversion={adr%+4}
  IF {adr%}=&H8011954
    RETURN TRUE
  ELSE
    RETURN FALSE
  ENDIF
  ERASE dum&()
ENDFUNC
'
PROCEDURE user_routine
  ALERT 1," Ahhh, nen ACC in SIGNUM",1,"  OK  ",a%
RETURN

