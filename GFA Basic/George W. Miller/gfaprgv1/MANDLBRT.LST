Dim Palette(15),Pal2(15)
'
Hidem
'
'
Rez=Xbios(4)
If Rez<>0
  Alrt$="Fractals look best in|Low Resolution"
  Alert 1,Alrt$,1,"Continue|Quit",B
  If B=2
    End
  Endif
Endif
'
If Rez=0
  Screen_height=200            ! Low res screen height
  Screen_width=320             ! Low res screen width
Endif
'
If Rez=1                       ! Medium resolution screen
  Screen_height=200
  Screen_width=640
Endif
'
If Rez=2                       ! High resolution screen
  Screen_height=400
  Screen_width=640
Endif
'
' Main Loop begins here
'
Gosub Save_palette
'
Do
  '
  Gosub Values
  If Rez=0
    Gosub Set_new_colors
  Endif
  Gosub Calculate
  If Rez=0
    Gosub Restore_palette
  Endif
  Gosub Display_it
  '
Loop
'
End
'
Procedure Display_it
  '
  Display:
  Cls
  Print At(1,3);"Fractal Image"
  Print At(1,5);Count;" Iterations Calculated."
  Seconds=Int((Time_finish-Time_start)/2)/100
  Min=Int(Seconds/60)
  If Min<=0
    Min=0
  Endif
  Seconds=Abs(Int((Seconds Mod 60)*100)/100)
  Print At(1,7);Min;" Minutes and ";Seconds;" Seconds required."
  Print At(1,8);"Number of iterations: ";Iteration_limit
  Print At(1,10);"X Center: ";X_center
  Print At(1,11);"Y Center: ";Y_center
  Print At(1,12);"Scaling factor: ";Range
  Print At(1,14);"Minimum real value (xmin): ";Real_min
  Print At(1,15);"Maximum real value (xmax): ";Real_max
  Print At(1,16);"Minimum imaginary value (ymin): ";Imag_min
  Print At(1,17);"Maximum Imaginary value (ymax): ";Imag_max
  Print At(1,19);"Press:"
  Print At(5,20);"<V> to View Fractal"
  Print At(5,21);"<P> to Plot new Fractal"
  Print At(5,22);"<S> to Save .NEO file"
  Print At(5,23);"<Q> to Quit"
  '
  Key:
  Repeat
    A$=Inkey$
  Until A$<>""
  '
  If Upper$(A$)="V"
    Sget Screen2$
    If Rez=0
      Gosub Set_new_colors
    Endif
    Sput Screen1$
    Repeat
    Until Inkey$<>""
    Gosub Restore_palette
    Sput Screen2$
  Endif
  '
  If Upper$(A$)="P"
    Goto Finito
  Endif
  '
  If Upper$(A$)="Q"
    Showm
    Cls
    End
  Endif
  '
  If Upper$(A$)="S"
    '
    If Rez<>0
      Cls
      Print "Must be Low Resolution for Neochrome"
      Repeat
      Until Inkey$<>""
      Cls
      Goto Not_neo
    Endif
    '
    Sget Screen2$
    Default$="A:\*.NEO"
    Fileselect Default$,"",Infile$
    Cls
    Gosub Set_new_colors
    Sput Screen1$
    Open "o",#1,Infile$
    '
    ' First three words to zero
    '
    For I=0 To 2
      Out #1,0
      Out #1,0
    Next I
    '
    ' Save color palette
    '
    For I=0 To 15
      Hi=Int(Pal2(I)/256)
      Lo=Pal2(I)-256*Hi
      Out #1,Hi
      Out #1,Lo
    Next I
    '
    ' Color cycling info (not needed)
    '
    For I=0 To 89
      Out #1,0
    Next I
    '
    ' Save screen info
    '
    Bput #1,Xbios(3),32000
    '
    Close
    Sput Screen2$
    Gosub Restore_palette
    '
    Not_neo:
    '
  Endif
  '
  Goto Display
  '
  Finito:
  '
Return
'
Procedure Values
  '
  Cls
  Input "Number of iterations";Iteration_limit
  Input "Center X";X_center
  Input "Center Y";Y_center
  Input "Scale Range";Range
  Real_min=X_center-(Range/2)
  Real_max=X_center+(Range/2)
  Imag_max=Y_center+((Range/2)*0.77)
  Imag_min=Y_center-((Range/2)*0.77)
  Cls
Return
'
Procedure Calculate
  '
  Time_start=Int(Timer)
  Max_limit=100000000
  Count=0
  Real=(Real_max-Real_min)/(Screen_width-1)
  Imaginary=(Imag_max-Imag_min)/(Screen_height-1)
  For Y=0 To Screen_height
    For X=0 To Screen_width
      Lreal=Real_min+X*Real
      Limag=Imag_min+Y*Imaginary
      Re=Lreal
      Im=Limag
      Depth=0
      Calc_loop:
      Inc Count
      X1=Re^2
      Y1=Im^2
      Im=2*Re*Im-Limag
      Re=X1-Y1-Lreal
      Inc Depth
      If ((Depth=Iteration_limit) Or ((X1+Y1)>Max_limit))
        Goto Finished_yet
      Else
        Goto Calc_loop
      Endif
      Finished_yet:
      If (Depth<Iteration_limit)
        Gosub Draw_point
      Endif
      If Inkey$<>""
        Gosub Break
      Endif
    Next X
  Next Y
  '
  ' Save screen into screen1$
  '
  Sget Screen1$
  '
  Time_finish=Int(Timer)         ! time of completion
  '
Return
'
Procedure Break
  '
  Sget Screen1$
  Time_finish=Int(Timer)
  If Rez=0
    Gosub Restore_palette
  Endif
  Cls
  Print At(1,3);"Fractal Image"
  Print At(1,5);Count;" Iterations Calculated."
  Seconds=Int((Time_finish-Time_start)/2)/100
  Min=Int(Seconds/60)
  If Min<=0
    Min=0
  Endif
  Seconds=Abs(Int((Seconds Mod 60)*100)/100)
  Print At(1,7);Min;" Minutes and ";Seconds;" Seconds required."
  Print At(1,8);"Number of iterations: ";Iteration_limit
  Print At(1,10);"X Center: ";X_center
  Print At(1,11);"Y Center: ";Y_center
  Print At(1,12);"Scaling factor: ";Range
  Print At(1,14);"Minimum real value (xmin): ";Real_min
  Print At(1,15);"Maximum real value (xmax): ";Real_max
  Print At(1,16);"Minimum imaginary value (ymin): ";Imag_min
  Print At(1,17);"Maximum Imaginary value (ymax): ";Imag_max
  Print At(1,19);"Press:"
  Print At(5,20);"<C> to Continue Plot"
  Print At(5,21);"<E> to Return to Main Menu"
  '
  Keyit:
  Repeat
    A$=Inkey$
  Until A$<>""
  '
  If Upper$(A$)="E"
    X=Screen_width
    Y=Screen_height
    Sput Screen1$
    Goto Finit
  Endif
  '
  If Upper$(A$)="C"
    If Rez=0
      Gosub Set_new_colors
    Endif
    Sput Screen1$
    Goto Finit
  Endif
  '
  Goto Keyit
  '
  Finit:
Return
'
Procedure Draw_point
  '
  Colour=Depth Mod 16
  Color Colour
  If (Depth>100)
    Plot X,Y
  Else
    Gosub Greater_than_two
  Endif
Return
'
Procedure Greater_than_two
  '
  If Depth Mod 2
    Plot X,Y
  Endif
Return
'
Procedure Set_new_colors
  '
  ' Set up color table
  '
  Pal2(0)=&H0
  Pal2(1)=&H75
  Pal2(2)=&H765
  Pal2(3)=&H401
  Pal2(4)=&H410
  Pal2(5)=&H530
  Pal2(6)=&H241
  Pal2(7)=&H62
  Pal2(8)=&H474
  Pal2(9)=&H744
  Pal2(10)=&H731
  Pal2(11)=&H165
  Pal2(12)=&H14
  Pal2(13)=&H750
  Pal2(14)=&H423
  Pal2(15)=&H601
  '
  '
  For I=0 To 15
    Setcolor I,Pal2(I)
  Next I
Return
'
'
' Save Original Color Palette
'
Procedure Save_palette
  Local I
  '
  ' must be located at start of program:  Dim Palette(15)
  '
  '
  For I=0 To 15
    Palette(I)=Xbios(7,W:I,W:-1)
  Next I
Return
'
'
'
' Restore Original Color Palette
'
Procedure Restore_palette
  Local I
  '
  For I=0 To 15
    Setcolor I,Palette(I)
  Next I
Return
