'
' Saddle Function  Plot
'
' This program plots the function:
'      Y^2/A^2 + X^2 / B^2 - CZ = 0
'
' The program is somewhat User friendly, but not much effort was put into
' making it exceptionally pretty, as it is only a demo.
'
Dim Palette(4),Pal2(4)
'
' Make sure Color monitor is being used.
'
If Xbios(4)<>1
  Alrt$="|Medium Resolution Only!!"
  Alert 3,Alrt$,1,"Okay",B
  End
Endif
'
Cls
Gosub Save_palette                        ! We want to restore original colors
'                                           when we exit from this application.
Hidem
Start:
'
' Set up color table for our med resolution colors
'
Pal2(0)=&H654
Pal2(1)=&H5
Pal2(2)=&H500
Pal2(3)=&H7
'
Gosub Set_colour         ! Install our colors in palette
'
Pi=4*Atn(1)
'
' Convert Degrees to Radians
'
Dr=Pi/180
'
'
' Define Med res screen size
'
Hmin=0
Hmax=639
Lmin=0
Lmax=199
Hres=Hmax-Hmin
Lres=Lmax-Lmin
'
' Define function for plot
'
Deffn F1(X,Y)=(Y*Y)-(X*X)
'
Hc=Hmin+Int(Hres/2)
Lc=Lmin+Int(Lres/2)
Scaleh=15
Scalev=3.5
'
' Find angles from center to corners of screen
'
C1=Atn((Lc-Lmin)/(Hmax-Hc))
C2=Pi-Atn((Lc-Lmin)/(Hc-Hmin))
C3=Pi+Atn((Lmax-Lc)/(Hc-Hmin))
C4=2*Pi-Atn((Lmax-Lc)/(Hmax-Hc))
'
Kolor=1
'
' Get desired coordinates for plot.
Cls
'
Print "Angles for :"
Input "             X= ";Degx
Input "             Y= ";Degy
Input "             Z= ";Degz
Cls
Degrees=Degx
Gosub Degrees_to_radians
Sinxa=Sin(A)
Cosxa=Cos(A)
Gosub Draw_the_axis
Degrees=Degy
Gosub Degrees_to_radians
Sinya=Sin(A)
Cosya=Cos(A)
Gosub Draw_the_axis
Degrees=Degz
Gosub Degrees_to_radians
Sinza=Sin(A)
Cosza=Cos(A)
Gosub Draw_the_axis
'
' Plot the surface
'
For Ty=-5 To 5 Step 0.4
  Kolor=2
  For Tx=0 To 5 Step 0.4
    Tz=Fn F1(Tx,Ty)
    Gosub Project
    Color Kolor
    Plot Mh,Mv
  Next Tx
  Kolor=3
  For Tx=-5 To 0 Step 0.1
    Tz=Fn F1(Tx,Ty)
    Gosub Project
    Color Kolor
    Plot Mh,Mv
  Next Tx
Next Ty
Print At(20,1);"Press any key to continue - <Esc> to End"
Repeat
  A=Asc(Inkey$)
Until A<>0
If A<>27
  Goto Start
Endif
Gosub Restore_palette
Cls
Showm
End
'
Procedure Draw_the_axis
  Tana=Tan(A)
  Gosub Find_endpoint
  H1=Hz
  L1=Lz
  Degrees=Degrees+180
  Gosub Degrees_to_radians
  Gosub Find_endpoint
  H2=Hz
  L2=Lz
  Color Kolor
  Line H1,L1,H2,L2
Return
'
Procedure Degrees_to_radians
  While Degrees<0
    Add Degrees,360
  Wend
  While Degrees>=360
    Sub Degrees,360
  Wend
  A=Degrees*Dr
Return
'
Procedure Find_endpoint
  If A<=C1 Or A>C4
    Hz=Hmax
    Lz=Lc-(Hmax-Hc)*Tana
    Goto Complete_find
  Endif
  If A<C2
    Lz=Lmin
    Hz=Hc+(Lc-Lmin)/Tana
    Goto Complete_find
  Endif
  If A<C3
    Hz=Hmin
    Lz=Lc+(Hc-Hmin)*Tana
    Goto Complete_find
  Endif
  If A<=C4
    Lz=Lmax
    Hz=Hc-(Lmax-Lc)/Tana
  Endif
  Complete_find:
Return
'
Procedure Project
  T1=Tx
  T2=Ty
  T3=Tz
  Mul T1,Cosxa
  Mul T2,Cosya
  Mul T3,Cosza
  Px=T1+T2+T3
  T1=Tx
  T2=Ty
  T3=Tz
  Mul T1,Sinxa
  Mul T2,Sinya
  Mul T3,Sinza
  Py=T1+T2+T3
  Mh=Int(Hc+Px*Scaleh)
  Mv=Int(Lc-Py*Scalev)
Return
'
'
' Save Original Color Palette
'
Procedure Save_palette
  Local I
  '
  ' must be located at start of program:  Dim Palette(15)
  '
  '
  For I=0 To 3
    Palette(I)=Xbios(7,W:I,W:-1)
  Next I
Return
'
'
'
' Restore Original Color Palette
'
Procedure Restore_palette
  Local I
  '
  For I=0 To 3
    Setcolor I,Palette(I)
  Next I
Return
'
Procedure Set_colour
  ' Set correct colors
  '
  '
  For I=0 To 3
    Setcolor I,Pal2(I)
  Next I
Return
'
