DIM palette%(15),vol%(3),n%(3),oct%(3),dur%(3)
RESERVE FRE(0)-32000   !Free space reserved for Alerts
@save_palette
CLS
'
' Check for proper (Medium) Resolution
'
rez%=XBIOS(4)
IF rez%<>1
  alrt$="Sound Editor only|runs in Medium Rez."
  ALERT 1,alrt$,1,"OK",b
  EDIT
  END
ENDIF
'
@draw_screen
'
DO
  MOUSE x%,y%,k%
  IF k% AND 1
    @check_mouse
    REPEAT
      PAUSE 10
    UNTIL NOT MOUSEK
    '
    ' This is not needed. It's a demo segment which
    ' prints the mouse position on the screen whenever
    ' the left mouse button is pressed.
    '   This is handy whenever designing a graphic screen, or
    ' when you need to check for graphic coordinates.
    '
    '    PRINT AT(65,1);SPACE$(14)
    '    PRINT AT(65,1);"x=";x%;" y=";y%
    '
  ENDIF
  IF k% AND 2
    WAVE 0,0
  ENDIF
LOOP
@restore_palette
EDIT
'
'
PROCEDURE check_mouse
  ' check WAVE
  IF y%>31 AND y%<42
    @voice
  ENDIF
  IF y%>52 AND y%<62
    @noise
  ENDIF
  IF y%>72 AND y%<82
    @envelope
  ENDIF
  IF y%>90 AND y%<103
    @length
  ENDIF
  IF y%>110 AND y%<123
    @duration
  ENDIF
  IF y%>150 AND y%<160
    @wave_1
  ENDIF
  IF y%>165 AND y%<175
    @wave_2
  ENDIF
  '
  ' Was a note selected?
  IF y%>110 AND y%<121 AND x%>331
    @note_1_select
  ENDIF
  IF y%>126 AND y%<137 AND x%>331
    @note_2_select
  ENDIF
  '
  ' Was Duration selected?
  IF y%>141 AND y%<153
    @sound_duration
  ENDIF
  '
  ' Display results
  ' WAVE settings
  v$=STR$(voc%)
  e$=STR$(env%)
  w$=STR$(wave%)
  l$=STR$(length%)
  d$=STR$(dur%)
  @wave_string
  '
  @sound_string
  '
  IF y%>13 AND y%<25 AND x%>299 AND x%<376
    SOUND 1,vol%(1),n%(1),oct%(1),dur%(1)
    SOUND 2,vol%(2),n%(2),oct%(2),dur%(2)
    SOUND 3,vol%(3),n%(3),oct%(3),dur%(3)
    IF wave%<>0
      WAVE voc%,env%,wave%,length%,dur%
    ENDIF
  ENDIF
  '
  IF y%>52 AND y%<103 AND x%>517 AND x%<637
    ALERT 1,"Really Quit?",2,"Yes|No",b
    IF b=1
      @restore_palette
      WAVE 0,0    !Turn off the sound before leaving
      RESERVE FRE(0)+32000
      END
    ENDIF
  ENDIF
  IF MOUSEK=2
    WAVE 0,0
  ENDIF
RETURN
'
PROCEDURE voice
  ' Is it WAVE?
  IF x%>120 AND x%<154
    i%=0
    IF POINT(121,39)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,32,154+i%*50,42
    voc%=voc% XOR &X1
  ENDIF
  '
  IF x%>167 AND x%<204
    i%=1
    IF POINT(175,39)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,32,154+i%*50,42
    voc%=voc% XOR &X10
  ENDIF
  '
  IF x%>219 AND x%<253
    i%=2
    IF POINT(221,39)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,32,154+i%*50,42
    voc%=voc% XOR &X100
  ENDIF
  TEXT 128,40," 1     2     3"
  '
  ' Is it SOUND?
  '
  opt%=pt%
  IF x%>411 AND x%<447
    PUT 330,31,s$        !Clear Voice Selector
    i%=0
    DEFFILL 2
    PBOX 412+i%*50,32,446+i%*50,42  !Paint Red for 'selected'
    pt%=1
  ENDIF
  '
  IF x%>461 AND x%<498
    PUT 330,31,s$
    i%=1
    DEFFILL 2
    PBOX 462,32,496,42
    pt%=2
  ENDIF
  '
  IF x%>510 AND x%<547
    PUT 330,31,s$
    i%=2
    DEFFILL 2
    PBOX 511,32,546,42
    pt%=3
  ENDIF
  '
  TEXT 419,40," 1     2     3"
  IF opt%<>pt%
    @show_note
  ENDIF
RETURN
'
PROCEDURE noise
  ' Was NOISE selected?
  IF x%>120 AND x%<154
    i%=0
    IF POINT(121,59)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,52,154+i%*50,62
    voc%=voc% XOR &X1000
  ENDIF
  '
  IF x%>167 AND x%<204
    i%=1
    IF POINT(175,59)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,52,154+i%*50,62
    voc%=voc% XOR &X10000
  ENDIF
  '
  IF x%>219 AND x%<253
    i%=2
    IF POINT(221,59)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,52,154+i%*50,62
    voc%=voc% XOR &X100000
  ENDIF
  TEXT 128,60," 1     2     3"
  '
  ' Was Volume selected?
  IF x%>412 AND x%<447
    INC vol%(pt%)
    IF vol%(pt%)>15
      vol%(pt%)=0
    ENDIF
  ENDIF
  IF x%>462 AND x%<498
    DEC vol%(pt%)
    IF vol%(pt%)<0
      vol%(pt%)=15
    ENDIF
  ENDIF
RETURN
'
PROCEDURE envelope
  ' Was Envelope selected?
  IF x%>120 AND x%<154
    i%=0
    IF POINT(121,79)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,72,154+i%*50,82
    env%=env% XOR &X1
  ENDIF
  '
  IF x%>167 AND x%<204
    i%=1
    IF POINT(175,79)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,72,154+i%*50,82
    env%=env% XOR &X10
  ENDIF
  '
  IF x%>219 AND x%<253
    i%=2
    IF POINT(221,79)=2
      DEFFILL 3
    ELSE
      DEFFILL 2
    ENDIF
    PBOX 120+i%*50,72,154+i%*50,82
    env%=env% XOR &X100
  ENDIF
  TEXT 128,80," 1     2     3"
  '
  ' Was Octave selected?
  IF x%>412 AND x%<447
    INC oct%(pt%)
    IF oct%(pt%)>8
      oct%(pt%)=1
    ENDIF
    IF oct%(pt%)<1
      oct%(pt%)=8
    ENDIF
  ENDIF
  IF x%>462 AND x%<498
    DEC oct%(pt%)
    IF oct%(pt%)>8
      oct%(pt%)=1
    ENDIF
    IF oct%(pt%)<1
      oct%(pt%)=8
    ENDIF
  ENDIF
RETURN
'
PROCEDURE duration
  ' Was Duration selected?
  IF x%>119 AND x%<154
    INC dur%
    IF dur%>65535
      dur%=0
    ENDIF
  ENDIF
  IF x%>169 AND x%<204
    DEC dur%
    IF dur%<0
      dur%=65535
    ENDIF
  ENDIF
RETURN
'
PROCEDURE length
  IF x%>119 AND x%<154
    INC length%
    IF length%>65535
      length%=0
    ENDIF
  ENDIF
  IF x%>169 AND x%<204
    DEC length%
    IF length%<0
      length%=65535
    ENDIF
  ENDIF
RETURN
'
PROCEDURE wave_1
  @wave_boxes
  IF x%>35 AND x%<75
    wave%=3
    PUT 34,149,r$
  ENDIF
  IF x%>85 AND x%<125
    wave%=4
    PUT 84,149,r$
  ENDIF
  IF x%>135 AND x%<175
    wave%=8
    PUT 134,149,r$
  ENDIF
  IF x%>185 AND x%<225
    wave%=9
    PUT 184,149,r$
  ENDIF
  IF x%>235 AND x%<275
    wave%=10
    PUT 234,149,r$
  ENDIF
  @waveform
RETURN
'
PROCEDURE wave_2
  @wave_boxes
  IF x%>35 AND x%<75
    wave%=11
    PUT 34,164,r$
  ENDIF
  IF x%>85 AND x%<125
    wave%=12
    PUT 84,164,r$
  ENDIF
  IF x%>135 AND x%<175
    wave%=13
    PUT 134,164,r$
  ENDIF
  IF x%>185 AND x%<225
    wave%=14
    PUT 184,164,r$
  ENDIF
  IF x%>235 AND x%<275
    wave%=15
    PUT 234,164,r$
  ENDIF
  @waveform
RETURN
'
PROCEDURE note_1_select
  @note_box
  IF x%>331 AND x%<372
    n%(pt%)=1
    PUT 330,110,r$
  ENDIF
  IF x%>381 AND x%<421
    n%(pt%)=2
    PUT 380,110,r$
  ENDIF
  IF x%>431 AND x%<471
    n%(pt%)=3
    PUT 430,110,r$
  ENDIF
  IF x%>481 AND x%<521
    n%(pt%)=4
    PUT 480,110,r$
  ENDIF
  IF x%>531 AND x%<571
    n%(pt%)=5
    PUT 530,110,r$
  ENDIF
  IF x%>581 AND x%<622
    n%(pt%)=6
    PUT 580,110,r$
  ENDIF
  @notes
RETURN
'
PROCEDURE note_2_select
  @note_box
  IF x%>331 AND x%<372
    n%(pt%)=7
    PUT 330,126,r$
  ENDIF
  IF x%>381 AND x%<421
    n%(pt%)=8
    PUT 380,126,r$
  ENDIF
  IF x%>431 AND x%<471
    n%(pt%)=9
    PUT 430,126,r$
  ENDIF
  IF x%>481 AND x%<521
    n%(pt%)=10
    PUT 480,126,r$
  ENDIF
  IF x%>531 AND x%<571
    n%(pt%)=11
    PUT 530,126,r$
  ENDIF
  IF x%>581 AND x%<622
    n%(pt%)=12
    PUT 580,126,r$
  ENDIF
  @notes
RETURN
'
PROCEDURE show_note
  ' Shows current note selected for voice
  @note_box
  IF n%(pt%)=1
    PUT 330,110,r$
  ENDIF
  IF n%(pt%)=2
    PUT 380,110,r$
  ENDIF
  IF n%(pt%)=3
    PUT 430,110,r$
  ENDIF
  IF n%(pt%)=4
    PUT 480,110,r$
  ENDIF
  IF n%(pt%)=5
    PUT 530,110,r$
  ENDIF
  IF n%(pt%)=6
    PUT 580,110,r$
  ENDIF
  IF n%(pt%)=7
    PUT 330,126,r$
  ENDIF
  IF n%(pt%)=8
    PUT 380,126,r$
  ENDIF
  IF n%(pt%)=9
    PUT 430,126,r$
  ENDIF
  IF n%(pt%)=10
    PUT 480,126,r$
  ENDIF
  IF n%(pt%)=11
    PUT 530,126,r$
  ENDIF
  IF n%(pt%)=12
    PUT 580,126,r$
  ENDIF
  @notes
RETURN
'
PROCEDURE sound_duration
  IF x%>412 AND x%<448
    INC dur%(pt%)
    IF dur%(pt%)>65535
      dur%(pt%)=0
    ENDIF
  ENDIF
  IF x%>462 AND y%<499
    DEC dur%(pt%)
    IF dur%(pt%)<0
      dur%(pt%)=65535
    ENDIF
  ENDIF
RETURN
'
PROCEDURE draw_screen
  '
  SETCOLOR 0,0,0,0
  SETCOLOR 1,7,0,0
  SETCOLOR 2,0,7,0
  SETCOLOR 3,7,7,7
  GRAPHMODE 2
  DEFTEXT 2,17,,32
  TEXT 50,24,"Sound Editor"
  DEFFILL 3
  FOR i%=0 TO 11
    READ x%,y%
    FILL x%,y%
  NEXT i%
  COLOR 2
  PBOX 300,14,375,24
  BOX 299,13,376,25
  COLOR 1
  '
  DATA 61,15,79,21,92,21,108,20,133,20,163,11,181,9
  DATA 203,2,201,8,218,7,238,8,254,9
  '
  DEFTEXT 1,1,,6
  TEXT 320,21,"Play"
  TEXT 350,6,"GFA BASIC Progammers Reference Guide"
  TEXT 472,15,"Vol. I"
  TEXT 430,24,"By George Miller"
  DRAW 1,27 TO 639,27
  DRAW 320,27 TO 320,199
  DRAW 321,27 TO 321,199
  '
  ' Voice selector for WAVE
  '
  DEFFILL 2
  PBOX 39,32,95,42
  BOX 38,31,96,43
  TEXT 40,40," Voice"
  DEFFILL 3
  FOR i%=0 TO 2
    BOX 119+i%*50,31,155+i%*50,43
    PBOX 120+i%*50,32,154+i%*50,42
  NEXT i%
  TEXT 128,40," 1     2     3"
  '
  ' Voice Selector for SOUND
  '
  ' Short, copy other Voice selector
  GET 38,31,319,44,s$
  PUT 330,31,s$
  '
  DEFFILL 2
  PBOX 39,52,95,62
  BOX 38,51,96,63
  TEXT 40,60," Noise"
  DEFFILL 3
  FOR i%=0 TO 2
    BOX 119+i%*50,51,155+i%*50,63
    PBOX 120+i%*50,52,154+i%*50,62
  NEXT i%
  TEXT 128,60," 1     2     3"
  '
  DEFFILL 2
  PBOX 39,72,95,82
  BOX 38,71,96,83
  TEXT 40,80,"  Env"
  DEFFILL 3
  FOR i%=0 TO 2
    BOX 119+i%*50,71,155+i%*50,83
    PBOX 120+i%*50,72,154+i%*50,82
  NEXT i%
  TEXT 128,80," 1     2     3"
  '
  DEFFILL 2
  PBOX 39,92,95,102
  BOX 38,91,96,103
  DEFFILL 3
  FOR i%=0 TO 1
    BOX 119+i%*50,91,155+i%*50,103
    PBOX 120+i%*50,92,154+i%*50,102
  NEXT i%
  TEXT 133,100,CHR$(1)
  TEXT 183,100,CHR$(2)
  DEFFILL 1
  FILL 136,97
  FILL 187,98
  '
  ' Box needed for SOUND command functions
  ' Get copy
  '
  GET 37,90,300,104,t$
  PUT 330,50,t$
  PUT 330,70,t$
  PUT 330,140,t$
  '
  TEXT 42,100,"Length"    !for WAVE
  TEXT 335,80,"Octave"    !for SOUND
  TEXT 335,60,"Volume"    !for SOUND
  TEXT 350,150,"Dur"
  '
  DEFFILL 2
  PBOX 39,112,95,122
  BOX 38,111,96,123
  TEXT 52,120,"Dur"
  DEFFILL 3
  FOR i%=0 TO 1
    BOX 119+i%*50,111,155+i%*50,123
    PBOX 120+i%*50,112,154+i%*50,122
  NEXT i%
  TEXT 133,120,CHR$(1)
  TEXT 183,120,CHR$(2)
  DEFFILL 1
  FILL 136,117
  FILL 187,118
  '
  DEFFILL 2
  PBOX 125,132,200,142
  BOX 124,131,201,143
  TEXT 148,140,"Form"
  '
  BOX 35,150,75,160
  DEFFILL 2
  PBOX 36,151,74,159
  GET 34,149,76,161,r$
  DEFFILL 3
  PBOX 36,151,74,159
  GET 34,149,76,161,g$
  '
  @wave_boxes
  @waveform
  v$="0"
  e$="0"
  w$="0"
  l$="0"
  d$="0"
  @wave_string
  PUT 455,90,r$
  TEXT 460,99,"Note"
  @note_box
  @notes
  @sound_string
  '
  ' Exit Button
  BOX 517,52,637,103
  PBOX 518,53,636,102
  DEFTEXT 1,5,,32
  TEXT 540,86,"Exit"
  DEFTEXT 1,1,,6
RETURN
'
PROCEDURE note_box
  '
  ' Draw selectors for Notes
  FOR c%=0 TO 1
    FOR i%=0 TO 5
      PUT 330+i%*50,110+c%*15,g$
    NEXT i%
  NEXT c%
RETURN
'
PROCEDURE notes
  RESTORE note_data
  FOR x%=0 TO 1
    FOR y%=0 TO 5
      READ n$
      TEXT 343+y%*50,119+x%*15,n$
    NEXT y%
  NEXT x%
  note_data:
  DATA C,C#,D,D#,E,F,F#,G,G#,A,A#,B
RETURN
'
PROCEDURE wave_boxes
  '
  FOR c%=0 TO 1
    FOR i%=0 TO 4
      PUT 34+i%*50,149+c%*15,g$
    NEXT i%
  NEXT c%
RETURN
'
PROCEDURE waveform
  '
  ' Draw wave forms
  '
  ' Linear falling
  DRAW 39,152 TO 46,158 TO 70,158
  DRAW 38,152 TO 45,158
  '
  ' Linear rising terminating
  DRAW 88,158 TO 99,152 TO 99,158 TO 120,158
  '
  ' Sawtooth Falling
  DRAW 140,152 TO 150,158 TO 150,152 TO 160,158 TO 160,152 TO 170,158
  '
  ' Linear Falling
  DRAW 190,152 TO 196,158 TO 220,158
  DRAW 191,152 TO 197,158
  '
  ' Peaked, begin falling
  DRAW 237,153 TO 245,158 TO 253,153 TO 262,158 TO 271,153
  '
  '
  ' Linear falling, variable loudness
  DRAW 38,168 TO 46,173 TO 46,168 TO 71,168
  '
  ' Sawtooth Rising
  DRAW 88,173 TO 98,168 TO 98,173 TO 108,168 TO 108,173 TO 118,168 TO 118,173
  '
  ' Linear Rising Continuous
  DRAW 140,173 TO 150,168 TO 170,168
  '
  ' Peaked, begin rising
  DRAW 190,173 TO 195,168 TO 200,173 TO 205,168 TO 210,173 TO 215,168 TO 220,173
  '
  ' Linear Rising
  DRAW 238,173 TO 250,168 TO 250,173 TO 271,173
  '
RETURN
'
PROCEDURE wave_string
  DEFFILL 3
  RBOX 1,177,317,199
  PRBOX 2,178,318,198
  DEFTEXT 2,0,,6
  TEXT 20,187,"WAVE voc, env, form, len, dur"
  wave$="WAVE "+v$+","+e$+","+w$+","+l$+","+d$
  TEXT 20,196,wave$
  DEFTEXT 1,1,,6
RETURN
'
PROCEDURE sound_string
  RBOX 324,158,639,199
  PRBOX 325,159,638,198
  DEFTEXT 2,0,,6
  TEXT 340,166,"SOUND voc, vol, note, oct, dur"
  @build_sound
  TEXT 340,176,s1$
  TEXT 340,186,s2$
  TEXT 340,196,s3$
  DEFTEXT 1,1,,6
RETURN
'
PROCEDURE build_sound
  ' build strings for SOUND display
  s1$="SOUND 1"+","+STR$(vol%(1))+","+STR$(n%(1))+","+STR$(oct%(1))+","+STR$(dur%(1))
  s2$="SOUND 2"+","+STR$(vol%(2))+","+STR$(n%(2))+","+STR$(oct%(2))+","+STR$(dur%(2))
  s3$="SOUND 3"+","+STR$(vol%(3))+","+STR$(n%(3))+","+STR$(oct%(3))+","+STR$(dur%(3))
RETURN
'
PROCEDURE save_palette
  LOCAL i%
  FOR i%=0 TO 15
    palette%(i%)=XBIOS(7,W:i%,W:-1)
  NEXT i%
RETURN
'
PROCEDURE restore_palette
  SHOWM
  LOCAL i%
  FOR i%=0 TO 15
    SETCOLOR i%,palette%(i%)
  NEXT i%
RETURN
