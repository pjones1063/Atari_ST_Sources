' startup
RESERVE 131500
a=HIMEM
' load_file
PRINT "bufor start=";HEX$(a)
OPEN "I",#80,"G:\LLC.ROM" ' load_file - zmien sciezke na rzeczywista
BGET #80,a,131072
CLOSE #80
PRINT "zaladowany obraz cartridge LLC"
' write_cart
' reset cartridge
u=PEEK(&HFA0008)                      ! reset cartridge
' set unblock command tOri
u=PEEK(&HFA7400)                      ! t
u=PEEK(&HFA4F00)                      ! O
u=PEEK(&HFA7200)                      ! r
u=PEEK(&HFA6900)                      ! i
u=PEEK(&HFA0004)                      ! set RAM UPPER
u=PEEK(&HFA0080)                      ! clear address counter
u=PEEK(&HFA0010)                      ! unblock write
'
' write high bytes from file here
'
PRINT "zapis bajtu starszego"
FOR i=0 TO 131070 STEP 2
  byte=PEEK(i+a)
  address=&HFB0000+byte*256
  in=PEEK(address)
NEXT i
'
u=PEEK(&HFA0002)                      ! set RAM LOWER
u=PEEK(&HFA0080)                      ! clear address counter
'
' write low bytes from file here
'
PRINT "zapis bajtu mlodszego"
FOR i=1 TO 131071 STEP 2
  byte=PEEK(i+a)
  address=&HFB0000+byte*256
  in=PEEK(address)
NEXT i
'
u=PEEK(&HFA0008)                      ! switch cartridge to read
