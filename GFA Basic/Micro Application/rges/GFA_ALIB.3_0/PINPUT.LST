PROCEDURE gfa_pinput__(x__%,y__%,length__%)
  LOCAL pos__|,mav__|,sentence01__$,sentence02__$
  lib_rv__$=""
  pos__|=1
  mav__|=0
  GOSUB p_input__
  DO
    char1__$=CHR$(INP(2))
    EXIT IF ASC(char1__$)=13
    IF ASC(char1__$)=8 AND pos__|>1
      sentence01__$=RIGHT$(lib_rv__$,mav__|-pos__|+1)
      sentence02__$=LEFT$(lib_rv__$,pos__|-2)
      lib_rv__$=sentence02__$+sentence01__$
      DEC mav__|
      DEC pos__|
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)=127 AND pos__|<mav__|+1
      sentence01__$=RIGHT$(lib_rv__$,mav__|-pos__|)
      sentence02__$=LEFT$(lib_rv__$,pos__|-1)
      lib_rv__$=sentence02__$+sentence01__$
      DEC mav__|
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)=203 AND pos__|>1
      DEC pos__|
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)=205 AND pos__|<mav__|+1
      INC pos__|
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)=200
      pos__|=1
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)=208
      pos__|=mav__|+1
      GOSUB p_input__
    ENDIF
    IF ASC(char1__$)>20 AND ASC(char1__$)<150 AND ASC(char1__$)<>127
      IF pos__|=mav__|+1
        lib_rv__$=lib_rv__$+char1__$
        INC pos__|
        INC mav__|
      ELSE
        sentence01__$=RIGHT$(lib_rv__$,mav__|-pos__|+1)
        sentence02__$=LEFT$(lib_rv__$,pos__|-1)
        lib_rv__$=sentence02__$+char1__$+sentence01__$
        INC pos__|
        INC mav__|
        GOSUB p_input__
      ENDIF
    ENDIF
    GOSUB p_input__
  LOOP
  TEXT x__%+8*mav__|,y__%," "
  TEXT x__%,y__%,lib_rv__$
RETURN
PROCEDURE p_input__
  LOCAL cnt__|,sentence__$
  cnt__|=LEN(lib_rv__$)
  IF cnt__|<length__%
    TEXT x__%+8*mav__|,y__%,"  "
  ENDIF
  IF cnt__|=length__%
    IF pos__|>cnt__|
      DEC pos__|
    ENDIF
    TEXT x__%+8*mav__|,y__%,"  "
  ENDIF
  IF cnt__|>length__%
    lib_rv__$=LEFT$(lib_rv__$,cnt__|-1)
    mav__|=LEN(lib_rv__$)
  ENDIF
  TEXT x__%,y__%,lib_rv__$
  '
  IF XBIOS(4)=2
    GET x__%+8*(pos__|-1),y__%-13,x__%+8*(pos__|-1)+7,y__%+2,sentence__$
    PUT x__%+8*(pos__|-1),y__%-13,sentence__$,8
  ELSE
    GET x__%+8*(pos__|-1),y__%-6,x__%+8*(pos__|-1)+7,y__%+1,sentence__$
    PUT x__%+8*(pos__|-1),y__%-6,sentence__$,8
  ENDIF
RETURN
