' WINDTEXT.LST
'
DEFLIST 0
DEFINT "a-z"
'
text_loader ! Load ASCII file to be displayed
'
DEFFILL 1,2,4
PBOX 0,19,639,399 ! Background pattern
DEFFILL 1,0
'
DIM word&(7)  ! 16 Byte for
adr_mes=V:word&(0)  ! Message Buffer
'
' Define window with all elements
handle=WIND_CREATE(&X111111111111,0,19,639,380)
'
title$="Window"+CHR$(0)
adr_tit=V:title$
info$=" This is an ASCII test file"+CHR$(0)
adr_inf=V:info$
'
' Window name and Info line
~WIND_SET(handle,2,CARD(SWAP(adr_tit)),CARD(adr_tit),0,0)
~WIND_SET(handle,3,CARD(SWAP(adr_inf)),CARD(adr_inf),0,0)
~WIND_OPEN(handle,100,100,200,150)
'
adjust_sliders
text_display
'
DO
  ~EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,adr_mes,100)
  SELECT word&(0)
  CASE 22 ! WM_CLOSED
    EXIT IF TRUE
    '
  CASE 23 ! WM_FULLED
    ~WIND_SET(handle,5,1,19,638,380)
    adjust_sliders
    text_display
    '
  CASE 24 ! WM_ARROWED
    ~WIND_GET(handle,4,wx,wy,ww,wh) ! Window size
    ~WIND_GET(handle,8,hslide,d,d,d)  ! Slider
    ~WIND_GET(handle,9,vslide,d,d,d)  ! position
    SELECT word&(4)
    CASE 0  ! side up
      side_height=wh/16-1
      print_line=MAX(1,print_line-side_height)
      vslide=(print_line-1)/num_lines*1000
      ~WIND_SET(handle,9,vslide,d,d,d)
      '
    CASE 1  ! side down
      side_height=wh/16-1
      print_line=MIN(num_lines,print_line+side_height)
      vslide=(print_line-1)/num_lines*1000
      ~WIND_SET(handle,9,vslide,d,d,d)
      '
    CASE 2  ! line up
      print_line=MAX(1,print_line-1)
      vslide=(print_line-1)/num_lines*1000
      ~WIND_SET(handle,9,vslide,d,d,d)
      '
    CASE 3  ! line down
      print_line=MIN(num_lines,print_line+1)
      vslide=(print_line-1)/num_lines*1000
      ~WIND_SET(handle,9,vslide,d,d,d)
      '
    CASE 4  ! side left
      sides_width=ww/8-1
      print_column=MAX(1,print_column-sides_width)
      hslide=(print_column-1)/max_length*1000
      ~WIND_SET(handle,8,hslide,d,d,d)
      '
    CASE 5  ! side right
      sides_width=ww/8-1
      print_column=MIN(max_length,print_column+sides_width)
      hslide=(print_column-1)/max_length*1000
      ~WIND_SET(handle,8,hslide,d,d,d)
      '
    CASE 6  ! line left
      print_column=MAX(1,print_column-1)
      hslide=(print_column-1)/max_length*1000
      ~WIND_SET(handle,8,hslide,d,d,d)
      '
    CASE 7  ! line right
      print_column=MIN(max_length,print_column+1)
      hslide=(print_column-1)/max_length*1000
      ~WIND_SET(handle,8,hslide,d,d,d)
      '
    ENDSELECT
    text_display
    '
  CASE 25 ! WM_HSLID
    ~WIND_SET(handle,8,word&(4),d,d,d)
    print_column=word&(4)*max_length/1000+1
    text_display
    '
  CASE 26 ! WM_VSLID
    ~WIND_SET(handle,9,word&(4),d,d,d)
    print_line=word&(4)*num_lines/1000+1
    text_display
    '
  CASE 27,28  ! WM_SIZED, WM_MOVED
    w=MAX(150,w)
    h=MAX(150,h)
    ~WIND_SET(handle,5,word&(4),word&(5),word&(6),word&(7))
    '
    adjust_sliders
    text_display
    '
  ENDSELECT
LOOP
~WIND_CLOSE(handle)
~WIND_DELETE(handle)
'
PROCEDURE text_loader
  FILESELECT "\*.LST","",file$
  max_length=0
  print_line=1
  print_column=1
  DIM t$(1000)
  OPEN "I",#1,file$
  RECALL #1,t$(),1000,num_lines
  CLOSE #1
  FOR i=1 TO num_lines
    max_length=MAX(max_length,LEN(t$(i)))
  NEXT i
  PRINT CHR$(27)+"w";
RETURN
'
PROCEDURE text_display
  ~WIND_GET(handle,4,wx,wy,ww,wh)
  PBOX wx-1,wy-1,wx+ww,wy+wh  ! Clear window
  y_pos=wy/16+2  ! Start of PRINT display
  max_y_pos=(wy+wh)/16 ! End of PRINT display
  text_line=print_line
  column=wx/8+2 ! Columns of text output
  char_per_line=ww/8-1  ! Length of text line
  REPEAT
    t$=MID$(t$(text_line),print_column,char_per_line)
    PRINT AT(column,y_pos);t$;
    INC y_pos
    INC text_line
  UNTIL y_pos>max_y_pos OR text_line>num_lines
  word&(0)=0
RETURN
'
PROCEDURE adjust_sliders
  ~WIND_GET(handle,4,wx,wy,ww,wh)
  side_height=wh/16-1
  vs_size=side_height/num_lines*1000
  ~WIND_SET(handle,16,vs_size,d,d,d)
  sides_width=ww/8-1
  hs_size=sides_width/max_length*1000
  ~WIND_SET(handle,15,hs_size,d,d,d)
RETURN
