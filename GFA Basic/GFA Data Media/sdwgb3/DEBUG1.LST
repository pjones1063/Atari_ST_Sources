' DEBUG1.LST
'
~BIOS(11,BCLR(BIOS(11,-1),4)) ! Caps-Lock Off
'
IF XBIOS(4)<>2
  v%=199
ELSE
  v%=399
ENDIF
'
TRON debug
'
DEFFILL 1,0
LINE 0,50,639,50
'
REPEAT
  MOUSE mx%,my%,mk%
  IF my%>50
    PBOX mx%,my%,mx%+25,my%+25
    IF mk%=1
      delete
    ENDIF
  ENDIF
UNTIL mk%=2
EDIT
'
PROCEDURE delete
  PBOX 0,50,639,v%
RETURN
'
PROCEDURE debug
  ' Address of Screen Memory
  x%=XBIOS(2)
  ' Status of Shift Keys
  b%=BIOS(11,-1)
  ' Control-Key (Show program lines)
  IF BTST(b%,2)
    BMOVE x%+1280,x%,2560
    PRINT AT(1,3);SPACE$(80);
    PRINT AT(1,3);LEFT$(TRACE$,80);
  ENDIF
  '
  ' Alternate Key (Display variables)
  IF BTST(BIOS(11,-1),3)
    SGET screen$
    CLS
    PRINT AT(2,2);"Press key of the first letter in the Variable name:"
    b|=INP(2)
    PRINT
    DUMP CHR$(b|)
    PRINT
    PRINT " Please press any key to continue."
    ~INP(2)
    SPUT screen$
  ENDIF
  '
  ' Caps-Lock (Single Step mode on Right shift key)
  IF BTST(b%,4)
    DO
    LOOP UNTIL BTST(BIOS(11,-1),0)
    DO
    LOOP WHILE BTST(BIOS(11,-1),0)
    BMOVE x%+1280,x%,2560
    PRINT AT(1,3);SPACE$(80);
    PRINT AT(1,3);LEFT$(TRACE$,80);
  ENDIF
RETURN
