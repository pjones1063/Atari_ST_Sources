' PULL_UP.LST
'
' Load Resource
RESERVE -32000
~RSRC_FREE()
IF RSRC_LOAD("pulldown.rsc")=0
  ALERT 1,"Error during RSC load.",1," Return ",a%
  RESERVE
  EDIT
ENDIF
'
' Address of the first Menu Tree
~RSRC_GADDR(0,0,tree_adr%)
'
' Set screen black, because of menu management of GEM
PBOX 0,0,639,399
'
' Menu box and it's children down
OB_Y(tree_adr%,1)=190
'
' Skip Menu title
cnt%=3
WHILE OB_TYPE(tree_adr%,cnt%)=32
  INC cnt%
WEND
'
' Background frame of menu boxes
OB_H(tree_adr%,cnt%)=190
OB_Y(tree_adr%,cnt%)=0
'
' Downward menu boxes
REPEAT
  ' as long as there is no menu entry
  REPEAT
    INC cnt%
  UNTIL OB_TYPE(tree_adr%,cnt%)<>28
  '
  ' If there is a box, then move it down
  IF OB_TYPE(tree_adr%,cnt%)=20
    OB_Y(tree_adr%,cnt%)=189-OB_H(tree_adr%,cnt%)
  ENDIF
UNTIL OB_TYPE(tree_adr%,cnt%)=0
'
' Pull-Up-Menu is finished and called
~MENU_BAR(tree_adr%,1)
REPEAT
  ON MENU
UNTIL MOUSEK=2
'
' Remove menu bar and free memory
~MENU_BAR(tree_adr%,0)
~RSRC_FREE()
RESERVE
EDIT
