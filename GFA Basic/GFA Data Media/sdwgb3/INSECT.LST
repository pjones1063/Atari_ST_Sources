' INSECT.LST
'
' Sample program for Dialog box management
'
' Main Routines
'
' Please refer to the book page 147, last paragraph. The large icon
' in INSECT.RSC does indeed suffer from this problem. This RSC has
' been produced for demonstration only and you will find that most
' Resource editors (including RSC2) will crash when editing the
' INSECT.RSC.
' If you wish to use INSECT.RSC you must delete the large butterfly
' icon before saving it. 

resource_load
obj_num
menu
program_end
PROCEDURE resource_load
  RESERVE FRE(0)-20000
  IF RSRC_LOAD("insect.rsc")=0
    ALERT 0,"RSC-File not found.",1," Return ",a%
    RESERVE FRE(0)+20000
    EDIT
  ENDIF
RETURN
> PROCEDURE obj_num
  '
  ' Determine object numbers for the file INSECT.RSC
  '
  ' First tree
  mem.t%=0 ! Tree 0, Membership List
  ~RSRC_GADDR(0,mem.t%,mem.adr%)
  m_name.e%=1 ! Name of member (Edit field)
  m_str.e%=2 ! Street address of the member (Edit field)
  m_city.e%=3 ! City where member lives (Edit field)
  m_can.b%=4 ! Cancel Button of the Membership Tree
  m_okbu.b%=5 ! Ok-Button of the Membership Tree
  '
  ' Second Tree
  butterfly.t%=1 ! Tree 1, Butterfly
  ~RSRC_GADDR(0,butterfly.t%,butterfly.adr%)
  s_okbu.b%=1 ! Ok-Button of Butterfly Tree
  s_abbr.b%=2 ! Exit Button of Butterfly Tree
  s_pare.b%=5 ! Parent Object of slider
  s_chil.b%=6 ! child object of slider
  s_minu.b%=7 ! Reduce wingspan slider
  s_plus.b%=8 ! Increase wingspan slider
  '
  ' Third Tree
  inquire.t%=2 ! Tree 2, Inquire
  ~RSRC_GADDR(0,inquire.t%,inquire.adr%)
  u_abbr.b%=1 ! Exit Button of the Inquire Tree
  u_okbu.b%=2 ! Ok Button of the Inquire Tree
  DIM high.r%(3)
  high.r%(1)=7 ! Radio Button high interest in Bees
  high.r%(2)=6 ! Radio Button high interest in Butterflys
  high.r%(3)=8 ! Radio Button high interest in Flys
  DIM low.r%(3)
  low.r%(1)=12 ! Radio Button low interest in Bees
  low.r%(2)=13 ! Radio Button low interest in Butterflys
  low.r%(3)=14 ! Radio Button low interest in Flys
  '
  ' Fourth Tree
  kind.t%=3 ! Tree 3, Number of kinds
  ~RSRC_GADDR(0,kind.t%,kind.adr%)
  a_num.b%=4 ! Number of kinds
  a_minu.b%=5 ! Reduce value of number of kinds
  a_plus.b%=6 ! Increase value of number of kinds
  a_exit.b%=7 ! Exit Button, Fly Icon
RETURN
> PROCEDURE menu
  REPEAT
    CLS
    '
    VDISYS 5,0,0,13 ! reverse text on
    PRINT AT(1,2);" Which Dialog Box would you like to see?"
    VDISYS 5,0,0,14 ! reverse text off
    PRINT AT(2,4);"1. Member"
    PRINT AT(2,5);"2. Butterfly"
    PRINT AT(2,6);"3. Inquiry"
    PRINT AT(2,7);"4. Kinds of Flies"
    PRINT AT(2,8);"5. End Program"
    PRINT AT(2,12);"Your selection: ???"
    '
    select%=INP(2)-ASC("0")
    '
    CLS
    SELECT select%
    CASE 1
      membership_box
    CASE 2
      butterfly_box
    CASE 3
      inquiry_box
    CASE 4
      kind_of_bug_box
    ENDSELECT
    '
  UNTIL select%=5
RETURN
> PROCEDURE program_end
  ~RSRC_FREE()
  RESERVE FRE(0)+20000
  EDIT
RETURN
> PROCEDURE membership_box
  ' Center Dialog Box
  ~FORM_CENTER(mem.adr%,x%,y%,w%,h%)
  '
  ' Place dummy information in Edit Fields
  CHAR{{OB_SPEC(mem.adr%,m_name.e%)}}="Harry Spyder"
  CHAR{{OB_SPEC(mem.adr%,m_str.e%)}}="123 Flytrap Rd"
  CHAR{{OB_SPEC(mem.adr%,m_city.e%)}}="Swamp City"
  '
  ~OBJC_DRAW(mem.adr%,0,5,x%,y%,w%,h%)
  ex_obj%=FORM_DO(mem.adr%,0)
  ~OBJC_CHANGE(mem.adr%,ex_obj%,0,x%,y%,w%,h%,0,0)
  '
  CLS
  PRINT
  VDISYS 5,0,0,13 ! reverse text on
  PRINT " Dialog Box status on exit: "
  VDISYS 5,0,0,14 ! reverse text off
  PRINT
  PRINT " Edit field Name:  "'CHAR{{OB_SPEC(mem.adr%,m_name.e%)}}
  PRINT " Edit field Street:"'CHAR{{OB_SPEC(mem.adr%,m_str.e%)}}
  PRINT " Edit field City:  "'CHAR{{OB_SPEC(mem.adr%,m_city.e%)}}
  '
  ex_txt$=CHAR{OB_SPEC(mem.adr%,ex_obj%)}
  PRINT
  PRINT " Number of Exit Button:"'ex_obj%
  PRINT " Text of Exit Button:  "'ex_txt$
  PRINT
  PRINT "   Press any key to continue."
  ~INP(2)
  '
RETURN
> PROCEDURE butterfly_box
  '
  ' Center object coordinates and draw form
  ~FORM_CENTER(butterfly.adr%,x%,y%,w%,h%)
  ~OBJC_DRAW(butterfly.adr%,0,5,x%,y%,w%,h%)
  '
  ' Width of the sliders, minus the slider buttons
  width%=OB_W(butterfly.adr%,s_pare.b%)-OB_W(butterfly.adr%,s_chil.b%)+1
  '
  ' preset slider position
  slide_pos%=500
  '
  REPEAT
    '
    ' Call GEM
    '
    ex_obj%=FORM_DO(butterfly.adr%,0)
    '
    ' If child object of slider is selected
    '
    IF ex_obj%=s_chil.b%
      '
      ' position slider at new position
      '
      slide_pos%=GRAF_SLIDEBOX(butterfly.adr%,s_pare.b%,s_chil.b%,0)
      new_pos%=width%/1000*slide_pos%
      OB_X(butterfly.adr%,s_chil.b%)=new_pos%
      '
      ' New slider button values
      new_value%=slide_pos%/5
      CHAR{OB_SPEC(butterfly.adr%,s_chil.b%)}=STR$(new_value%)
      '
      ' Redraw Slider
      ~OBJC_DRAW(butterfly.adr%,s_pare.b%,5,x%,y%,w%,h%)
    ENDIF
    '
    ' If minus is selected
    IF ex_obj%=s_minu.b%
      '
      ' decrement slider position value
      '
      SUB slide_pos%,5
      IF slide_pos%<0
        slide_pos%=0
      ENDIF
      '
      ' Change Slider position
      new_pos%=width%/1000*slide_pos%
      OB_X(butterfly.adr%,s_chil.b%)=new_pos%
      '
      ' Change Slider value
      new_value%=slide_pos%/5
      CHAR{OB_SPEC(butterfly.adr%,s_chil.b%)}=STR$(new_value%)
      '
      ' Redraw slider
      ~OBJC_DRAW(butterfly.adr%,s_pare.b%,5,x%,y%,w%,h%)
    ENDIF
    '
    ' When plus is selected
    IF ex_obj%=s_plus.b%
      ADD slide_pos%,5
      IF slide_pos%>1000
        slide_pos%=1000
      ENDIF
      '
      ' Change Slider position
      new_pos%=width%/1000*slide_pos%
      OB_X(butterfly.adr%,s_chil.b%)=new_pos%
      '
      ' Change Slider value
      new_value%=slide_pos%/5
      CHAR{OB_SPEC(butterfly.adr%,s_chil.b%)}=STR$(new_value%)
      '
      ' redraw slider
      ~OBJC_DRAW(butterfly.adr%,s_pare.b%,5,x%,y%,w%,h%)
    ENDIF
    '
    ' Wait for Cancel or OK button
  UNTIL ex_obj%=s_abbr.b% OR ex_obj%=s_okbu.b%
  '
  ' Deselect exit button
  ~OBJC_CHANGE(butterfly.adr%,ex_obj%,0,x%,y%,w%,h%,0,0)
  '
  CLS
  PRINT
  VDISYS 5,0,0,13 ! reverse text on
  PRINT " Status of the Dialog Box on exit: "
  VDISYS 5,0,0,14 ! reverse text off
  PRINT
  PRINT " Slider Position:"'slide_pos%
  PRINT " Value in Slider:"'new_value%
  '
  ex_txt$=CHAR{OB_SPEC(butterfly.adr%,ex_obj%)}
  PRINT
  PRINT " Number of the EXIT Buttons:"'ex_obj%
  PRINT " Text of the EXIT Buttons  :"'ex_txt$
  PRINT
  PRINT "   Please press any key to continue."
  ~INP(2)
  '
RETURN
> PROCEDURE kind_of_bug_box
  ~FORM_CENTER(kind.adr%,x%,y%,w%,h%)
  ~OBJC_DRAW(kind.adr%,0,5,x%,y%,w%,h%)
  '
  kind_anz%=0
  '
  REPEAT
    ex_obj%=FORM_DO(kind.adr%,0)
    IF ex_obj%=a_minu.b%
      DEC kind_anz%
      IF kind_anz%<0
        kind_anz%=0
      ENDIF
      CHAR{OB_SPEC(kind.adr%,a_num.b%)}=STR$(kind_anz%)
      ~OBJC_DRAW(kind.adr%,a_num.b%,5,x%,y%,w%,h%)
    ENDIF
    IF ex_obj%=a_plus.b%
      INC kind_anz%
      IF kind_anz%>999
        kind_anz%=999
      ENDIF
      CHAR{OB_SPEC(kind.adr%,a_num.b%)}=STR$(kind_anz%)
      ~OBJC_DRAW(kind.adr%,a_num.b%,5,x%,y%,w%,h%)
    ENDIF
    '
  UNTIL ex_obj%=a_exit.b%
  ' Exit-Objekt deselektieren
  ~OBJC_CHANGE(kind.adr%,ex_obj%,0,x%,y%,w%,h%,0,0)
  '
  CLS
  PRINT
  VDISYS 5,0,0,13 ! reverse text on
  PRINT " Dialog Box Status on exit: "
  VDISYS 5,0,0,14 ! reverse text off
  PRINT
  PRINT " Value in Number box:"'kind_anz%
  PRINT
  PRINT "   Please press any key to continue."
  ~INP(2)
  '
RETURN
> PROCEDURE inquiry_box
  ~FORM_CENTER(inquire.adr%,x%,y%,w%,h%)
  ~OBJC_DRAW(inquire.adr%,0,5,x%,y%,w%,h%)
  ex_obj%=FORM_DO(inquire.adr%,0)
  ~OBJC_CHANGE(inquire.adr%,ex_obj%,0,x%,y%,w%,h%,0,0)
  '
  CLS
  PRINT
  VDISYS 5,0,0,13 ! reverse text on
  PRINT " Dialog Box Status on exit: "
  VDISYS 5,0,0,14 ! reverse text off
  PRINT
  '
  PRINT " First Radio Button Group:"
  sel%=0
  FOR cnt%=1 TO 3
    IF BTST(OB_STATE(inquire.adr%,high.r%(cnt%)),0)
      sel%=high.r%(cnt%)
    ENDIF
  NEXT cnt%
  IF sel%<>0
    txt$=CHAR{OB_SPEC(inquire.adr%,sel%)}
    PRINT " Number of the selected Button:"'sel%
    PRINT " Text of the selected Button:  "'txt$
  ELSE
    PRINT " No Radio Button was selected."
  ENDIF
  '
  PRINT
  PRINT " Second Radio Button Group:"
  sel%=0
  FOR cnt%=1 TO 3
    IF BTST(OB_STATE(inquire.adr%,low.r%(cnt%)),0)
      sel%=low.r%(cnt%)
    ENDIF
  NEXT cnt%
  IF sel%<>0
    txt$=CHAR{OB_SPEC(inquire.adr%,sel%)}
    PRINT " Number of the selected Button:"'sel%
    PRINT " Text of the selected Button:  "'txt$
  ELSE
    PRINT " No Radio Button was selected."
  ENDIF
  '
  ex_txt$=CHAR{OB_SPEC(inquire.adr%,ex_obj%)}
  PRINT
  PRINT " Number of the Exit Button:"'ex_obj%
  PRINT " Text of the Exit Button:  "'ex_txt$
  PRINT
  PRINT "   Please press any key to continue."
  ~INP(2)
  '
RETURN
