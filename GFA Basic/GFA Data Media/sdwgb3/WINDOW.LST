' WINDOW.LST
'
DEFLIST 0
DEFWRD "x,y,w,h"
DEFINT "adr,mes"
DEFFLT "cnt"
'
DEFFILL 1,2,4
PBOX 0,19,639,399 ! Background pattern (saves redraw)
DEFFILL 1,0
'
DIM message_buffer(3) ! 16 Bytes
adr=V:message_buffer(0)
'
ABSOLUTE word0,adr
ABSOLUTE x,adr+8
ABSOLUTE y,adr+10
ABSOLUTE w,adr+12
ABSOLUTE h,adr+14
'
handle=WIND_CREATE(&X101111,0,19,639,190)
'
title$="Window"+CHR$(0)
adr_tit=V:title$
~WIND_SET(handle,2,CARD(SWAP(adr_tit)),CARD(adr_tit),0,0)
~WIND_OPEN(handle,100,100,199,100)
draw
'
DO
  ~EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,adr,100,d,d,d,d,d,d)
  SELECT word0
  CASE 22 ! WM_CLOSED
    EXIT IF TRUE
  CASE 23 ! WM_FULLED
    ~WIND_SET(handle,5,1,19,638,381)
    draw
  CASE 27,28  ! WM_SIZED, WM_MOVED
    w=MAX(w,100)
    h=MAX(h,80)
    ~WIND_SET(handle,5,x,y,w,h)
    draw
  ENDSELECT
LOOP
~WIND_CLOSE(handle)
~WIND_DELETE(handle)
'
PROCEDURE draw
  ~WIND_GET(handle,4,wx,wy,ww,wh)
  PBOX wx,wy,wx+ww,wy+wh
  FOR cnt=0 TO ww STEP ww/10
    LINE wx+cnt,wy,wx+cnt,wy+wh
  NEXT cnt
  word0=0
RETURN
