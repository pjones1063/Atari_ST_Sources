' PULLDOWN.LST
'
' reserve memory
RESERVE -32000
' Supply Message Buffer
DIM message%(3)
' Determine the address of the message buffer
mes_adr%=V:message%(0)
' Assign variable names to relevant
' message buffer words
ABSOLUTE evt&,mes_adr%
ABSOLUTE tit&,mes_adr%+6
ABSOLUTE obj&,mes_adr%+8
'
' Load Resource
ok%=RSRC_LOAD("PULLDOWN.RSC")
'
' Icons in menu bars should not be selected
' in this program
'
IF ok%=0
  ALERT 1,"Error during RSC Load",1," Return ",a%
  RESERVE
  EDIT
ENDIF
' Determine address of menu tree
~RSRC_GADDR(0,0,menu_adr%)
' Install Menu Bar
~MENU_BAR(menu_adr%,1)
REPEAT
  ~EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,mes_adr%,100,d%,d%,d%,d%,d%,d%)
  ' If a Menu Item was selected
  IF evt&=10
    one$=CHAR{OB_SPEC(menu_adr%,obj&)}
    PRINT AT(3,20);"OBJ NR :  "'obj&;SPC(20)
    PRINT AT(3,21);"Entry  : "'one$;SPC(20)
    evt&=0
    ' Restore inverse menu title
    ~MENU_TNORMAL(menu_adr%,tit&,1)
  ENDIF
UNTIL MOUSEK=2
' Remove menu bar
~MENU_BAR(menu_adr%,0)
' Remove resource from memory
~RSRC_FREE()
' Release memory
RESERVE
