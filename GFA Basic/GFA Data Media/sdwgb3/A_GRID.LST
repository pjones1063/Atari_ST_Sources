' A_GRID.LST
'
DEFLIST 0
DEFWRD "a-z"
'
rez=XBIOS(4)  !Check screen resolution
SELECT rez
CASE 0
  h=319
  v=199
CASE 1
  h=639
  v=199
CASE 2
  h=639
  v=399
ENDSELECT
HIDEM
ACLIP 1,0,0,h,v
'
mask=&X1111110000 ! Mask for of 16
distance=16
type%=&X1000000000000000  ! set 16 raster
old_mx=160  ! Beginning coordinates for the reticle
old_my=160
'
ALINE old_mx,0,old_mx,v,1,&HFFFF,2
ALINE 0,old_my,h,old_my,1,&HFFFF,2
'
REPEAT
  mx=AND(MOUSEX,mask) ! Filters mouse coordinates
  my=AND(MOUSEY,mask) ! and passes them to the adjusted raster
  ' ! Coordinates
  SELECT INKEY$
  CASE "1"
    mask=&X1111111100 ! Only coordinates that can be divided by 4
    distance=4  ! 4 Raster
    type%=&X1000100010001000  ! Line pattern for 4 raster
  CASE "2"
    mask=&X1111111000
    distance=8
    type%=&X1000000010000000
  CASE "3"
    mask=&X1111110000
    distance=16
    type%=&X1000000000000000
  ENDSELECT
  '
  ' New type selected
  IF old_type%<>type%
    old_type%=type%
    CLS
    i=0
    REPEAT
      ADD i,distance
      ALINE 0,i,h,i,1,type%,0
    UNTIL i>=v
    ' Buffer old mouse coordinates
    old_mx=AND(MOUSEX,mask)
    old_my=AND(MOUSEY,mask)
    ' Draw reticule
    ALINE old_mx,0,old_mx,v,1,&HFFFF,2
    ALINE 0,old_my,h,old_my,1,&HFFFF,2
  ENDIF
  '
  ' New Reticule Position
  ' If mouse coordinates changed
  IF OR(mx<>old_mx,my<>old_my)
    ALINE old_mx,0,old_mx,v,1,&HFFFF,2
    ALINE 0,old_my,h,old_my,1,&HFFFF,2
    ALINE mx,0,mx,v,1,&HFFFF,2
    ALINE 0,my,h,my,1,&HFFFF,2
    old_mx=mx
    old_my=my
  ENDIF
UNTIL MOUSEK=2
'
SHOWM
ACLIP 0,0,0,h,v
