' MAKEICON.LST
'
dimension
build_picture
mark_icon
make_icon_rsc
sav_icon_rsc
'
PROCEDURE dimension
  ' resource header: 36 bytes+2 null bytes
  DIM rsc_header%(10)
  ' icon data: max. 32000 bytes
  DIM icon_data%(8000)
  ' mask data: max. 32000 bytes
  DIM mask_data%(8000)
  ' icon block structure: 34 bytes
  DIM icon_blk%(9)
  ' parent object structure: 24 bytes
  DIM box_struc%(6)
  ' icon object structure: 24 bytes
  DIM icon_struc%(6)
RETURN
PROCEDURE build_picture
  build_pic!=FALSE
  REPEAT
    FILESELECT "\*.*","",pic_file$
    '
    IF EXIST(pic_file$)
      OPEN "I",#1,pic_file$
      build_pic!=TRUE
      '
      SELECT LOF(#1)
      CASE 32000 ! Doodle Format
        BGET #1,XBIOS(2),32000
      CASE 32034,32066 ! Degas or Degas Elite Format
        SEEK #1,34
        BGET #1,XBIOS(2),32000
      DEFAULT
        ALERT 1,"Not correct|Picture Format",1,"Continue|Cancel",a%
        IF a%=2
          EDIT
        ENDIF
        build_pic!=FALSE
      ENDSELECT
      '
      CLOSE #1
    ELSE
      ALERT 1,"Picture File doesn't exist",1,"Continue|Abort",a%
      IF a%=2
        EDIT
      ENDIF
    ENDIF
  UNTIL build_pic!
RETURN
PROCEDURE mark_icon
  z1$="Click Left Mouse Button on|"
  z2$="the upper Left and lower|"
  z3$="Right corners of the desired|"
  z4$="Picture segment."
  ALERT 1,z1$+z2$+z3$+z4$,1," Return ",a%
  '
  ' Define mouse pointer as small cross and wait for left mouse button
  DEFMOUSE 5
  REPEAT
  UNTIL MOUSEK=1
  '
  ' Upper left corner
  x1%=MOUSEX
  y1%=MOUSEY
  '
  ' wait for mouse button to be released
  REPEAT
  UNTIL MOUSEK=0
  '
  ' Select lower right corner
  HIDEM
  GRAPHMODE 3
  REPEAT
    MOUSE mx%,my%,mk%
    dx%=AND(mx%-x1%,&HFFF0)
    dy%=AND(my%-y1%,&HFFF0)
    x2%=x1%+dx%
    y2%=y1%+dy%
    IF x1%<x2% AND y1%<y2%
      BOX x1%,y1%,x2%,y2%
      PAUSE 5
      BOX x1%,y1%,x2%,y2%
    ENDIF
  UNTIL mk%=1
  '
  ' Display Icon in upper left corner of screen
  GET x1%,y1%,x2%,y2%,icon$
  CLS
  PUT 0,0,icon$
  '
  ' Size of Icon
  width%=x2%-x1% ! Width of Icon in Pixels
  height%=y2%-y1% ! Height of Icon in Pixel
  dat%=width%/8*height% ! Number of Data Bytes in the Icon
RETURN
PROCEDURE make_icon_rsc
  make_rsc_header
  make_icon_data
  make_mask_data
  make_icon_block_structure
  make_object_structure_parent_box
  make_object_structure_icon
RETURN
PROCEDURE make_rsc_header
  header_adr%=V:rsc_header%(1)
  '
  CARD{header_adr%}=1 ! reserved
  '
  ' Positionsblock
  CARD{header_adr%+2}=38+2*dat%+34 ! Object Field Position
  CARD{header_adr%+4}=38+2*dat% ! Tedinfo Position
  CARD{header_adr%+6}=38+2*dat% ! Icon Block Position
  CARD{header_adr%+8}=38+2*dat% ! Bit Block Position
  CARD{header_adr%+10}=38+2*dat% ! Position of free strings
  CARD{header_adr%+12}=36 ! Position tied Strings
  CARD{header_adr%+14}=38 ! Image Data Position
  CARD{header_adr%+16}=38+2*dat% ! Position free Images
  CARD{header_adr%+18}=38+2*dat%+34+2*24 ! Object Tree Table Pointer
  '
  ' Number Block
  CARD{header_adr%+20}=2 ! Number of Objects in the RSC file
  CARD{header_adr%+22}=1 ! Number of Trees in RSC file
  CARD{header_adr%+24}=0 ! Number of Tedinfo structures
  CARD{header_adr%+26}=1 ! Number of Icon Block structures
  CARD{header_adr%+28}=0 ! Number of Bit Block structures
  CARD{header_adr%+30}=0 ! Number of Strings
  CARD{header_adr%+32}=0 ! Number of Images
  '
  CARD{header_adr%+34}=38+2*dat%+34+48+4 ! Length of the RSC file
  CARD{header_adr%+36}=0 ! Null byte marking end of string
RETURN
PROCEDURE make_icon_data
  xb%=XBIOS(2)
  icon_adr%=V:icon_data%(1)
  '
  ' Copy Icon data one line at a time
  byte_width%=width%/8
  FOR count%=0 TO height%-1
    BMOVE xb%+count%*80,icon_adr%,byte_width%
    ADD icon_adr%,byte_width%
  NEXT count%
RETURN
PROCEDURE make_mask_data
  ' Black mask, can also be made with ARRAYFILL
  mask_adr%=V:mask_data%(1)
  FOR count%=mask_adr% TO dat% STEP 2
    CARD{count%}=65535
  NEXT count%
RETURN
PROCEDURE make_icon_block_structure
  icnblk_adr%=V:icon_blk%(1)
  '
  ' Pointer Block
  {icnblk_adr%}=38+dat% ! Pointer to Icon Mask
  {icnblk_adr%+4}=38 ! Pointer to Icon Data
  {icnblk_adr%+8}=36 ! Pointer to Icon String
  '
  ' Icon Letter Block
  CARD{icnblk_adr%+12}=2^12 ! Letter of Icon, see text
  CARD{icnblk_adr%+14}=0 ! x-Position of letter
  CARD{icnblk_adr%+16}=0 ! y-Position of letter
  '
  ' Icon Position and Size block
  CARD{icnblk_adr%+18}=0 ! x-Position of Icon
  CARD{icnblk_adr%+20}=0 ! y-Position of Icon
  CARD{icnblk_adr%+22}=width%    ! Width if Icon in Pixels
  CARD{icnblk_adr%+24}=height% ! Height of Icon in Pixels
  '
  ' Icon Text Block
  CARD{icnblk_adr%+26}=1 ! x-Position of text
  CARD{icnblk_adr%+28}=0 ! y-Position of text
  CARD{icnblk_adr%+30}=0 ! Text width in Pixels
  CARD{icnblk_adr%+32}=8 ! Text height in Pixels
RETURN
PROCEDURE make_object_structure_parent_box
  box_adr%=V:box_struc%(1)
  CARD{box_adr%}=65535 ! OB_NEXT
  CARD{box_adr%+2}=1 ! OB_HEAD
  CARD{box_adr%+4}=1 ! OB_TAIL
  CARD{box_adr%+6}=20 ! OB_TYPE
  CARD{box_adr%+8}=0 ! OB_FLAGS
  CARD{box_adr%+10}=16 ! OB_STATE
  LONG{box_adr%+12}=2^8+2^12+2^17 ! OB_SPEC
  CARD{box_adr%+16}=0 ! OB_X
  CARD{box_adr%+18}=0 ! OB_Y
  CARD{box_adr%+20}=400/8 ! OB_W (in bytes)
  CARD{box_adr%+22}=320/16 ! OB_H (in words)
RETURN
PROCEDURE make_object_structure_icon
  icon_adr%=V:icon_struc%(1)
  CARD{icon_adr%}=0 ! OB_NEXT
  CARD{icon_adr%+2}=65535 ! OB_HEAD
  CARD{icon_adr%+4}=65535 ! OB_TAIL
  CARD{icon_adr%+6}=31 ! OB_TYPE
  CARD{icon_adr%+8}=32 ! OB_FLAGS
  CARD{icon_adr%+10}=0 ! OB_STATE
  LONG{icon_adr%+12}=38+2*dat% ! OB_SPEC
  CARD{icon_adr%+16}=1 ! OB_X
  CARD{icon_adr%+18}=1 ! OB_Y
  CARD{icon_adr%+20}=width%*2^8 ! OB_W
  CARD{icon_adr%+22}=height%*2^8 ! OB_H
RETURN
PROCEDURE sav_icon_rsc
  '
  ' Object tree index pointer
  obj_tz%=38+2*dat%+34
  '
  FILESELECT "\*.RSC","",icon_rsc_file$
  IF icon_rsc_file$<>"" AND icon_rsc_file$<>"\"
    OPEN "O",#1,icon_rsc_file$
    BPUT #1,V:rsc_header%(1),38
    BPUT #1,V:icon_data%(1),dat%
    BPUT #1,V:mask_data%(1),dat%
    BPUT #1,V:icon_blk%(1),34
    BPUT #1,V:box_struc%(1),24
    BPUT #1,V:icon_struc%(1),24
    BPUT #1,V:obj_tz%,4
    CLOSE
  ENDIF
RETURN
