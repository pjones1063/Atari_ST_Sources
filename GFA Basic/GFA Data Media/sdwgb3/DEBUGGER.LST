' DEBUGGER.LST
'
TRON debugger
'
'Test Program goes here
'
PROCEDURE debugger
  LOCAL screen$,key$,dump.par$
  '
  ' Global Variables are: debug.speed%, debug.tron!, debug.line$
  '
  IF BIOS(11,-1)=13 OR INSTR(TRACE$,debug.line$)
    PRINT CHR$(27)+"j"; ! Cursor position marked
    SGET screen$
    DO
      CLS
      PRINT AT(2,2);"DEBUGGER MENU:"
      LOCATE 4,1
      PRINT " A.) Direct Mode (CONT = Continue Program)"
      PRINT " B.) Change Execution Speed"'
      PRINT "(Actual:"'debug.speed%;")"
      IF debug.tron!
        PRINT " C.) TRON activated"
      ELSE
        PRINT " C.) TRON off"
      ENDIF
      PRINT " D.) List Variable"
      PRINT " E.) Wait for program lines"
      PRINT " F.) Continue Program"
      '
      IF INSTR(TRACE$,debug.line$)
        PRINT AT(1,20);" Line reached:"'
      ELSE
        PRINT AT(1,20);" Actual line:"'
      ENDIF
      PRINT TRACE$
      '
      SELECT CHR$(INP(2))
      CASE "A","a"
        STOP
      CASE "B","b"
        PRINT AT(2,15);"Program delay (in 1/50 Sec.):"'
        INPUT debug.speed%
      CASE "C","c"
        debug.tron!=NOT debug.tron!
      CASE "D","d"
        PRINT AT(2,15);"DUMP-Parameter:"'
        INPUT dump.par$
        CLS
        DUMP dump.par$
        PRINT
        PRINT " Please press any key."
        ~INP(2)
      CASE "E","e"
        PRINT AT(2,15);"Program line component:"'
        INPUT debug.line$
      CASE "F","f"
        EXIT IF TRUE
      ENDSELECT
    LOOP
    SPUT screen$
    PRINT CHR$(27)+"k"; ! Cursor restored
  ENDIF
  '
  PAUSE debug.speed%
  '
  IF debug.tron!
    BMOVE XBIOS(2) + 21 * 1280, XBIOS(2) + 20 * 1280, 4 * 1280
    PRINT CHR$(27)+"j"; ! Cursor position marked
    PRINT AT(1,25);SPACE$(79);
    PRINT AT(1,25);LEFT$(TRACE$,79);
    PRINT CHR$(27)+"k"; ! Cursor restored
  ENDIF
RETURN
