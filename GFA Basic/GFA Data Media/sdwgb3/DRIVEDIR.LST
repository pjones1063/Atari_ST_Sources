' DRIVEDIR.LST
'
drive_dir("CDE")
PRINT "Path:"'path_count&
PRINT
FOR cnt&=1 TO path_count&
  PRINT path$(cnt&)
NEXT cnt&
PRINT
PRINT "Files:"'file_count&
PRINT
FOR cnt&=1 TO file_count&
  PRINT file$(cnt&)
NEXT cnt&
~INP(2)
'
PROCEDURE drive_dir(drive$)
  DIM path$(400),file$(1000),length%(1000)
  ABSOLUTE length%,BASEPAGE+154
  ABSOLUTE attrib|,BASEPAGE+149
  ~GEMDOS(26,L:BASEPAGE+128)  ! fsetdta
  FOR d%=1 TO LEN(drive$)
    read_dir(MID$(drive$,d%,1)+":\")
    WHILE path_count&>path_counter&
      INC path_counter&
      read_dir(path$(path_counter&))
    WEND
  NEXT d%
RETURN
'
PROCEDURE read_dir(path$)
  search$=path$+"*.*"+CHR$(0)
  finished&=GEMDOS(78,L:V:search$,&X10000)
  DO UNTIL finished&
    file$=CHAR{BASEPAGE+158}
    IF file$<>"." AND file$<>".."
      IF BTST(attrib|,4)
        INC path_count&
        path$(path_count&)=path$+file$+"\"
      ELSE
        INC file_count&
        file$(file_count&)=path$+file$
        length%(file_count&)=length%
      ENDIF
    ENDIF
    finished&=GEMDOS(79)  ! fsnext
  LOOP
RETURN
