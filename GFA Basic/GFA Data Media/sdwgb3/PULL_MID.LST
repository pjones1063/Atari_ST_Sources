' PULL_MID.LST
'
' Reserve memory and load resource
RESERVE -32000
~RSRC_FREE()
IF RSRC_LOAD("pulldown.rsc")=0
  ALERT 1,"Error during RSC load.",1," Return ",a%
  RESERVE
  EDIT
ENDIF
'
' Address of the first tree
~RSRC_GADDR(0,0,tree_adr%)
'
CLS
'
' Skip over boxes, Menu title, and frame box
cnt%=0
REPEAT
  INC cnt%
UNTIL OB_TYPE(tree_adr%,cnt%)=32
'
' Skip Menu title list
REPEAT
  INC cnt%
UNTIL OB_TYPE(tree_adr%,cnt%)<>32
'
' Menu boxes in center
REPEAT
  ' Search until an object is not a string type
  REPEAT
    INC cnt%
  UNTIL OB_TYPE(tree_adr%,cnt%)<>28
  ' If this entry is a box, change coordinates
  IF OB_TYPE(tree_adr%,cnt%)=20
    OB_X(tree_adr%,cnt%)=250
    OB_Y(tree_adr%,cnt%)=50
  ENDIF
  ob_t%=OB_TYPE(tree_adr%,cnt%)
  '
  ' Keep searching until no valid object type is found
UNTIL ob_t%<20 OR ob_t%>32
'
' Menu is finished and displayed
~MENU_BAR(tree_adr%,1)
REPEAT
  ON MENU
UNTIL MOUSEK=2
'
' Clear menu bar and release memory
~MENU_BAR(tree_adr%,0)
~RSRC_FREE()
RESERVE
END
