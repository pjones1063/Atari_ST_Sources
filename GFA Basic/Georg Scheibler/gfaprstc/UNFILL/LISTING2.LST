' #########################
' #  UNFILL
' #  ------
' # zweite (optimierte) Version
' # Voll funktionsfÑhig
' # (c) MAXON Computer GmbH 1989
' ####################################
'
xmax%=639                     !FÅr colorbetrieb mÅssen nur diese zwei
ymax%=399                     !Variablen angepasst werden!
'
FILESELECT "\*.PI3","",a$     !Bild im DEGAS-Format auswÑhlen
OPEN "I",#1,a$                !Datei îffnen
SEEK #1,34                    !Header abschneiden (bei Color Farben anpassen!)
BGET #1,XBIOS(2),32000        !Bild in Bildschirmspeicher
CLOSE                         !Datei schlieûen
SGET t$                       !Bild zwischenspeichern
'
FOR y%=0 TO ymax%-1           !Alle Zeilen einmal abtasten
  GET 0,y%+1,639,y%+1,nachf$  !Eine Zeile Nachfolgerpunkte
  PUT 0,y%,nachf$,6           !XOR-VerknÅpfen
NEXT y%                       !Das war schon die erste Schleife
GET 0,0,639,399,u$            !waagerechte Abtastung speichern
'
SPUT t$                       !ursprÅngliches Bild wiederherstellen
FOR x%=0 TO xmax%-1           !Alle Spalten
  GET x%+1,0,x%+1,399,nachf$  !Eine Spalte Punkte
  PUT x%,0,nachf$,6           !Mit XOR hineinkopieren
NEXT x%                       !Schleife abschlieûen; und jetzt wieder:
'
PUT 0,0,u$,7                  !waagerechte Abtastung hinein-ODER-n
VOID INP(2)                   !Das wars...
