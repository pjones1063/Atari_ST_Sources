' ****************************************************************************
' *                                                                          *
' *  Titel: SIGNUM!-Manager                                                  *
' *  Zweck: Verwaltung der SIGNUM!-Programmteile und SIGPIC.TOS              *
' *  Autor:   Martin Wunderli & Patrick Seemann                              *
' *           Eschenweg 7       Landstraûe 122                               *
' *           CH-8057 ZÅrich    CH-5430 Wettingen                            *
' *  Zielmaschine: ATARI ST                                                  *
' *  Sprache: GfA Basic Version 2.0  Compiler Version 1.8                    *
' *  Erstellungsdatum der Version 1.0: 19.9.1987                             *
' *  Aktuelle Version: 1.2                                                   *
' *                                                                          *
' *  (c) MAXON Computer GmbH 1987                                            *
' *                                                                          *
' ****************************************************************************
'
' Initialisierung:
'
RESERVE 50000
@desktop_loeschen
@init_menu
'
' Hauptprogrammschleife:
'
DO
  ON MENU
LOOP
'
'
PROCEDURE init_menu
  '
  LOCAL i%
  '
  ' Globale Variablen:
  ' ==================
  ' Titel$()
  '
  DIM titel$(19)
  FOR i%=0 TO 19
    READ titel$(i%)
  NEXT i%
  DATA   ,  Info SIGNUM!-Manager
  DATA --------------------------
  DATA 1,2,3,4,5,6,""
  '
  ' Selektierbare Punkte: 11,12,14,15,17
  DATA  SIGNUM! ,  Editiere Text ...          S ,  Drucke Text ...            P ,-------------------------------
  DATA   Wandle Bild ...            B ,  Bearbeite Zeichensatz ...  Z ,-------------------------------,  Ende                       Q ,""
  '
  DATA ""
  MENU titel$()
  '
  ' Disable Item
  '
  MENU 13,2
  MENU 16,2
  '
  ON MENU  GOSUB menu_bestimmen
  ON MENU KEY GOSUB key_bestimmen
RETURN  ! Init_menu
'
'
PROCEDURE key_bestimmen
  '
  LOCAL code%
  '
  code%=MENU(14) MOD 256
  @select_item(code%+32*(code%>90))
RETURN  ! Key_bestimmen
'
'
PROCEDURE menu_bestimmen
  @select_item(MENU(0))
RETURN  ! Menu_bestimmen
'
'
PROCEDURE select_item(nummer%)
  MENU OFF
  MENU KILL
  '
  ' Nummer des MenÅeintrages oder ASCII-Code
  ' der gedrÅckten Taste:
  '
  IF nummer%=1 OR nummer%=73 THEN
    @info
  ENDIF
  IF nummer%=11 OR nummer%=83 THEN
    @programm_starten("SIGNUM.PRG")
  ENDIF
  IF nummer%=12 OR nummer%=80 THEN
    @programm_starten("PR24N.PRG")
  ENDIF
  IF nummer%=14 OR nummer%=66 THEN
    @programm_starten("SIGPIC.TOS")
  ENDIF
  IF nummer%=15 OR nummer%=90 THEN
    @programm_starten("DCS24N.PRG")
  ENDIF
  IF nummer%=17 OR nummer%=81 THEN
    @desktop
  ENDIF
  @desktop_loeschen
  MENU titel$()
  '
  ' Disable Item
  '
  MENU 13,2
  MENU 16,2
  '
  ON MENU  GOSUB menu_bestimmen
  ON MENU KEY GOSUB key_bestimmen
RETURN  ! Select_item
'
'
PROCEDURE info
  '
  LOCAL entscheid%
  '
  ALERT 0,"    SIGNUM!-Manager|Version 1.2 (c) 1987 by|    Martin Wunderli|   & Patrick Seemann",1," Goil! ",entscheid%
RETURN  ! Info
'
'
PROCEDURE programm_starten(name$)
  '
  ' Name zeigen
  '
  DEFFILL 1,0,0
  PBOX 0,0,639,18
  TEXT (40-LEN(name$+" wird geladen")/2)*8,15,name$+" wird geladen"
  HIDEM
  EXEC 0,name$,"",""
  SHOWM
RETURN  ! Programm_starten
'
'
PROCEDURE desktop
  MENU KILL
  END
RETURN  ! Desktop
'
'
PROCEDURE desktop_loeschen
  DEFFILL 1,2,4
  PBOX 0,0,639,399
RETURN  ! Desktop_loeschen
