' (c) MAXON Computer GmbH 1988
'
DIM menue$(50),a%(10000)
DEFTEXT ,,,6
CLR i%
DO
  READ menue$(i%)
  EXIT IF menue$(i%)="***"
  INC i%
LOOP
DATA Desk,  info,-----------------------,1,2,3,4,5,6,""
DATA Laden,  *.E24   ,""
DATA Anzeigen,  zeige,""
DATA Ende, Exit,""
DATA "",***
ON MENU  GOSUB menu
MENU menue$()
'
REPEAT
  ON MENU &HFFFFFF
UNTIL ende!
MENU KILL
EDIT
PROCEDURE menu
  ON MENU(4)-2 GOSUB info,laden,anzeigen,ende
  MENU OFF
RETURN
PROCEDURE info
  info$="Programm zur Anzeige von|SIGNUM!-EditorzeichensÑtze mit|"
  info$=info$+"Nummer und Breite|Ω Georg Scheibler, 4920 Lemgo"
  ALERT 0,info$,1,"OK",dummy%
RETURN
PROCEDURE ende
  ende!=TRUE
RETURN
PROCEDURE laden
  FILESELECT "\*.E24","",namen$
  IF EXIST(namen$)
    BLOAD namen$,VARPTR(a%(0))
    IF a%(0)=CVL("eset")
      CLOSEW 0
      TEXT 300,11,SPACE$(12)
      TEXT 300,11,namen$
      OPENW 0
    ELSE
      ALERT 1,"Dies ist kein korrekter|Zeichensatz",1,"Schade",dummy%
    ENDIF
  ENDIF
RETURN
PROCEDURE anzeigen
  IF a%(0)=CVL("eset")
    CLS
    p1%=XBIOS(2)+1600
    n%=0
    FOR l%=36 TO 163 STEP 16
      FOR l1%=l% TO l%+15
        pz%=VARPTR(a%(163))+a%(l1%)
        p%=p1%+PEEK(pz%)*80
        zaehler%=PEEK(pz%+1)
        spalte%=PEEK(pz%+2)
        breite%=2
        ADD pz%,4
        WHILE zaehler%>0
          BMOVE pz%,p%,breite%
          ADD pz%,breite%
          ADD p%,80
          DEC zaehler%
        WEND
        ADD p1%,5
        TEXT (n% MOD 16)*40,(n% DIV 16)*46+30,n%+1
        TEXT (n% MOD 16)*40,(n% DIV 16)*46+40,spalte%
        INC n%
        EXIT IF n%>126
      NEXT l1%
      ADD p1%,3600
    NEXT l%
  ELSE
    ALERT 1,"Sie haben keinen|gÅltigen Zeichensatz|geladen",1," Ach so ",dummy%
  ENDIF
RETURN
