' >> Joystickabfrage von Dirk Beyer <<
' >>          in GfA BASIC          <<
'
Hidem             ! Maus aus
Tabanf%=Xbios(34) ! Tabelle mit Adresse fuer Abfrage
Ereignis1%=Tabanf%+60      ! Adresse fuer Joystick 1
Ereignis2%=Tabanf%+61      ! Adresse fuer Joystick 2
'
' Befehlcodes, die durch Xbios(&h19) an den
' Tastaturprozessor geschickt werden:
'
A$=Chr$(&H14) ! Joystick abfragen
'
B$=Chr$(&H15) ! \
'                Maus wieder an
C$=Chr$(&H8)  ! /
'
X=Xbios(&H19,3,L:Varptr(A$)) ! Joystickabfrage ein
While Inkey$=""
  Gosub Pruefe
Wend
'
X=Xbios(&H19,3,L:Varptr(B$)) ! Joystickabfrage aus
X=Xbios(&H19,3,L:Varptr(C$)) ! Maus ein
'
Procedure Pruefe
  '
  '  Ereigniswerte:
  '
  '    5   1   9
  '      \ | /
  '    4 - 0 - 8
  '      / | \
  '    6   2  10
  '
  '  Feuertaste = 128
  '  Ausserdem m”glich: z.B. Feuer + links
  '                         = 128  +   4
  '                         = 132
  J1%=Peek(Ereignis1%)
  J2%=Peek(Ereignis2%)
  If J1%<>0
    Print At(22,10);"Joystick 1: ";J1%;"  "
  Endif
  If J2%<>0
    Print At(40,10);"Joystick 2: ";J2%;"  "
  Endif
Return
