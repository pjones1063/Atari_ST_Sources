' (c) MAXON Computer GmbH 1988
'
DIM m$(22)            !MenÅ erstellen
RESTORE mmenue
FOR z%=0 TO 21
  READ m$(z%)
NEXT z%
mmenue:
DATA DESK  ,  Info ,--------------------
DATA 1,2,3,4,5,6,""
DATA GRAFIK  ,  FlÑchen  ,  Text  ,---------------,  Quit,""
DATA LAYOUT  ,  Verschieben  ,  Kopieren  ,---------------,"","",""
MENU m$()
FULLW 1               !GEM-Fenster îffnen
'
GOSUB menu
PROCEDURE menu
  DEFMOUSE 0
  DO                  ! Auf Ereignis
    ON MENU           ! warten
    REPEAT            ! und wenn
    UNTIL MOUSEK=0    ! Maustaste losgelassen
    ON MENU  GOSUB auswahl    ! verzweigen
  LOOP
RETURN
'
PROCEDURE auswahl     !Auswahl treffen
  IF MENU(0)=1
    MENU OFF
    GOSUB copyright
  ENDIF
  IF MENU(0)=11
    MENU OFF
    GOSUB flaechen
  ENDIF
  IF MENU(0)=12
    MENU OFF
    GOSUB text
  ENDIF
  IF MENU(0)=14
    MENU OFF
    END
  ENDIF
  IF MENU(0)=17
    MENU OFF
    GOSUB verschieben(0)
  ENDIF
  IF MENU(0)=18
    MENU OFF
    GOSUB verschieben(1)
  ENDIF
RETURN
'
'
PROCEDURE verschieben(copy%)
  DEFMOUSE 5        ! Fadenkreuz
  DO                ! Hauptschleife
    WHILE MOUSEK=0
      ON MENU
    WEND
    ON MENU
    MOUSE xl%,yl%,k% ! Mauskoordinaten links-oben
    GRAPHMODE 3
    DEFLINE 2,1,,
    '
    ' ***** Rahmen zeichnen *****
    '
    WHILE MOUSEK=1
      MOUSE xr%,yr%,k% ! Mauskoordinaten rechts-unten
      BOX xl%,yl%,xr%,yr% ! Verschiebe-Bild umrahmen
      REPEAT
      UNTIL MOUSEK=0 OR MOUSEX<>xr% OR MOUSEY<>yr% ! Wenn Maus bewegt
      BOX xl%,yl%,xr%,yr% ! alten Rahmen loeschen
    WEND
    DEFLINE 1,1,,
    GET xl%,yl%+38,xr%,yr%+38,snp$ ! Ausschnitt puffern
    IF NOT copy%=1 ! Nicht kopieren?
      DEFFILL 0    !
      GRAPHMODE 1  !
      PBOX xl%,yl%,xr%,yr% !dann Original Åberdecken
      DEFFILL 1
    ENDIF
    DPOKE 9952,xl%+1 ! Mauscursor nach links-oben
    DPOKE 9954,yl%+38! zurÅcksetzen
    '
    ' ***** Verschieben/Kopieren *****
    '
    WHILE MOUSEK=0
      ON MENU
      MOUSE x%,y%,k%
      SGET bild$     ! Hintergrund puffern
      PUT x%,y%+38,snp$,7 ! Ausschnitt zeigen
      WHILE MOUSEX=x% AND MOUSEY=y% AND MOUSEK=0 ! Auf Mausbewegung warten
        ON MENU
      WEND
      SPUT bild$   !Hintergrund Åber Ausschnitt legen
    WEND
    PUT x%,y%+38,snp$,7
    WHILE MOUSEK=1  !Warten bis Taste losgelassen
    WEND
  LOOP
  DEFTEXT ,0,,13
RETURN
'
PROCEDURE flaechen
  GRAPHMODE 1
  FOR z%=1 TO 3 STEP 2
    DEFFILL ,2,RANDOM(24)
    PBOX 50*z%,50*z%,100*z%,100*z%
  NEXT z%
  DEFFILL 1
  GOSUB menu
RETURN
'
PROCEDURE text
  GRAPHMODE 2
  FOR z%=60 TO 300 STEP 80
    TEXT 340,z%," Verschieben oder kopieren   "
  NEXT z%
  GOSUB menu
RETURN
'
'
PROCEDURE copyright
  CLS
  BOX 120,70,530,270
  DEFTEXT ,,,13
  TEXT 150,130,"Verschieben und Kopieren von Bildausschnitten"
  DEFTEXT ,,,13
  TEXT 260,170,"in GfA-Basic"
  DEFTEXT ,,,6
  TEXT 230,210,"MAXON Computer 1988"
  DEFTEXT ,,,13
  PAUSE 100
  CLS
  GOSUB menu
RETURN
