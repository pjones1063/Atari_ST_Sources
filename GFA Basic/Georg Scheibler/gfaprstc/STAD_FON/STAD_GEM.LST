' STADT:GEM.GFA
' PROGRAMM ZUM UMWANDELN VON STAD-FONTS IN GEM-FONTS
' HEINZ KATZENMAYER, 10/88
'
' Ordner anlegen:
MKDIR "GEMFONTS"
font%=1
'
REPEAT
  CLS
  '
  ' Speicherplatz fuer Fontdaten:
  font_adr$=SPACE$(4096)
  help_adr$=SPACE$(4096)
  font_adr%=VARPTR(font_adr$)
  help_adr%=VARPTR(help_adr$)
  '
  ' STAD-Font auswaehlen:
  FILESELECT "\STADFONT\*.FNT","",fstad$
  IF fstad$=""
    END
  ENDIF
  '
  ' Name von Font fuer Fontheader:
  n$=SPACE$(32)
  LSET n$=MID$(fstad$,11,INSTR(fstad$,".")-11)+".STAD"
  '
  ' Fontnummer:
  INC font%
  '
  PRINT AT(20,8);"Das STAD-Font ";LEFT$(n$,INSTR(n$,".")-1)
  PRINT AT(20,10);"wird neu abgespeichert im Ordner GEMFONTS"
  '
  ' Fontheader-Eintrag Nummer und Name:
  header$=MKI$(font%)+n$
  '
  ' STAD-Fontdaten einlesen ...
  OPEN "I",#1,fstad$
  BGET #1,help_adr%,4096
  '
  ' ... und auf GEM-Format aendern:
  b%=-1
  FOR zeichen%=0 TO 255
    FOR scan%=0 TO 15
      INC b%
      POKE font_adr%+zeichen%+scan%*256,PEEK(help_adr%+b%)
    NEXT scan%
  NEXT zeichen%
  '
  ' Fontdaten plus Header in GEMFONT abspeichern:
  fgem$="\GEMFONTS\GEM_F"+STR$(font%)+".FNT"
  OPEN "O",#2,fgem$
  BPUT #2,VARPTR(header$),34
  BPUT #2,font_adr%,4096
  CLOSE
  '
  ' Noch ein Font umwandeln?:
  al1$="Das STAD-Font "+LEFT$(n$,INSTR(n$,".")-1)
  al2$="|ist abgespeichert unter: "+"|"
  al$=al1$+al2$+fgem$
  ALERT 3,al$+"|Noch ein Font umwandeln?",1," JA |NEIN",al%
UNTIL al%=2
CLS
END
