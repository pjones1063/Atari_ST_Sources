' Kompakter Fraktal-Generator
' (Entw. mit GFA-Basic.)
' D. Rabich, 2.2.1988
'
' (c) MAXON Computer 1989
'
GOSUB fractal(0,0,639,399,-2.5,1.5,4,3,75,0,0) ! zum Fractal-Generator
OPEN "O",#1,"FRACTAL.DOO"     ! Bildausgabe
GET 0,0,639,399,bild$
PRINT #1,MID$(bild$,7);
CLOSE #1
END
'
' Fractal-Generator
PROCEDURE fractal(x%,y%,w%,h%,xk,yk,wk,hk,max_iter%,a,b)
  LOCAL v_re_schritt,v_im_schritt ! Schrittweiten
  LOCAL i%,j%              ! fÅr Pixelkoordinaten
  LOCAL z_re_h             ! fÅr Iteration
  LOCAL z_re_m,z_im_m
  v_re_schritt=wk/w%       ! Schrittweite horizontal
  v_im_schritt=-hk/h%      ! Schrittweite vertikal
  v_im=yk                  ! Startwert vertikal/oben
  FOR j%=y% TO y%+h%
    v_re=xk                ! Startwert horizontal/links
    FOR i%=x% TO x%+w%
      z_re_m=a             ! Iteration Mandelbrot
      z_im_m=b             ! z-Werte konstant
      zaehler%=0
      WHILE zaehler%<max_iter% AND z_re_m*z_re_m+z_im_m*z_im_m<4
        z_re_h=z_re_m
        z_re_m=z_re_m*z_re_m-z_im_m*z_im_m+v_re
        z_im_m=2*z_re_h*z_im_m+v_im
        INC zaehler%
      WEND
      ' Die folgende Farbbestimmung kînnte man auch
      ' in einer Zeile zusammenfassen, aber das wÅrde
      ' geringfÅgig komplizierter.
      IF zaehler%>=max_iter%
        COLOR 1
      ELSE
        COLOR -((zaehler%<=max_iter% DIV 2) AND (zaehler% MOD 2=1) AND ((i%+j%) MOD 2=1))
      ENDIF
      ' Ohne Grauabstufung:
      COLOR -(zaehler%>=max_iter%)
      ' FÅr Farbe (Bsp.: 8 Farben) :
      '   If Zaehler%>=Max_iter%
      '      Color=0
      '   Else
      '      Color (Zaehler% Mod 7)+1
      '   Endif
      PLOT i%,j%                       ! Ausgabe
      ADD v_re,v_re_schritt
    NEXT i%
    ADD v_im,v_im_schritt
  NEXT j%
RETURN
' Ende
