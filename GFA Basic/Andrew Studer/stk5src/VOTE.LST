PROCEDURE vote
  LOCAL vote_room&,vote_answers|,vote_quest$
  DIM vote_a$(26)                       ! vote choices
  DIM vote_a|(26)                       ! vote answers
  ~FSETDTA(BASEPAGE+128)
  eof%=FSFIRST(vote$+"*.V*",55)
  DO UNTIL eof%
    fn$=vote$+CHAR{BASEPAGE+158}
    dt%=INT{BASEPAGE+152}
    tm%=INT{BASEPAGE+150}
    new_date(last.date%,last.time%,dt%,tm%)
    temp|=0
    temp$=MID$(fn$,LEN(fn$)-1,2)
    IF MID$(temp$,1,1)<>"O"
      temp|=VAL(temp$)-1
    ENDIF
    IF (MID$(fn$,LEN(fn$)-1,1)="O" OR BTST(u_group&,temp|)) AND z|=1
      OPEN "I",#2,fn$
      INPUT #2;vote_room&
      INPUT #2;vote_answers|
      LINE INPUT #2;vote_quest$
      FOR i|=1 TO vote_answers|
        LINE INPUT #2;vote_a$(i|)
      NEXT i|
      CLOSE #2
      temp2$=fn$
      MID$(temp2$,LEN(temp2$)-2,3)="ANS"
      IF EXIST(temp2$)
        OPEN "I",#2,temp2$
        FOR i|=1 TO vote_answers|
          INPUT #2;vote_a|(i|)
        NEXT i|
        CLOSE #2
        REPEAT
          sendtext(0,cr$+vote_quest$+cr$+cr$)
          FOR i|=1 TO vote_answers|
            m$="["+CHR$(i|+64)+"] "+vote_a$(i|)+cr$
            sendtext(0,m$)
          NEXT i|
          sendtext(0,cr$+"Enter your choice:  ")
          inp(1)
          w$=UPPER$(w$)
        UNTIL ASC(w$)-64>0 AND ASC(w$)-64<=vote_answers|
        INC vote_a|(ASC(UPPER$(w$))-64)
        OPEN "O",#2,temp2$
        FOR i|=1 TO vote_answers|
          PRINT #2;vote_a|(i|)
        NEXT i|
        CLOSE #2
        IF vote_room&>0
          w$="Vote results for "+u_name$+":"+cr$+cr$+"Q:  "+vote_quest$+cr$+"A:  "+vote_a$(ASC(UPPER$(w$))-64)+"."+cr$
          subject$=""
          savmsg(vote_room&,"ST-Keep","",w$)
        ENDIF
      ENDIF
    ENDIF
    eof%=FSNEXT()
  LOOP
  ERASE vote_a|(),vote_a$()
RETURN
'
PROCEDURE read_vote
  LOCAL vote_room&,vote_answers|,vote_quest$
  DIM vote_a$(26)                       ! vote choices
  DIM vote_a|(26)                       ! vote answers
  ~FSETDTA(BASEPAGE+128)
  eof%=FSFIRST(vote$+"*.v*",55)
  DO UNTIL eof%
    fn$=vote$+CHAR{BASEPAGE+158}
    temp|=0
    temp$=MID$(fn$,LEN(fn$)-1,2)
    IF MID$(temp$,1,1)<>"O"
      temp|=VAL(temp$)-1
    ENDIF
    IF (MID$(fn$,LEN(fn$)-1,1)="O" OR BTST(u_group&,temp|))
      OPEN "I",#2,fn$
      INPUT #2;vote_room&
      INPUT #2;vote_answers|
      LINE INPUT #2;vote_quest$
      FOR i|=1 TO vote_answers|
        LINE INPUT #2;vote_a$(i|)
      NEXT i|
      CLOSE #2
      temp2$=fn$
      MID$(temp2$,LEN(temp2$)-2,3)="ANS"
      OPEN "I",#2,temp2$
      FOR i|=1 TO vote_answers|
        INPUT #2;vote_a|(i|)
      NEXT i|
      CLOSE #2
      m$=cr$+vote_quest$+cr$+cr$
      y&=0
      FOR i|=1 TO vote_answers|
        space_pad_left(STR$(vote_a|(i|)),3)
        m$=m$+w$+" - "+vote_a$(i|)+cr$
        ADD y&,vote_a|(i|)
      NEXT i|
      buffer$=m$+cr$+"Total votes counted:  "+STR$(y&)+" out of "+STR$(active.users&)+" = "+STR$(INT((y&/active.users&)*100))+"%"+cr$+cr$
      sendtext(0,buffer$)
      IF BTST(u_privs|,aide|)
        m$="Do you wish to save these results as a message in this room"
        yesno(m$)
        IF xit|
          subject$=""
          savmsg(room&,"ST-Keep","",buffer$)
        ENDIF
      ELSE
        press_return
      ENDIF
    ENDIF
    eof%=FSNEXT()
  LOOP
  ERASE vote_a|(),vote_a$()
RETURN
'
