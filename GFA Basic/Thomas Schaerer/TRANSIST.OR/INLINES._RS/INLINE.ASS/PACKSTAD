; Packt genau wie STAD ein Byte zu viel ........



;d1-d3 aktuelle Datenbytes

;a1=Bildanf
;a2=Bildakt
          movea.l   4(sp),a1  ;Bildadr. vom Stack holen
          movea.l   a1,a2

;a3=puffanf
;a4=puffakt
          movea.l   8(sp),a3  ;Puffadr. vom Stack holen
          movea.l   a3,a4
          adda.l    #7,a4
;a5=Bildende



          movea.l   a1,a5     ;Bildende in d5 speichern
          adda.l    #32001,a5

;d4=Packbyte
;d5=Kennbyte
;d6=Spezbyte

          clr.l     d1
          clr.l     d2
          clr.l     d3

          move.b    4(a3),d5  ;Kennbyte holen
          move.b    5(a3),d4  ;Packbyte holen
          move.b    6(a3),d6  ;Spezbyte holen

schleife1:          

          move.b    (a2),d1   ; drei Datenbytes holen
          move.b    1(a2),d2
          move.b    2(a2),d3

          cmp.b     d1,d2     ; Springe wenn ungleich
          bne       ungleich

          cmp.b     d1,d4     ; Packbytes ?
          bne       keinpackbyte

;Packbytes packen
;===========================
          move.b    d5,(a4)   ;Kennbyte speichern
          move.b    #0,1(a4)  ;n=0
          adda.l    #1,a2     ;Bildzeiger+1

schleife2:          
          cmp.b     (a2),d4   ;noch Packbyte ?
          bne       endezwei

          cmpi.b    #255,1(a4)          ;Block voll ?
          beq       endezwei

          cmpa.l    a5,a2     ;Bildende erreicht ?
          bge       endezwei


          adda.l    #1,a2     ;Bildzeiger+1
          addi.b    #1,1(a4)  ;n+1

          bra       schleife2

endezwei: 
          adda.l    #2,a4     ;puffzeiger+2

          bra       packerende

;===========================

keinpackbyte:       


          cmp.b     d2,d3     ; 3. Byte auch gleich ?
          bne       ungleich

;Spezbytes packen
;===============================
          move.b    d6,(a4)   ;Spezbyte speichern
          move.b    d1,1(a4)  ;Datenbyte speichern
          move.b    #0,2(a4)  ;n=0
          adda.l    #1,a2     ;Bildzeiger+1
schleife3:          
          cmp.b     (a2),d1   ;Byte noch gleich ?
          bne       endedrei

          cmpa.l    a5,a2     ;Bildende erreicht ?
          bge       endedrei

          cmpi.b    #255,2(a4)          ;Block voll ?
          beq       endedrei

          adda.l    #1,a2     ;Bildzeiger+1
          addi.b    #1,2(a4)  ;n+1

          bra       schleife3

endedrei: 

          adda.l    #3,a4     ;Puffzeiger+3

          bra       packerende

;===============================
ungleich: 
          cmp.b     d1,d5     ;Datenbyte=Kennbyte ?
          beq       spezindaten

          cmp.b     d1,d6     ;Datenbyte=Spezbyte ?
          beq       spezindaten

          bra       nichtszupacken

spezindaten:        
;Kenn- oder Spezbyte in den Daten
;===============================
          move.b    d6,(a4)   ;Spezbyte speichern
          move.b    d1,1(a4)  ;Datenbyte speichern
          move.b    #0,2(a4)  ;n=0
          adda.l    #1,a2     ;Bildzeiger+1
schleife4:          
          cmp.b     (a2),d1   ;Byte noch gleich ?
          bne       endevier

          cmpi.b    #255,2(a4)          ;Block voll ?
          beq       endevier

          cmpa.l    a5,a2     ;Bildende erreicht ?
          bge       endevier


          adda.l    #1,a2     ;Bildzeiger+1
          addi.b    #1,2(a4)  ;n+1

          bra       schleife4

endevier: 

          adda.l    #3,a4     ;Pufferzeiger+3

          bra       packerende

;===============================

nichtszupacken:     

;Normale Bytes kopieren
;===============================
          move.b    (a2)+,(a4)+
;===============================

packerende:         

          cmpa.l    a5,a2     ;Bildende erreicht ?
          bge       schluss


          bra       schleife1
schluss:  

          suba.l    a3,a4     ;LÑnge berechnen und
          move.l    a4,d0     ;in d0 ans Basic Åbergeben

          rts       
