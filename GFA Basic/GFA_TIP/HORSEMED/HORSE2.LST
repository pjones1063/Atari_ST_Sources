Gosub Text
Deffill 0,0,8
Dim A$(9)
Deftext 1,1,0,20
Text 190,125,"LOADING..."
Text 350,125,"Horse"
If Exist("horse.bi2")
  Open "I",#1,"horse.bi2"
Else
  Open "I",#1,"\horse.bi2"
Endif
For T=1 To 9
  Text 420,125,Str$(T)
  A$(T)=Input$(Cvi(Input$(2,#1)),#1)
Next T
Close #1
Pbox 190,100,500,140
'
A=40
Y=-100
S=5
Hidem
Setcolor 3,0,0,0
Setcolor 1,7,7,7
Setcolor 2,0,0,7
Setcolor 0,7,7,7
Repeat
  For T=1 To 9
    Add X,S
    Add Y,2*S
    If X>640
      X=-187
    Endif
    If Y>700
      Y=-187
    Endif
    Pbox Y,90,Y-2*S,150  !  Clear screen area
    Vsync                !  Try to get rid of the screen glitches...
    Put Y,90,A$(T)       !  Here's where we get PUT horse image to the screen
    B$=Inkey$
    If B$="-"
      Add A,5
      If A>1000
        A=1000
      Endif
    Endif
    If B$="("
      Sub S,1
      If S<0
        S=0
      Endif
    Endif
    If B$="+"
      Sub A,5
      If A<-10
        A=-10
      Endif
    Endif
    If B$=")"
      Add S,1
      If S>30
        S=30
      Endif
    Endif
  Next T
  For G=1 To A  !   Adjustable delay due to 'A'
  Next G
Until Mousek
Showm
End
Procedure Text
  Box 110,10,520,75
  Box 107,7,524,77
  Box 106,6,523,78
  Deftext 1,5,0,16
  Text 195,28,"ANIMATION with GFA-BASIC"
  Deftext 1,0,0,7
  Text 130,42,"Programmed by:"
  Deftext 1,1,0,8
  Text 298,42,"Carl-Christian Meyer"
  Deftext 1,0,0,4
  Text 163,53,"Translated by: Thomas F. Collins, Las Vegas, NV, USA"
  Deftext 1,0,0,6
  Text 150,62,"Converted to Med Resolution by Terry White"
  Deftext 1,1,0,6
  Text 138,73,"GFA-BASIC: distributed in USA by MichTron Inc."
  Box 40,160,600,199
  Box 38,162,602,199
  Box 37,164,603,388
  Deftext 1,,0,6
  Text 240,171,"Program-controls:"
  Deftext 1,0,0,6
  Text 70,180,"'+' = Horse moves forward            ')' = Horse runs faster"
  Text 70,188,"'-' = horse moves backward           '(' = Horse runs slower"
  Text 70,196,"         Pressing either Mouse button will exit program"
Return
