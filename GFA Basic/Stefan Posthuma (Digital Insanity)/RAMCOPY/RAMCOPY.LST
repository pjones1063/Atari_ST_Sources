' ramcopy
'
Cls
Print
Print " Ramcopy v1.0 By Stefan Posthuma"
Print " Created by Digital Insanity"
Print
If Fre(0)<40000                                ! need about 40000 bytes
  Print " Sorry, not enough free memory...."
  Print
  Print " Press a key...."
  Void Inp(2)
  Cls
  System
Endif
Einde!=False
@Bestaat                ! check for ramdisk.inf
Buf$=Space$(32000)      ! 32000 bytes buffer for copy
Void Fre(0)
While Einde!=False      ! while not EOF
  @Lezen                ! read next filename from ramdisk.inf
  If Bestaat!=True      ! file exists?
    @Copy               ! yes, copy it
  Endif
Wend
Close #1
Print
Print " Press a key...."
Void Inp(2)
Clr Buf$
Clear
Cls
System                  ! back to the desktop
'
Procedure Bestaat
  N$="a:\auto\ramdisk.inf"
  If Not Exist(N$)
    N$="a:\ramdisk.inf"
    If Not Exist(N$)
      Einde!=True
      Print " ramdisk.inf does not exist. No copy..."
    Endif
  Endif
  If Einde!=False
    Print " Found ramdisk.inf....copy files (Y/N)"
    Print
    Repeat
      K%=Inp(2)
    Until K%=121 Or K%=110
    If K%=110               ! user pressed N, exit program
      Cls
      System
    Endif
    Open "i",#1,N$          ! open ramdisk.inf
  Endif
Return
'
Procedure Lezen
  Bestaat!=True
  If Eof(#1)                ! End-Of-File
    Einde!=True
    Bestaat!=False
  Else
    Line Input #1,A$        ! get filename
    If Left$(A$)="@"        ! create folder
      A$=Right$(A$,Len(A$)-1)
      Print " Creating folder ";A$;"...";
      Chdrive (4)
      @Dir_exist(A$,*E%)    ! check if folder already exists
      If E%=-33             ! no
        Mkdir A$            ! create it
      Else
        Print "it already exists!"
        Bestaat!=False
      Endif
      Chdrive (1)
      If Bestaat!=True
        Print "done!"
        Bestaat!=False
      Endif
    Else
      A$="A:\"+A$           ! add pathname
      Print " Checking ";A$;
      If Not Exist(A$)      ! check if file exists
        Bestaat!=False
        Print At(50,Crslin);"it does not exist!"
      Endif
    Endif
  Endif
Return
'
Procedure Copy
  Open "i",#2,A$
  L%=Lof(#2)       ! determine length of the file to be copied
  If L%>=Dfree(4)  ! not enough memory in ramdisk?
    Print At(50,Crslin);"no way, file too large!"
  Else
    Print At(50,Crslin);"copying....";
    Open "o",#3,"d:"+Right$(A$,Len(A$)-3)
    While L%>32000
      Bget #2,Varptr(Buf$),32000  ! read 32000 bytes from original file
      Bput #3,Varptr(Buf$),32000  ! write them to ramdisk
      Sub L%,32000
    Wend
    Bget #2,Varptr(Buf$),L%       ! read remainder
    Bput #3,Varptr(Buf$),L%       ! write it
    Close #3
    Print "done!"
  Endif
  Close #2
Return
'
Procedure Dir_exist(Name$,E.%)
  Local A$
  A$=Space$(44)
  Void Gemdos(&H1A,L:Varptr(A$))  ! create disk transfer buffer
  *E.%=Gemdos(&H4E,L:Varptr(Name$),&H10)  ! search for filename
Return
