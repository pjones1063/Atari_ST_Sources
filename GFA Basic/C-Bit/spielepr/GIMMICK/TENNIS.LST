$i+
$u+
ON BREAK GOSUB aus
ON ERROR GOSUB aus
CHDIR "\st„bchen.gam"
RESERVE
RESERVE FRE(0)-64000
DIM player$(2),ball$(4)
scmem%=XBIOS(2)
scmem2%=scmem%-32000
scmem3%=scmem%-64000
GOSUB neoload
OPEN "i",#1,"schl„ger.res"
lange&=CVI(INPUT$(2,#1))
FOR t|=0 TO 1
  player$(t|)=INPUT$(lange&,#1)
NEXT t|
CLOSE #1
OPEN "i",#1,"Ball.res"
lange&=CVI(INPUT$(2,#1))
FOR t|=0 TO 4
  ball$(t|)=INPUT$(lange&,#1)
NEXT t|
CLOSE #1
start:
a|=INT(4*RND)
IF a|=0
  bx&=1
  by&=INT(150*RND)+20
  zeix&=4
  zeiy&=4
ENDIF
IF a|=1
  bx&=280
  by&=INT(150*RND)+20
  zeix&=-4
  zeiy&=4
ENDIF
IF a|=2
  bx&=0
  by&=INT(150*RND)+20
  zeix&=4
  zeiy&=-4
ENDIF
IF a|=3
  bx&=280
  by&=INT(150*RND)+20
  zeix&=-4
  zeiy&=-4
ENDIF
EVERY 10 GOSUB sound
aplayer|=0
bplayer|=0
shoot1&=10
shoot2&=10
vol|=14
vol2|=14
y&=85
yy&=85
x&=50
xx&=270
key%=0
robot!=0
GRAPHMODE 2
DO
  FOR t&=220 TO -40 STEP -2
    GOSUB bildflip
    DEFTEXT 6,0,0,8
    TEXT 60,t&,"PONG's  MEGA TENNIS"
    DEFTEXT 5,0,0,6
    TEXT 40,t&+20,"(c) C.A.S  (SPACE - SOFT) 1988"
    TEXT 20,t&+40,"PRESS F1 for ONE F2 for TWO PLAYER"
    EXIT IF INP?(2)=TRUE
  NEXT t&
  IF INP?(2)=TRUE
    key%=INP(2)
  ENDIF
  EXIT IF key%=187 OR key%=188
LOOP
IF key%=187
  robot!=-1
ENDIF
DEFTEXT 1,0,0,6
GOSUB bildflip
PUT 99,2,a$
PUT 196,2,b$
BMOVE scmem2%,scmem3%,32000
DO
  IF aplayer|=15 OR bplayer|=15
    EVERY STOP
    GOSUB game_over
    GOTO start
  ENDIF
  IF INP(-2)=-1
    key%=INP(2)
    IF key%=32
      DO
        EXIT IF INP(2)
      LOOP
    ENDIF
    IF key%=187 OR key%=188
      EVERY STOP
      GOTO start
    ENDIF
  ENDIF
  a|=STICK(1)
  b|=STICK(0)
  IF robot!=-1
    GOSUB robot
  ENDIF
  IF STRIG(1)=0
    ers1!=0
  ENDIF
  IF STRIG(0)=0
    ers2!=0
  ENDIF
  IF STRIG(1)=-1
    SELECT a|
    CASE 1
      shoot1&=-4
    CASE 9
      shoot1&=-2
    CASE 8
      shoot1&=0
    CASE 10
      shoot1&=2
    CASE 2
      shoot1&=4
    CASE 0
      IF ers1!=0
        shoot1&=10
      ENDIF
    ENDSELECT
    ers1!=-1
  ELSE
    SELECT a|
    CASE 1
      SUB y&,6
    CASE 2
      ADD y&,6
    CASE 8
      ADD x&,5
    CASE 4
      SUB x&,5
    CASE 9
      SUB y&,6
      ADD x&,5
    CASE 6
      ADD y&,6
      SUB x&,5
    CASE 10
      ADD x&,5
      ADD y&,6
    CASE 5
      SUB x&,5
      SUB y&,6
    ENDSELECT
  ENDIF
  IF STRIG(0)=-1 AND robot!=0
    SELECT b|
    CASE 5
      shoot2&=-2
    CASE 4
      shoot2&=0
    CASE 6
      shoot2&=2
    CASE 0
      IF ers2!=0
        shoot2&=10
      ENDIF
    CASE 1
      shoot2&=-4
    CASE 2
      shoot2&=4
    ENDSELECT
    ers2!=-1
  ELSE IF robot!=0
    SELECT b|
    CASE 1
      SUB yy&,6
    CASE 2
      ADD yy&,6
    CASE 8
      ADD xx&,5
    CASE 4
      SUB xx&,5
    CASE 9
      SUB yy&,6
      ADD xx&,5
    CASE 6
      ADD yy&,6
      SUB xx&,5
    CASE 10
      ADD xx&,5
      ADD yy&,6
    CASE 5
      SUB xx&,5
      SUB yy&,6
    ENDSELECT
  ENDIF
  IF yy&<4
    yy&=4
  ENDIF
  IF yy&>170
    yy&=170
  ENDIF
  IF y&<4
    y&=4
  ENDIF
  IF y&>170
    y&=170
  ENDIF
  IF x&<10
    x&=10
  ENDIF
  IF x&>155
    x&=155
  ENDIF
  IF xx&<161
    xx&=161
  ENDIF
  IF xx&>305
    xx&=305
  ENDIF
  GOSUB schlager
LOOP
PROCEDURE schlager
  GOSUB bildflip
  PUT x&,y&,player$(0)
  PUT xx&,yy&,player$(1)
  GOSUB ball
RETURN
PROCEDURE ball
  INC ball|
  IF ball|=3
    ball|=0
  ENDIF
  ADD bx&,zeix&
  ADD by&,zeiy&
  IF bx&<x&+5 AND bx&>x&-10 AND by&<y&+25 AND by&+11>y& AND zeix&=-4
    aa!=-1
    vol2|=14
    zeix&=4
    IF shoot1&<>10
      zeiy&=shoot1&
    ENDIF
  ENDIF
  IF bx&>xx&-10 AND bx&<xx&+5 AND by&<yy&+25 AND by&+11>yy& AND zeix&=4
    aa!=-1
    vol2|=14
    zeix&=-4
    IF shoot2&<>10
      zeiy&=shoot2&
    ENDIF
  ENDIF
  IF bx&>319
    bx&=0
    INC bplayer|
    PUT 99,2,a$
    TEXT 104,11,bplayer|
    RC_COPY scmem2%,99,2,25,12 TO scmem3%,99,2
    vol2&=-15
    oo!=-1
  ENDIF
  IF bx&<0
    bx&=319
    INC aplayer|
    PUT 196,2,b$
    TEXT 201,11,aplayer|
    RC_COPY scmem2%,196,2,25,12 TO scmem3%,196,2
    vol2&=-15
    oo!=-1
  ENDIF
  IF by&>185 AND zeiy&>0
    zeiy&=-zeiy&
    nn!=-1
    vol|=14
  ENDIF
  IF by&<4 AND zeiy&<0
    zeiy&=-zeiy&
    nn!=-1
    vol|=14
  ENDIF
  PUT bx&,by&,ball$(4),4
  PUT bx&,by&,ball$(ball|),7
RETURN
PROCEDURE bildflip
  SWAP scmem%,scmem2%
  VOID XBIOS(5,L:scmem2%,L:scmem%,-1)
  VSYNC
  BMOVE scmem3%,scmem2%,32000
RETURN
PROCEDURE neoload
  OPEN "i",#1,"tfeld.neo"
  SEEK #1,4
  reg$=INPUT$(32,#1)
  SEEK #1,128
  VOID XBIOS(6,L:VARPTR(reg$))
  BGET #1,scmem3%,32000
  CLOSE #1
  VOID XBIOS(5,L:scmem3%,L:-1,-1)
  GET 99,2,124,14,a$
  GET 196,2,221,14,b$
RETURN
PROCEDURE aus
  CONT
RETURN
PROCEDURE sound
  IF nn!=-1
    SUB vol|,2
    SOUND 1,vol|,#1000+vol|*10
    IF vol|=0
      nn!=0
    ENDIF
  ENDIF
  IF aa!=-1
    SUB vol2|,2
    SOUND 2,vol2|,4,4
    WAVE 17+256*vol2|,0
    IF vol2|=0
      aa!=0
    ENDIF
  ENDIF
  IF oo!=-1
    IF vol2&<15
      ADD vol2&,2
      vol1&=15-ABS(vol2&)
      SOUND 2,vol1&,#100-vol1&
      SOUND 1,vol1&,#1000-vol1&*20
      SOUND 3,vol1&,#2000-vol1&*30
      WAVE 23+ABS(vol2&*512),0
    ELSE
      oo!=0
      WAVE 1,0
    ENDIF
  ENDIF
RETURN
PROCEDURE game_over
  GOSUB bildflip
  DEFTEXT 6,0,0,32
  TEXT 90,100,"GAME OVER"
  GOSUB bildflip
  vol2&=-30
  EVERY 10 GOSUB ssound
  FOR t%=0 TO 15000
    ADD ax&,&H1110
    SDPOKE &HFF8246,ax&
  NEXT t%
  VOID XBIOS(6,L:VARPTR(reg$))
  EVERY STOP
RETURN
PROCEDURE ssound
  IF vol2&<30
    INC vol2&
    vol1&=30-ABS(vol2&)
    SOUND 2,vol1&/2
    SOUND 1,vol1&/2,#150-vol1&
    SOUND 3,vol1&/2,#70-vol1&
    WAVE 21+ABS(vol2&*256),0
  ELSE
    WAVE 0,0
  ENDIF
RETURN
PROCEDURE robot
  IF bx&<150
    SUB xx&,5
  ENDIF
  IF bx&>xx&
    ADD xx&,5
  ENDIF
  IF y&>yy&+75
    shoot2&=-4
  ELSE IF y&<yy&-50
    shoot2&=4
  ELSE
    IF shoot1&<>0
      shoot2&=10
    ELSE
      IF x&<85
        shoot2&=4
      ELSE
        shoot2&=-4
      ENDIF
    ENDIF
  ENDIF
  IF yy&>by&+5 AND richt|=1
    INC verz|
    IF verz|=6
      richt|=0
      verz|=0
    ENDIF
  ENDIF
  IF richt|=0 AND yy&>by&+5
    SUB yy&,6
  ENDIF
  IF yy&+24<by&+5 AND richt|=0
    INC verz2|
    IF verz2|=8
      richt|=1
      verz2|=0
    ENDIF
  ENDIF
  IF richt|=1 AND yy&+24<by&+5
    ADD yy&,8
    richt|=1
  ENDIF
RETURN
