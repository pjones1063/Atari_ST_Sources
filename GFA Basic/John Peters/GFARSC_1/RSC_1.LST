@Rsrc_free
@Rsrc_load("rsc_1.rsc")
A%=Dpeek(Gintout)
If A%=0
  Alert 1,"Can't find Resource file|Try and select it with|the fileselector",1,"OK",V%
  Fileselect "rsc_1.rsc","",R$
  If R$<>""
    @Rsrc_load(R$)
  Else
    End
  Endif
Endif
@Set_vars
@Rsrc_gaddr(0,Infobox,*Tree%)
@Form_center(Tree%,*X%,*Y%,*W%,*H%)
Get X%,Y%,X%+W%,Y%+H%,Temp$
@Objc_draw(Tree%,0,7,X%,Y%,W%,H%)
@Form_do(Tree%,0)
Result%=Dpeek(Gintout)
Put X%,Y%,Temp$
@Ob_clr(Tree%,Result%)
'
Procedure Ob_clr(Tree%,Obj%)
  Dpoke ((Tree%+24*Obj%)+10),Dpeek((Tree%+24*Obj%)+10) Xor 1
Return
'
Procedure Objc_draw(Tree.%,Firstob.%,Depth.%,Clipx.%,Clipy.%,Clipw.%,Cliph.%)
  @Fill_gcontrol(42,6,1,1,0)
  @Fill_gintin(Firstob.%,Depth.%,Clipx.%,Clipy.%,Clipw.%,Cliph.%)
  Lpoke Addrin,Tree.%
  Gemsys
Return
'
Procedure Form_do(Tree.%,Editobj.%)
  @Fill_gcontrol(50,1,1,1,0)
  Dpoke Gintin,Editobj.%
  Lpoke Addrin,Tree.%
  Gemsys
Return
'
Procedure Form_center(Tree.%,X.%,Y.%,Width.%,Height.%)
  @Fill_gcontrol(54,0,5,1,0)
  Lpoke Addrin,Tree.%
  Gemsys
  *X.%=Dpeek(Gintout+2)
  *Y.%=Dpeek(Gintout+4)
  *Width.%=Dpeek(Gintout+6)
  *Height.%=Dpeek(Gintout+8)
Return
'
Procedure Rsrc_load(F_name$)
  @Fill_gcontrol(110,0,1,1,0)
  Lpoke Addrin,Varptr(F_name$)
  Gemsys
Return
'
Procedure Rsrc_free
  @Fill_gcontrol(111,0,1,0,0)
  Gemsys
Return
'
Procedure Rsrc_gaddr(Type.%,Index.%,Address.%)
  @Fill_gcontrol(112,2,1,0,1)
  Dpoke Gintin,Type.%
  Dpoke Gintin+2,Index.%
  Gemsys
  *Address.%=Lpeek(Addrout)
Return
'
Procedure Fill_gcontrol(V1.%,V2.%,V3.%,V4.%,V5.%)
  Dpoke Gcontrl,V1.%
  Dpoke Gcontrl+2,V2.%
  Dpoke Gcontrl+4,V3.%
  Dpoke Gcontrl+6,V4.%
  Dpoke Gcontrl+8,V5.%
Return
'
Procedure Fill_gintin(V1.%,V2.%,V3.%,V4.%,V5.%,V6.%)
  Dpoke Gintin,V1.%
  Dpoke Gintin+2,V2.%
  Dpoke Gintin+4,V3.%
  Dpoke Gintin+6,V4.%
  Dpoke Gintin+8,V5.%
  Dpoke Gintin+10,V6.%
Return
'
Procedure Set_vars
  ' (* resource set indicies for RSC_1 *)
  Let Infobox=0    !form/dialog ## Tree Structure ##
  Let Paper=0      !BOX in tree INFOBOX
  Let Line1=1      !STRING in tree INFOBOX
  Let Line2=2      !STRING in tree INFOBOX
  Let Line3=3      !STRING in tree INFOBOX
  Let Line4=4      !STRING in tree INFOBOX
  Let Line5=5      !STRING in tree INFOBOX
  Let Exit=6       !BUTTON in tree INFOBOX
Return
