RESERVE -666
~RSRC_FREE()
a&=RSRC_LOAD("prnt_set.rsc")
IF a&=0
  ALERT 1,"Can't find Resource file|Try and select it with|the fileselector",1,"OK",v|
  FILESELECT "prnt_set.rsc","",r$
  IF r$<>""
    a&=RSRC_LOAD(r$)
  ELSE
    END
  ENDIF
ENDIF
set_vars
~RSRC_GADDR(0,prntset|,tree%)
'
mask=63                                                  ! mask off bits 6 to 15
current_setting&=XBIOS(33,W:-1) AND mask
set_form(current_setting&)
'
~FORM_CENTER(tree%,x&,y&,w&,h&)
GET x&,y&,x&+w&,y&+h&,temp$
~OBJC_DRAW(tree%,0,7,x&,y&,w&,h&)
result|=FORM_DO(tree%,0)
OB_STATE(tree%,result|)=BCLR(OB_STATE(tree%,result|),0)
'
button=3
FOR n=0 TO 5
  IF OB_STATE(tree%,button)                         ! bit 0 is set (ie selected)
    new_setting&=BCLR(new_setting&,n)
  ELSE
    new_setting&=BSET(new_setting&,n)
  ENDIF
  ADD button,3
NEXT n
PRINT new_setting&
~XBIOS(33,W:new_setting&)                              ! set printer to new type
'
PUT x&,y&,temp$
~RSRC_FREE()
RESERVE
END
'
PROCEDURE set_form(set_&)
  button=3
  FOR n=0 TO 5
    IF BTST(set_&,n)  ! bit is set
      OB_STATE(tree%,button)=BCLR(OB_STATE(tree%,button),0)
      OB_STATE(tree%,button+1)=BSET(OB_STATE(tree%,button+1),0)
    ELSE
      OB_STATE(tree%,button)=BSET(OB_STATE(tree%,button),0)
      OB_STATE(tree%,button+1)=BCLR(OB_STATE(tree%,button+1),0)
    ENDIF
    ADD button,3
  NEXT n
RETURN
'
PROCEDURE set_vars
  LET prntset|=0  ! DIALOG TREE
  ' the various bits of XBIOS(33) contain information
  ' about your printer
  ' Bit 0
  LET dmp|=3        ! dot matrix
  LET daisy|=4      ! daisy wheel
  ' Bit 1
  LET mono|=6       ! monochrome printer
  LET colour|=7     ! colour printer
  ' Bit 2
  LET atarip|=9     ! Atari printer
  LET epson|=10     ! Epson printer
  ' Bit 3
  LET draft|=12     ! Test mode
  LET nlq|=13       ! NLQ mode
  ' Bit 4
  LET centronc|=15  ! Centronics Port
  LET rs232|=16     ! RS232 Port
  ' Bit 5
  LET continus|=18  ! Continuous paper
  LET single|=19    ! Single Sheet
  '
  LET prtok|=20                       !   Button
RETURN
