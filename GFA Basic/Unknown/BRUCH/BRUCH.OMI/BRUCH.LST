10  DIM Wert#(40)
20  REPEAT
30    REPEAT
40      INPUT Eingabe$
45      Dez#= VAL(Eingabe$+"000000000000000000")
50      Signum= SGN(Dez#)
60      Vorkomma#= INT( ABS(Dez#))
70      Wert#(0)= FRAC( ABS(Dez#))
80      IF Wert#(0)=0 THEN
90        PRINT Vorkomma#*Signum;"/1": ENDIF
100   UNTIL Wert#(0)>0
110   T= TIMER
120   Genau#=1D-12
130   I=0
140   REPEAT
150     Zahl#=1
160     Wert#(I+1)= FRAC(1/Wert#(I))
170     I=I+1
180     Merk=I
190     REPEAT
200       I=I-1
210       Zahl#= INT(Zahl#/Wert#(I)+.5)
220     UNTIL I=0
230     Nenner#=Zahl#
240     Zaehler#= INT(Zahl#*Wert#(I)+.5)
250     I=Merk
260   UNTIL ABS(Wert#(0)-Zaehler#/Nenner#)-Genau#<=0
270   IF Vorkomma#=0 THEN
280     IF Signum<0 THEN
290       PRINT "  = -";
300     ELSE
310       PRINT "  = ";: ENDIF
320     PRINT Zaehler#;"/";Nenner#
330   ELSE
340     PRINT "  = ";Vorkomma#*Signum;" ";Zaehler#;"/";Nenner#;
350     PRINT "  = ";Vorkomma#*Signum*Nenner#+Zaehler#;"/";Nenner#
360   ENDIF
370   T= TIMER -T
380   PRINT T/200;" sec"
390   PRINT
400   PRINT " Abbruch: Undo"
410   PRINT " Weiter:  sonst."
420   PRINT
430   A$= INPUT$(1)
440 UNTIL ASC(A$)=0
450 END
