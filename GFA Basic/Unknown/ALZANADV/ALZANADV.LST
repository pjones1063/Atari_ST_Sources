'
Print At(25,5);"C i t y  O f  A l z a n ."
Print
Print "           This Adventure takes place in a fictious city named 'Alzan',"
Print "    which is built on top of the sea cliffs. It is inhabited by thieves"
Print "    and cut-throats. Your quest is to find a way out of the city before"
Print "    they grab you or before the plague takes hold of you. Unfortunately"
Print "    the city is surrounded by extremely high walls and so you must find"
Print "    a way to scale them!!!"
Print
Print "                              Press any key."
'
Key=Inp(2)
Cls
Gosub Initialisation
L100:
Print
If S%(2)=0 Then
  Goto L200
Endif
If C%(2)>0 Then
  C%(2)=C%(2)-1
Endif
If S%(3)<>0 Then
  Goto L200
Endif
Deffill 1,1,1
Fill 0,0
Deftext 1,1,0,8
Text 16,100,"It is dark! Better get some light or you may be in trouble."
Print At(10,10);
If C%(3)>0 Then
  C%(3)=C%(3)-1
Endif
Goto L1000
L200:
On Room% Gosub Room_1,Room_2,Room_3,Room_4,Room_5,Room_6,Room_7,Room_8,Room_9,Room_10,Room_11,Room_12,Room_13,Room_14,Room_15,Room_16,Room_17,Room_18,Room_19
F%=0
For X%=1 To O%
  If O%(X%)<>Room% Then
    Goto L500
  Endif
  If F%<>0 Then
    Goto L400
  Endif
  Print
  Print "  There is also :-"
  F%=1
  L400:
  Print "   ";O$(X%)
  L500:
Next X%
L1000:
T%=1
Goto L2000
L1100:
If C%(1)>0 Then
  C%(1)=C%(1)-1
Endif
If C%(4)>0 Then
  C%(4)=C%(4)-1
Endif
Print
Line Input "  What now :- ";Y$
Y$=Upper$(Y$)
Cls
Print
Print "  What now :- ";Y$
Y%=0
P$(2)="00"
For W%=1 To 2
  L1210:
  Gosub L6000
  If Y%>=Len(Y$) Then
    W%=3
  Else
    If P$(W%)="00" Then
      Goto L1210
    Endif
  Endif
Next W%
L1300:
If P$(1)<>"00" Then
  Goto L1600
Endif
Print "  Pardon?"
Goto L100
L1600:
Z%=1
L1620:
T$=Mid$(M$(Room%),Z%,2)
If T$="00" Then
  Goto L1900
Endif
If T$<>P$(1) Then
  Goto L1700
Endif
Room%=Val(Mid$(M$(Room%),Z%+2,2))
Goto L100
L1700:
Z%=Z%+4
Goto L1620
L1900:
T%=0
Match%=0
L2000:
Cp%=0
L2100:
Cp%=Cp%+1
If T%=0 Then
  Goto L2300
Endif
E$=C$(Cp%)
Goto L2600
L2300:
If Cp%<=A% Then
  Goto L2400
Endif
If Match%<>0 Then
  Goto L1000
Endif
Print "  You can't";
If Val(P$(1))<13 Then
  Print " go that way";
Endif
Print "."
Goto L100
L2400:
If Mid$(A$(Cp%),1,2)<>P$(1) Then
  Goto L2100
Endif
Y$=Mid$(A$(Cp%),3,2)
If Y$<>"00" And Y$<>P$(2) Then
  Goto L2100
Endif
E$=Mid$(A$(Cp%),5)
L2600: !  *  *  *  *  *  *   Conditions   *  *  *  *  *  *
E%=1
L2700:
If Mid$(E$,E%,1)="." Then
  Goto L3000
Endif
Type=Asc(Mid$(E$,E%,1))-64
N%=Val(Mid$(E$,E%+1,2))
On Type Gosub Cond_a,Cond_b,Cond_c,Cond_d,Cond_e,Cond_f,Cond_g,Cond_h
If Not Ok% Then
  Goto L2100
Endif
E%=E%+3
Goto L2700
'
Procedure Cond_a
  Ok%=(N%=Room%)
Return
'
Procedure Cond_b
  Ok%=(O%(N%)=Room% Or O%(N%)<0)
Return
'
Procedure Cond_c
  Ok%=(O%(N%)<>Room% And O%(N%)>=0)
Return
'
Procedure Cond_d
  Ok%=(O%(N%)<0)
Return
'
Procedure Cond_e
  Ok%=(S%(N%)=1)
Return
'
Procedure Cond_f
  Ok%=(S%(N%)=0)
Return
'
Procedure Cond_g
  Ok%=(C%(N%)=1)
Return
'
Procedure Cond_h
  Ok%=0!(Rnd(100)+1<N%)
Return
'
L3000:
Match%=1
E%=E%+1
L3100:
If Mid$(E$,E%,1)="." Then
  Goto L2100
Endif
Type=Asc(Mid$(E$,E%,1))-64
If Mid$(E$,E%+1)<>"." Then
  N%=Val(Mid$(E$,E%+1,2))
Endif
Break%=0
On Type Gosub Act_a,Act_b,Act_c,Act_d,Act_e,Act_f,Act_g,Act_h,Act_i,Act_j,Act_k,Act_l,Act_m,Act_n,Act_o,Act_p,Act_q,Act_r,Act_s
If Break%=1 Then
  Goto L100
Endif
If Break%=2 Then
  Goto L1000
Endif
If Break%=3 Then
  Goto L1100
Endif
E%=E%+3
Goto L3100
'
Procedure Act_a
  Print
  Print "  You are holding:-"
  F%=1
  For X%=1 To O%
    If O%(X%)>=0
      Goto L4070
    Endif
    Print "   ";O$(X%)
    F%=0
    L4070:
  Next X%
  If F%>0 Then
    Print "  NOTHING!"
  Endif
  Break%=1
Return
'
Procedure Act_b
  If S%(1)<5 Then
    Goto L4140
  Endif
  Print "  You can not carry more."
  L4120:
  Break%=1
  Goto L4095
  L4140:
  If O%(N%)=-1 Then
    Goto L4180
  Endif
  O%(N%)=-1
  S%(1)=S%(1)+1
  Goto L4195
  L4180:
  Print "  You already have it."
  Goto L4120
  L4195:
Return
'
Procedure Act_c
  If O%(N%)=-1 Then
    Goto L4240
  Endif
  Print "  You don't have ";O$(N%)
  Break%=1
  Goto L4260
  L4240:
  O%(N%)=Room%
  S%(1)=S%(1)-1
  L4260:
Return
'
Procedure Act_d
  Print
  On N% Gosub Txt_1,Txt_2,Txt_3,Txt_4,Txt_5,Txt_6,Txt_7,Txt_8,Txt_9,Txt_10,Txt_11,Txt_12,Txt_13,Txt_14,Txt_15,Txt_16
Return
'
Procedure Act_e
  S%(N%)=1
Return
'
Procedure Act_f
  S%(N%)=0
Return
'
Procedure Act_g
  C%(N%)=Val(Mid$(E$,E%+3,2))
  E%=E%+2
Return
'
Procedure Act_h
  Swap O%(N%),O%(N%+1)
Return
'
Procedure Act_i
  O%(N%)=Room%
Return
'
Procedure Act_j
  If O%(N%)<0 Then
    S%(1)=S%(1)-1
  Endif
  O%(N%)=0
Return
'
Procedure Act_k
  Room%=N%
Return
'
Procedure Act_l
  Print "  OKAY."
  Break%=2
Return
'
Procedure Act_m
  Break%=2
Return
'
Procedure Act_n
  Break%=3
Return
'
Procedure Act_o
  Break%=1
Return
'
Procedure Act_p
  Alert 2,"Do you want to Save|the Data first?|Or do you vant|a new Game?",3,"Save|New|Quit",Void
  If Void=1 Then
    Gosub Act_s
    End
  Else
    If Void=2 Then
      Run
    Else
      If Void=3 Then
        End
      Endif
    Endif
  Endif
Return
'
Procedure Act_q
  Key=Inp(2)
  End
Return
'
Procedure Act_r
  Gosub Data_name
  Open "I",#1,Temp$
  Input #1,O%
  Input #1,Room%
  For X%=1 To O%
    Input #1,O%(X%)
  Next X%
  For X%=1 To 10
    Input #1,S%(X%)
  Next X%
  For X%=1 To 5
    Input #1,C%(X%)
  Next X%
  Close #1
  Break%=1
  Cls
Return
'
Procedure Act_s
  Gosub Data_name
  Open "O",#1,Temp$
  Print #1,O%
  Print #1,Room%
  For X%=1 To O%
    Print #1,O%(X%)
  Next X%
  For X%=1 To 10
    Print #1,S%(X%)
  Next X%
  For X%=1 To 5
    Print #1,C%(X%)
  Next X%
  Close
  Break%=1
  Cls
Return
'
Procedure Data_name
  Cls
  If Mid$(E$,E%,1)="R" Then
    E$="LOAD "
  Else
    E$="SAVE "
  Endif
  Print
  Print Tab(28);"Choose Data File to ";E$
  Fileselect "A:\alzanadv\*.dat","",Temp$
Return
'
Procedure L6000
  W$="    "
  P$(W%)="00"
  Gosub L6600
  If Finish%<>0 Then
    Goto L6140
  Endif
  For Q%=1 To 4
    T$=Mid$(Y$,Y%,1)
    Mid$(W$,Q%)=T$
    Gosub L6500
    If Finish%<>0 Then
      Q%=5
    Endif
  Next Q%
  If Finish%<>0 Then
    Goto L6100
  Endif
  L6080:
  Gosub L6500
  If Not Finish% Then
    Goto L6080
  Endif
  L6100:
  If W$="    " Then
    Goto L6140
  Endif
  For Q%=1 To V%
    If W$=Mid$(V$(Q%),3) Then
      P$(W%)=Mid$(V$(Q%),1,2)
    Endif
  Next Q%
  L6140:
Return
'
Procedure L6500
  Y%=Y%+1
  Finish%=(Y%>Len(Y$))
  If Finish% Then
    Goto L6540
  Endif
  Finish%=(Mid$(Y$,Y%,1)=" ")
  L6540:
Return
'
Procedure L6600
  L6600:
  Y%=Y%+1
  Finish%=(Y%>Len(Y$))
  If Finish%<>0 Then
    Goto L6640
  Endif
  If Mid$(Y$,Y%,1)=" " Then
    Goto L6600
  Endif
  L6640:
Return
'
Procedure Txt_1
  Print "  Oh dear. You must have caught the Plague in the Tomb."
  Print "  It seems that you have died!"
Return
'
Procedure Txt_2
  Print Tab(12);"---Whoosh---"
  Print "  El Grabo, the local thief snatches your money"
  Print "  and disappears into the sea mist."
Return
'
Procedure Txt_3
  Print "  'Stop Thief' shouts the Usher,"
  Print "  but you manage to escape."
Return
'
Procedure Txt_4
  Print "  The cover is already open."
Return
'
Procedure Txt_5
  Print "  It costs more than you can afford."
Return
'
Procedure Txt_6
  Print "  'That'll do nicily, Sir', the Shopkeeper says,"
  Print "  and hand you the hammer."
Return
'
Procedure Txt_7
  Print "  The manhole cover is open."
Return
'
Procedure Txt_8
  Print "  The manhole cover is shut."
Return
'
Procedure Txt_9
  Print "  The shopkeeper is bigger than you..."
Return
'
Procedure Txt_10
  Print "  You will need a ladder to get over these walls."
Return
'
Procedure Txt_11
  Print "  It is already on."
Return
'
Procedure Txt_12
  Print "  What a stroke of Genius."
Return
'
Procedure Txt_13
  Print "  You catch the guards unaware and manage to snatch a wad"
  Print "  of notes. No-one has noticed (funny lot, these Alzans)."
Return
'
Procedure Txt_14
  Print "  You have taken all there is."
Return
'
Procedure Txt_15
  Print "  I don't see a torch."
Return
'
Procedure Txt_16
  Print "  The cinema is booked for a private function."
Return
'
Procedure Room_1
  Print Tab(10);"Wellcome to Alzan"
  Print "  You must scale the walls if you wish to escape from"
  Print "  this city of thieves and cut-throats."
Return
'
Procedure Room_2
  Print "  You are in the main street outside a hardware shop."
  Print "  The street stretches east/west and a small alley"
  Print "  leads north beside the shop."
Return
'
Procedure Room_3
  Print "  You are inside the shop. The shopkeeper looks shifty,"
  Print "  but he has many fine goods on display."
Return
'
Procedure Room_4
  Print "  You are in an alley behind the tall buildings. There"
  Print "  are many full dustbins under the fire escape."
Return
'
Procedure Room_5
  Print "  You are on the fire escape, which leads past a door"
  Print "  in the buildings."
Return
'
Procedure Room_6
  Print "  You have come down a secret staircase into the shop."
Return
'
Procedure Room_7
  Print "  You are on some catwalks between the buildings."
Return
'
Procedure Room_8
  Print "  This is part of the city walls. There is an unused door"
  Print "  in the wall here."
Return
'
Procedure Room_9
  Print "  You are at a crossroad."
Return
'
Procedure Room_10
  Print "  Here is part of the city walls. The sea mist is quite thick,"
  Print "  making it hard to see far."
Return
'
Procedure Room_11
  Print "  You plunge from the wall - right down onto the rocks by the sea,"
  Print "  500ft below. Well, never mind, better luck next time."
Return
'
Procedure Room_12
  Print "  You are outside the town bank."
Return
'
Procedure Room_13
  Print "  Inside the bank there are many guards"
  Print "  who seem rather bored."
Return
'
Procedure Room_14
  Print "  You have arrived at a dead end,"
  Print "  but there is a manhole in the road..."
Return
'
Procedure Room_15
  Print "  You are in a small alcove underneath the manhole."
  Print "  A passage leads south."
Return
'
Procedure Room_16
  Print "  The passage leads to an ancient tomb where many"
  Print "  sarcophagi lie scattered about."
Return
'
Procedure Room_17
  Print "  The usher will not let you in as the programme has started."
  Print "  He blocks your path with his torch."
Return
'
Procedure Room_18
  Print "  You are outside the cinema. Sounds of"
  Print "  gunfire comes from within."
Return
'
Procedure Room_19
  Print Tab(8);"*** CONGRATULATIONS ***"
  Print "  You made it outside the city walls."
  Print "  This is indeed a rare occasion."
  Print "  Well done!!!"
Return
'
Procedure Initialisation
  Room%=1
  O%=11
  V%=45
  R%=19
  C%=9
  A%=49
  Dim S%(10),C%(5),P$(2)
  Dim O%(O%),O$(O%),V$(V%),M$(R%),C$(C%+1),A$(A%)
  For X%=1 To O%
    Read O%(X%),O$(X%)
  Next X%
  For X%=1 To V%
    Read V$(X%)
  Next X%
  For X%=1 To R%
    Read M$(X%)
  Next X%
  For X%=1 To C%
    Read C$(X%)
  Next X%
  C$(C%+1)=".N."
  For X%=1 To A%
    Read A$(X%)
  Next X%
Return
'
Data 0,"a Lighted Torch"
Data 0,"a Torch"
Data 3,"a Ladder"
Data 3,"a Hammer"
Data 0,"a Hammer"
Data 0,"a Wad of Notes"
Data 0,"Manhole Cover"
Data 15,"a Bag of Nails"
Data 16,"a Barclaycard"
Data 0,"a Rough Ladder"
Data 4,"some Wood"
'
Data "01N   ","01NORT","02E   ","02EAST"
Data "03S   ","03SOUT","04W   ","04WEST"
Data "05U   ","05UP  ","06D   ","06DOWN"
Data "13TAKE","14PUT ","14DROP","15ENTE"
Data "15IN  ","16OUT ","16EXIT","16LEAV"
Data "17TORC","18LADD","19HAMM","20WAD "
Data "20NOTE","22BAG ","22NAIL","23BARC"
Data "05SCAL","05CLIM","29OPEN","29LIFT"
Data "30MAKE","30BUIL","31SWIT","31LIGH"
Data "32BUY ","33WOOD","34ROB ","34STEA"
Data "35INVE","36QUIT","37LOOK","38LOAD","39SAVE"
'
Data "00","01040209041800"
Data "00","0202050500"
Data "0604040700","00"
Data "0108030500","030700"
Data "011202100314040200","040900"
Data "00","0209041800"
Data "00","010900"
Data "031600","011500"
Data "00","0112020200","00"
'
Data "A01.K02O."
Data "A16H30.G0121."
Data "G01.D01Q."
Data "B06H10.D02J06."
Data "A14E07.D07N."
Data "A14F07.D08N."
Data "A11.Q."
Data "A19.Q."
Data "A06.K03O."
'
Data "1317B01.B01E03L."
Data "1317A17C01C02.I02B02D03K18E10O."
Data "3218B03.D05N."
Data "1319B05.B05L."
Data "1320B06.B06L."
Data "2900A14E07.D04N."
Data "2900A14.E07M."
Data "1322B08.B08L."
Data "1323B09.B09L."
Data "1417B01.C01F03L."
Data "1417B02.C02L."
Data "1419B05.C05L."
Data "1420B06.C06L."
Data "1422B08.C08L."
Data "1423B09.C09L."
Data "0500A10C10.D10M."
Data "0500A08C10.D10M."
Data "0500A10.K11O."
Data "0500A08.K19O."
Data "0500A15.F02K14O."
Data "0600A14.E02K15O."
Data "3100D02.H01E03L."
Data "3100B01.D11N."
Data "3219B04B06.H04J06B05L."
Data "3219B04B09.H04D06B05M."
Data "3000B05B11B08.D12I10J08J11M."
Data "1333B11.B11L."
Data "1433B11.C11L."
Data "1500A02.K03O."
Data "1500A12.K13O."
Data "1500A18F10.K17O."
Data "1600A03.K02O."
Data "1600A13.K12O."
Data "1600A17.K18O."
Data "1500A05.K06O."
Data "3400A03.D09M."
Data "3400A13E08.D14M."
Data "3400A13.E08D13I06B06M."
Data "1500A18E10.D16M."
Data "1318B10.B10L."
Data "1418B10.C10L."
Data "1318B03.D09M."
Data "1317B02.B02L"
Data "3500.A."
Data "3600.P."
Data "3700.O."
Data "3800.R."
Data "3900.S."
Data "5000.N."
