Path$="\*.*"
Start:
Fullw 1
Titlew 1," The Bells & Whistles 1st Word¿ text file displayer "
Deftext 3,4,0,8
Text 145,172,"Select 1st Word¿ file to display:"
Fileselect Path$,"",File$
If File$="" Then
  Closew 1
  Edit
Endif
Do
  A=Instr(A+1,File$,"\")
  If A<>0 Then
    B=A
  Endif
  Exit If A=0
Loop
Path$=Left$(File$,B)+"*.*"
'
Cls
Hidem
@Screen
Repeat
  Titlew 1," Press any key to continue... "
  Pause 20
  Titlew 1,""
  Pause 20
Until Inkey$<>""
Cls
Goto Start
'
'
Procedure Screen
  Close #1
  Open "I",#1,File$
  A$=Input$(Lof(#1),#1)
  Pointer=Instr(A$,"..]")+3  ! get rid of garbage at beginning of 1st Word files
  Titlew 1," Press any key to pause... "
  Deftext 1,0,0,6
  Xpos=0
  Ypos=8
  Do
    Exit If Pointer=Lof(#1)-1
    X$=Mid$(A$,Pointer,1)
    X=Asc(X$)
    If X>31 And X<127 Then
      Text Xpos,Ypos,X$
      Xpos=Xpos+8
    Else
      If Inkey$<>"" Then
        Titlew 1," ** Press [Esc] to quit, any other key to continue... ** "
        A=Inp(2)
        If A=27
          Cls
          Resume Start
        Endif
        Titlew 1," Press any key to pause... "
      Endif
      If X=28 Or X=29 Or X=30 Then   ! space
        Xpos=Xpos+8
        Goto Skip
      Endif
      If X=130 Then                  ! Light
        Deftext 1,2,0,6
      Endif
      If X=132 Then                  ! Italic
        Deftext 1,4,0,6
      Endif
      If X=134 Then                  ! Italic light
        Deftext 1,6,0,6
      Endif
      If X=129 Then                  ! Bold
        Deftext 1,1,0,6
      Endif
      If X=136 Then                  ! Underlined
        Deftext 1,8,0,6
      Endif
      If X=138 Then                  ! Underlined light
        Deftext 1,10,0,6
      Endif
      If X=137 Then                  ! Bold underlined
        Deftext 1,9,0,6
      Endif
      If X=133 Then                  ! Bold italic
        Deftext 1,5,0,6
      Endif
      If X=140 Then                  ! Underlined italic
        Deftext 1,12,0,6
      Endif
      If X=158 Then                  ! Underlined italic light
        Deftext 1,14,0,6
      Endif
      If X=144 Then                  ! Superscript
        Deftext 1,0,0,4
      Endif
      If X=157 Then                  ! Bold underlined italic
        Deftext 1,13,0,6
      Endif
      If X=160 Then                  ! Subscript
        Deftext 1,0,0,4
      Endif
      If X=128 Then                  ! Turn off special modes
        Deftext 1,0,0,6
      Endif
      If X=13
        Xpos=0
        Ypos=Ypos+8
        If Ypos>170 Then
          Titlew 1," Press any key to continue... "
          Repeat
          Until Inkey$<>""
          Cls
          Titlew 1," Press any key to pause... "
          Ypos=8
        Endif
      Endif
    Endif
    Skip:
    Inc Pointer
  Loop
Return
