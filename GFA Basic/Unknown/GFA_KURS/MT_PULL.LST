' /*** PulldownmenÅ-Bedienung per Tastatur ***/
'
DIM m$(200)
i%=-1
REPEAT
  INC i%
  READ m$(i%)
UNTIL m$(i%)="*"
m$(i%)=""
MENU m$()
ON MENU GOSUB auswertung
OPENW 0
DATA 1:Desk,Info,-------------------,1,2,3,4,5,6,
DATA 2:File,Load,Save,
DATA 3:Titel,Dies,sind,Platz-,halter,fÅr,MenÅ-
DATA eintrÑge,,*
'
init_key_pull
ON MENU KEY GOSUB key_pull
REPEAT
  ON MENU
UNTIL MOUSEK AND 2
'
PROCEDURE auswertung
  MENU OFF
  PRINT AT(2,10);"GewÑhlt:"'m$(MENU(0));SPACE$(10)
RETURN
PROCEDURE init_key_pull
  ' Bestimme den ASCII-Code der
  ' hîchsten zulÑssigen Wahl.
  max_wahl%=49        ! ASCII-Code von 1
  WHILE OB_SPEC(MENU(-1),max_wahl%-45)<>0
    INC max_wahl%
  WEND
  xp%=20              ! Maus zu Beginn
  yp%=10              ! auf das erste
  SETMOUSE xp%,yp%,0  ! MenÅ setzen.
RETURN
PROCEDURE key_pull
  t%=BYTE(MENU(14))      ! ASCII-Code der Taste bestimmen
  SELECT t%
  CASE 45                !*** Minus
    IF OB_TYPE(MENU(-1),OBJC_FIND(MENU(-1),0,5,xp%,yp%-16))=28
      SUB yp%,16
      SETMOUSE xp%,yp%,0         ! Einen Eintrag rauf
    ENDIF
  CASE 43                !*** Plus
    IF OB_TYPE(MENU(-1),OBJC_FIND(MENU(-1),0,5,xp%,yp%+16))=28
      ADD yp%,16
      SETMOUSE xp%,yp%,0         ! Einen Eintrag runter.
    ENDIF
  CASE 49 TO max_wahl%   !*** ZulÑssige Zahlen
    xp%=OB_X(MENU(-1),t%-46)+20  ! Setze die Maus
    yp%=10                       ! auf das gewÑhlte
    SETMOUSE xp%,yp%,0           ! MenÅ.
  CASE 13                !*** Return und Enter
    ' Achtung: in Ñlteren GFA-BASIC 3.0-Versionen muû der
    '          dritte SETMOUSE-Parameter 2 statt 1 sein !!!
    SETMOUSE xp%,yp%,1   ! WÑhle den aktuellen MenÅeintrag.
    SETMOUSE xp%,yp%,0   ! Loslassen der Maustaste simulieren.
  ENDSELECT
RETURN
